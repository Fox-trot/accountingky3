#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Корзина 	= Параметры.Корзина;
	Количество			= Параметры.Количество;
	ЕдиницаИзмерения	= Параметры.Номенклатура.ЕдиницаИзмерения;
	
	Если Корзина = "КорзинаРеализации" Тогда
		Цена			= Параметры.Цена;
		Элементы.ДекорацияСумма.Заголовок = ОбщегоНазначенияБПКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	КонецЕсли;
	
	УстановитьВидимость(Параметры.КешНастройкиПодбора);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(Новый Структура("Количество, ЕдиницаИзмерения, Цена", Количество, ЕдиницаИзмерения, Цена));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Количество.
//
&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Цена.
//
&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимость(Параметры)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Количество", "Доступность", Параметры.ЗапрашиватьКоличество);
	
	Элементы.Цена.Видимость = Ложь;
	Элементы.ДекорацияСумма.Видимость = Ложь;
	
	Если Корзина = "КорзинаРеализации" Тогда
		
		Элементы.Цена.Видимость = Истина;
		Элементы.ДекорацияСумма.Видимость = Истина;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕдиницаИзмерения", "Доступность", Параметры.ЗапрашиватьЦену);
		
		РазрешеноИзменятьЦену = ?(Параметры.ЗапрашиватьЦену, Параметры.РазрешеноИзменятьЦену, Параметры.ЗапрашиватьЦену);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Цена", "Доступность", РазрешеноИзменятьЦену);
		
		// Если запрашиваем только цену, сразу позиционируем фокус на реквизите Цена
		Если НЕ Параметры.ЗапрашиватьКоличество
			И Параметры.ЗапрашиватьЦену Тогда
			
			ЭтаФорма.ТекущийЭлемент = Элементы.Цена;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьИтоговаяСумма()
	
	Элементы.ДекорацияСумма.Заголовок = ОбщегоНазначенияБПКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	
КонецПроцедуры

#КонецОбласти
