
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Корзина = Параметры.Корзина;
	ЗапрашиватьКоличество 	= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ЗапрашиватьКоличество");
	
	Если Корзина = "КорзинаРеализации" Тогда
		ЗапрашиватьЦену	= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ЗапрашиватьЦену");
	КонецЕсли;
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ИзменныеНастройки = Новый Структура;
	
	Если Модифицированность Тогда
		
		ИзменныеНастройки.Вставить("ЗапрашиватьКоличество", ЗапрашиватьКоличество);
		
		Если Корзина = "КорзинаРеализации" Тогда
			ИзменныеНастройки.Вставить("ЗапрашиватьЦену", ЗапрашиватьЦену);
		КонецЕсли;
		
		СохранитьНовыеНастройкиИОбновитьИнтерфейс(ИзменныеНастройки);
				
	КонецЕсли;
	
	Закрыть(ИзменныеНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьНовыеНастройкиИОбновитьИнтерфейс(ИзменныеНастройки)
	
	Для Каждого ЭлементНастройки Из ИзменныеНастройки Цикл
		
		ТекущееЗначениеНастройки = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки(ЭлементНастройки.Ключ);
		Если ЭлементНастройки.Значение <> ТекущееЗначениеНастройки Тогда
			
			БухгалтерскийУчетСервер.УстановитьНастройкуПользователя(ЭлементНастройки.Значение, ЭлементНастройки.Ключ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()

	Элементы.ЗапрашиватьЦену.Видимость = Истина;
	
	Если НЕ Корзина = "КорзинаРеализации" Тогда
		Элементы.ЗапрашиватьЦену.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
