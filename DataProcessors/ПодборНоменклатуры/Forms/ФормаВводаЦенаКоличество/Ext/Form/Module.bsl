////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВидПодбора = Параметры.ВидПодбора;
	КоличественныйУчет = Истина;
	СписокСвойств = "Номенклатура, Организация, Склад, Количество, СчетУчета, ЕдиницаИзмерения, Цена, Валюта,
		|ЕстьКоличество, ЕстьЦена, Партия";
	Если ВидПодбора = "ПодборМБПЭксплуатация" Тогда
		СписокСвойств = СписокСвойств + ", ИндивидуальныйНомер, ДатаВыдачи, СтатусМБП";
	ИначеЕсли ВидПодбора = "ПодборМБПСклад" Тогда
		СписокСвойств = СписокСвойств + ", ИндивидуальныйНомер, СтатусМБП";
	Иначе
		СписокСвойств = СписокСвойств + ", Сумма";
		КоличественныйУчет = Параметры.КоличественныйУчет;
	КонецЕсли;
		
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если НЕ ЕстьКоличество И НЕ ЕстьЦена Тогда
		Отказ = Истина;
	КонецЕсли;
	
	СвойстваНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Наименование, ЕдиницаИзмерения");
	
	Заголовок = СвойстваНоменклатуры.Наименование;
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ЕдиницаИзмерения = СвойстваНоменклатуры.ЕдиницаИзмерения;
	КонецЕсли;
	
	Элементы.Количество.Видимость       = ЕстьКоличество И КоличественныйУчет;
	Элементы.ЕдиницаИзмерения.Видимость = ЕстьКоличество;
	
	Элементы.Цена.Видимость         = ЕстьЦена И КоличественныйУчет;
	Элементы.ВалютаЦены.Видимость   = ЕстьЦена;
	Элементы.Сумма.Видимость        = ЕстьЦена ИЛИ НЕ КоличественныйУчет;
	Элементы.ВалютаСуммы.Видимость  = ЕстьЦена;
	
	Если КоличественныйУчет Тогда
	 	Сумма = Цена * Количество;
	Иначе
		Элементы.Сумма.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	СпособОценки = ПолучитьСпособОценки(ЭтотОбъект);
	Если СпособОценки = ПредопределенноеЗначение("Перечисление.СпособыОценки.ПоФиксированной") Тогда
		ЕстьЦена = Истина;
		Элементы.Цена.Видимость     = ЕстьЦена;
		Элементы.Цена.КнопкаВыпадающегоСписка = Истина;
		МассивЦен = ПолучитьСписокФиксированныхЦен(ЭтотОбъект, СпособОценки);
		Элементы.Цена.СписокВыбора.ЗагрузитьЗначения(МассивЦен);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ЕстьКоличество Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Количество");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	Если ЭтотОбъект.СпособОценки = ПредопределенноеЗначение("Перечисление.СпособыОценки.ПоФиксированной") Тогда
		ПроверяемыеРеквизиты.Добавить("Цена");
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	Сумма = Цена * Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	Сумма = Цена * Количество;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = НЕ ПроверитьЗаполнение();
	
	Если НЕ Отказ Тогда
		Если ВидПодбора = "ПодборМБПЭксплуатация" Тогда
			ПараметрЗакрытия = Новый Структура("Номенклатура, Количество, Цена, Валюта, СчетУчета, Партия, ИндивидуальныйНомер, ДатаВыдачи, СтатусМБП");
		ИначеЕсли ВидПодбора = "ПодборМБПСклад" Тогда
			ПараметрЗакрытия = Новый Структура("Номенклатура, Количество, Цена, Валюта, СчетУчета, Партия, ИндивидуальныйНомер, СтатусМБП");			
		Иначе
			ПараметрЗакрытия = Новый Структура("Номенклатура, Количество, Цена, Валюта, СчетУчета, Партия, Сумма");
		КонецЕсли;		
		ЗаполнитьЗначенияСвойств(ПараметрЗакрытия, ЭтотОбъект);
		Закрыть(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩЕГО НАЗНАЧЕНИЯ
&НаСервереБезКонтекста
Функция ПолучитьСпособОценки(ЭтотОбъект)
	
	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(ЭтотОбъект.Дата, ЭтотОбъект.Организация);
	Возврат БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСпособОценкиЗапасов(ЭтотОбъект.СчетУчета, Перечисления.СпособыОценки.ПоСредней)

КонецФункции // ПолучитьСпособОценки(ЭтотОбъект)

Функция ПолучитьСписокФиксированныхЦен(ЭтотОбъект, СпособОценки)
	ДанныеСТЧ =Новый Структура;
	ДанныеСТЧ.Вставить("Номенклатура", 			ЭтотОбъект.Номенклатура);
	ДанныеСТЧ.Вставить("Количество", 			ЭтотОбъект.Количество);
	ДанныеСТЧ.Вставить("СпособОценки", 			СпособОценки);
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("Организация", 	ЭтотОбъект.Организация);
	ПараметрыОбъекта.Вставить("Дата", 			ЭтотОбъект.Дата);
	ПараметрыОбъекта.Вставить("Склад", 			ЭтотОбъект.Склад);
	
	ТаблицаЗначений = БухгалтерскийУчетСервер.ПолучитьЦенуНоменклатурыПоСпособуОценки(ДанныеСТЧ, ПараметрыОбъекта);
	Если ТипЗнч(ТаблицаЗначений) = ТИП("ТаблицаЗначений") Тогда
		Возврат ТаблицаЗначений.ВыгрузитьКолонку("Цена");
	Иначе
	    Возврат Неопределено;
	КонецЕсли;
	 
КонецФункции // ПолучитьСписокФиксированныхЦен(ЭтотОбъект, СпособОценки)

	//Если СпособОценки = ПредопределенноеЗначение("Перечисление.СпособыОценки.ПоФиксированной") 
	//		И НЕ ЗначениеЗаполнено(ЭтотОбъект.Цена) Тогда
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо выбрать цену!'"),,"Цена",,Отказ);
	//КонецЕсли;	

