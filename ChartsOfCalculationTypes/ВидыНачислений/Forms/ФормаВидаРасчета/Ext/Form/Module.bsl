
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура вызывается при нажатии кнопки "Редактировать формулу расчета". 
//
&НаКлиенте
Процедура КомандаРедактироватьФормулуРасчета(Команда)
	
	СтруктураПараметров = Новый Структура("ТекстФормулы, Ссылка", Объект.Формула, Объект.Ссылка);
	Оповещение = Новый ОписаниеОповещения("КомандаРедактироватьФормулуРасчетаЗавершение",ЭтаФорма);
	ОткрытьФорму("ПланВидовРасчета.ВидыНачислений.Форма.ФормаРедактированияФормулыРасчета", СтруктураПараметров,,,,,Оповещение);
	
КонецПроцедуры // КомандаРедактироватьФормулуРасчетаВыполнить()

&НаКлиенте
Процедура КомандаРедактироватьФормулуРасчетаЗавершение(ТекстФормулы,Параметры) Экспорт

	Если ТипЗнч(ТекстФормулы) = Тип("Строка") Тогда
		Объект.Формула = ТекстФормулы;
	КонецЕсли;

	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода СпособРасчета.
//
&НаКлиенте
Процедура СпособРасчетаПриИзменении(Элемент)
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

// Процедура устанавливает видимость и доступность элементов.
//
&НаКлиенте
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	Элементы.Наименование.ТолькоПросмотр = Объект.Предопределенный;
	
	Элементы.СтраницаБазовыеВидыРасчета.Видимость = Ложь;
	Элементы.СтраницаВытесняющиеВидыРасчета.Видимость = Ложь;
	Элементы.Коэффициент.Видимость = Ложь;
	Элементы.Доля.Видимость = Ложь;
	Элементы.ПериодРасчетаСреднегоЗаработка.Видимость = Ложь;	
	Элементы.СтраницаПроизвольнаяФормула.Видимость = Ложь;
	
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоМесячнойСтавкеПоДням")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоМесячнойСтавкеПоЧасам")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоДневнойСтавке")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоЧасовойСтавке") Тогда 
		Элементы.СтраницаВытесняющиеВидыРасчета.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.РаботаСверхурочно") Тогда 
		Элементы.Коэффициент.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ОплатаОтпуска")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ОплатаБольничного") Тогда 		
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
		Элементы.ПериодРасчетаСреднегоЗаработка.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.Процентом") Тогда 		
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ГодоваяПремия") Тогда 
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
		Элементы.Доля.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПроизвольнаяФормула") Тогда 
		Элементы.СтраницаПроизвольнаяФормула.Видимость = Истина;
		
		Если СтрНайти(Объект.Формула, "ФактДней") > 0
			Или СтрНайти(Объект.Формула, "ФактЧасов") > 0 Тогда 
			
			Элементы.СтраницаВытесняющиеВидыРасчета.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;	
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяОрганизация")));
	
КонецПроцедуры // УстановитьВидимостьДоступностьЭлементов()

#КонецОбласти
