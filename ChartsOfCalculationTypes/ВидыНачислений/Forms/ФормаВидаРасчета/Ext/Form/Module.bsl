
#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиенте
// Процедура устанавливает видимость и доступность элементов.
//
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	Элементы.Наименование.ТолькоПросмотр = Объект.Предопределенный;
	
	Элементы.СтраницаБазовыеВидыРасчета.Видимость = Ложь;
	Элементы.СтраницаВытесняющиеВидыРасчета.Видимость = Ложь;
	Элементы.Коэффициент.Видимость = Ложь;
	Элементы.Доля.Видимость = Ложь;
	Элементы.ПериодРасчетаСреднегоЗаработка.Видимость = Ложь;	
	
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоМесячнойСтавкеПоДням")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоМесячнойСтавкеПоЧасам")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоДневнойСтавке")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПоЧасовойСтавке") Тогда 
		Элементы.СтраницаВытесняющиеВидыРасчета.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.РаботаСверхурочно")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.РаботаСверхурочноБаза") Тогда 
		Элементы.Коэффициент.Видимость = Истина;
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;		
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ОплатаОтпуска")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ОплатаБольничного") Тогда 		
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
		Элементы.ПериодРасчетаСреднегоЗаработка.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.Процентом")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПроцентомОтРазмера")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ПроцентомЗаПредМесяц")
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ОтСтажаПроцентом") Тогда 		
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ГодоваяПремия") Тогда 
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
		Элементы.Доля.Видимость = Истина;
	ИначеЕсли Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ЗаВредность") 
		Или Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ВыслугаЛет") Тогда 
		Элементы.СтраницаБазовыеВидыРасчета.Видимость = Истина;
		Элементы.СтраницаВытесняющиеВидыРасчета.Видимость = Истина;
	КонецЕсли;	
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяОрганизация")));
	
КонецПроцедуры // УстановитьВидимостьДоступностьЭлементов()

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособРасчетаПриИзменении(Элемент)
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти
