////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Физлицо") Тогда
		Физлицо = Параметры.Отбор.Физлицо;
		
		УдостоверениеЛичности = РегистрыСведений.ДокументыФизическихЛиц.ДокументУдостоверяющийЛичностьФизлица(Физлицо);
		
		ЕстьУдостоверение = Не ПустаяСтрока(УдостоверениеЛичности);
		
		Элементы.УдостоверениеЛичности.Высота		= ?(ЕстьУдостоверение, 2, 0);
		УдостоверениеЛичности = ?(ЕстьУдостоверение, "Удостоверение личности: ", "") + УдостоверениеЛичности;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Физлицо",	Физлицо);
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументыФизическихЛиц.Представление
		|ИЗ
		|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
		|ГДЕ
		|	ДокументыФизическихЛиц.Физлицо = &Физлицо";
		ЕстьДокументы = Не Запрос.Выполнить().Пустой();
		
		Если Не ЕстьУдостоверение И ЕстьДокументы Тогда
			Элементы.НетУдостоверения.Видимость		= Истина;
			УдостоверениеЛичности = СтрШаблон(НСтр("ru = 'Для физлица %1 не задан документ, удостоверяющий личность.'"), Физлицо);
		КонецЕсли;
		
		Элементы.УдостоверениеЛичности.Видимость	= Не ПустаяСтрока(УдостоверениеЛичности);
	КонецЕсли;
	
КонецПроцедуры

