#Область ПрограммныйИнтерфейс

// Возвращает валюту регламентированного учета
// Если переданная в качестве параметра валюта уже заполнена - возвращает ее.
// Если валюта не передана в качестве параметра или передан пустой,
// валюту рег. учета. Если валюта рег. учета не заполнена - возвращает пустую ссылку на валюту
//
// Параметры:
// Валюта - СправочникСсылка.Валюты - Валюта, которую нужно заполнить
//
// Возвращаемое значение:
// СправочникСсылка.Валюты
//
Функция ПолучитьВалютуРегламентированногоУчета(Знач Валюта = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Возврат Константы.ВалютаРегламентированногоУчета.Получить();
	Иначе
		Возврат Валюта;
	КонецЕсли;
	
КонецФункции 

Функция ИспользоватьУправляемыеБлокировки() Экспорт

	Результат = Метаданные.РежимУправленияБлокировкойДанных <> Метаданные.СвойстваОбъектов.РежимУправленияБлокировкойДанныхПоУмолчанию.Автоматический
		И СтрНайти(СтрокаСоединенияИнформационнойБазы(), "Srvr=") = 1;
		
	Возврат Результат;

КонецФункции 

// Функция - Классификатор организационно правовых форм
//
// Возвращаемое значение:
//  ТаблицаЗначений - Колонки:
//  	Код - Строка - код ОКОПФ
//  	ПолнаяФорма - Строка - полная форма наименования ОПФ
//  	КраткаяФорма - Строка - аббревиатура наименования ОПФ (может быть незаполнена)
//
Функция КлассификаторОрганизационноПравовыхФорм() Экспорт
	
	Макет = ПолучитьОбщийМакет("КлассификаторОрганизационноПравовыхФорм");
	Таблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти