///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняется при интерактивном начале работы пользователя с областью данных или в локальном режиме.
// Соответствует обработчику ПриНачалеРаботыСистемы.
//
// Параметры:
//  Параметры - Структура - структура со свойствами:
//   * Отказ         - Булево - Возвращаемое значение. Если установить Истина, то работа программы будет прекращена.
//   * Перезапустить - Булево - Возвращаемое значение. Если установить Истина и параметр Отказ тоже установлен
//                              в Истина, то выполняется перезапуск программы.
//
//   * ДополнительныеПараметрыКоманднойСтроки - Строка - Возвращаемое значение. Имеет смысл
//                              когда Отказ и Перезапустить установлены Истина.
//
//   * ИнтерактивнаяОбработка - ОписаниеОповещения - Возвращаемое значение. Для открытия окна, блокирующего вход
//                              в программу, следует присвоить в этот параметр описание обработчика оповещения,
//                              который открывает окно. См. пример в ПередНачаломРаботыСистемы.
//
//   * ОбработкаПродолжения   - ОписаниеОповещения - если открывается окно, блокирующее вход в программу, то в
//                              обработке закрытия этого окна необходимо выполнить оповещение ОбработкаПродолжения.
//                              См. пример в ОбщегоНазначенияКлиентПереопределяемый.ПередНачаломРаботыСистемы.
//
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
	
	// ИнтернетПоддержкаПользователей
	ИнтернетПоддержкаПользователейКлиент.ПриНачалеРаботыСистемы(Параметры);
	// Конец ИнтернетПоддержкаПользователей
	
	// НачалоРаботыСПрограммой
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		
		Если ПараметрыРаботыКлиента.ПоказатьОкноНачалоРаботыСПрограммой Тогда
			
			ПараметрыНачалаРаботы = Новый Структура("ПараметрыРаботыКлиента, ОткрытьФорму", ПараметрыРаботыКлиента, Ложь);
			НачалоРаботыСПрограммойКлиент.ИнтерфейсНачалоРаботы(ПараметрыНачалаРаботы);
			
		КонецЕсли;
		
		//ОбновитьИнтерфейс();
	КонецЕсли;
	// Конец НачалоРаботыСПрограммой
	
КонецПроцедуры

// Выполняется при интерактивном начале работы пользователя с областью данных или в локальном режиме.
// Вызывается после завершения действий ПриНачалеРаботыСистемы.
// Используется для подключения обработчиков ожидания, которые не должны вызываться
// в случае интерактивных действий перед и при начале работы системы.
//
// Начальная страница (рабочий стол) в этот момент еще не открыта, поэтому запрещено открывать
// формы напрямую, а следует использовать для этих целей обработчик ожидания.
// Запрещено использовать это событие для интерактивного взаимодействия с пользователем
// (ПоказатьВопрос и аналогичные действия). Для этих целей следует размещать код в процедуре ПриНачалеРаботыСистемы.
//
Процедура ПослеНачалаРаботыСистемы() Экспорт
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();

	// ЗнакомствоСРедакциейВ30
	Если ПараметрыРаботыКлиента.Свойство("ПоказатьЗнакомствоСРедакциейВ30")
		И ПараметрыРаботыКлиента.ПоказатьЗнакомствоСРедакциейВ30 Тогда
		ОбщегоНазначенияБПКлиент.ОткрытьНачинаемРаботатьВ30(ПараметрыРаботыКлиента.ИмяОбработкиЗнакомствоСРедакциейВ30);
	КонецЕсли;	
	// Конец ЗнакомствоСРедакциейВ30
	
	// ПечатьПараметровУчета
	Если ПараметрыРаботыКлиента.Свойство("ПоказатьПечатьПараметровУчета")
		И ПараметрыРаботыКлиента.ПоказатьПечатьПараметровУчета Тогда
		ОбщегоНазначенияБПКлиент.ОткрытьПечатьПараметровУчета(ПараметрыРаботыКлиента.ИмяОбработкиПечатьПараметровУчета);
	КонецЕсли;	
	// Конец ПечатьПараметровУчета

	Если ПараметрыРаботыКлиента.Свойство("ПоказатьПредложитьОбновитьВерсиюПрограммы") 
		И ПараметрыРаботыКлиента.ПоказатьПредложитьОбновитьВерсиюПрограммы Тогда
		// Информация о необходимости обновить конфигурацию
		ОбщегоНазначенияБПКлиент.ПредупредитьОНеобходимостиОбновленияПрограммы(ПараметрыРаботыКлиента);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
