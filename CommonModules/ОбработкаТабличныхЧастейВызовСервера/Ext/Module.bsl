
////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Рассчитывает сумму НДС, НСП исходя из суммы и ставок

Процедура РасчетСтрокиРеализации(ДанныеСТЧ, ПараметрыОбъекта, ТабличнаяЧасть = Неопределено) Экспорт	
	ЗначСтавкаНДС 	= ПараметрыОбъекта.ЗначСтавкаНДС;
	ЗначСтавкаНСП 	= ДанныеСТЧ.ЗначСтавкаНСП;
	
	Если ЗначениеЗаполнено(ПараметрыОбъекта.ДоговорКонтрагента) Тогда
		СуммаВключаетНалоги = ПараметрыОбъекта.ДоговорКонтрагента.СуммаВключаетНалоги;
		Если ТабличнаяЧасть = "ОС" Тогда
			Если СуммаВключаетНалоги Тогда
				ДанныеСТЧ.СуммаНДС	= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНДС / (100 + ЗначСтавкаНДС + ЗначСтавкаНСП), 2);
				ДанныеСТЧ.СуммаНСП	= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНСП / (100 + ЗначСтавкаНДС + ЗначСтавкаНСП), 2);
			Иначе
				ДанныеСТЧ.СуммаНДС	= Окр(ЗначСтавкаНДС * ДанныеСТЧ.Сумма / 100, 2);
				ДанныеСТЧ.СуммаНСП	= Окр(ЗначСтавкаНСП * ДанныеСТЧ.Сумма / 100, 2);
			КонецЕсли;
		
		Иначе
			Если СуммаВключаетНалоги Тогда
				ДанныеСТЧ.Всего 	= ДанныеСТЧ.Цена * ДанныеСТЧ.Количество;
				ДанныеСТЧ.СуммаНДС	= Окр(ДанныеСТЧ.Всего * ЗначСтавкаНДС / (100 + ЗначСтавкаНДС + ЗначСтавкаНСП), 2);
				ДанныеСТЧ.СуммаНСП	= Окр(ДанныеСТЧ.Всего * ЗначСтавкаНСП / (100 + ЗначСтавкаНДС + ЗначСтавкаНСП), 2);
			    ДанныеСТЧ.Доход 	= ДанныеСТЧ.Всего - ДанныеСТЧ.СуммаНДС - ДанныеСТЧ.СуммаНСП;
			Иначе
				ДанныеСТЧ.Доход 	= ДанныеСТЧ.Цена * ДанныеСТЧ.Количество;
				ДанныеСТЧ.СуммаНДС	= Окр(ЗначСтавкаНДС * ДанныеСТЧ.Доход / 100, 2);
				ДанныеСТЧ.СуммаНСП	= Окр(ЗначСтавкаНСП * ДанныеСТЧ.Доход / 100, 2);
			    ДанныеСТЧ.Всего 	= ДанныеСТЧ.Доход + ДанныеСТЧ.СуммаНДС + ДанныеСТЧ.СуммаНСП;
			КонецЕсли;
			
			РасчетСкидкиВРеализации(ДанныеСТЧ, ПараметрыОбъекта, ТабличнаяЧасть);
				
		КонецЕсли;
		
	Иначе	
		Возврат;	
	КонецЕсли;
	
	
КонецПроцедуры

Процедура РасчетСкидкиВРеализации(ДанныеСТЧ, ПараметрыОбъекта, ТабличнаяЧасть = Неопределено) Экспорт
		
	Если ПараметрыОбъекта.ВидСкидки = Перечисления.ВидыСкидок.ПроцентОбщий 
			ИЛИ ПараметрыОбъекта.ВидСкидки = Перечисления.ВидыСкидок.ПустаяСсылка() Тогда
			
		Если ТабличнаяЧасть = Неопределено Тогда
			ДанныеСТЧ.СуммаСкидки 		= Окр(ДанныеСТЧ.Всего * ПараметрыОбъекта.СкидкаПроцент / 100, 2);
			ДанныеСТЧ.СкидкаПроцент 	= 0;		
			ДанныеСТЧ.Итого 			= ДанныеСТЧ.Всего - ДанныеСТЧ.СуммаСкидки;
		Иначе
			Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
				СтрокаТабличнойЧасти.СуммаСкидки 	= Окр(СтрокаТабличнойЧасти.Всего * ПараметрыОбъекта.СкидкаПроцент / 100, 2);
				СтрокаТабличнойЧасти.СкидкаПроцент 	= 0;		
				СтрокаТабличнойЧасти.Итого 			= СтрокаТабличнойЧасти.Всего - СтрокаТабличнойЧасти.СуммаСкидки;
			КонецЦикла;	
		КонецЕсли;			
		
	ИначеЕсли ПараметрыОбъекта.ВидСкидки = Перечисления.ВидыСкидок.ПроцентПоСтроке Тогда
		Если ТабличнаяЧасть = Неопределено Тогда
			Если ЗначениеЗаполнено(ПараметрыОбъекта.СкидкаПроцент) Тогда
				ДанныеСТЧ.СкидкаПроцент 	= ПараметрыОбъекта.СкидкаПроцент;		
			КонецЕсли;
			ДанныеСТЧ.СуммаСкидки 	= Окр(ДанныеСТЧ.Всего * ДанныеСТЧ.СкидкаПроцент / 100, 2);
			ДанныеСТЧ.Итого 		= ДанныеСТЧ.Всего - ДанныеСТЧ.СуммаСкидки;
		Иначе
			Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
				Если ЗначениеЗаполнено(ПараметрыОбъекта.СкидкаПроцент) Тогда
					СтрокаТабличнойЧасти.СкидкаПроцент = ПараметрыОбъекта.СкидкаПроцент;
				КонецЕсли;
				СтрокаТабличнойЧасти.СуммаСкидки 	= Окр(СтрокаТабличнойЧасти.Всего * СтрокаТабличнойЧасти.СкидкаПроцент / 100, 2);		
				СтрокаТабличнойЧасти.Итого 			= СтрокаТабличнойЧасти.Всего - СтрокаТабличнойЧасти.СуммаСкидки;
			КонецЦикла;	
		КонецЕсли;
		
	ИначеЕсли ПараметрыОбъекта.ВидСкидки = Перечисления.ВидыСкидок.СуммаПоСтроке И ТипЗнч(ТабличнаяЧасть) = Тип("ДанныеФормыКоллекция") Тогда
		МассивКоэффициентов = ТабличнаяЧасть.Выгрузить(,"Всего").ВыгрузитьКолонку("Всего");
		МассивСкидок = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ПараметрыОбъекта.СуммаСкидкиОбщая, МассивКоэффициентов);
		Если МассивСкидок.Количество() > 0 Тогда
			Сч = 0;
			Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
				СтрокаТабличнойЧасти.СуммаСкидки 	= МассивСкидок[Сч];
				СтрокаТабличнойЧасти.СкидкаПроцент 	= 0;
				СтрокаТабличнойЧасти.Итого 			= СтрокаТабличнойЧасти.Всего - СтрокаТабличнойЧасти.СуммаСкидки;
				Сч = Сч + 1;
			КонецЦикла;				
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьРеквизитТабЧастиДокумента(ИмяРеквизита, МетаданныеДокумента, ИмяТабЧасти) Экспорт
	
	ТабЧасть = МетаданныеДокумента.ТабличныеЧасти.Найти(ИмяТабЧасти);
	
	Если ТабЧасть = Неопределено Тогда // Нет такой таб. части в документе
		Возврат Ложь;  		
	Иначе
		Возврат НЕ (ТабЧасть.Реквизиты.Найти(ИмяРеквизита) = Неопределено);    		
	КонецЕсли;
	
КонецФункции // ЕстьРеквизитТабЧастиДокумента()

// Позволяет определить есть ли среди реквизитов шапки документа
// реквизит с переданным именем.
//
// Параметры: 
//  ИмяРеквизита - строковое имя искомого реквизита, 
//  МетаданныеДокумента - объект описания метаданных документа, среди реквизитов которого производится поиск.
//
// Возвращаемое значение:
//  Булево: Истина - нашли реквизит с таким именем, Ложь - не нашли.
//
Функция ЕстьРеквизитДокумента(ИмяРеквизита, МетаданныеДокумента) Экспорт
	
	Возврат НЕ (МетаданныеДокумента.Реквизиты.Найти(ИмяРеквизита) = Неопределено);
	
КонецФункции // ЕстьРеквизитДокумента()  

// Процедура заполняет единицу измерения в ТЧ по номенклатуре
//
// Параметры:
//  СТЧ								- строка табличной части документа,
//  ДокументОбъект                 - объект редактируемого документа.
//
Процедура ЗаполнитьЕдиницуИзмеренияТабЧасти(ДанныеСТЧ) Экспорт

	// Заполнение ЕИ
	ДанныеСТЧ.ЕдиницаИзмерения 	= ДанныеСТЧ.Номенклатура.ЕдиницаИзмерения;
	ДанныеСТЧ.Емкость			= 1;
	ДанныеСТЧ.Цена             	= 0;

КонецПроцедуры // ЗаполнитьЕдиницуЦенуПокупкиТабЧасти()

// Процедура заполняет цену в строке табличной части документа
//
// Параметры:
//  СТЧ					           - строка табличной части документа,
//  ДокументОбъект                 - объект редактируемого документа.
//
Процедура ЗаполнитьЦенуСтрокиТабличнойЧасти(ДанныеСТЧ, ДокументОбъект) Экспорт

	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("Дата", ДокументОбъект.Дата);
	СтруктураДанных.Вставить("Номенклатура", ДанныеСТЧ.Номенклатура);
	СтруктураДанных.Вставить("ТипЦен", ДокументОбъект.ДоговорКонтрагента.ТипЦен);
	СтруктураДанных.Вставить("ВалютаДокумента", ДокументОбъект.ВалютаДокумента);
	
	ДанныеСТЧ.Цена = БухгалтерскийУчетСервер.ПолучитьЦенуНоменклатуры(СтруктураДанных);
	
КонецПроцедуры // ЗаполнитьЦенуСтрокиТабличнойЧасти()

// Процедура заполняет цену в строке табличной части документа
//
// Параметры:
//  СТЧ					           - строка табличной части документа,
//  ДокументОбъект                 - объект редактируемого документа.
//
Процедура ЗаполнитьСебестоимостьСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта) Экспорт

	Если ДанныеСТЧ.СпособОценки 	= ПредопределенноеЗначение("Перечисление.СпособыОценки.ПоСредней") 
			ИЛИ ДанныеСТЧ.СпособОценки = ПредопределенноеЗначение("Перечисление.СпособыОценки.ФИФО")Тогда
			
		ДанныеСТЧ.Себестоимость 	= БухгалтерскийУчетСервер.ПолучитьЦенуНоменклатурыПоСпособуОценки(ДанныеСТЧ, ПараметрыОбъекта);
		ДанныеСТЧ.Всего 			= ДанныеСТЧ.Цена * ДанныеСТЧ.Количество;
	Иначе
		//ДанныеСТЧ.Количество = 0;	
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьЕдиницуЦенуПокупкиТабЧасти()

// Процедура заполняет счет БУ в строке табличной части документа
//
// Параметры:
//  СТЧ					           - строка табличной части документа,
//  ДокументОбъект                 - объект редактируемого документа.
//
Процедура ЗаполнитьСчетУчетаСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта) Экспорт

	СтруктураСчетов 	= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(ПараметрыОбъекта.Организация, ДанныеСТЧ.Номенклатура);
	
	Если ДанныеСТЧ.Свойство("СчетУчета") Тогда
		ДанныеСТЧ.СчетУчета 		= СтруктураСчетов.СчетБУ;
	КонецЕсли;
	
	Если ДанныеСТЧ.Свойство("СчетДохода") Тогда
		ДанныеСТЧ.СчетДохода 		= СтруктураСчетов.СчетДохода;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьЕдиницуЦенуПокупкиТабЧасти()

Процедура РасчетСтрокиПоступления(ДанныеСТЧ, ПараметрыОбъекта, Изменение) Экспорт
	
	ОперацияПокупка 	= ПараметрыОбъекта.ВидОперации = Справочники.ОперацииПоступлениеТоваровУслуг.Покупка;
	ОперацияИмпортТС 	= ПараметрыОбъекта.ВидОперации = Справочники.ОперацииПоступлениеТоваровУслуг.ИмпортТС;
	
	// Изменение - колонка, которая редактируется:
	// * КоличествоМест 		- считаем все
	// * Количество             - считаем суммы и далее 
	// * Цена					- то же
	// * Сумма					- это сумма без НДС. Накручиваем НДС и далее
	// * СуммаСФ                - не считаем  
	// * НДС
	
	ЭтоУслуга 	= ?(ЗначениеЗаполнено(ДанныеСТЧ.Номенклатура), ДанныеСТЧ.Номенклатура.Услуга, Ложь);
	ЭтоОС 		= ЗначениеЗаполнено(ДанныеСТЧ.ОсновноеСредство);
	
	ЕстьНалоги 		= ОперацияПокупка
					ИЛИ ОперацияИмпортТС
					ИЛИ ТипЗнч(ПараметрыОбъекта.Ссылка) = Тип("ДокументСсылка.АвансовыйОтчет")
					ИЛИ ТипЗнч(ПараметрыОбъекта.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику");	
		
	РассчитыватьНДС = ПараметрыОбъекта.Свойство("ПлательщикНДС") И ПараметрыОбъекта.ПлательщикНДС 
					И (ДанныеСТЧ.СтавкаНДС <> Справочники.СтавкиНДС.БезНДС)  
					И  (ОперацияПокупка ИЛИ ОперацияИмпортТС)
					ИЛИ ТипЗнч(ПараметрыОбъекта.Ссылка) = Тип("ДокументСсылка.АвансовыйОтчет")
					ИЛИ ТипЗнч(ПараметрыОбъекта.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику");
					
	РассчитыватьНСП = ПараметрыОбъекта.Свойство("ПлательщикНСП") И ПараметрыОбъекта.ПлательщикНСП 
					ИЛИ ТипЗнч(ПараметрыОбъекта.Ссылка) = Тип("ДокументСсылка.АвансовыйОтчет")
					ИЛИ ТипЗнч(ПараметрыОбъекта.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику");					
														
	Если ЕстьНалоги И РассчитыватьНСП Тогда
		ЗначСтавкаНСП 	= ?(ТипЗнч(ДанныеСТЧ.ЗначСтавкаНСП) = Тип("Структура"), ДанныеСТЧ.ЗначСтавкаНСП.Ставка, ДанныеСТЧ.ЗначСтавкаНСП);
	Иначе
		ЗначСтавкаНСП 	= 0;
	КонецЕсли;
	Если ЕстьНалоги И РассчитыватьНДС Тогда
		ЗначСтавкаНДС  	= ?(ТипЗнч(ДанныеСТЧ.ЗначСтавкаНДС) = Тип("Структура"), ДанныеСТЧ.ЗначСтавкаНДС.Ставка, ДанныеСТЧ.ЗначСтавкаНДС);
	Иначе
		ЗначСтавкаНДС 	= 0;
	КонецЕсли;	
	
	// Расчет количества
	Если Изменение = "КоличествоМест" И ПараметрыОбъекта.ИспользоватьДополнительныеЕдиницыИзмерения Тогда
		ДанныеСТЧ.Количество = Окр(ДанныеСТЧ.КоличествоМест * ДанныеСТЧ.Емкость, 2);
	КонецЕсли;
	
	// Расчет суммы 
	Если (Изменение = "Количество" ИЛИ Изменение = "Цена" ИЛИ Изменение = "КоличествоМест") Тогда
		Если НЕ ПараметрыОбъекта.ПлательщикНДС 
			ИЛИ НЕ ЗначениеЗаполнено(ПараметрыОбъекта.ДоговорКонтрагента.СтавкаНДС)
			ИЛИ ПараметрыОбъекта.ДоговорКонтрагента.СтавкаНДС = Справочники.СтавкиНДС.БезНДС Тогда
			ДанныеСТЧ.Сумма = ДанныеСТЧ.Количество * ДанныеСТЧ.Цена;			
	 		Если ПараметрыОбъекта.ДоговорКонтрагента.СуммаВключаетНалоги Тогда 
				ДанныеСТЧ.СуммаНСП		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНСП / (100 + ЗначСтавкаНСП), 2);
			Иначе
				ДанныеСТЧ.СуммаНСП		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНСП /100, 2);
			КонецЕсли;	

		Иначе
			
			Если Изменение = "Количество" И ДанныеСТЧ.Количество <> 0 Тогда
				Если РассчитыватьНДС Тогда
					ДанныеСТЧ.Цена 	= ДанныеСТЧ.Сумма / ДанныеСТЧ.Количество;
				Иначе
				    ДанныеСТЧ.Сумма = ДанныеСТЧ.Количество * ДанныеСТЧ.Цена;
				КонецЕсли;				
			ИначеЕсли Изменение = "Цена" И ДанныеСТЧ.Цена <> 0 Тогда
				
				Если РассчитыватьНДС Тогда
					ДанныеСТЧ.Количество = ДанныеСТЧ.Сумма / ДанныеСТЧ.Цена;
				Иначе
					ДанныеСТЧ.Сумма = ДанныеСТЧ.Количество * ДанныеСТЧ.Цена;
				КонецЕсли;
				
			КонецЕсли;			
		
		КонецЕсли;
		ДанныеСТЧ.Всего			= ДанныеСТЧ.Сумма + ДанныеСТЧ.СуммаНДС;		
			
	КонецЕсли;
	
		// Расчет суммы услуги (для случаев, когда в СТЧ нет колонок Цена и Количество)
	Если Изменение = "ТЧУслугиПрочее" Тогда
		Сумма 			= ДанныеСТЧ.Сумма;
		
		Если ПараметрыОбъекта.ДоговорКонтрагента.СуммаВключаетНалоги Тогда 
			ДанныеСТЧ.Сумма 		= Окр(Сумма * 100 / (100 + ЗначСтавкаНДС + ЗначСтавкаНСП), 2);
			ДанныеСТЧ.СуммаНДС		= Окр(Сумма * ЗначСтавкаНДС / (100 + ЗначСтавкаНДС + ЗначСтавкаНСП), 2);
			ДанныеСТЧ.Всего   		= ДанныеСТЧ.Сумма;
		Иначе
			ДанныеСТЧ.СуммаНДС		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНДС /100, 2);
			ДанныеСТЧ.Всего 		= ДанныеСТЧ.Сумма + ДанныеСТЧ.СуммаНСП + ДанныеСТЧ.СуммаНДС;
		КонецЕсли;	
				
		ДанныеСТЧ.СуммаНСП 			= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНСП /100, 2);
		ДанныеСТЧ.СуммаСФ			= ДанныеСТЧ.Сумма + ДанныеСТЧ.СуммаНДС;	
		
	КонецЕсли;
	
	// Расчет НДС по сумме
	Если Изменение = "Сумма" Тогда		
		Если ПараметрыОбъекта.ДоговорКонтрагента.СуммаВключаетНалоги Тогда 
			ДанныеСТЧ.СуммаНДС 		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНДС / (100 + ЗначСтавкаНСП), 2);
			ДанныеСТЧ.СуммаНСП 		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНСП / (100 + ЗначСтавкаНСП), 2);
		Иначе
			ДанныеСТЧ.СуммаНДС		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНДС /100, 2);
			ДанныеСТЧ.СуммаНСП		= Окр(ДанныеСТЧ.Сумма * ЗначСтавкаНСП /100, 2);
		КонецЕсли;
		Если НЕ ЭтоОС И ДанныеСТЧ.Количество <> 0 Тогда 
			ДанныеСТЧ.Цена			= Окр(?(НЕ ЗначениеЗаполнено(ДанныеСТЧ.Количество), ДанныеСТЧ.Сумма, ДанныеСТЧ.Сумма / ДанныеСТЧ.Количество), 2);		
		КонецЕсли;
		Если ПараметрыОбъекта.ДоговорКонтрагента.СуммаВключаетНалоги Тогда 
			ДанныеСТЧ.Всего 		= ДанныеСТЧ.Сумма;
		Иначе
			ДанныеСТЧ.Всего			= ДанныеСТЧ.Сумма + ДанныеСТЧ.СуммаНДС + ДанныеСТЧ.СуммаНСП;
		КонецЕсли;		
	КонецЕсли;	
		
	// Расчет от СуммаСФ
	Если Изменение = "СуммаСФ" Тогда
		
		Если ПараметрыОбъекта.ПлательщикНДС Тогда
			ДанныеСТЧ.СуммаНДС	= Окр(ДанныеСТЧ.СуммаСФ * ЗначСтавкаНДС / (100 + ЗначСтавкаНДС), 2);
			ДанныеСТЧ.СуммаНСП 	= Окр((ДанныеСТЧ.СуммаСФ - ДанныеСТЧ.СуммаНДС) * ЗначСтавкаНСП / 100, 2);
			ДанныеСТЧ.Всего 	= ДанныеСТЧ.СуммаСФ + ДанныеСТЧ.СуммаНСП;
			ДанныеСТЧ.Сумма 	= ДанныеСТЧ.СуммаСФ + ДанныеСТЧ.СуммаНСП - ДанныеСТЧ.СуммаНДС;
			Если НЕ ЭтоОС И ДанныеСТЧ.Количество <> 0 Тогда 
				ДанныеСТЧ.Цена 	= ДанныеСТЧ.Сумма / ДанныеСТЧ.Количество;	
			КонецЕсли;									
		КонецЕсли;
		
	КонецЕсли;	
	
	// Расчет при известной Цене
	Если Изменение = "КоличествоПриИзвестнойЦене" Тогда
		
		Если ПараметрыОбъекта.ПлательщикНДС Тогда		
			ДанныеСТЧ.СуммаНСП  	= Окр(ДанныеСТЧ.Цена * ДанныеСТЧ.Количество * ЗначСтавкаНСП / (100 - ЗначСтавкаНСП));
			СуммаСФПропорциональная	= Окр(ДанныеСТЧ.Цена * ДанныеСТЧ.Количество * 100 / (100 - ЗначСтавкаНСП));			
			ДанныеСТЧ.СуммаНДС		= Окр(СуммаСФПропорциональная * ЗначСтавкаНДС / (100 + ЗначСтавкаНДС), 2);
			ДанныеСТЧ.Всего 		= СуммаСФПропорциональная + ДанныеСТЧ.СуммаНСП;					
			ДанныеСТЧ.Сумма 		= СуммаСФПропорциональная + ДанныеСТЧ.СуммаНСП - ДанныеСТЧ.СуммаНДС;
		КонецЕсли;
		
	КонецЕсли;
	
	// Расчет при известной Цене
	Если Изменение = "КоличествоМестПриИзвестнойЦене" Тогда
		// Расчет количества
		Если Изменение = "КоличествоМест" И ПараметрыОбъекта.ИспользоватьДополнительныеЕдиницыИзмерения Тогда
			ДанныеСТЧ.Количество = Окр(ДанныеСТЧ.КоличествоМест * ДанныеСТЧ.Емкость, 2);
		КонецЕсли;
		Если ПараметрыОбъекта.ПлательщикНДС Тогда
			
			ДанныеСТЧ.СуммаНСП  	= Окр(ДанныеСТЧ.Цена * ДанныеСТЧ.Количество * ЗначСтавкаНСП / (100 - ЗначСтавкаНСП));
			СуммаСФПропорциональная	= Окр(ДанныеСТЧ.Цена * ДанныеСТЧ.Количество * 100 / (100 - ЗначСтавкаНСП));			
			ДанныеСТЧ.СуммаНДС		= Окр(СуммаСФПропорциональная * ЗначСтавкаНДС / (100 + ЗначСтавкаНДС), 2);
			ДанныеСТЧ.Всего 		= СуммаСФПропорциональная + ДанныеСТЧ.СуммаНСП;					
			ДанныеСТЧ.Сумма 		= СуммаСФПропорциональная + ДанныеСТЧ.СуммаНСП - ДанныеСТЧ.СуммаНДС;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если Изменение = "НДС" Тогда
		ДанныеСТЧ.СуммаСФ			= ДанныеСТЧ.Сумма + ДанныеСТЧ.СуммаНДС;	
	КонецЕсли;	
	
КонецПроцедуры

