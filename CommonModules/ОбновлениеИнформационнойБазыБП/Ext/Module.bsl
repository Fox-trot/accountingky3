////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы Бухгалтерия для предприятия, редакция 3.0.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Сведения о библиотеке (или конфигурации).

// См. описание в общем модуле ОбновлениеИнформационнойБазыБСП.
Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	
	Описание.Имя = "БухгалтерияДляКыргызстана";
	Описание.Версия = "3.1.7.1";
	Описание.ИдентификаторИнтернетПоддержки = ИнтернетПоддержкаПользователейПереопределяемыйБП.ИмяПрограммы();

	// Требуется библиотека стандартных подсистем.
	Описание.ТребуемыеПодсистемы.Добавить("СтандартныеПодсистемы");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления информационной базы.

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.РежимВыполнения     = "Монопольно";
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	// Обработчики этого события для подсистем БСП добавляются через подписку на служебное событие:
	// "СтандартныеПодсистемы.ОбновлениеВерсииИБ\ПриДобавленииОбработчиковОбновления".
	//
	// Процедуры обработки этого события всех подсистем БСП имеют то же имя, что и эта процедура,
	// но размещены в своих подсистемах.
	// Чтобы найти процедуры можно выполнить глобальный поиск по имени процедуры.
	// Чтобы найти модули в которых размещены процедуры, можно выполнить поиск по имени события.
	
	// Обработчики, выполняемые при заполнении пустой ИБ.
	//
	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ПервыйЗапуск";
	Обработчик.Комментарий = НСтр("ru = 'Выполнение обработчиков первого запуска.'");
	
	// Обработчики, выполняемые при каждом обновлении ИБ.
	//
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.УстановитьДатуТекущейВерсии";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.РежимВыполнения = "Оперативно";

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ПроверитьВалютуРегламентированногоУчета";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.УстановкаИУдалениеИсправлений";

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.0.0";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ИнициализироватьПользователей";
	Обработчик.Комментарий = НСтр("ru = 'Инициализация пользователей.'");
	
	// Обработчики, выполняемые при заполнении пустой ИБ и при переходе на новую версию.
	//
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.6.30";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьПредопределенныеВидыКонтактнойИнформацииКонтрагентов";
	Обработчик.ВыполнятьВГруппеОбязательных = Истина;
	Обработчик.Приоритет = 99;
	Обработчик.НачальноеЗаполнение = Истина;
	
	// Обработчики отложенного обновления.
	//

	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.1.5";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОчиститьСубконтоСчета1250";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("9e104bda-905f-492a-b106-922929d28fd2");
	Обработчик.Комментарий = НСтр("ru = 'Очищение субконто счета 1250.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.1.9";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьФормуОплатыСчетовФактурВыписанных";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d8ead052-e0ec-41f6-8c05-2ee3d35f28bc");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизита ""Форма оплаты"" в документах ""Счет фактура выписанный"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.1.13";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ДозаполнитьСчетаФактурыВыписанные";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ace18f52-cb0c-465b-8c89-5cba77ce35d6");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизитов ""Ставка НДС"" и ""Ставка НСП"" в табличных частях документов.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.2.1";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьПредопределенныеВидыКонтактнойИнформацииПользователей";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ed3781a3-bd5b-454e-bf74-52024aec9b7a");
	Обработчик.Комментарий = НСтр("ru = 'Обновить предопределенные виды контактной информации пользователей.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.2.4";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьОтветственныеЛица";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ed3781a3-bd5b-454e-bf74-51024aec9b7a");
	Обработчик.Комментарий = НСтр("ru = 'Обновление ответственных лиц.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.2.6";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнениеВидаОтчетаВОтчетеПоНДС";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1fc1dbd7-4cb5-4108-b61e-599d3ed81356");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизита ""Вид отчета"" в документах ""Отчет по НДС"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.2.9";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнениеРеквизитовДокумента_СФВыписанный";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("035a13aa-2520-4f97-8673-a2eb55f09517");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизитов ""Ставка НДС"" и ""Валюта"" табличной части ""Документы"" в документах ""СФ выписанный"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.2.10";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ПереносНастроекВЗначенияПоУмолчанию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("7e553104-f469-4d89-8cac-b6bd16f4ccd9");
	Обработчик.Комментарий = НСтр("ru = 'Перенос настроек в значения по умолчанию.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.3.4";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ПерепроведениеДокументовПоступления";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d69ad7fc-07fb-4342-ad9a-44ba7ced26fb");
	Обработчик.Комментарий = НСтр("ru = 'Перепроведение документов ""Поступление товаров и услуг"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.3.5";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнениеСтавокВСчетФактуреВыписанной";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e990d5ac-e50f-4b8f-820a-495357f37990");
	Обработчик.Комментарий = НСтр("ru = 'Перезаполнение ставок НДС и НСП в табличной части ""Товары"" документов ""СФ выписанная"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.4.7";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.УстановитьТочностьЦены";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("afdecb45-780a-4cce-be79-7957647e9866");
	Обработчик.Комментарий = НСтр("ru = 'Установка точности цены значением по умолчанию.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.4.8";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнениеВидаОтчетаВОтчетеПоНСП";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("6ee388c3-5549-4f36-a032-ac15cf0f8a1a");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизита ""Вид отчета"" в документах ""Отчет по НСП"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.4.16";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнениеСтавок";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2da10a15-dc36-4009-8a9b-f64dfddd7e64");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение нулевых ставок в кадровых документах.'");
		
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.3";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ДобавитьПатентыПоВладельцам";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("77625fc7-3c22-405b-bd99-957dfb154883");
	Обработчик.Комментарий = НСтр("ru = 'Создание патентов.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.8";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОтредактироватьЗадачиБухгалтера";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("66751c64-fcb0-4708-9cf8-987477bfe0b3");
	Обработчик.Комментарий = НСтр("ru = 'Исправление наименований задач бухгалтера.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.9";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновлениеНаборовДополнительныхРеквизитовИСведений";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2e1c6807-e16a-43fe-ae81-13d96a1e0adb");
	Обработчик.Комментарий = НСтр("ru = 'Исправление наименований задач бухгалтера.'");
			
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.14";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ДобавитьЭлементПроверкиУчета";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("77625fc7-3c22-405b-bd99-957dfb154885");
	Обработчик.Комментарий = НСтр("ru = 'Обновление проверок учета.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.16";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнениеНомераАктаВПоступлении";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("5351e416-9fed-460b-9f97-71176353c0db");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизита ""Номер акта"" в документе ""Поступление товаров и услуг"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.22";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.УдалитьПравилоПроверкиУчета";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a7a38a3f-2fba-4156-a0cc-815665fa7591");
	Обработчик.Комментарий = НСтр("ru = 'Удаление помеченного на удаление правила проверки учета.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.23";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьКурсИКратостьВЗаявлениях";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("b27ada25-2f32-46c6-8c4e-97c89162394c");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение курс и кратности в табличной части ""Товары"" документов ""Заявление о ввозе товаров"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.24";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьНастройкиНалоговойДекларации";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d3f4f141-18d3-46cc-b745-b20a2cad8f10");
	Обработчик.Комментарий = НСтр("ru = 'Обновление настроек налоговой декларации.'");	
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Версия = "3.1.6.32";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьПолеРазногласиеВДокументахАктСверкиВзаиморасчетов";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2ebe8d9e-df85-4228-8d8f-b27715d4335d");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение поля Разногласие в документах Акт сверки взаиморасчетов.'");
	
	// Обработчики оперативного обновления.
	//
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.6.21";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьПредопределенныеВидыКонтактнойИнформации";
	Обработчик.ВыполнятьВГруппеОбязательных = Истина;
	Обработчик.Комментарий = НСтр("ru = 'Обновление видов контактной информации.'");

	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Версия = "3.1.6.33";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьДанныеРегистраНакопленияПодоходныйНалог";
	Обработчик.Комментарий = НСтр("ru = 'Обновление данных регистра накопления Подоходный налог.'");
	Обработчик.НачальноеЗаполнение = Ложь;

	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Версия = "3.1.6.34";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьДанныеРегистраНакопленияКорректировкиНУ";
	Обработчик.Комментарий = НСтр("ru = 'Обновление данных регистра накопления Корректировки НУ.'");
	Обработчик.НачальноеЗаполнение = Ложь;

КонецПроцедуры

// Вызывается перед процедурами-обработчиками обновления данных ИБ.
//
Процедура ПередОбновлениемИнформационнойБазы() Экспорт
	
КонецПроцедуры

// Вызывается после завершения обновления данных ИБ.
// 
// Параметры:
//   ПредыдущаяВерсия       - Строка - версия до обновления. "0.0.0.0" для "пустой" ИБ.
//   ТекущаяВерсия          - Строка - версия после обновления.
//   ВыполненныеОбработчики - ДеревоЗначений - список выполненных процедур-обработчиков обновления,
//                                             сгруппированных по номеру версии.
//   ВыводитьОписаниеОбновлений - Булево - если установить Истина, то будет выведена форма
//                                с описанием обновлений. По умолчанию, Истина.
//                                Возвращаемое значение.
//   МонопольныйРежим           - Булево - Истина, если обновление выполнялось в монопольном режиме.
//
Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
		Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
	
КонецПроцедуры

// Вызывается при подготовке табличного документа с описанием изменений в программе.
//
// Параметры:
//   Макет - ТабличныйДокумент - описание обновления всех библиотек и конфигурации.
//           Макет можно дополнить или заменить.
//           См. также общий макет ОписаниеИзмененийСистемы.
//
Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
	
КонецПроцедуры

// Позволяет переопределить режим обновления данных информационной базы.
// Для использования в редких (нештатных) случаях перехода, не предусмотренных в
// стандартной процедуре определения режима обновления.
//
// Параметры:
//   РежимОбновленияДанных - Строка - в обработчике можно присвоить одно из значений:
//              "НачальноеЗаполнение"     - если это первый запуск пустой базы (области данных);
//              "ОбновлениеВерсии"        - если выполняется первый запуск после обновление конфигурации базы данных;
//              "ПереходСДругойПрограммы" - если выполняется первый запуск после обновление конфигурации базы данных, 
//                                          в которой изменилось имя основной конфигурации.
//
//   СтандартнаяОбработка  - Булево - если присвоить Ложь, то стандартная процедура
//                                    определения режима обновления не выполняется, 
//                                    а используется значение РежимОбновленияДанных.
//
Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Добавляет в список процедуры-обработчики перехода с другой программы (с другим именем конфигурации).
// Например, для перехода между разными, но родственными конфигурациями: базовая -> проф -> корп.
// Вызывается перед началом обновления данных ИБ.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - с колонками:
//    * ПредыдущееИмяКонфигурации - Строка - имя конфигурации, с которой выполняется переход;
//                                           или "*", если нужно выполнять при переходе с любой конфигурации.
//    * Процедура                 - Строка - полное имя процедуры-обработчика перехода с программы ПредыдущееИмяКонфигурации. 
//                                  Например, "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику"
//                                  Обязательно должна быть экспортной.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.ПредыдущееИмяКонфигурации  = "УправлениеТорговлей";
//  Обработчик.Процедура                  = "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику";
//
Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
		
КонецПроцедуры

// Вызывается после выполнения всех процедур-обработчиков перехода с другой программы (с другим именем конфигурации),
// и до начала выполнения обновления данных ИБ.
//
// Параметры:
//  ПредыдущееИмяКонфигурации    - Строка - имя конфигурации до перехода.
//  ПредыдущаяВерсияКонфигурации - Строка - имя предыдущей конфигурации (до перехода).
//  Параметры                    - Структура - 
//    * ВыполнитьОбновлениеСВерсии   - Булево - по умолчанию Истина. Если установить Ложь, 
//        то будут выполнена только обязательные обработчики обновления (с версией "*").
//    * ВерсияКонфигурации           - Строка - номер версии после перехода. 
//        По умолчанию, равен значению версии конфигурации в свойствах метаданных.
//        Для того чтобы выполнить, например, все обработчики обновления с версии ПредыдущаяВерсияКонфигурации, 
//        следует установить значение параметра в ПредыдущаяВерсияКонфигурации.
//        Для того чтобы выполнить вообще все обработчики обновления, установить значение "0.0.0.1".
//    * ОчиститьСведенияОПредыдущейКонфигурации - Булево - по умолчанию Истина. 
//        Для случаев когда предыдущая конфигурация совпадает по имени с подсистемой текущей конфигурации, следует
//        указать Ложь.
//
Процедура ПриЗавершенииПереходаСДругойПрограммы(Знач ПредыдущееИмяКонфигурации, 
	Знач ПредыдущаяВерсияКонфигурации, Параметры) Экспорт
	
	Если ПредыдущееИмяКонфигурации = "ПредыдущееИмяКонфигурацииБазовая" Тогда
		Параметры.ВерсияКонфигурации = ПредыдущаяВерсияКонфигурации;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение пустой ИБ

// Процедура заполняет пустую ИБ.
//
Процедура ПервыйЗапуск() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	// В новой базе устанавливается режим интерфейса "Такси"
	Константы.ИнтерфейсТакси.Установить(Истина);
	Константы.ИнтерфейсВерсии82.Установить(Ложь);
	Константы.НеВестиУчетПоОрганизациям.Установить(Истина);

	////////////////////////////////////////////////////////////////////////////////
	// Параметры учета и план счетов
	ЗаполнитьПараметрыУчета();
	
	////////////////////////////////////////////////////////////////////////////////
	// Подсистема Банк/Касса
	ЗаполнитьДанныеПоПодсистемеБанкКасса();
	
	////////////////////////////////////////////////////////////////////////////////	
	// Подсистема Основные средства
	ЗаполнитьДанныеПоПодсистемеОсновныеСредства();
	
	////////////////////////////////////////////////////////////////////////////////
	// Подсистема Заработная плата
	ЗаполнитьДанныеПоЗаработнойПлате();

	// Запасы
	ЗаполнитьДанныеПоЗапасам();
	
	// Налоги
	ЗаполнитьДанныеНалогообложения();
	
	// Регламентированная отчетность.
	Справочники.НастройкиРегламентированныхОтчетов.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры // ПервыйЗапуск()

////////////////////////////////////////////////////////////////////////////////
// Обновление ИБ

Процедура УстановитьДатуТекущейВерсии() Экспорт
	// В режиме сервиса не требуется напоминать об обновлении
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Константы.ДатаТекущейВерсии.Установить(Дата(2020,01,24));
КонецПроцедуры

Процедура УстановкаИУдалениеИсправлений(Параметры = Неопределено) Экспорт
	// В режиме сервиса не требуется удалять патчи
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Удаляемые = Новый Массив;

	КлассификаторXML = ПолучитьОбщийМакет("ИдентификаторыИсправлений").ПолучитьТекст();
	КлассификаторТаблица  = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Удаляемые.Добавить(СтрокаТаблицыЗначений.id);
	КонецЦикла;	
	
	ОбновлениеКонфигурации.УстановкаИУдалениеИсправлений(Новый Структура("Удалить", Удаляемые), Истина);
КонецПроцедуры

Процедура ПроверитьВалютуРегламентированногоУчета(Параметры = Неопределено) Экспорт
	
	Если Константы.ВалютаРегламентированногоУчета.Получить() = Справочники.Валюты.ПустаяСсылка() Тогда
		
		ВалютаСом = Справочники.Валюты.НайтиПоКоду("417");
		
		Если ВалютаСом = Неопределено Тогда
			
			ВалютаСом = Справочники.Валюты.НайтиПоНаименованию("KGS");	
			
		КонецЕсли;	
		
		Константы.ВалютаРегламентированногоУчета.Установить(ВалютаСом);	
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет значения реквизитов предопределенных видов контактной информации
//
Процедура ОбновитьПредопределенныеВидыКонтактнойИнформации() Экспорт
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	
	// Организации
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ЮрАдресОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ВидРедактирования = "Диалог";
	ПараметрыВида.Порядок = 1;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ФактАдресОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ВидРедактирования = "Диалог";
	ПараметрыВида.Порядок = 2;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 3;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Факс");
	ПараметрыВида.Вид = "ФаксОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 4;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("АдресЭлектроннойПочты");
	ПараметрыВида.Вид = "EmailОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 5;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ПочтовыйАдресОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ВидРедактирования = "Диалог";
	ПараметрыВида.Порядок = 6;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Другое");
	ПараметрыВида.Вид = "ДругаяИнформацияОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 7;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
		
	// Контрагенты
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ЮрАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 1;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ФактАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 2;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 3;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Факс");
	ПараметрыВида.Вид = "ФаксКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 4;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("АдресЭлектроннойПочты");
	ПараметрыВида.Вид = "EmailКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 5;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ПочтовыйАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ВидРедактирования = "Диалог";
	ПараметрыВида.Порядок = 6;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	// Физические Лица
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "АдресМестаПроживанияФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ВидРедактирования = "Диалог";
	ПараметрыВида.Порядок = 1;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "АдресПоПропискеФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ВидРедактирования = "Диалог";
	ПараметрыВида.Порядок = 2;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонДомашнийФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 3;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонРабочийФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 4;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("АдресЭлектроннойПочты");
	ПараметрыВида.Вид = "EmailФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 5;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
КонецПроцедуры // ВключитьМножественныйВводКонтактнойИнформации()

#КонецОбласти

#Область ОбработчикиПервогоЗапуска

// Заполняет данные параметров учета
//
Процедура ЗаполнитьПараметрыУчета()

	// Добавление организации.
	Справочники.Организации.ЗаполнитьПоУмолчанию();
	
	// Заполнение плана счетов.
	ПланыСчетов.Хозрасчетный.ЗаполнитьПоУмолчанию();	
	
	// Заполнение справочника ГНС
	Справочники.ГНС.ЗаполнитьПоУмолчанию();
		
	// Заполнение справочника Классификатор единиц измерения
	Справочники.КлассификаторЕдиницИзмерения.ЗаполнитьПоУмолчанию();
	
	// Заполнение счетов учета номенклатуры
	РегистрыСведений.СчетаУчетаНоменклатуры.ЗаполнитьПоУмолчанию();
	РегистрыСведений.СчетаРасчетовСКонтрагентами.ЗаполнитьПоУмолчанию();
		
	Константы.ФункциональнаяОпцияУчетПоНесколькимОрганизациям.Установить(Ложь);
	Константы.МакетОформленияОтчетов.Установить("Арктика");
	Константы.КонтролироватьОстаткиПриПроведении.Установить(Истина);
	Константы.ФункциональнаяОпцияДопРасходыНаОдноПоступление.Установить(Истина);
	Константы.ИспользоватьСтатьиДвиженияДенежныхСредств.Установить(Истина);
	
	// Корректировка себестоимости
	Константы.БазаРаспределенияКорректировкиСебестоимости.Установить(Перечисления.БазыРаспределенияКосвенныхРасходов.ПлановаяСебестоимость);
	
	// Прочее.
	Справочники.СтраныМира.ЗаполнитьПоУмолчанию();
	Константы.РазмерОтступаВОтчетах.Установить(20);

	// Заполнение справочника Задачи бухгалтера
	Справочники.ЗадачиБухгалтера.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры // ЗаполнитьПараметрыУчета()  

// Заполняет данные по подсистеме Банк/Касса
//
Процедура ЗаполнитьДанныеПоПодсистемеБанкКасса()
	// Заполнение справочника "Валюты" 
	Справочники.Валюты.ЗаполнитьПоУмолчанию();
	
	// Добавление национальной валюты
	НациональнаяВалютаСсылка = Справочники.Валюты.НайтиПоКоду("417");	
	Константы.ВалютаРегламентированногоУчета.Установить(НациональнаяВалютаСсылка);
			
	// РС Курсы валют на 01.01.2015
	РегистрыСведений.КурсыВалют.ЗаполнитьПоУмолчанию();
	
	// Добавление касс 
	Справочники.Кассы.ЗаполнитьПоУмолчанию();
	
	// Заполнение справочника "Статьи ДДС"
	Справочники.СтатьиДвиженияДенежныхСредств.ЗаполнитьПоУмолчанию();
	Константы.СтатьяДДСКурсоваяРазница.Установить(Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Нереализованные прибыли и убытки от курсовых разниц", Истина));
	
	// Заполнение справочника "Виды статей"
	Справочники.ВидыЗатрат.ЗаполнитьПоУмолчанию();
	
	// Заполнение справочника "ТипыЦенНоменклатуры"
	Справочники.ТипыЦенНоменклатуры.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

// Заполняет данные по подсистеме ЗаработнойПлаты
//
Процедура ЗаполнитьДанныеПоЗаработнойПлате()
	Справочники.ГрафикиРаботы.ЗаполнитьПоУмолчанию();
	Справочники.МетодыРасчетаОтпуска.ЗаполнитьПоУмолчанию();
	Справочники.Статусы.ЗаполнитьПоУмолчанию();
	Справочники.СпособыОтраженияЗаработнойПлаты.ЗаполнитьПоУмолчанию();
	
	Справочники.СтрокиОтчетаПН.ЗаполнитьПоУмолчанию();
	
	Справочники.СтатьиТрудовогоКодекса.ЗаполнитьПоУмолчанию();
	
	Справочники.СостояниеВБраке.ЗаполнитьПоУмолчанию();
	Справочники.СтепениЗнанияЯзыка.ЗаполнитьПоУмолчанию();
	Справочники.УченыеЗвания.ЗаполнитьПоУмолчанию();
	Справочники.УченыеСтепени.ЗаполнитьПоУмолчанию();
	Справочники.Города.ЗаполнитьПоУмолчанию();
	
	РегистрыСведений.НормыБольничныхЛистов.ЗаполнитьПоУмолчанию();
	РегистрыСведений.ТарифыКомандировочных.ЗаполнитьПоУмолчанию();
	РегистрыСведений.УчетнаяПолитикаПоПерсоналу.ЗаполнитьПоУмолчанию();
КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

// Заполняет данные по Запасам
//
Процедура ЗаполнитьДанныеПоЗапасам()
	СправочникОбъект = Справочники.Номенклатура.Услуги.ПолучитьОбъект();
	СправочникОбъект.Услуга = Истина;
	БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникОбъект);	
	
	Константы.ТочностьЦены.Установить(2);
КонецПроцедуры 

// Заполняет данные по Налогообложению
//
Процедура ЗаполнитьДанныеНалогообложения()
	РегистрыСведений.СтавкиНДС.ЗаполнитьПоУмолчанию();
	РегистрыСведений.СтавкиНСП.ЗаполнитьПоУмолчанию();
	Справочники.НастройкиНалоговойДекларации.ЗаполнитьПоУмолчанию();
КонецПроцедуры // ЗаполнитьДанныеНалогообложения()

// Заполняет данные по подсистеме Основные средства
//
Процедура ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

	// Заполнение справочника ГруппыНалоговогоУчетаОС
	Справочники.ГруппыНалоговогоУчетаОС.ЗаполнитьПоУмолчанию();
	
	// Заполнение справочника ГруппыИмущества
	Справочники.ГруппыИмущества.ЗаполнитьПоУмолчанию();
	
	// Заполнение справочника ВидыМатериаловСтен
	Справочники.ВидыМатериаловСтенНалогНаИмущество.ЗаполнитьПоУмолчанию(); 
	
	// Заполнение справочника ВидыТранспортаНалогНаИмущество
	Справочники.ВидыТранспортаНалогНаИмущество.ЗаполнитьПоУмолчанию();
	
	// Заполнение справочника СпособыОтраженияРасходовПоАмортизации
	Справочники.СпособыОтраженияРасходовПоАмортизации.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

#КонецОбласти

#Область ОбработчикиСозданиеПользователей

// Процедура инициализации пользователей конфигурации. 
//
Процедура ИнициализироватьПользователей() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	//// Добавление администратора (администратор системы - полные права).
	//Пользователи.СоздатьАдминистратора();
	//
	//// Добавление пользователя.
	//ДобавитьПользователя(НСтр("ru = 'Пользователь'"));
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбновления

#Область Обновление_3_1_1

Процедура ОчиститьСубконтоСчета1250(Параметры) Экспорт

	БылиИсключения = Ложь;
	Попытка
		СчетОбъект = ПланыСчетов.Хозрасчетный.ДенежныеСредстваВПути.ПолучитьОбъект();
		СчетОбъект.ВидыСубконто.Очистить();
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СчетОбъект);			
	Исключение
		БылиИсключения = Истина;

		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось очистить субконто на счете 1250 по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		
		ВызватьИсключение;
	КонецПопытки;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ЗаполнитьФормуОплатыСчетовФактурВыписанных(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыписанный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СчетФактураВыписанный КАК СчетФактураВыписанный";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если ДокументОбъект.УдалитьБезналичныйРасчет Тогда
			ДокументОбъект.ФормаОплаты = Перечисления.ФормыОплаты.Безналичная;	
		Иначе
			ДокументОбъект.ФормаОплаты = Перечисления.ФормыОплаты.Наличная;
		КонецЕсли;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить форму оплаты счетов фактур по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ДозаполнитьСчетаФактурыВыписанные(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыписанный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СчетФактураВыписанный КАК СчетФактураВыписанный";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.ДокументыОснования Цикл
			СтрокаТабличнойЧасти.СтавкаНДС = СтрокаТабличнойЧасти.ДокументОснование.СтавкаНДС;	
		КонецЦикла;
		
		Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.Товары Цикл
			СтрокаТабличнойЧасти.СтавкаНДС = СтрокаТабличнойЧасти.ДокументОснования.СтавкаНДС;
			СтрокаТабличнойЧасти.СтавкаНСП = СтрокаТабличнойЧасти.ДокументОснования.СтавкаНСП;
		КонецЦикла;
			
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить счета фактуры по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

#КонецОбласти

#Область Обновление_3_1_2

Процедура ОбновитьПредопределенныеВидыКонтактнойИнформацииПользователей(Параметры) Экспорт

	ПользователиСлужебный.ОбновитьПредопределенныеВидыКонтактнойИнформацииПользователей();
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

Процедура ОбновитьОтветственныеЛица(Параметры) Экспорт

	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтветственныеЛицаОрганизаций");
		Блокировка.Заблокировать();
		
		// Заменить значения в записях РС ОтветственныеЛицаОрганизаций и ОтветственныеЛицаОрганизацийДляПечати
		// Вместо применения перечисления использовать ссылку.
		
		// ОтветственныеЛицаОрганизаций
		ОтветственныеЛица = РегистрыСведений.ОтветственныеЛицаОрганизаций.СоздатьНаборЗаписей();
		ОтветственныеЛица.Прочитать();
		
		Для Каждого ЗаписьОтветственныеЛица Из ОтветственныеЛица Цикл 
			
			Если ЗаписьОтветственныеЛица.удалитьОтветственноеЛицо = Перечисления.удалитьОтветственныеЛицаОрганизаций.Руководитель Тогда 
				ОтветственноеЛицо = Справочники.ВидыОтветственныхЛиц.Руководитель;
			ИначеЕсли ЗаписьОтветственныеЛица.удалитьОтветственноеЛицо = Перечисления.удалитьОтветственныеЛицаОрганизаций.ГлавныйБухгалтер Тогда	
				ОтветственноеЛицо = Справочники.ВидыОтветственныхЛиц.ГлавныйБухгалтер;
			ИначеЕсли ЗаписьОтветственныеЛица.удалитьОтветственноеЛицо = Перечисления.удалитьОтветственныеЛицаОрганизаций.Кассир Тогда	
				ОтветственноеЛицо = Справочники.ВидыОтветственныхЛиц.Кассир;
			Иначе 
				Продолжить;
			КонецЕсли;	
				
			ЗаписьОтветственныеЛица.ОтветственноеЛицо = ОтветственноеЛицо;	
		КонецЦикла;
		
		ОтветственныеЛица.Записать();

		// ОтветственныеЛицаОрганизацийДляПечати
		ОтветственныеЛица = РегистрыСведений.ОтветственныеЛицаОрганизацийДляПечати.СоздатьНаборЗаписей();
		ОтветственныеЛица.Прочитать();
		
		Для Каждого ЗаписьОтветственныеЛица Из ОтветственныеЛица Цикл 
			
			Если ЗаписьОтветственныеЛица.удалитьОтветственноеЛицо = Перечисления.удалитьОтветственныеЛицаОрганизаций.Руководитель Тогда 
				ОтветственноеЛицо = Справочники.ВидыОтветственныхЛиц.Руководитель;
			ИначеЕсли ЗаписьОтветственныеЛица.удалитьОтветственноеЛицо = Перечисления.удалитьОтветственныеЛицаОрганизаций.ГлавныйБухгалтер Тогда	
				ОтветственноеЛицо = Справочники.ВидыОтветственныхЛиц.ГлавныйБухгалтер;
			ИначеЕсли ЗаписьОтветственныеЛица.удалитьОтветственноеЛицо = Перечисления.удалитьОтветственныеЛицаОрганизаций.Кассир Тогда	
				ОтветственноеЛицо = Справочники.ВидыОтветственныхЛиц.Кассир;
			Иначе 
				Продолжить;
			КонецЕсли;	
				
			ЗаписьОтветственныеЛица.ОтветственноеЛицо = ОтветственноеЛицо;	
		КонецЦикла;	
		
		ОтветственныеЛица.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обновить ответственные лица по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
	КонецПопытки;
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

Процедура ЗаполнениеВидаОтчетаВОтчетеПоНДС(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНДС.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ОтчетПоНДС КАК ОтчетПоНДС";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.ВидОтчета = Перечисления.ВидыОтчетаПоНалогам.Первоначальный;
			
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить вид отчета по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ЗаполнениеРеквизитовДокумента_СФВыписанный(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыписанный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СчетФактураВыписанный КАК СчетФактураВыписанный";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.ДокументыОснования Цикл	
			СтрокаТабличнойЧасти.СтавкаНДС 	= СтрокаТабличнойЧасти.ДокументОснование.СтавкаНДС;
			СтрокаТабличнойЧасти.Валюта 	= СтрокаТабличнойЧасти.ДокументОснование.ВалютаДокумента;		
		КонецЦикла;	
			
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить реквизиты по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ПереносНастроекВЗначенияПоУмолчанию(Параметры) Экспорт
	
	ОсновнаяОрганизация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОсновнаяОрганизация", "");
	Если НЕ ОсновнаяОрганизация = Справочники.Организации.ПустаяСсылка() Тогда 
		ОбщегоНазначенияБПСервер.УстановитьЗначениеПоУмолчанию("ОсновнаяОрганизация", ОсновнаяОрганизация);
	КонецЕсли;	
	
	ОсновнойСклад = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОсновнойСклад", "");
	Если НЕ ОсновнойСклад = Справочники.Организации.ПустаяСсылка() Тогда 
		ОбщегоНазначенияБПСервер.УстановитьЗначениеПоУмолчанию("ОсновнойСклад", ОсновнойСклад);
	КонецЕсли;
	
	ОсновнаяФормаПрайсЛиста = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОсновнаяФормаПрайсЛиста", "");
	Если НЕ ОсновнаяФормаПрайсЛиста = Справочники.Организации.ПустаяСсылка() Тогда 
		ОбщегоНазначенияБПСервер.УстановитьЗначениеПоУмолчанию("ОсновнаяФормаПрайсЛиста", ОсновнаяФормаПрайсЛиста);
	КонецЕсли;	
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Обновление_3_1_3

Процедура ПерепроведениеДокументовПоступления(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	НЕ ПоступлениеТоваровУслуг.ПометкаУдаления
		|	И ПоступлениеТоваровУслуг.Проведен";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;
	Пока Выборка.Следующий() Цикл	
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();		
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,, РежимЗаписиДокумента.Проведение);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось провести документы поступления товаров и услуг по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
		
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ЗаполнениеСтавокВСчетФактуреВыписанной(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыписанный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СчетФактураВыписанный КАК СчетФактураВыписанный
		|ГДЕ
		|	НЕ СчетФактураВыписанный.ПометкаУдаления
		|	И СчетФактураВыписанный.Проведен";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;
	Пока Выборка.Следующий() Цикл	
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.Товары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДокументОснования) Тогда
				СтрокаТабличнойЧасти.СтавкаНДС = СтрокаТабличнойЧасти.ДокументОснования.СтавкаНДС;
				
				Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
					Если СтрокаТабличнойЧасти.Номенклатура.Услуга Тогда
						СтрокаТабличнойЧасти.СтавкаНСП = СтрокаТабличнойЧасти.ДокументОснования.СтавкаНСПУслуги;
					Иначе
						СтрокаТабличнойЧасти.СтавкаНСП = СтрокаТабличнойЧасти.ДокументОснования.СтавкаНСП;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать документы СФ выписанные по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;	
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

#КонецОбласти

#Область Обновление_3_1_4

Процедура УстановитьТочностьЦены(Параметры) Экспорт

	Константы.ТочностьЦены.Установить(2);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УстановкаЦенНоменклатуры.Ссылка
		|ИЗ
		|	Документ.УстановкаЦенНоменклатуры КАК УстановкаЦенНоменклатуры
		|ГДЕ
		|	НЕ УстановкаЦенНоменклатуры.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;

	Пока Выборка.Следующий() Цикл	
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();		
		ДокументОбъект.ТочностьЦены = 2;
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать документы по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
		
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ЗаполнениеВидаОтчетаВОтчетеПоНСП(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНСП.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ОтчетПоНСП КАК ОтчетПоНСП";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;

	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.ВидОтчета = Перечисления.ВидыОтчетаПоНалогам.Первоначальный;
			
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить вид отчета по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ЗаполнениеСтавок(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровоеПеремещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.КадровоеПеремещение КАК КадровоеПеремещение
		|ГДЕ
		|	КадровоеПеремещение.ЗанимаемыхСтавок = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриемНаРаботу.Ссылка
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|ГДЕ
		|	ПриемНаРаботу.ЗанимаемыхСтавок = 0";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;

	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ЗанимаемыхСтавок = 1;
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить ставку по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;

КонецПроцедуры

#КонецОбласти

#Область Обновление_3_1_6

Процедура ДобавитьПатентыПоВладельцам(Параметры) Экспорт

	БылиИсключения = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Патенты.Ссылка КАК Ссылка,
	               |	Патенты.Владелец КАК Владелец,
	               |	Патенты.Наименование КАК НомерПатента
	               |ИЗ
	               |	Справочник.Патенты КАК Патенты";
	ТаблицаПатентов = Запрос.Выполнить().Выгрузить();
	ТаблицаПатентов.Индексы.Добавить("НомерПатента");
	ТаблицаПатентов.Индексы.Добавить("Владелец");
	
	СправочникМенеджер = Справочники.Патенты;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АвансовыйОтчет.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.АвансовыйОтчет КАК АвансовыйОтчет
	               |ГДЕ
	               |	АвансовыйОтчет.Ссылка.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для Каждого СтрокаПатенты Из ДокументОбъект.ВладельцыПатентов Цикл
			
			Отбор = Новый Структура();
			Отбор.Вставить("Владелец", СтрокаПатенты.Контрагент);
			Отбор.Вставить("НомерПатента", СтрокаПатенты.удалитьНомерПатента);

			НайденныеСтроки = ТаблицаПатентов.НайтиСтроки(Отбор);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				СправочникПатентыОбъект = СправочникМенеджер.СоздатьЭлемент();
				СправочникПатентыОбъект.Наименование = СтрокаПатенты.удалитьНомерПатента;
				СправочникПатентыОбъект.ДатаС = СтрокаПатенты.удалитьДатаПатентаС;
				СправочникПатентыОбъект.ДатаПо = СтрокаПатенты.удалитьДатаПатентаПо;
				СправочникПатентыОбъект.ГНС = СтрокаПатенты.удалитьГНС;
				СправочникПатентыОбъект.Паспорт = СтрокаПатенты.удалитьПаспорт;
				СправочникПатентыОбъект.Владелец = СтрокаПатенты.Контрагент;
				
				Попытка
					Патент = СправочникМенеджер.ПолучитьСсылку();
					СправочникПатентыОбъект.УстановитьСсылкуНового(Патент);
					БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникПатентыОбъект);
				Исключение
					БылиИсключения = Истина;
					
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось создать патент по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
					
					ВызватьИсключение;
				КонецПопытки;

			Иначе
				Патент = НайденныеСтроки[0].Ссылка;
			КонецЕсли;
			                  
			СтрокаПатенты.Патент = Патент;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не записать Авансовый отчет по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;

КонецПроцедуры

Процедура ОтредактироватьЗадачиБухгалтера(Параметры) Экспорт
	
	СправочникСсылка = Справочники.ЗадачиБухгалтера.НайтиПоНаименованию("Предварительный расчет по НП за 2 квартал.");
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		СправочникОбъект.Наименование = "Предварительный расчет по НП за 1 квартал.";	
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать элемент справочника по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
	СправочникСсылка = Справочники.ЗадачиБухгалтера.НайтиПоНаименованию("Предварительный расчет по НП за 3 квартал.");
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		СправочникОбъект.Наименование = "Предварительный расчет по НП за 2 квартал.";	
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать элемент справочника по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;	
	КонецЕсли;
	
	СправочникСсылка = Справочники.ЗадачиБухгалтера.НайтиПоНаименованию("Предварительный расчет по НП за 4 квартал.");
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		СправочникОбъект.Наименование = "Предварительный расчет по НП за 3 квартал.";	
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать элемент справочника по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;	
	КонецЕсли;
	
	СправочникСсылка = Справочники.ЗадачиБухгалтера.НайтиПоНаименованию("Уплата по НП за 2 квартал.");
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		СправочникОбъект.Наименование = "Уплата по НП за 1 квартал.";	
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать элемент справочника по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;	
	КонецЕсли;
	
	СправочникСсылка = Справочники.ЗадачиБухгалтера.НайтиПоНаименованию("Уплата по НП за 3 квартал.");
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		СправочникОбъект.Наименование = "Уплата по НП за 2 квартал.";	
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать элемент справочника по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
	СправочникСсылка = Справочники.ЗадачиБухгалтера.НайтиПоНаименованию("Уплата по НП за 4 квартал.");
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		СправочникОбъект.Наименование = "Уплата по НП за 3 квартал.";	
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать элемент справочника по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;	
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Истина;

КонецПроцедуры

Процедура ОбновлениеНаборовДополнительныхРеквизитовИСведений(Параметры) Экспорт

	// Очищаем группу номенклатуры.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборыДополнительныхРеквизитовИСведений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведений.Наименование = ""Номенклатура""
		|	И НЕ НаборыДополнительныхРеквизитовИСведений.Родитель = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;

	Пока Выборка.Следующий() Цикл
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.Родитель = Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка();
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);			
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обновить наборы дополнительных реквизитов и сведений для Номенклатуры по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	// Удаляем группы номенклатуры.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборыДополнительныхРеквизитовИСведений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведений.Наименование = ""Номенклатура""
		|	И НаборыДополнительныхРеквизитовИСведений.ЭтоГруппа";
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Попытка
			СправочникОбъект.ОбменДанными.Загрузка = Истина;
			СправочникОбъект.Удалить();
		Исключение
			БылиИсключения = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось установить пометку на уделание группы набора дополнительных реквизитов и сведений для Номенклатуры по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;

КонецПроцедуры

Процедура ДобавитьЭлементПроверкиУчета(Параметры) Экспорт

	КонтрольВеденияУчетаСлужебный.ОбновитьВспомогательныеДанныеПоИзменениямКонфигурации();
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

Процедура ЗаполнениеНомераАктаВПоступлении(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	ПоступлениеТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ЗакупочныйАкт)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;

	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.НомерАкта = ДокументОбъект.НомерБланкаСФ;
			
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить номер акта по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура УдалитьПравилоПроверкиУчета(Параметры) Экспорт

	ЭлементУдален = Истина;
	
	СправочникСсылка = Справочники.ПравилаПроверкиУчета.НайтиПоНаименованию("Проверка счетов в настройках налоговой декларации.", Истина);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
	
		НаборЗаписей = РегистрыСведений.РезультатыПроверкиУчета.СоздатьНаборЗаписей();     
		НаборЗаписей.Отбор.ПравилоПроверки.Установить(СправочникСсылка);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.СостоянияПроверокВеденияУчета.СоздатьНаборЗаписей();     
		НаборЗаписей.Отбор.Проверка.Установить(СправочникСсылка);
		НаборЗаписей.Записать();
		
		СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		
		Попытка
			СправочникОбъект.Удалить();			
		Исключение
			ЭлементУдален = Ложь;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось удалить правило проверки учета по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = ЭлементУдален;
	
КонецПроцедуры

Процедура ЗаполнитьКурсИКратостьВЗаявлениях(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявлениеОВвозеТоваров.Ссылка КАК Ссылка,
		|	ЗаявлениеОВвозеТоваров.Дата КАК Дата,
		|	ЗаявлениеОВвозеТоваров.ВалютаДокумента КАК Валюта
		|ИЗ
		|	Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БылиИсключения = Ложь;

	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.Товары Цикл
			
			Период = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДокументПоступления),
						СтрокаТабличнойЧасти.ДокументПоступления.Дата, Выборка.Дата);
			
			ДанныеКурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Выборка.Валюта, Период);
			
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеКурсКратность);
		КонецЦикла;	
			
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			БылиИсключения = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать документ по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ БылиИсключения;
	
КонецПроцедуры

Процедура ОбновитьНастройкиНалоговойДекларации(Параметры) Экспорт

	ЕстьОшибки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиНалоговойДекларации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиНалоговойДекларации КАК НастройкиНалоговойДекларации";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();	
		
		СтрокаТабличнойЧасти = СправочникОбъект.СтрокиОтчета.Найти("298а", "КодСтроки");
			
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			СтрокаТабличнойЧасти.НаименованиеСтроки = "13а. ПРЕВЫШЕНИЕ ОТРИЦАТЕЛЬНЫХ КР НАД ПОЛОЖИТЕЛЬНЫМИ";	
		КонецЕсли;	
		
		СтрокаТабличнойЧасти = СправочникОбъект.СтрокиОтчета.Найти("299", "КодСтроки");
			
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			СтрокаТабличнойЧасти.НаименованиеСтроки = "ВСЕГО: (= 250+251+252+253+254+255+256+257+258+259+260+261+298+298а)";	
		КонецЕсли;
		
		Попытка
			СправочникОбъект.Записать();			
		Исключение
			ЕстьОшибки = Истина;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать настройку налоговой декларации по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ЕстьОшибки;
	
КонецПроцедуры

Процедура ЗаполнитьПолеРазногласиеВДокументахАктСверкиВзаиморасчетов(Параметры) Экспорт

	ЕстьОшибки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АктСверкиВзаиморасчетовПоДаннымКонтрагента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АктСверкиВзаиморасчетов.ПоДаннымКонтрагента КАК АктСверкиВзаиморасчетовПоДаннымКонтрагента
		|ГДЕ
		|	ВЫРАЗИТЬ(АктСверкиВзаиморасчетовПоДаннымКонтрагента.Ссылка.Разногласие КАК СТРОКА(150)) ПОДОБНО """"";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();	
		ДокументОбъект.Разногласие = "Есть разногласия";
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);			
		Исключение
			ЕстьОшибки = Истина;

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось записать документ по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ВызватьИсключение;
		КонецПопытки;

		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ЕстьОшибки;
	
КонецПроцедуры

// Вызывается при переходе на версию конфигурации 3.1.6.30.
//
Процедура ОбновитьПредопределенныеВидыКонтактнойИнформацииКонтрагентов() Экспорт
	
	ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
	ПараметрыВида.Вид = "ЮрАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ЗапретитьРедактированиеПользователем = Истина;
	ПараметрыВида.Порядок = 1;
	ПараметрыВида.ВидРедактирования = "ПолеВводаИДиалог";
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
	ПараметрыВида.Вид = "ФактАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.ЗапретитьРедактированиеПользователем = Истина;
	ПараметрыВида.Порядок = 2;
	ПараметрыВида.ВидРедактирования = "ПолеВводаИДиалог";
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Телефон);
	ПараметрыВида.Вид = "ТелефонКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.ЗапретитьРедактированиеПользователем = Истина;
	ПараметрыВида.Порядок = 3;
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
КонецПроцедуры

// Вызывается при переходе на версию конфигурации 3.1.6.33.
//
Процедура ОбновитьДанныеРегистраНакопленияПодоходныйНалог() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрытиеМесяцаЗП.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП КАК ЗакрытиеМесяцаЗП
		|ГДЕ
		|	ЗакрытиеМесяцаЗП.Проведен";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НаборЗаписей = РегистрыНакопления.ПодоходныйНалог.СоздатьНаборЗаписей();
		ОтборРегистратор = НаборЗаписей.Отбор.Регистратор;
		ОтборРегистратор.Установить(ВыборкаДетальныеЗаписи.Ссылка);
		НаборЗаписей.Прочитать();
		
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
			ЗаписьНабора.Вычеты = ЗаписьНабора.удалитьПиИВычеты - ЗаписьНабора.ПФР - ЗаписьНабора.ГНПФР;
		КонецЦикла;
		
		Если НЕ НаборЗаписей.Количество() = 0 Тогда 
			НаборЗаписей.Записать();
		КонецЕсли;	
		
		НаборЗаписей = РегистрыНакопления.ДанныеОтчетаПоПН.СоздатьНаборЗаписей();
		ОтборРегистратор = НаборЗаписей.Отбор.Регистратор;
		ОтборРегистратор.Установить(ВыборкаДетальныеЗаписи.Ссылка);
		НаборЗаписей.Прочитать();
		
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
			ЗаписьНабора.Вычеты = ЗаписьНабора.удалитьПиИВычеты - ЗаписьНабора.ПФР - ЗаписьНабора.ГНПФР;
		КонецЦикла;
		
		Если НЕ НаборЗаписей.Количество() = 0 Тогда 
			НаборЗаписей.Записать();
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

// Вызывается при переходе на версию конфигурации 3.1.6.34.
//
Процедура ОбновитьДанныеРегистраНакопленияКорректировкиНУ() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КорректировкиНУ.Период КАК Период,
		|	КорректировкиНУ.Регистратор КАК Регистратор,
		|	КорректировкиНУ.НомерСтроки КАК НомерСтроки,
		|	КорректировкиНУ.Активность КАК Активность,
		|	КорректировкиНУ.Организация КАК Организация,
		|	КорректировкиНУ.Счет КАК Счет,
		|	КорректировкиНУ.ВидУчета КАК ВидУчета,
		|	КорректировкиНУ.АмортизацияОС КАК АмортизацияОС,
		|	КорректировкиНУ.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаЗаписиРегистра
		|ИЗ
		|	РегистрНакопления.КорректировкиНУ КАК КорректировкиНУ
		|ГДЕ
		|	(КорректировкиНУ.Регистратор ССЫЛКА Документ.СписаниеОС
		|			ИЛИ КорректировкиНУ.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
		|			ИЛИ КорректировкиНУ.Регистратор ССЫЛКА Документ.МодернизацияОС
		|			ИЛИ КорректировкиНУ.Регистратор ССЫЛКА Документ.ЗакрытиеМесяца)
		|	И НЕ КорректировкиНУ.АмортизацияОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаЗаписиРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	ВременнаяТаблицаЗаписиРегистра КАК ВременнаяТаблицаЗаписиРегистра";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаРегистратор = РезультатЗапроса.Выбрать();
	
	НаборЗаписей = РегистрыНакопления.КорректировкиНУ.СоздатьНаборЗаписей();
	ОтборРегистратор = НаборЗаписей.Отбор.Регистратор;
	Пока ВыборкаРегистратор.Следующий() Цикл				
		ОтборРегистратор.Установить(ВыборкаРегистратор.Регистратор);
		НаборЗаписей.Прочитать();
	
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
			ЗаписьНабора.АмортизацияОС = Истина;
		КонецЦикла;
		
		Если НЕ НаборЗаписей.Количество() = 0 Тогда 
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти
