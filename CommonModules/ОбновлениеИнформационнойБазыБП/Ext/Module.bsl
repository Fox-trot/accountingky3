////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы Бухгалтерия для предприятия, редакция 3.0.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Сведения о библиотеке (или конфигурации).

// См. описание в общем модуле ОбновлениеИнформационнойБазыБСП.
Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	
	Описание.Имя = "БухгалтерияДляКыргызстана";
	Описание.Версия = "3.0.3.20";

	// Требуется библиотека стандартных подсистем.
	Описание.ТребуемыеПодсистемы.Добавить("СтандартныеПодсистемы");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления информационной базы.

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.РежимВыполнения     = "Монопольно";
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	//// Обработчики этого события для подсистем БСП добавляются через подписку на служебное событие:
	//// "СтандартныеПодсистемы.ОбновлениеВерсииИБ\ПриДобавленииОбработчиковОбновления".
	////
	//// Процедуры обработки этого события всех подсистем БСП имеют то же имя, что и эта процедура,
	//// но размещены в своих подсистемах.
	//// Чтобы найти процедуры можно выполнить глобальный поиск по имени процедуры.
	//// Чтобы найти модули в которых размещены процедуры, можно выполнить поиск по имени события.
	
	//// Обработчики, выполняемые при каждом обновлении ИБ
	//Обработчик = Обработчики.Добавить();
	//Обработчик.Версия = "*";
	//Обработчик.ОбщиеДанные = Истина;
	//Обработчик.УправлениеОбработчиками = Истина;
	//Обработчик.РежимВыполнения = "Оперативно";
	//Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ВыполнятьВсегдаПриСменеВерсии";
		
	// Обработчики, выполняемые при заполнении пустой ИБ
	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ПервыйЗапуск";
	Обработчик.Комментарий = НСтр("ru = 'Выполнение обработчиков первого запуска.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.0.0";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ИнициализироватьПользователей";
	Обработчик.Комментарий = НСтр("ru = 'Инициализация пользователей.'");
		
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.0.0";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьПредопределенныеВидыКонтактнойИнформации";
	Обработчик.ВыполнятьВГруппеОбязательных = Истина;
	Обработчик.Комментарий = НСтр("ru = 'Обновление видов контактной информации.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.1.12";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьКадровыеПриказы";
	Обработчик.Комментарий = НСтр("ru = 'Обновление кадровых приказов.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.1.18";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьНачальныеНастройки";
	Обработчик.Комментарий = НСтр("ru = 'Обновление настроек пользователя- установка первоначальных.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.5.5";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьПредопределенныеОперацииРКОиППИ";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение новых предопределенных элементов.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.5.6";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьКодыСправочников";
	Обработчик.Комментарий = НСтр("ru = 'Обновление кодов справочников.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.5.6";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьКодыГНС";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение новых предопределенных элементов.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.8";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ОбновитьСчетаУчета";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение парных счетов учета.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.14";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ПерепровестиДокументыУстановкиЦен";
	Обработчик.Комментарий = НСтр("ru = 'Перепроведение документов ""Установка цен номенклатуры"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.16";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьСпособыОтраженияРасходовПоАмортизации";
	Обработчик.Комментарий = НСтр("ru = 'Заполнить способы отражения расходов по амортизации.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.17";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ДобавитьДопРеквизитыВСправочникКонтрагенты";
	Обработчик.Комментарий = НСтр("ru = 'Добавить доп. реквизиты и доп. свойства в справочник Контрагенты.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.28";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьПризнакСтраныКонтрагента";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение признака страны контрагента.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.29";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьВидыЗатрат";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение справочника ""ВидыЗатрат"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.9.30";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ЗаполнитьВидОперацииВПостепении";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение вида операции в документах поступление.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.3.9";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ИзменитьСубконтоСчетовПроизводства";
	Обработчик.Комментарий = НСтр("ru = 'Изменение субконто у счетов 1630 и 1640.'");

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.3.20";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБП.ДобавитьЭлементПроверкиУчета";
	Обработчик.Комментарий = НСтр("ru = 'Добавление элемента справочника'");
	
КонецПроцедуры

// Вызывается перед процедурами-обработчиками обновления данных ИБ.
//
Процедура ПередОбновлениемИнформационнойБазы() Экспорт
	
КонецПроцедуры

// Вызывается после завершения обновления данных ИБ.
// 
// Параметры:
//   ПредыдущаяВерсия       - Строка - версия до обновления. "0.0.0.0" для "пустой" ИБ.
//   ТекущаяВерсия          - Строка - версия после обновления.
//   ВыполненныеОбработчики - ДеревоЗначений - список выполненных процедур-обработчиков обновления,
//                                             сгруппированных по номеру версии.
//   ВыводитьОписаниеОбновлений - Булево - если установить Истина, то будет выведена форма
//                                с описанием обновлений. По умолчанию, Истина.
//                                Возвращаемое значение.
//   МонопольныйРежим           - Булево - Истина, если обновление выполнялось в монопольном режиме.
//
Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
		Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
	
КонецПроцедуры

// Вызывается при подготовке табличного документа с описанием изменений в программе.
//
// Параметры:
//   Макет - ТабличныйДокумент - описание обновления всех библиотек и конфигурации.
//           Макет можно дополнить или заменить.
//           См. также общий макет ОписаниеИзмененийСистемы.
//
Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
	
КонецПроцедуры

// Позволяет переопределить режим обновления данных информационной базы.
// Для использования в редких (нештатных) случаях перехода, не предусмотренных в
// стандартной процедуре определения режима обновления.
//
// Параметры:
//   РежимОбновленияДанных - Строка - в обработчике можно присвоить одно из значений:
//              "НачальноеЗаполнение"     - если это первый запуск пустой базы (области данных);
//              "ОбновлениеВерсии"        - если выполняется первый запуск после обновление конфигурации базы данных;
//              "ПереходСДругойПрограммы" - если выполняется первый запуск после обновление конфигурации базы данных, 
//                                          в которой изменилось имя основной конфигурации.
//
//   СтандартнаяОбработка  - Булево - если присвоить Ложь, то стандартная процедура
//                                    определения режима обновления не выполняется, 
//                                    а используется значение РежимОбновленияДанных.
//
Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Добавляет в список процедуры-обработчики перехода с другой программы (с другим именем конфигурации).
// Например, для перехода между разными, но родственными конфигурациями: базовая -> проф -> корп.
// Вызывается перед началом обновления данных ИБ.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - с колонками:
//    * ПредыдущееИмяКонфигурации - Строка - имя конфигурации, с которой выполняется переход;
//                                           или "*", если нужно выполнять при переходе с любой конфигурации.
//    * Процедура                 - Строка - полное имя процедуры-обработчика перехода с программы ПредыдущееИмяКонфигурации. 
//                                  Например, "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику"
//                                  Обязательно должна быть экспортной.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.ПредыдущееИмяКонфигурации  = "УправлениеТорговлей";
//  Обработчик.Процедура                  = "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику";
//
Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
		
КонецПроцедуры

// Вызывается после выполнения всех процедур-обработчиков перехода с другой программы (с другим именем конфигурации),
// и до начала выполнения обновления данных ИБ.
//
// Параметры:
//  ПредыдущееИмяКонфигурации    - Строка - имя конфигурации до перехода.
//  ПредыдущаяВерсияКонфигурации - Строка - имя предыдущей конфигурации (до перехода).
//  Параметры                    - Структура - 
//    * ВыполнитьОбновлениеСВерсии   - Булево - по умолчанию Истина. Если установить Ложь, 
//        то будут выполнена только обязательные обработчики обновления (с версией "*").
//    * ВерсияКонфигурации           - Строка - номер версии после перехода. 
//        По умолчанию, равен значению версии конфигурации в свойствах метаданных.
//        Для того чтобы выполнить, например, все обработчики обновления с версии ПредыдущаяВерсияКонфигурации, 
//        следует установить значение параметра в ПредыдущаяВерсияКонфигурации.
//        Для того чтобы выполнить вообще все обработчики обновления, установить значение "0.0.0.1".
//    * ОчиститьСведенияОПредыдущейКонфигурации - Булево - по умолчанию Истина. 
//        Для случаев когда предыдущая конфигурация совпадает по имени с подсистемой текущей конфигурации, следует указать Ложь.
//
Процедура ПриЗавершенииПереходаСДругойПрограммы(Знач ПредыдущееИмяКонфигурации, 
	Знач ПредыдущаяВерсияКонфигурации, Параметры) Экспорт
	
	Если ПредыдущееИмяКонфигурации = "ПредыдущееИмяКонфигурацииБазовая" Тогда
		Параметры.ВерсияКонфигурации = ПредыдущаяВерсияКонфигурации;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение пустой ИБ

// Процедура заполняет пустую ИБ.
//
Процедура ПервыйЗапуск() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	// В новой базе устанавливается режим интерфейса "Такси"
	Константы.ИнтерфейсТакси.Установить(Истина);
	Константы.ИнтерфейсВерсии82.Установить(Ложь);

	НачатьТранзакцию();
	
	////////////////////////////////////////////////////////////////////////////////
	// Параметры учета и план счетов
	ЗаполнитьПараметрыУчета();
	
	////////////////////////////////////////////////////////////////////////////////
	// Подсистема Банк/Касса
	ЗаполнитьДанныеПоПодсистемеБанкКасса();
	
	////////////////////////////////////////////////////////////////////////////////	
	// Подсистема Основные средства
	ЗаполнитьДанныеПоПодсистемеОсновныеСредства();
	
	////////////////////////////////////////////////////////////////////////////////
	// Подсистема Заработная плата
	ЗаполнитьДанныеПоЗаработнойПлате();

	////////////////////////////////////////////////////////////////////////////////
	// Подсистема Настройки
	ЗаполнитьДанныеПоНастрокам();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры // ПервыйЗапуск()

////////////////////////////////////////////////////////////////////////////////
// Обновление ИБ

// Демонстрирует процедуру-обработчик обновления данных ИБ, 
// которая выполняется каждый раз при смене версии конфигурации.
//
Процедура ВыполнятьВсегдаПриСменеВерсии(Параметры = Неопределено) Экспорт
	
	// Проверка необходимости обновления критичных общих данных
	ТребуетсяОбновитьКритичныеОбщиеДанные = Ложь;
	// Конец Проверка необходимости обновления критичных общих данных
	Если ТребуетсяОбновитьКритичныеОбщиеДанные Тогда
		Если Параметры <> Неопределено И НЕ Параметры.МонопольныйРежим Тогда
			Параметры.МонопольныйРежим = Истина;
			Возврат;
		КонецЕсли;
		// Код обновления критичных общих данных
	КонецЕсли;
КонецПроцедуры

// Обновляет значения реквизитов предопределенных видов контактной информации
//
Процедура ОбновитьПредопределенныеВидыКонтактнойИнформации() Экспорт
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	
	// Организации
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ЮрАдресОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 1;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ФактАдресОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 2;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 3;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Факс");
	ПараметрыВида.Вид = "ФаксОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 4;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("АдресЭлектроннойПочты");
	ПараметрыВида.Вид = "EmailОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 5;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ПочтовыйАдресОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 6;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Другое");
	ПараметрыВида.Вид = "ДругаяИнформацияОрганизации";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 7;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
		
	// Контрагенты
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ЮрАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 1;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ФактАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 2;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 3;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Факс");
	ПараметрыВида.Вид = "ФаксКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 4;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("АдресЭлектроннойПочты");
	ПараметрыВида.Вид = "EmailКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 5;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "ПочтовыйАдресКонтрагента";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 6;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	// Физические Лица
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "АдресМестаПроживанияФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 1;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Адрес");
	ПараметрыВида.Вид = "АдресПоПропискеФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.Порядок = 2;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);

	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонДомашнийФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 3;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
	ПараметрыВида = МодульУправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации("Телефон");
	ПараметрыВида.Вид = "ТелефонРабочийФизическиеЛица";
	ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
	ПараметрыВида.РазрешитьВводНесколькихЗначений = Истина;
	ПараметрыВида.Порядок = 4;
	МодульУправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
	
КонецПроцедуры // ВключитьМножественныйВводКонтактнойИнформации()

#КонецОбласти

#Область ОбработчикиПервогоЗапуска

// Заполняет данные параметров учета
//
Процедура ЗаполнитьПараметрыУчета()

	// Заполнение плана счетов
	ПланыСчетов.Хозрасчетный.ЗаполнитьПоУмолчанию();	
	
	// Заполнение справочника ГНС
	Справочники.ГНС.ЗаполнитьПоУмолчанию();
	
	//Заполнение справочника Списки счетов
	Справочники.СпискиСчетов.ЗаполнитьПоУмолчанию();
	
	// Заполнение счетов учета номенклатуры
	РегистрыСведений.СчетаУчетаНоменклатуры.ЗаполнитьПоУмолчанию();
	РегистрыСведений.СчетаРасчетовСКонтрагентами.ЗаполнитьПоУмолчанию();
		
	Константы.ФункциональнаяОпцияУчетПоНесколькимОрганизациям.Установить(Ложь);
	Константы.МакетОформленияОтчетов.Установить("Без оформления");
	
	БухгалтерскийУчетСервер.УстановитьНастройкуПользователя(ПредопределенноеЗначение("Справочник.Организации.ОсновнаяОрганизация"), "ОсновнаяОрганизация");		
	
КонецПроцедуры // ЗаполнитьПараметрыУчета()  

// Заполняет данные по подсиситеме Банк/Касса
//
Процедура ЗаполнитьДанныеПоПодсистемеБанкКасса()
	// 1. Заполнение справочника "Валюты" 
	Справочники.Валюты.ЗаполнитьПоУмолчанию();
	
	// 2. Добавление национальной валюты
	НациональнаяВалютаСсылка = Справочники.Валюты.НайтиПоКоду("417");	
	Константы.НациональнаяВалюта.Установить(НациональнаяВалютаСсылка);
	Константы.ВалютаРегламентированногоУчета.Установить(НациональнаяВалютаСсылка);
			
	// 3. РС Курсы валют на 01.01.2015
	РегистрыСведений.КурсыВалют.ЗаполнитьПоУмолчанию();
	
	// 4. Добавление касс 
	Справочники.Кассы.ЗаполнитьПоУмолчанию();
	
	// 5. Заполнение справоника "Банки"
	Справочники.Банки.ЗаполнитьПоУмолчанию();
	
	//// 6. Заполнение счетов у опреций РКО 
	//Справочники.ОперацииРКО.ЗаполнитьПоУмолчанию();
	//
	//// 7. Заполнение счетов у опреций ПКО
	//Справочники.ОперацииПКО.ЗаполнитьПоУмолчанию();
	//
	//// 8. Заполнение счетов у опреций ППВ
	//Справочники.ОперацииППВ.ЗаполнитьПоУмолчанию();
	//
	//// 9. Заполнение счетов у опреций ППИ
	//Справочники.ОперацииППИ.ЗаполнитьПоУмолчанию();
	
	// 10. Заполнение справочника "СтатьиДДС"
	Справочники.СтатьиДвиженияДенежныхСредств.ЗаполнитьПоУмолчанию();
	
	// 11. Заполнение справочника "ТипыЦенНоменклатуры"
	Справочники.ТипыЦенНоменклатуры.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

// Заполняет данные по подсиситеме ЗаработнойПлаты
//
Процедура ЗаполнитьДанныеПоЗаработнойПлате()
	// Заполнение справочника Статусы
	Справочники.Статусы.ЗаполнитьПоУмолчанию();
	
	Справочники.СтрокиОтчетаПН.ЗаполнитьПоУмолчанию();

	Справочники.СтрокиОтчетаПоСФКвартальному.ЗаполнитьПоУмолчанию();
	
	Справочники.СтатьиТрудовогоКодекса.ЗаполнитьПоУмолчанию();
	
	Справочники.СостояниеВБраке.ЗаполнитьПоУмолчанию();
	Справочники.СтепениЗнанияЯзыка.ЗаполнитьПоУмолчанию();
	Справочники.СтепениРодстваФизическихЛиц.ЗаполнитьПоУмолчанию();
	Справочники.УченыеЗвания.ЗаполнитьПоУмолчанию();
	Справочники.УченыеСтепени.ЗаполнитьПоУмолчанию();
	
	РегистрыСведений.НормыБольничныхЛистов.ЗаполнитьПоУмолчанию();
КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

//// Заполняет данные по подсиситеме Основные средства
////
Процедура ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

	//Заполнение справочника ГруппыНалоговогоУчетаОС
	Справочники.ГруппыНалоговогоУчетаОС.ЗаполнитьПоУмолчанию();
	
	//Заполнение справочника ГруппыИмущества
	Справочники.ГруппыИмущества.ЗаполнитьПоУмолчанию();
	
	//Заполнение справочника ВидыМатериаловСтен
	Справочники.ВидыМатериаловСтен.ЗаполнитьПоУмолчанию(); 
	
	//Заполнение справочника ВидыТранспортаПоНалогуНаИмущество
	Справочники.ВидыТранспортаПоНалогуНаИмущество.ЗаполнитьПоУмолчанию();
	
	//Создание документа УстановкаПараметровРасчетаНалогаНаИмущества
	СоздатьДокументУстановкаПараметровНИ();   

КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

// Заполняет данные по подсиситеме ЗаработнойПлаты
//
Процедура ЗаполнитьДанныеПоНастрокам()
	
	//Заполнение справочника Виды статей
	Справочники.ВидыЗатрат.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры // ЗаполнитьДанныеПоПодсистемеОсновныеСредства()

//Процедура создаёт документ УстановкаПараметровРасчетаНалогаНаИмущество 
// и заполняет данными из макетов документа:
// * ПараметрыРасчетаНИПоНедвижимости2013
// * ПараметрыРасчетаНИПоТранспорту
//
Процедура СоздатьДокументУстановкаПараметровНИ()
	
		ВыборкаДокументов = Документы.УстановкаПараметровРасчетаНалогаНаИмущество.Выбрать();
		Если ВыборкаДокументов.Следующий() Тогда
			НовыйДокумент = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
			НовыйДокумент.Недвижимость.Очистить();
			НовыйДокумент.Транспорт.Очистить();
		Иначе
			НовыйДокумент = Документы.УстановкаПараметровРасчетаНалогаНаИмущество.СоздатьДокумент();
			НовыйДокумент.Дата = Дата("20130101");
		КонецЕсли;     			
		
		// Заполнение табличных частей документа
		МакетНедвижимость = НовыйДокумент.ПолучитьМакет("ПараметрыРасчетаНИПоНедвижимости2013");
		Для Счетчик = 1 По МакетНедвижимость.ВысотаТаблицы Цикл
			Запись = НовыйДокумент.Недвижимость.Добавить();
			Запись.ВидМатериалаСтен = Справочники.ВидыМатериаловСтен.НайтиПоНаименованию(СокрЛП(МакетНедвижимость.Область(Счетчик, 1).Текст));
			Запись.ОтЛет 			= СокрЛП(МакетНедвижимость.Область(Счетчик, 2).Текст);
			Запись.ДоЛет 			= СокрЛП(МакетНедвижимость.Область(Счетчик, 3).Текст);
			Запись.Стоимость 		= СокрЛП(МакетНедвижимость.Область(Счетчик, 4).Текст);
		КонецЦикла;
		
		МакетТранспорт = НовыйДокумент.ПолучитьМакет("ПараметрыРасчетаНИПоТранспорту");
		Для Счетчик = 1 По МакетТранспорт.ВысотаТаблицы Цикл
			Запись = НовыйДокумент.Транспорт.Добавить();
			Запись.ВидТранспорта 	= Справочники.ВидыТранспортаПоНалогуНаИмущество.НайтиПоНаименованию(СокрЛП(МакетТранспорт.Область(Счетчик, 1).Текст));
			Запись.ОтЛет 			= СокрЛП(МакетТранспорт.Область(Счетчик, 2).Текст);
			Запись.ДоЛет 			= СокрЛП(МакетТранспорт.Область(Счетчик, 3).Текст);
			Запись.Ставка	 		= СокрЛП(МакетТранспорт.Область(Счетчик, 4).Текст);
		КонецЦикла;
		
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
		КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСозданиеПользователей

// Процедура инициализации пользователей конфигурации. 
//
Процедура ИнициализироватьПользователей() Экспорт
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	//// Добавление администратора (администратор системы - полные права).
	//Пользователи.СоздатьАдминистратора();
	//
	//// Добавление пользователя.
	//ДобавитьПользователя(НСтр("ru = 'Пользователь'"));
	
КонецПроцедуры

Процедура ДобавитьПользователя(ПолноеИмяПользователя, ИмяПользователяИБ = Неопределено)
	
	Если ИмяПользователяИБ = Неопределено Тогда
		ИмяПользователяИБ = ПолноеИмяПользователя;
	КонецЕсли;
	
	ЗаписатьПользователя = Ложь;
	
	Пользователь = Справочники.Пользователи.НайтиПоНаименованию(ПолноеИмяПользователя);
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		ЗаписатьПользователя = Истина;
		ПользовательОбъект = Справочники.Пользователи.СоздатьЭлемент();
		ПользовательОбъект.Наименование = ПолноеИмяПользователя;
	КонецЕсли;
	
	ЗаписатьПользователяИБ = Ложь;
	ВозможноЗаписатьПользователяИБ = Истина;
	ВозможноПривязатьПользователяИБ = Ложь;
	ОписаниеПользователяИБ = Новый Структура;
	
	Если ЗначениеЗаполнено(Пользователь.ИдентификаторПользователяИБ) Тогда
		УчетнаяЗапись = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			Пользователь.ИдентификаторПользователяИБ);
	КонецЕсли;
	
	Если УчетнаяЗапись = Неопределено Тогда
		УчетнаяЗапись = ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователяИБ);
		
		Если УчетнаяЗапись = Неопределено Тогда
			ЗаписатьПользователяИБ = Истина;
			ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
			
		ИначеЕсли Пользователи.ПользовательИБЗанят(УчетнаяЗапись) Тогда
			// Пользователь ИБ с указанным именем уже существует и занят,
			// возможно записать только пользователя справочника.
			ВозможноЗаписатьПользователяИБ = Ложь;
		Иначе
			// Пользователь ИБ с указанным именем уже существует и не занят,
			// возможно привязать пользователя ИБ к пользователю справочника.
			ЗаписатьПользователяИБ = Истина;
			ВозможноПривязатьПользователяИБ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ВозможноЗаписатьПользователяИБ Тогда
		
		Если ВозможноПривязатьПользователяИБ Тогда
			
			ОписаниеПользователяИБ.Вставить(
				"УникальныйИдентификатор", УчетнаяЗапись.УникальныйИдентификатор);
		КонецЕсли;
		
		Если УчетнаяЗапись = Неопределено Тогда
			ОписаниеПользователяИБ.Вставить("Имя",       ИмяПользователяИБ);
			ОписаниеПользователяИБ.Вставить("ПолноеИмя", ПолноеИмяПользователя);
			ОписаниеПользователяИБ.Вставить("АутентификацияСтандартная", Истина);
			ОписаниеПользователяИБ.Вставить("ПоказыватьВСпискеВыбора", Истина);
			
		ИначеЕсли УчетнаяЗапись.ПолноеИмя <> ПолноеИмяПользователя Тогда
			ЗаписатьПользователяИБ = Истина;
			УчетнаяЗапись.ПолноеИмя = ПолноеИмяПользователя;
		КонецЕсли;
		ОписаниеПользователяИБ.Вставить("Действие", "Записать");
	КонецЕсли;
	
	Если ЗаписатьПользователя
	 ИЛИ ЗаписатьПользователяИБ Тогда
		
		Если ЗначениеЗаполнено(Пользователь) Тогда
			ПользовательОбъект = Пользователь.ПолучитьОбъект();
		КонецЕсли;
		
		Если ЗаписатьПользователяИБ Тогда
			ПользовательОбъект.ДополнительныеСвойства.Вставить(
				"ОписаниеПользователяИБ", ОписаниеПользователяИБ);
		КонецЕсли;
		
		ПользовательОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Обновление_1_1_1_12

// Процедура - провести обновить представление ДокументыФизическихЛиц
//
Процедура ОбновитьКадровыеПриказы() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриемНаРаботу.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|ГДЕ
		|	ПриемНаРаботу.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровоеПеремещение.Ссылка
		|ИЗ
		|	Документ.КадровоеПеремещение КАК КадровоеПеремещение
		|ГДЕ
		|	КадровоеПеремещение.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Увольнение.Ссылка
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Проведен";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Попытка
			ДокументОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры // ОбновитьДокументыФизическихЛиц()

#КонецОбласти

#Область Обновление_1_1_1_18

// Процедура - Обновить начальные настройки
//
Процедура ОбновитьНачальныеНастройки(СтруктураПараметров = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.Ссылка,
		|	Пользователи.Наименование
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ПользователиСлужебный.УстановитьНачальныеНастройки(ВыборкаДетальныеЗаписи.Наименование);
	КонецЦикла;
КонецПроцедуры // ОбновитьНачальныеНастройки()

#КонецОбласти

#Область Обновление_1_1_5_5

Процедура ЗаполнитьПредопределенныеОперацииРКОиППИ() Экспорт
	// Заполнение счетов и видов операций у операций РКО и ППИ
	Справочники.ОперацииРКО.ЗаполнитьПоУмолчанию();
	Справочники.ОперацииППИ.ЗаполнитьПоУмолчанию();	
КонецПроцедуры

#КонецОбласти

#Область Обновление_1_1_5_6

// Процедура - Обновить коды справочников
//
Процедура ОбновитьКодыСправочников(СтруктураПараметров = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = "";
	
	Для Каждого Справочник Из Метаданные.Справочники Цикл		
		ДлинаКода = Справочник.ДлинаКода;
		Если ДлинаКода > 8 Или ДлинаКода = 0 Тогда 
			Продолжить;
		КонецЕсли;	
		
		ТекстЗапроса = ТекстЗапроса 
		+ Символы.ПС 
		+ ?(ТекстЗапроса = "", ТекстЗапроса, "Объединить все")
		+ Символы.ПС 
		+ "ВЫБРАТЬ
		|	ТаблицаСправочника.Ссылка
		|ИЗ
		|	Справочник." + Справочник.Имя + " КАК ТаблицаСправочника
		|ГДЕ
		|	ТаблицаСправочника.Код = """ + Лев("000000000", ДлинаКода) + """";
	КонецЦикла;	
	
	Запрос = Новый Запрос(ТекстЗапроса);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Попытка
			СправочникОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			СправочникОбъект.УстановитьНовыйКод();
			СправочникОбъект.Записать();
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры // ОбновитьКодыСправочников()

// Процедура - Обновить коды ГНС
//
Процедура ОбновитьКодыГНС(СтруктураПараметров = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	СправочникМенеджер = Справочники.ГНС;

	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	Макет = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения");
	Макет.Записать(ИмяВременногоФайла);  
	ТаблицаЗаполнения = ЗначениеИзФайла(ИмяВременногоФайла);
	
	Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗаполнения Цикл 
		СправочникСсылка = СправочникМенеджер.НайтиПоНаименованию(СтрокаТаблицыЗначений.Наименование, Истина);
		
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();	
		Иначе 
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
		КонецЕсли;	
		
		СправочникОбъект.Код = СтрокаТаблицыЗначений.Код;
		СправочникОбъект.Наименование = СтрокаТаблицыЗначений.Наименование;
		
		БухгалтерскийУчетКлиентСервер.ЗаписатьСправочникОбъект(СправочникОбъект);
	КонецЦикла;	
КонецПроцедуры // ОбновитьКодыГНС()

#КонецОбласти

#Область Обновление_1_1_8_3

Процедура УстановитьПараметрыНабораСвойств(СтруктураПараметров = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Параметры = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_Номенклатура_Общие", Параметры);
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_Номенклатура", Параметры);
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_ФизическиеЛица", Параметры);
	
КонецПроцедуры // УстановитьПараметрыНабораСвойств()

#КонецОбласти

#Область Обновление_1_1_9_8

// Процедура - Обновить парные счета учета в плане счетов - "Хозрасчетный"
//
Процедура ОбновитьСчетаУчета(СтруктураПараметров = Неопределено) Экспорт
	ПланыСчетов.Хозрасчетный.ЗаполнитьПоУмолчанию();
КонецПроцедуры

#КонецОбласти

#Область Обновление_1_1_9_14

// Процедура очищает РС "ЦеныНоменклатуры" и затем перепроводит документы "УстановкаЦенНоменклатуры".
//
Процедура ПерепровестиДокументыУстановкиЦен() Экспорт

	// Очищаем регистр сведений "ЦеныНоменклатуры"
	НаборЗаписей = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	//Перепроводим документы "Установка цен номенклатуры"
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаЦенНоменклатуры.Ссылка
	|ИЗ
	|	Документ.УстановкаЦенНоменклатуры КАК УстановкаЦенНоменклатуры
	|ГДЕ
	|	УстановкаЦенНоменклатуры.Проведен = ИСТИНА";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		ДокументОбъект = Результат.Ссылка.ПолучитьОбъект();
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область Обновление_1_1_9_16

// Процедура - Заполнить Способы отражения расходов по амортизации
//
Процедура ЗаполнитьСпособыОтраженияРасходовПоАмортизации() Экспорт
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпособыОтраженияРасходовПоАмортизации.Ссылка КАК Способ
		|ИЗ
		|	Справочник.СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
		|ГДЕ
		|	НЕ СпособыОтраженияРасходовПоАмортизации.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СчетЗатрат = ПланыСчетов.Хозрасчетный.РасходыНаАмортизациюОсновныхСредств;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если НЕ ВыборкаДетальныеЗаписи.Способ.Способы.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = ВыборкаДетальныеЗаписи.Способ.ПолучитьОбъект();
		СтрокаТабличнойЧасти = СправочникОбъект.Способы.Добавить();
		СтрокаТабличнойЧасти.СчетЗатрат = СчетЗатрат; 
		СтрокаТабличнойЧасти.Коэффициент = 1;		
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область Обновление_1_1_9_17

Процедура ДобавитьДопРеквизитыВСправочникКонтрагенты() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Параметры = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_Контрагенты", Параметры);
	
КонецПроцедуры // ДобавитьДопРеквизитыВСправочникКонтрагенты()

#КонецОбласти

#Область Обновление_1_1_9_28

Процедура ЗаполнитьПризнакСтраныКонтрагента() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка,
		|	Контрагенты.СтранаРезидентства,
		|	Контрагенты.СтранаРезидентства.Код КАК КодСтраныРезидентства
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.ЭтоГруппа
		|	И НЕ Контрагенты.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СправочникОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			
		Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СтранаРезидентства) Тогда 
			Продолжить;
		КонецЕсли;	
		
		Если ВыборкаДетальныеЗаписи.КодСтраныРезидентства = "417" Тогда  // Коргизия
			СправочникОбъект.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР;
		ИначеЕсли ВыборкаДетальныеЗаписи.КодСтраныРезидентства = "051"  // Армения
			Или ВыборкаДетальныеЗаписи.КодСтраныРезидентства = "112"  // Беларусь
			Или ВыборкаДетальныеЗаписи.КодСтраныРезидентства = "398" // Кахахстан
			Или ВыборкаДетальныеЗаписи.КодСтраныРезидентства = "643" Тогда // Россия
			СправочникОбъект.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС;
		Иначе 
			СправочникОбъект.ПризнакСтраны = Перечисления.ПризнакиСтраны.ИмпортЭкспорт;
		КонецЕсли;	
		
		// Попытка
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры	
		
#КонецОбласти

#Область Обновление_1_1_9_29

Процедура ЗаполнитьВидыЗатрат() Экспорт
	
	Справочники.ВидыЗатрат.ЗаполнитьПоУмолчанию();
	
КонецПроцедуры	
		
#КонецОбласти

#Область Обновление_1_1_9_28

Процедура ЗаполнитьВидОперацииВПостепении() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	НЕ ПоступлениеТоваровУслуг.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВидОперацииПокупка = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Покупка;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДокументОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ВидОперации = ВидОперацииПокупка;			
		
		// Попытка
		Попытка
			ДокументОбъект.Записать();
		Исключение
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры	
		
#КонецОбласти

#Область Обновление_3_0_3_9

Процедура ИзменитьСубконтоСчетовПроизводства() Экспорт

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка,
		|	Хозрасчетный.Код
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Код = ""1630""
		|	ИЛИ Хозрасчетный.Код = ""1640""";
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицыЗначений Из ТЗ Цикл
		ПланСчетовОбъект = СтрокаТаблицыЗначений.Ссылка.ПолучитьОбъект();
		
		Если СтрокаТаблицыЗначений.Код = "1630" Тогда
			ПланСчетовОбъект.ВидыСубконто[0].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
			ПланСчетовОбъект.ВидыСубконто[1].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
			ПланСчетовОбъект.ВидыСубконто[2].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат;
		Иначе
			ПланСчетовОбъект.ВидыСубконто[0].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
			ПланСчетовОбъект.ВидыСубконто[1].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
			ПланСчетовОбъект.ВидыСубконто[2].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
		КонецЕсли;	
			
		Попытка
			ПланСчетовОбъект.Записать();
		Исключение
			ТекстСообщения = НСтр("ru='Не удалось изменить субконто.
			|Техническая информация об ошибке: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));	
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			#Если Сервер Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось изменить субконто по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			#КонецЕсли
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Обновление_3_0_3_20

Процедура ДобавитьЭлементПроверкиУчета() Экспорт

	КонтрольВеденияУчетаСлужебный.ЗаполнитьПравилаПроверкиУчета();
	
КонецПроцедуры

#КонецОбласти
