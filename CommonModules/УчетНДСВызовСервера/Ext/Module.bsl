
#Область ПрограммныйИнтерфейс

// Функция - Получить ставку НДС
//
// Параметры:
//  Период		 - дата	- дата на которую нужно получить значения ставки
//  СтавкаНДС	 - СправочникСсылка.СтавкиНДС - ссылка на справочник ставки НДС
// Возвращаемое значение:
//   - число - размер ставки
Функция ПолучитьСтавкуНДС(Период, СтавкаНДС) Экспорт
	Возврат УчетНДСПовтИсп.ПолучитьСтавкуНДС(НачалоМесяца(Период), СтавкаНДС);
КонецФункции // ПолучитьСтавкуНДС()

// Функция - Получить ставку НСП
//
// Параметры:
//  Период		 - дата	- дата на которую нужно получить значения ставки
//  Организация	 - СправочникСсылка.Организации - ссылка на справочник Организация
//  СтавкаНСП	 - СправочникСсылка.СтавкаНСП - ссылка на справочник ставки НСП
// Возвращаемое значение:
//   - число - размер ставки
Функция ПолучитьСтавкуНСП(Период, Организация, СтавкаНСП) Экспорт
	Возврат УчетНДСПовтИсп.ПолучитьСтавкуНСП(НачалоМесяца(Период), Организация, СтавкаНСП);
КонецФункции // ПолучитьСтавкуНСП()  

// Функция - Получить значения ставок НДС и НСП
//
// Параметры:
//  Период		- Дата - дата на которую нужно получить значения ставки
//  Контрагент	- СправочникСсылка.Контрагенты	- ссылка на справочник Контрагент
//  Договор	 	- СправочникСсылка.ДоговорыКонтрагентов	- ссылка на справочник Договоры контрагентов
// 
// Возвращаемое значение:
//   - Структура 
//		* ЗначениеСтавкиНДС - число
//		* ЗначениеСтавкиНСП - число
//
Функция ПолучитьЗначенияСтавокНДСиНСП(Период, Договор) Экспорт
	
	СтруктураДанные = Новый Структура("ЗначениеСтавкиНДС, ЗначениеСтавкиНСП", 0, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияСтавокНалогов.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ЗначенияСтавокНалогов.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ИЗ
		|	РегистрСведений.ЗначенияСтавокНалогов КАК ЗначенияСтавокНалогов
		|ГДЕ
		|	ЗначенияСтавокНалогов.ДоговорКонтрагента = &Договор";
	Запрос.УстановитьПараметр("Договор", Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(СтруктураДанные, ВыборкаДетальныеЗаписи);		
	КонецЕсли;             

	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьЗначенияСтавокНДСиНСП()

// Функция - Расчет всех сумм отчета НДС - как основных сумм, например, "050",
//           так и вспомогательных, например, СуммаДоотгрузки или КоэффициентОсвобожденныхПоставок
//
// Параметры:
//  ВидСуммы	- строка - вид расчета суммы. Например, расчет суммы строки отчета "050" или вспомогательной суммы СуммаДоотгрузки
//  Параметры 	- структура - содержит перечень всех используемых данных для каждого вида расчета суммы.
// Возвращаемое значение:
//   - число 	- значение расчитанной суммы
Функция РасчетСуммыОтчетаНДС(ВидСуммы, Знач Параметры) Экспорт
	СтруктураВозврата = Новый Структура;
	РезультатРасчета = 0;
	ТаблицаРасшифровка = Неопределено;
	
	Если ВидСуммы = "СуммаДоотгрузки" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.Сумма;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаНДСДоотгрузки" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНДС;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаНСПДоотгрузки" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНСП;
		КонецЦикла;	
		
	ИначеЕсли ВидСуммы = "СуммаДоотгрузкаНулеваяСтавка" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.Сумма;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаНДСДоотгрузкаНулеваяСтавка" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНДС;
		КонецЦикла;
				
	ИначеЕсли ВидСуммы = "СуммаДоотгрузкаОсвобожденнаяСтавка" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.Сумма;
		КонецЦикла;		
						
	ИначеЕсли ВидСуммы = "СуммаНДСДоотгрузкаОсвобожденнаяСтавка" Тогда
		// Параметры.Таблица - табличная часть Доотгрузка 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНДС;
		КонецЦикла;		
		
	ИначеЕсли ВидСуммы = "СуммаАвансы" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС <> Справочники.СтавкиНДС.Нулевая Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаНДСАвансы" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС <> Справочники.СтавкиНДС.Нулевая Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаНСПАвансы" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС <> Справочники.СтавкиНДС.Нулевая Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНСП;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаАвансыНулеваяСтавка" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Нулевая Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "СуммаНДСАвансыНулеваяСтавка" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Нулевая Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
				
	ИначеЕсли ВидСуммы = "СуммаАвансыОсвобожденнаяСтавка" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
		КонецЦикла;		
						
	ИначеЕсли ВидСуммы = "СуммаНДСАвансыОсвобожденнаяСтавка" Тогда
		// Параметры.Таблица - табличная часть Авансы 
		Для каждого СтрокаТабличнойЧасти Из Параметры.Таблица Цикл
			Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТабличнойЧасти.СуммаНДС;
			КонецЕсли;
		КонецЦикла;		
						
	ИначеЕсли ВидСуммы = "СуммаПродажОбщая" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации 
		РезультатРасчета  = Параметры.Таблица.Итог("СуммаБезНДС") + Параметры.СуммаАвансы - Параметры.СуммаДоотгрузки;		
						
	ИначеЕсли ВидСуммы = "СуммаПродажОсвобожденная" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл		
			Если СтрокаТаблицыЗначений.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаБезНДС;
			КонецЕсли;					
		КонецЦикла;		
		 РезультатРасчета = РезультатРасчета + Параметры.СуммаАвансы - Параметры.СуммаДоотгрузки;
						
	ИначеЕсли ВидСуммы = "КоэффициентОсвобожденныхПоставокРасчетный" Тогда
		Если Параметры.Сумма054 = 0 Тогда
			КоэффициентОсвобожденныхПоставок = 0;
		Иначе
			КоэффициентОсвобожденныхПоставок = Окр(Параметры.Сумма053 / Параметры.Сумма054, 4);
		КонецЕсли;
		
		Если КоэффициентОсвобожденныхПоставок <= (Параметры.ПороговыйПроцентОсвобожденныхПоставок / 100) 
			ИЛИ КоэффициентОсвобожденныхПоставок = 0 Тогда
			КоэффициентОсвобожденныхПоставокРасчетный = 0;
		Иначе
			КоэффициентОсвобожденныхПоставокРасчетный = КоэффициентОсвобожденныхПоставок;
		КонецЕсли;	
		
		РезультатРасчета = КоэффициентОсвобожденныхПоставокРасчетный;
		
	ИначеЕсли ВидСуммы = "НДСДляРаспределенияКР" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Распределение 
					И СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ВидСуммы = "НДСДляРаспределенияЕАЭС" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Распределение 
					И СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ВидСуммы = "НДСДляРаспределенияНеКР" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Распределение 
					И НЕ СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если НЕ СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
		
	ИначеЕсли ВидСуммы = "НДСКРСебестоимость" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Себестоимость 
					И СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			РезультатРасчета = 0;		
		КонецЕсли;
		
	ИначеЕсли ВидСуммы = "НДСЕАЭССебестоимость" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Себестоимость 
					И СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			РезультатРасчета = 0;		
		КонецЕсли;
		
	ИначеЕсли ВидСуммы = "НДСНеКРСебестоимость" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Себестоимость 
					И НЕ СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			РезультатРасчета = 0;		
		КонецЕсли;
		
	ИначеЕсли ВидСуммы = "НДСКРНеРазрешенныйКЗачету" Тогда
		РезультатРасчета = Окр(Параметры.КфОПР * Параметры.НДСДляРаспределенияКР + Параметры.НДСКРСебестоимость, 2);
		
	ИначеЕсли ВидСуммы = "НДСЕАЭСНеРазрешенныйКЗачету" Тогда
		РезультатРасчета = Окр(Параметры.КфОПР * Параметры.НДСДляРаспределенияЕАЭС + Параметры.НДСЕАЭССебестоимость, 2);
		
	ИначеЕсли ВидСуммы = "НДСНеКРНеРазрешенныйКЗачету" Тогда
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Если Параметры.УказыватьПризнакЗачетаНДСПриПоступлении Тогда
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				Если СтрокаТаблицыЗначений.ЗачетНДС = Перечисления.ВидыЗачетаНДС.Распределение Тогда
					РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
					СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
			КонецЦикла;
		КонецЕсли;
		
		РезультатРасчета = Окр(Параметры.КфОПР * (РезультатРасчета - Параметры.НДСДляРаспределенияКР) + Параметры.НДСНеКРСебестоимость, 2);		
		
	ИначеЕсли ВидСуммы = "Сумма050" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаБезНДС");
		
		ТаблицаСтавок = ПолучитьСтавкиНДССНулевымиЗначениями(Параметры.Период);
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("СтавкаНДС", Справочники.СтавкиНДС.ПустаяСсылка());
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл	
			СтруктураПоиска.СтавкаНДС = СтрокаТаблицыЗначений.СтавкаНДС;
			МассивСтавок = ТаблицаСтавок.НайтиСтроки(СтруктураПоиска);
			
			Если МассивСтавок.Количество() = 0 И СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаБезНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "Авансы";
		СтрокаРасшифровки.СуммаБезНДС = Параметры.СуммаАвансы;
		
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "АвансыНДС";
		СтрокаРасшифровки.СуммаБезНДС = Параметры.СуммаНДСАвансы;
		
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "АвансыНСП";
		СтрокаРасшифровки.СуммаБезНДС = Параметры.СуммаНСПАвансы;
		
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "Доотгрузка";
		СтрокаРасшифровки.СуммаБезНДС = Параметры.СуммаДоотгрузки;
		
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "ДоотгрузкаНДС";
		СтрокаРасшифровки.СуммаБезНДС = Параметры.СуммаНДСДоотгрузки;
		
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "ДоотгрузкаНСП";
		СтрокаРасшифровки.СуммаБезНДС = Параметры.СуммаНСПДоотгрузки;
		
		РезультатРасчета = РезультатРасчета + Параметры.СуммаАвансы - Параметры.СуммаНДСАвансы - Параметры.СуммаНСПАвансы 
							- (Параметры.СуммаДоотгрузки - Параметры.СуммаНДСДоотгрузки - Параметры.СуммаНСПДоотгрузки);
		
	ИначеЕсли ВидСуммы = "Сумма051" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаБезНДС");
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если СтрокаТаблицыЗначений.СтавкаНДС = Справочники.СтавкиНДС.Нулевая Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаБезНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;	
						
	ИначеЕсли ВидСуммы = "Сумма052" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаБезНДС");
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если СтрокаТаблицыЗначений.СтавкаНДС = Справочники.СтавкиНДС.Нулевая 
				И СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаБезНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ВидСуммы = "Сумма053" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаБезНДС");
		
		ТаблицаСтавок = ПолучитьСтавкиНДССНулевымиЗначениями(Параметры.Период);
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("СтавкаНДС", Справочники.СтавкиНДС.ПустаяСсылка());
		
		СуммаБезНДССтавокСНулями = 0;
		СуммаБезНДСНулевойСтавки = 0;
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			СтруктураПоиска.СтавкаНДС = СтрокаТаблицыЗначений.СтавкаНДС;
			МассивСтавок = ТаблицаСтавок.НайтиСтроки(СтруктураПоиска);
			
			Если МассивСтавок.Количество() <> 0 Тогда
				// СуммаБезНДС всех ставок с значением 0
				СуммаБезНДССтавокСНулями = СуммаБезНДССтавокСНулями + СтрокаТаблицыЗначений.СуммаБезНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
			КонецЕсли;	
			
			Если СтрокаТаблицыЗначений.СтавкаНДС = Справочники.СтавкиНДС.Нулевая Тогда
				СуммаБезНДСНулевойСтавки = СуммаБезНДСНулевойСтавки + СтрокаТаблицыЗначений.СуммаБезНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;	
		
		РезультатРасчета = СуммаБезНДССтавокСНулями - СуммаБезНДСНулевойСтавки;
								
	ИначеЕсли ВидСуммы = "Сумма054" Тогда
		// Параметры - суммы ранее расчитанных строк 050, 051 и 053 	
		РезультатРасчета = Параметры.Сумма050 + Параметры.Сумма051 + Параметры.Сумма053;		
								
	ИначеЕсли ВидСуммы = "Сумма055" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о поступлении
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, Сумма");
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР  Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.Сумма;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;		
								
	ИначеЕсли ВидСуммы = "Сумма056" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о поступлении
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, Сумма");
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если НЕ СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.Сумма;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;			
								
	ИначеЕсли ВидСуммы = "Сумма057" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о поступлении
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, Сумма");
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.Сумма;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
			КонецЕсли;
		КонецЦикла;		
									
	ИначеЕсли ВидСуммы = "Сумма058" Тогда
		// Параметры - суммы ранее расчитанных строк 055 и 056
		РезультатРасчета = Параметры.Сумма055 + Параметры.Сумма056;
	
	ИначеЕсли ВидСуммы = "Сумма059" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о реализации
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
		
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
			СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);				
		КонецЦикла;
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "Авансы";
		СтрокаРасшифровки.СуммаНДС = Параметры.СуммаНДСАвансы;
		
		СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
		СтрокаРасшифровки.Регистратор = "Доотгрузка";
		СтрокаРасшифровки.СуммаНДС = -Параметры.СуммаНДСДоотгрузки;
		
		РезультатРасчета = РезультатРасчета + Параметры.СуммаНДСАвансы - Параметры.СуммаНДСДоотгрузки;						
		
	//НДС ЗА ПРИОБРЕТЕННЫЕ МАТЕРИАЛЬНЫЕ РЕСУРСЫ, ПОДЛЕЖАЩИЙ ЗАЧЕТУ		
	//
	ИначеЕсли ВидСуммы = "Сумма060" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о поступлении
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
 
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
			КонецЕсли;
		КонецЦикла;						

		РезультатРасчета = РезультатРасчета - Параметры.НДСКРНеРазрешенныйКЗачету;
		
	// НДС НА ИМПОРТ ПОДЛЕЖАЩИЙ ЗАЧЕТУ
	//
	ИначеЕсли ВидСуммы = "Сумма061" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о поступлении
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
 
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если НЕ СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
			КонецЕсли;
		КонецЦикла;						

		РезультатРасчета = РезультатРасчета - Параметры.НДСНеКРНеРазрешенныйКЗачету;		
		
	// В ТОМ ЧИСЛЕ ИЗ ЕВРАЗИЙСКОГО ЭКОНОМИЧЕСКОГО  СОЮЗА
	//
	ИначеЕсли ВидСуммы = "Сумма062" Тогда
		// Параметры.Таблица - таблица значений из РС Сведения о поступлении
		ТаблицаРасшифровка = Параметры.Таблица.СкопироватьКолонки("Регистратор, СуммаНДС");
 
		Для каждого СтрокаТаблицыЗначений Из Параметры.Таблица Цикл
			Если СтрокаТаблицыЗначений.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
				РезультатРасчета = РезультатРасчета + СтрокаТаблицыЗначений.СуммаНДС;
				СтрокаРасшифровки = ТаблицаРасшифровка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицыЗначений);
			КонецЕсли;
		КонецЦикла;						

		РезультатРасчета = РезультатРасчета - Параметры.НДСЕАЭСНеРазрешенныйКЗачету;		
		
	// НДС ЗА ПРИОБРЕТЕННЫЕ МАТЕРИАЛЬНЫЕ РЕСУРСЫ, НЕ ПОДЛЕЖАЩИЙ ЗАЧЕТУ
	//
	ИначеЕсли ВидСуммы = "Сумма063" Тогда
		РезультатРасчета = Параметры.НДСКРНеРазрешенныйКЗачету + Параметры.НДСНеКРНеРазрешенныйКЗачету;				
				
	//НДС ЗА ПРИОБРЕТЕННЫЕ МАТЕРИАЛЬНЫЕ РЕСУРСЫ, ПОДЛЕЖАЩИЙ ЗАЧЕТУ		
	//
	ИначеЕсли ВидСуммы = "Сумма064" Тогда
		// Параметры - суммы ранне расчитанных строк 060 и 061
		РезультатРасчета = Параметры.Сумма060 + Параметры.Сумма061;				
		
	// В ТОМ ЧИСЛЕ НДС ЗА ПРИОБРЕТЕННЫЕ МАТЕРИАЛЬНЫЕ РЕСУРСЫ, ИСПОЛЬЗУЕМЫЕ ДЛЯ ПОСТАВОК С НУЛЕВОЙ СТАВКОЙ
	//
	ИначеЕсли ВидСуммы = "Сумма065" Тогда
		// Параметры - умножение коэфициента нулевых поставок (КфНП) и ранее расчитанной строки 064
		Сумма = Параметры.Сумма050 + Параметры.Сумма051;
		
		Если Сумма = 0 ИЛИ Параметры.Сумма051 = 0 Тогда
			РезультатРасчета = 0;
		Иначе
			КфНП = Окр(Параметры.Сумма051 / (Сумма), 4);
			РезультатРасчета = Окр(КфНП * Параметры.Сумма064, 2);
		КонецЕсли;
		
	// НАЛОГОВОЕ ОБЯЗАТЕЛЬСТВО (=059-064) (УКАЖИТЕ ЗНАК МИНУС, ЕСЛИ ЗНАЧЕНИЕ ОТРИЦАТЕЛЬНОЕ)
	//
	ИначеЕсли ВидСуммы = "Сумма066" Тогда
		// Параметры - разность ранне расчитанных строк 059 и 064
		РезультатРасчета = Параметры.Сумма059 - Параметры.Сумма064;		
		
	// НАЛОГОВОЕ ОБЯЗАТЕЛЬСТВО ВСЕГО (=066-067) (УКАЖИТЕ ЗНАК МИНУС, ЕСЛИ ЗНАЧЕНИЕ ОТРИЦАТЕЛЬНОЕ)
	//
	ИначеЕсли ВидСуммы = "Сумма068" Тогда
		// Параметры - разность ранне расчитанных строк 066 и 067
		РезультатРасчета = Параметры.Сумма066 - Параметры.Сумма067;		
		
	Иначе
	    РезультатРасчета = 0;           
			
	КонецЕсли;
	
	СтруктураВозврата.Вставить("РезультатРасчета", 		РезультатРасчета);
	СтруктураВозврата.Вставить("ТаблицаРасшифровка", 	ТаблицаРасшифровка);
	
	Возврат СтруктураВозврата;
	
КонецФункции // РасчетСуммыОТчетаНДС()

Функция ПолучитьСтавкиНДССНулевымиЗначениями(Период)

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДССрезПоследних.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.СтавкиНДС.СрезПоследних(
		|			&Период,
		|			Ставка = 0) КАК СтавкиНДССрезПоследних";
	Запрос.УстановитьПараметр("Период", Период);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции // ПолучитьСтавкиНДССНулевымиЗначениями()

Функция ВсеСведенияОРеализации(Организация, НачалоМесяца, КонецМесяца) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|			ТОГДА СчетаФактурыВыписанные.Регистратор
		|		ИНАЧЕ ""             ""
		|	КОНЕЦ КАК Регистратор,
		|	СчетаФактурыВыписанные.Организация КАК Организация,
		|	СчетаФактурыВыписанные.Контрагент КАК Контрагент,
		|	СчетаФактурыВыписанные.Контрагент.ПризнакСтраны КАК ПризнакСтраны,
		|	СчетаФактурыВыписанные.ДатаПоставки КАК ДатаПоставки,
		|	СчетаФактурыВыписанные.СтавкаНДС КАК СтавкаНДС,
		|	СчетаФактурыВыписанные.СуммаБезНДС КАК СуммаБезНДС,
		|	СчетаФактурыВыписанные.СуммаНДС КАК СуммаНДС,
		|	СчетаФактурыВыписанные.Документ КАК ДокументРеализации,
		|	СчетаФактурыВыписанные.Период КАК Период,
		|	СчетаФактурыВыписанные.СерияБланкаСФ КАК СерияБланкаСФ
		|ИЗ
		|	РегистрСведений.СчетаФактурыВыписанные КАК СчетаФактурыВыписанные
		|ГДЕ
		|	ВЫБОР
		|			КОГДА СчетаФактурыВыписанные.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|				ТОГДА СчетаФактурыВыписанные.ДатаПоставки <= &КонецМесяца
		|						И (СчетаФактурыВыписанные.Период МЕЖДУ &НачалоМесяца И &КонецМесяца)
		|			ИНАЧЕ СчетаФактурыВыписанные.ДатаПоставки МЕЖДУ &НачалоМесяца И &КонецМесяца
		|		КОНЕЦ
		|	И СчетаФактурыВыписанные.Организация = &Организация
		|	И НЕ СчетаФактурыВыписанные.Регистратор ССЫЛКА Документ.СчетФактураВыписанный";
	
	Запрос.УстановитьПараметр("КонецМесяца", 	КонецМесяца);
	Запрос.УстановитьПараметр("НачалоМесяца", 	НачалоМесяца);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ВсеСведенияОРеализации()

Функция ВсеСведенияОПоступлении(Организация, НачалоМесяца, КонецМесяца) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОПоступлении.Контрагент КАК Контрагент,
		|	СведенияОПоступлении.ЗачетНДС КАК ЗачетНДС,
		|	СведенияОПоступлении.Сумма КАК Сумма,
		|	СведенияОПоступлении.СуммаНДС КАК СуммаНДС,
		|	СведенияОПоступлении.СуммаНСП КАК СуммаНСП,
		|	ВЫБОР
		|		КОГДА СведенияОПоступлении.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗАчетаНДС.Зачет)
		|				ИЛИ СведенияОПоступлении.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗАчетаНДС.ПустаяСсылка)
		|			ТОГДА СведенияОПоступлении.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДСЗачет,
		|	СведенияОПоступлении.ПризнакСтраны КАК ПризнакСтраны,
		|	СведенияОПоступлении.Регистратор КАК Регистратор,
		|	СведенияОПоступлении.ДоговорКонтрагента КАК ДоговорКонтрагента
		|ИЗ
		|	РегистрСведений.СведенияОПоступлении КАК СведенияОПоступлении
		|ГДЕ
		|	СведенияОПоступлении.ДатаСФ МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И СведенияОПоступлении.Организация = &Организация";
	Запрос.УстановитьПараметр("КонецМесяца", 	КонецМесяца);
	Запрос.УстановитьПараметр("НачалоМесяца", 	НачалоМесяца);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ВсеСведенияОРеализации()

#КонецОбласти
