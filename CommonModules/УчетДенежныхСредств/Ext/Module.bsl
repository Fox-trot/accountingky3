
#Область ОперационнаяКурсоваяРазница

Процедура СформироватьДвиженияОперационнаяКурсоваяРазница(ДополнительныеСвойства, Движения, Отказ, ПриходДенежныхСредств, Конвертация = Ложь) Экспорт
	
	ТаблицаДанных = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДанных;
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыОперационнаяКурсоваяРазница(ТаблицаДанных, Конвертация);
	
	Данные = Параметры.Данные[0]; 

	СчетУчетаДоходыОКР = ПланыСчетов.Хозрасчетный.ДоходыОтОперационныхКурсовыхРазниц;
	СчетУчетаУбыткиОКР = ПланыСчетов.Хозрасчетный.УбыткиОтОперационныхКурсовыхРазниц;
	
	// Валюта взаиморасчетов в валюте регламентированного учета. 
	Если Данные.ВалютаРасчетов = Данные.ВалютаРегламентированногоУчета Тогда 
		СуммаКурсовойРазницы = Данные.СуммаПлатежа * Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку - Данные.СуммаВзаиморасчетов;
		
	// В документа в валюте регламентированного учета.				
	ИначеЕсли Данные.ВалютаДокумента = Данные.ВалютаРегламентированногоУчета Тогда
		СуммаКурсовойРазницы = Данные.СуммаПлатежа - Данные.СуммаВзаиморасчетов * Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку;
		
	// Валюты отличны от валюты регламентированного учета.
	Иначе 
		СуммаКурсовойРазницы = Данные.СуммаПлатежа * Данные.КурсДокумента / Данные.КратностьДокумента - Данные.СуммаВзаиморасчетов * Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку;
	КонецЕсли;				
	
	СчетРасчетовВалютный = Данные.СчетРасчетов.Валютный;
	ДенежныйСчетВалютный = Данные.ДенежныйСчет.Валютный;
	ОбаСчетаВалютные 	 = Ложь;
	
	Если Окр(СуммаКурсовойРазницы, 2) = 0 Тогда 
		Возврат;
		
	// Расход валюты, убыток
	ИначеЕсли НЕ ДенежныйСчетВалютный И СчетРасчетовВалютный И СуммаКурсовойРазницы < 0 Тогда
		Если ПриходДенежныхСредств Тогда
			Доход = Ложь;
		Иначе
			Доход = Истина;                                                         
		КонецЕсли;
		
	// Расход валюты, доход		
	ИначеЕсли НЕ ДенежныйСчетВалютный И СчетРасчетовВалютный И СуммаКурсовойРазницы > 0 Тогда
		Если ПриходДенежныхСредств Тогда
			Доход = Истина;
		Иначе
			Доход = Ложь;
		КонецЕсли;
		
	// Приход валюты, убыток	
	ИначеЕсли ДенежныйСчетВалютный И НЕ СчетРасчетовВалютный И СуммаКурсовойРазницы < 0 Тогда
		Если ПриходДенежныхСредств Тогда
			Доход = Ложь;
		Иначе
			Доход = Истина;
		КонецЕсли;
		
	// Приход валюты, доход	
	ИначеЕсли ДенежныйСчетВалютный И НЕ СчетРасчетовВалютный И СуммаКурсовойРазницы > 0 Тогда
		Если ПриходДенежныхСредств Тогда
			Доход = Истина;
		Иначе
			Доход = Ложь;
		КонецЕсли;
		
	// Валюта - Валюта, убыток	
	ИначеЕсли ДенежныйСчетВалютный И СчетРасчетовВалютный И СуммаКурсовойРазницы < 0 Тогда
		Если ПриходДенежныхСредств Тогда
			Доход = Ложь;
		Иначе
			Доход = Истина;
		КонецЕсли;
		ОбаСчетаВалютные = Истина;
		
	// Валюта - Валюта, доход		
	ИначеЕсли ДенежныйСчетВалютный И СчетРасчетовВалютный И СуммаКурсовойРазницы > 0 Тогда
		Если ПриходДенежныхСредств Тогда
			Доход = Истина;
		Иначе
			Доход = Ложь;
		КонецЕсли;	
		ОбаСчетаВалютные = Истина;
	КонецЕсли;	
	
	Проводка = Движения.Хозрасчетный.Добавить();
	
	Проводка.Период      	= Данные.Период;
	Проводка.Организация 	= Данные.Организация;
	
	Если Доход Тогда
		Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
			Проводка.СчетДт = Данные.ДенежныйСчет;
			Проводка.ВалютаДт = Данные.ВалютаДокумента;
		Иначе
			Проводка.СчетДт = Данные.СчетРасчетов;
			Проводка.ВалютаДт = Данные.ВалютаРасчетов;
		КонецЕсли;
		
		Проводка.СчетКт = СчетУчетаДоходыОКР;
		
		Если Конвертация Тогда
			Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
				БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ДенежныеСредства", Данные.КассаБанковскийСчетПриход);
			Иначе
				БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ДенежныеСредства", Данные.КассаБанковскийСчетРасход);
			КонецЕсли;
			
			БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиДвиженияДенежныхСредств", ДополнительныеСвойства.УчетнаяПолитика.СтатьяДДСДляКурсовыхРазниц);	
		Иначе
			БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты", Данные.Контрагент);
			БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Договоры", Данные.ДоговорКонтрагента);
		КонецЕсли;
			
		Проводка.Содержание = НСтр("ru = 'Доходы от операционных курсовых разниц'");
		
	Иначе 
		Проводка.СчетДт = СчетУчетаУбыткиОКР;
		
		Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
			Проводка.СчетКт = Данные.ДенежныйСчет;
			Проводка.ВалютаКт = Данные.ВалютаДокумента;
		Иначе
			Проводка.СчетКт = Данные.СчетРасчетов;
			Проводка.ВалютаКт = Данные.ВалютаРасчетов;
		КонецЕсли;
		
		Если Конвертация Тогда
			Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
				БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДенежныеСредства", Данные.КассаБанковскийСчетПриход);
			Иначе
				БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДенежныеСредства", Данные.КассаБанковскийСчетРасход);
			КонецЕсли;
			
			БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиДвиженияДенежныхСредств", ДополнительныеСвойства.УчетнаяПолитика.СтатьяДДСДляКурсовыхРазниц);
		Иначе
			БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", Данные.Контрагент);
			БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры", Данные.ДоговорКонтрагента);
		КонецЕсли;	
			
		БухгалтерскийУчетСервер.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиЗатрат", ДополнительныеСвойства.УчетнаяПолитика.СтатьяЗатратДляОКР);
		
		Проводка.Содержание = НСтр("ru = 'Убытки от операционных курсовых разниц'");
	
	КонецЕсли;
	
	Проводка.Сумма = ?(СуммаКурсовойРазницы > 0, СуммаКурсовойРазницы, СуммаКурсовойРазницы * (-1));;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыОперационнаяКурсоваяРазница(ТаблицаДанных, Конвертация)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаДанных

	СписокОбязательныхКолонок = ""
	+ "Период,"                         	// <Дата> - период движений - дата документа
	+ "Организация,"                    	// <СправочникСсылка.Организация> - организация
	+ "ВалютаДокумента,"     				// <СправочникСсылка.Валюты> - валюта кассы/банка
	+ "ВалютаРегламентированногоУчета,"     // <СправочникСсылка.Валюты> - валюта регл.учета
	+ "ВалютаРасчетов,"           			// <СправочникСсылка.Валюты> - валюта взаиморасчетов	
	+ "КурсДокумента,"     					// <Число,10,4> - курс валюты документа
	+ "КратностьДокумента,"					// <Число,10,0> - кратность валюты документа
	+ "СчетРасчетов,"          				// <ПланСчетовСсылка.Хозрасчетный> - счет учета расчетов с контрагентом
	+ "ДенежныйСчет,"          				// <ПланСчетовСсылка.Хозрасчетный> - денежный счет учета
	+ "СуммаПлатежа,"           			// <Число,15,2> - сумма в валюте регламентированного учета
	+ "СуммаВзаиморасчетов," 				// <Число,15,2> - сумма в валюте взаиморасчетов
	+ "КурсВзаиморасчетовПоНацБанку,"     	// <Число,10,4> - курс валюты взаиморасчетов но НБКР
	+ "КратностьВзаиморасчетовПоНацБанку";	// <Число,10,0> - кратность валюты взаиморасчетов по НБКР
	
	Если Конвертация Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ ",КассаБанковскийСчетПриход"            	// <СправочникСсылка.Кассы> ИЛИ <СправочникСсылка.БанковскиеСчета> - касса или банковский счет прихода	
		+ ",КассаБанковскийСчетРасход";            	// <СправочникСсылка.Кассы> ИЛИ <СправочникСсылка.БанковскиеСчета> - касса или банковский счет расхода
		
	Иначе
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ ",Контрагент"            			// <СправочникСсылка.Контраегенты> - контрагент	
		+ ",ДоговорКонтрагента";            // <СправочникСсылка.ДоговорыКонтрагентов> - договор контрагента
	КонецЕсли;
	
	Параметры.Вставить("Данные",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаДанных, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

#КонецОбласти