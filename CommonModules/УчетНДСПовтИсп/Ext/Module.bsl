
#Область ПрограммныйИнтерфейс

// Функция - Получить ставку НДС, Нужно использовать процедуру УчетНДССервер.ПолучитьСтавкуНДС
//
// Параметры:
//  Период		 - Дата	- дата на которую нужно получить значения ставки
//  СтавкаНДС	 - СправочникСсылка.СтавкиНДС - ссылка на справочник ставки НДС
// 
// Возвращаемое значение:
//  Ставка - число - размер ставки
//
Функция ПолучитьСтавкуНДС(Период, СтавкаНДС) Экспорт
	Ставка = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДССрезПоследних.Ставка
		|ИЗ
		|	РегистрСведений.СтавкиНДС.СрезПоследних(&Период, СтавкаНДС = &СтавкаНДС) КАК СтавкиНДССрезПоследних";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("СтавкаНДС", СтавкаНДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Ставка = ВыборкаДетальныеЗаписи.Ставка;
	КонецЕсли;
	
	Возврат Ставка;
КонецФункции // ПолучитьСтавкуНДС()

// Функция - Получить ставку НСП, Нужно использовать процедуру УчетНДССервер.ПолучитьСтавкуНСП
//
// Параметры:
//  Период		 - Дата	- дата на которую нужно получить значения ставки
//  СтавкаНДС	 - СправочникСсылка.СтавкиНДС - ссылка на справочник ставки НДС
//  СтавкаНДС	 - СправочникСсылка.СтавкиНСП - ссылка на справочник ставки НСП
// 
// Возвращаемое значение:
//   Ставка - число - размер ставки
//
Функция ПолучитьСтавкуНСП(Период, СтавкаНДС, СтавкаНСП) Экспорт
	
	Ставка = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтавкиНСПСрезПоследних.Период,
	|	ВЫБОР
	|		КОГДА &СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА СтавкиНСПСрезПоследних.СтавкаНеПлательщикНДС
	|		ИНАЧЕ СтавкиНСПСрезПоследних.СтавкаПлательщикНДС
	|	КОНЕЦ КАК Ставка
	|ИЗ
	|	РегистрСведений.СтавкиНСП.СрезПоследних(&Период, СтавкаНСП = &СтавкаНСП) КАК СтавкиНСПСрезПоследних";	
	Запрос.УстановитьПараметр("Период", Период);	
	Запрос.УстановитьПараметр("СтавкаНСП", СтавкаНСП);	
	Запрос.УстановитьПараметр("СтавкаНДС", СтавкаНДС);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Ставка = ВыборкаДетальныеЗаписи.Ставка;
	КонецЕсли;
	
	Возврат Ставка;
КонецФункции // ПолучитьСтавкуНСП()

#КонецОбласти

