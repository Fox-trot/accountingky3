#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       см. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина);
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "");
	НастройкиВарианта.Описание = НСтр("ru = 'Приложение к отчету по налогу c продаж.'");

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Процедура формирования отчета.
//
// Параметры:
// ПараметрыОтчета - структура - набор параметров, необходимых для построения отчета.
// 	АдресХранилища - адрес временного хранилища.
Процедура Сформировать(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатФормирования = Новый Структура("Результат, ОписаниеОшибки", Неопределено, "");
	СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования);
	ПоместитьВоВременноеХранилище(РезультатФормирования, АдресХранилища);
	
КонецПроцедуры

// Процедура - Сформировать табличный документ
//
// Параметры:
//  СтруктураПараметров	 - Структура - Параметры формирования отчета.
//  РезультатФормирования	 	- Структура - 
//
Процедура СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать = Истина;
		
	// Опеределение макета
	Макет = ПолучитьМакет("ПФ_MXL_ПриложениеКОтчетуПоНСП");
	
	//1. Заполнение шапки
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ЗаполнитьШапкуОтчета(СтруктураПараметров, ОбластьШапка);
		
	//2. Заполнение основной части
	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(СтруктураПараметров.КонецПериода, СтруктураПараметров.Организация);
	Если УПП.ПлательщикНДС Тогда
		Секция = "";
	Иначе 
		Секция = "б";
	КонецЕсли;

	ДатаН = СтруктураПараметров.НачалоПериода;
	
	Для Сч = 1 По 3 Цикл
		//Определим даты с 1го месяца квартала
		Если  Сч = 1 Тогда 
			НачалоПериода = НачалоКвартала(ДатаН);
			ЗаполнитьДанныеОтчетаЗаМесяц(ОбластьШапка,СтруктураПараметров.Организация,НачалоПериода,1,Секция);
		ИначеЕсли Сч = 2 Тогда 
			НачалоПериода = ДобавитьМесяц(НачалоКвартала(ДатаН),1);
			ЗаполнитьДанныеОтчетаЗаМесяц(ОбластьШапка,СтруктураПараметров.Организация,НачалоПериода,2,Секция);
		ИначеЕсли Сч = 3 Тогда  
			НачалоПериода = ДобавитьМесяц(НачалоКвартала(ДатаН),2);
			ЗаполнитьДанныеОтчетаЗаМесяц(ОбластьШапка,СтруктураПараметров.Организация,НачалоПериода,3,Секция);
		КонецЕсли;
	КонецЦикла;
	
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	РезультатФормирования.Результат = ТабличныйДокумент;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеОтчетаЗаМесяц(ОбластьШапка,Организация, НачалоПериода, МесяцКвартала = 1,Секция)

	ОбластьШапка.Параметры["Месяц" + МесяцКвартала] = "" + Формат(НачалоПериода,"ДФ=MMMM") +" "+ Год(НачалоПериода);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецМесяца", 	КонецМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("НачалоМесяца", 	НачалоПериода);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("ПлательщикНДС", 	?(Секция = "б",Ложь,Истина));
	
	Запрос.Текст =	
	"ВЫБРАТЬ
	|	СУММА(РеализацияОбороты.ДоходОборот) КАК Выручка,
	|	РеализацияОбороты.СтавкаНСП,
	|	СУММА(РеализацияОбороты.СуммаНСПОборот) КАК СуммаНСП,
	|	ВЫБОР
	|		КОГДА &ПлательщикНДС
	|			ТОГДА СтавкиНСПСрезПоследних.СтавкаПлательщикНДС
	|		ИНАЧЕ СтавкиНСПСрезПоследних.СтавкаНеПлательщикНДС
	|	КОНЕЦ КАК Ставка
	|ИЗ
	|	РегистрНакопления.Реализация.Обороты(&НачалоМесяца, &КонецМесяца, , Организация = &Организация) КАК РеализацияОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних КАК СтавкиНСПСрезПоследних
	|		ПО РеализацияОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияОбороты.СтавкаНСП,
	|	ВЫБОР
	|		КОГДА &ПлательщикНДС
	|			ТОГДА СтавкиНСПСрезПоследних.СтавкаПлательщикНДС
	|		ИНАЧЕ СтавкиНСПСрезПоследних.СтавкаНеПлательщикНДС
	|	КОНЕЦ";

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();	
				
	СуммаТорг	= 0;
	НСПТорг		= 0;
	СтавкаТорг	= 0;			
	
	СуммаПроч   = 0;
	НСППроч	 	= 0;
	СтавкаПроч  = 0;
	
	СуммаСвязь  = 0;
	НСПСвязь    = 0;
	СтавкаСвязь = 0;	
	
	Пока Выборка.Следующий() Цикл
		Если  Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
			СуммаТорг 	= Выборка.Выручка;
			НСПТорг		= Выборка.СуммаНСП;
			СтавкаТорг	= Выборка.Ставка;			
		ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
			СуммаПроч   = Выборка.Выручка;
			НСППроч	 	= Выборка.СуммаНСП;
			СтавкаПроч  = Выборка.Ставка;
		Иначе
			СуммаСвязь  = Выборка.Выручка;
			НСПСвязь    = Выборка.СуммаНСП;
			СтавкаСвязь = Выборка.Ставка;
		КонецЕсли;
	КонецЦикла;
	НСПИтог = НСПТорг + НСППроч + НСПСвязь;
		
	//Торговля
	ОбластьШапка.Параметры["СуммаТорг" + МесяцКвартала+Секция]	= СуммаТорг;
	ОбластьШапка.Параметры["НСПТорг" + МесяцКвартала +Секция]	= НСПТорг;
	ОбластьШапка.Параметры["СтавкаТорг" + МесяцКвартала +Секция]= СтавкаТорг;

	//Связь
	ОбластьШапка.Параметры["СуммаСвязь" + МесяцКвартала +Секция]= СуммаСвязь;
	ОбластьШапка.Параметры["НСПСвязь" + МесяцКвартала +Секция]	= НСПСвязь;
	ОбластьШапка.Параметры["СтавкаСвязь" + МесяцКвартала +Секция]= СтавкаСвязь;

	//Прочее
	ОбластьШапка.Параметры["СуммаПроч" + МесяцКвартала +Секция]	= СуммаПроч;
	ОбластьШапка.Параметры["НСППроч" + МесяцКвартала +Секция]	= НСППроч;
	ОбластьШапка.Параметры["СтавкаПроч" + МесяцКвартала +Секция]= СтавкаПроч;
	
	ОбластьШапка.Параметры["НСПИтог" + МесяцКвартала +Секция]	= НСПИтог;
	ОбластьШапка.Параметры["НСПИтогОбщий" + МесяцКвартала]		= НСПИтог;
	//ОбластьШапка.Параметры.НСПИтог = НСПИтог;

КонецПроцедуры
 	
// Процедура - Заполнить шапку отчета
//
// Параметры:
//  НачалоПериода	 - 	 - 
//  КонецПериода	 - 	 - 
//  Организация		 - 	 - 
//  ОбластьШапка	 - 	 - 
//
Процедура ЗаполнитьШапкуОтчета(СтруктураПараметров, ОбластьШапка)
	
	Организация 	= СтруктураПараметров.Организация;
	НачалоПериода 	= СтруктураПараметров.НачалоПериода;
	КонецПериода 	= СтруктураПараметров.КонецПериода;	

	ГоловнаяОрганизация = Организация.ГоловнаяОрганизация;
	
	ОКПО              				= Организация.КодПоОКПО;
	ИНН 							= ГоловнаяОрганизация.ИНН;
	КодГНС 							= ГоловнаяОрганизация.ГНС.Код;
	ОрганизацияНаменованиеПолное    = ГоловнаяОрганизация.НаименованиеПолное
										+ ?(ЗначениеЗаполнено(ГоловнаяОрганизация.НаименованиеПолное), Символы.ПС + " (", "")
										+ Организация.НаименованиеПолное
										+ ?(ЗначениеЗаполнено(ГоловнаяОрганизация.НаименованиеПолное), ")", "");	

	ОбластьШапка.Параметры.ОрганизацияНаименованиеПолное = ОрганизацияНаменованиеПолное;

	Если ЗначениеЗаполнено(ОКПО) Тогда 
		ОбластьШапка.Параметры.ОКПО1 = Сред(ОКПО,1,1);
		ОбластьШапка.Параметры.ОКПО2 = Сред(ОКПО,2,1);
		ОбластьШапка.Параметры.ОКПО3 = Сред(ОКПО,3,1);
		ОбластьШапка.Параметры.ОКПО4 = Сред(ОКПО,4,1);
		ОбластьШапка.Параметры.ОКПО5 = Сред(ОКПО,5,1);
		ОбластьШапка.Параметры.ОКПО6 = Сред(ОКПО,6,1);
		ОбластьШапка.Параметры.ОКПО7 = Сред(ОКПО,7,1);
		ОбластьШапка.Параметры.ОКПО8 = Сред(ОКПО,8,1);
	КонецЕсли;
	
	ОбластьШапка.Параметры.ИНН1 = Сред(ИНН, 1, 1);
	ОбластьШапка.Параметры.ИНН2 = Сред(ИНН, 2, 1);
	ОбластьШапка.Параметры.ИНН3 = Сред(ИНН, 3, 1);                           
	ОбластьШапка.Параметры.ИНН4 = Сред(ИНН, 4, 1);
	ОбластьШапка.Параметры.ИНН5 = Сред(ИНН, 5, 1);
	ОбластьШапка.Параметры.ИНН6 = Сред(ИНН, 6, 1);
	ОбластьШапка.Параметры.ИНН7 = Сред(ИНН, 7, 1);
	ОбластьШапка.Параметры.ИНН8 = Сред(ИНН, 8, 1);
	ОбластьШапка.Параметры.ИНН9 = Сред(ИНН, 9, 1);
	ОбластьШапка.Параметры.ИНН10 = Сред(ИНН, 10, 1);
	ОбластьШапка.Параметры.ИНН11 = Сред(ИНН, 11, 1);
	ОбластьШапка.Параметры.ИНН12 = Сред(ИНН, 12, 1);
	ОбластьШапка.Параметры.ИНН13 = Сред(ИНН, 13, 1);
	ОбластьШапка.Параметры.ИНН14 = Сред(ИНН, 14, 1);
	
	ОбластьШапка.Параметры.Налоговая = Организация.ГНС;
	
	ОбластьШапка.Параметры.Код1 = Сред(КодГНС, 1, 1);
	ОбластьШапка.Параметры.Код2 = Сред(КодГНС, 2, 1);
	ОбластьШапка.Параметры.Код3 = Сред(КодГНС, 3, 1);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
