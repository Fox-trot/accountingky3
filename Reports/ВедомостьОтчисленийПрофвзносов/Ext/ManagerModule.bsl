#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       см. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина);
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "");
	НастройкиВарианта.Описание = НСтр("ru = 'Ведомость отчислений профвзносов.'");

	НастройкиВарианта.НастройкиДляПоиска.НаименованияПолей =
		НСтр("ru = 'Сумма дохода
		|Сумма взноса
		|Возврат профвзносов
		|0.4 %
		|Комментарий'");
	НастройкиВарианта.НастройкиДляПоиска.НаименованияПараметровИОтборов =
		НСтр("ru = 'НачалоПериода
		|КонецПериода
		|Организация'");
	НастройкиВарианта.НастройкиДляПоиска.КлючевыеСлова = НСтр("ru = 'Ведомость отчислений профвзносов'");
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Процедура формирования отчета.
//
// Параметры:
// ПараметрыОтчета - структура - набор параметров, необходимых для построения отчета.
// 	АдресХранилища - адрес временного хранилища.
Процедура Сформировать(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатФормирования = Новый Структура("Результат, ОписаниеОшибки", Неопределено, "");
	СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования);
	ПоместитьВоВременноеХранилище(РезультатФормирования, АдресХранилища);
	
КонецПроцедуры

// Процедура - Сформировать табличный документ
//
// Параметры:
//  СтруктураПараметров	 - Структура - Параметры формирования отчета.
//  РезультатФормирования	 	- Структура - 
//
Процедура СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ВедомостьОтчисленийПрофвзносов_Ведомость";
	
	Организация = СтруктураПараметров.Организация;
	НачалоПериода = НачалоДня(СтруктураПараметров.НачалоПериода);
	КонецПериода = КонецДня(СтруктураПараметров.КонецПериода);
	
	Макет = ПолучитьМакет("ПФ_MXL_Ведомость");

	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.ПериодПредставление = ПредставлениеПериода(НачалоПериода, КонецПериода);
	ОбластьШапка.Параметры.Организация = Организация;
	ТабличныйДокумент.Вывести(ОбластьШапка);
		
	Запрос = Новый Запрос;
	ТекстЗапроса =	
	
	"ВЫБРАТЬ
	|	НалогиПоЗаработнойПлатеОбороты.Физлицо КАК Физлицо,
	|	НалогиПоЗаработнойПлатеОбороты.СуммаОборот КАК СуммаВзноса
	|ПОМЕСТИТЬ ВременнаяТаблицаНачисленныеПрофВзносы
	|ИЗ
	|	РегистрНакопления.НалогиПоЗаработнойПлате.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И (ВидНалога = ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.Кыргызпрофсож)
	|					ИЛИ ВидНалога = ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.Рабпрофсож))) КАК НалогиПоЗаработнойПлатеОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ФизЛицо КАК ФизЛицо,
	|	СУММА(Начисления.Результат) КАК СуммаОбагаемая
	|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Начисления.Организация = &Организация
	|	И НЕ Начисления.Результат = 0
	|	И Начисления.ВидРасчета.ОблагаетсяПрофВзнос
	|	И Начисления.ФизЛицо В
	|			(ВЫБРАТЬ
	|				ВременнаяТаблицаНачисленныеПрофВзносы.Физлицо
	|			ИЗ
	|				ВременнаяТаблицаНачисленныеПрофВзносы КАК ВременнаяТаблицаНачисленныеПрофВзносы)
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВременнаяТаблицаНалоги.СуммаОбагаемая) КАК СуммаОбагаемая,
	|	СУММА(ВременнаяТаблицаНачисленныеПрофВзносы.СуммаВзноса) КАК СуммаВзноса,
	|	СУММА(ВременнаяТаблицаНачисленныеПрофВзносы.СуммаВзноса * 0.4) КАК Сумма04
	|ИЗ
	|	ВременнаяТаблицаНачисленныеПрофВзносы КАК ВременнаяТаблицаНачисленныеПрофВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНалоги
	|		ПО ВременнаяТаблицаНачисленныеПрофВзносы.Физлицо = ВременнаяТаблицаНалоги.ФизЛицо
	|
	|";	
	
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",	КонецДня(КонецПериода));
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организация = &Организация", "Истина");
	КонецЕсли;		
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	ОбластьМакета	= Макет.ПолучитьОбласть("Строка");

	Пока Выборка.Следующий() Цикл 		
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
	РезультатФормирования.Результат = ТабличныйДокумент;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
