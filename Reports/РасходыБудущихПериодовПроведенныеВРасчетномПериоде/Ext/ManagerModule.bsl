#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       см. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина);
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "");
	НастройкиВарианта.Описание = НСтр("ru = 'Расходы будущих периодов, проведенные в расчетном периоде.'");

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Процедура формирования отчета.
//
// Параметры:
// ПараметрыОтчета - структура - набор параметров, необходимых для построения отчета.
// 	АдресХранилища - адрес временного хранилища.
Процедура Сформировать(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатФормирования = Новый Структура("Результат, ОписаниеОшибки", Неопределено, "");
	СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования);
	ПоместитьВоВременноеХранилище(РезультатФормирования, АдресХранилища);
	
КонецПроцедуры

// Процедура - Сформировать табличный документ
//
// Параметры:
//  СтруктураПараметров	 - Структура - Параметры формирования отчета.
//  РезультатФормирования	 	- Структура - 
//
Процедура СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Организация = СтруктураПараметров.Организация;
	НачалоПериода = НачалоДня(СтруктураПараметров.НачалоПериода);
	КонецПериода = КонецДня(СтруктураПараметров.КонецПериода);
	
	Макет = ПолучитьМакет("ПФ_MXL_РасходыБудущихПериодов");

	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.ПериодПредставление = ПредставлениеПериода(НачалоПериода, КонецПериода);
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	ТекстЗапроса =		
	"ВЫБРАТЬ
	|	Начисления1.ФизЛицо КАК Сотрудник,
	|	Начисления1.ФизЛицо.Код КАК ТабельныйНомер,
	|	СУММА(Начисления1.Результат) КАК Сумма1,
	|	Начисления1.Регистратор КАК Документ1
	|ПОМЕСТИТЬ ВременнаяТаблицаМесяц1
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления1
	|ГДЕ
	|	Начисления1.ПериодРегистрации = &ПериодРегистрации1
	|	И Начисления1.Результат <> 0
	|	И Начисления1.Регистратор.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления1.ФизЛицо,
	|	Начисления1.Регистратор,
	|	Начисления1.ФизЛицо.Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления2.ФизЛицо КАК Сотрудник,
	|	Начисления2.ФизЛицо.Код КАК ТабельныйНомер,
	|	СУММА(Начисления2.Результат) КАК Сумма2,
	|	Начисления2.Регистратор КАК Документ2
	|ПОМЕСТИТЬ ВременнаяТаблицаМесяц2
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления2
	|ГДЕ
	|	Начисления2.ПериодРегистрации = &ПериодРегистрации2
	|	И Начисления2.Результат <> 0
	|	И Начисления2.Регистратор.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления2.ФизЛицо,
	|	Начисления2.Регистратор,
	|	Начисления2.ФизЛицо.Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВременнаяТаблицаМесяц1.Сотрудник, ВременнаяТаблицаМесяц2.Сотрудник) КАК Сотрудник,
	|	ЕСТЬNULL(ВременнаяТаблицаМесяц1.ТабельныйНомер, ВременнаяТаблицаМесяц2.ТабельныйНомер) КАК ТабельныйНомер,
	|	ВременнаяТаблицаМесяц1.Сумма1,
	|	ВременнаяТаблицаМесяц1.Документ1,
	|	ВременнаяТаблицаМесяц2.Сумма2,
	|	ВременнаяТаблицаМесяц2.Документ2,
	|	ЕСТЬNULL(ВременнаяТаблицаМесяц1.Сумма1, 0) + ЕСТЬNULL(ВременнаяТаблицаМесяц2.Сумма2, 0) КАК Итого
	|ИЗ
	|	ВременнаяТаблицаМесяц1 КАК ВременнаяТаблицаМесяц1
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВременнаяТаблицаМесяц2 КАК ВременнаяТаблицаМесяц2
	|		ПО ВременнаяТаблицаМесяц1.Сотрудник = ВременнаяТаблицаМесяц2.Сотрудник
	|ГДЕ
	|	ЕСТЬNULL(ВременнаяТаблицаМесяц1.Сумма1, 0) + ЕСТЬNULL(ВременнаяТаблицаМесяц2.Сумма2, 0) <> 0
	|ИТОГИ ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",	КонецПериода);
	Запрос.УстановитьПараметр("ПериодРегистрации1",	ДобавитьМесяц(НачалоПериода, 1));
	Запрос.УстановитьПараметр("ПериодРегистрации2",	ДобавитьМесяц(НачалоПериода, 2));

	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организация = &Организация", "Истина");
	КонецЕсли;		
	
	Запрос.Текст = ТекстЗапроса;
	ВыборкаОбщая = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	
	ОбластьШапкаТаблицы	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапкаТаблицы.Параметры.МесяцПлюс1  = ПредставлениеПериода(ДобавитьМесяц(НачалоПериода, 1), КонецМесяца(ДобавитьМесяц(НачалоПериода, 1)));
	ОбластьШапкаТаблицы.Параметры.МесяцПлюс2  = ПредставлениеПериода(ДобавитьМесяц(НачалоПериода, 2), КонецМесяца(ДобавитьМесяц(НачалоПериода, 2)));
    ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);	
	
	Пока ВыборкаОбщая.Следующий() Цикл		
		ВыборкаСтрока = ВыборкаОбщая.Выбрать();
		Пока ВыборкаСтрока.Следующий() Цикл
			ОбластьСтрока	= Макет.ПолучитьОбласть("Строка");
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрока);
		    ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;												
		ИтогОбщий	= Макет.ПолучитьОбласть("ИтогоПоПредприятию");
		ИтогОбщий.Параметры.Заполнить(ВыборкаОбщая);
		ТабличныйДокумент.Вывести(ИтогОбщий);		
	КонецЦикла;	
		
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
	РезультатФормирования.Результат = ТабличныйДокумент;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
