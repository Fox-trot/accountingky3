#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       см. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина);
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "");
	НастройкиВарианта.Описание = НСтр("ru = 'Журнал закупок.'");

	НастройкиВарианта.НастройкиДляПоиска.НаименованияПолей =
		НСтр("ru = 'Дата СФ
		|Счет-фактура
		|Док. поставки
		|Дата поставки
		|Поставщик,ИНН
		|Наименование поставки
		|Сумма без НДС
		|НДС
		|Необл.сумма
		|Всего'");
	НастройкиВарианта.НастройкиДляПоиска.НаименованияПараметровИОтборов =
		НСтр("ru = 'НачалоПериода
		|КонецПериода
		|СортировкаОтчета
		|ГруппировкаОтчета
		|Организация'");
	НастройкиВарианта.НастройкиДляПоиска.КлючевыеСлова = НСтр("ru = 'Журнал закупок'");
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Процедура формирования отчета.
//
// Параметры:
// ПараметрыОтчета - структура - набор параметров, необходимых для построения отчета.
// 	АдресХранилища - адрес временного хранилища.
Процедура Сформировать(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатФормирования = Новый Структура("Результат, ОписаниеОшибки", Неопределено, "");
	СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования);
	ПоместитьВоВременноеХранилище(РезультатФормирования, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ЖурналЗакупок_ЖурналЗакупок";
	                                                                                
	Организация 	= СтруктураПараметров.Организация;
	НачалоПериода 	= НачалоМесяца(СтруктураПараметров.НачалоПериода);
	КонецПериода 	= КонецДня(СтруктураПараметров.КонецПериода);
	Сортировка 		= СтруктураПараметров.Сортировка;
	Группировка		= СтруктураПараметров.Группировка;
	
	// Опеределение макета
	Макет = Отчеты.ШаблонБезСКД.ПолучитьМакет("ПФ_MXL_ЖурналЗакупок");
	
	// Реализация
	Запрос = Новый Запрос;
	ОсновнойТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПоступленияОбороты.Контрагент КАК Контрагент,
		|	ПоступленияОбороты.Контрагент.ИНН КАК КонтрагентИНН,
		|	ПоступленияОбороты.Контрагент.Наименование КАК КонтрагентНаименование,
		|	ПоступленияОбороты.ПериодСекунда КАК ДатаПоставки,
		|	ЕСТЬNULL(СчетаФактурыПолученные.ДатаСФ, ПоступленияИтоги.ДатаСФ) КАК ДатаСФ,
		|	ЕСТЬNULL(ЕСТЬNULL(СчетаФактурыПолученные.СерияБланкаСФ, ПоступленияИтоги.СерияБланкаСФ), """") КАК СерияБланкаСФ,
		|	ЕСТЬNULL(ЕСТЬNULL(СчетаФактурыПолученные.НомерБланкаСФ, ПоступленияИтоги.НомерБланкаСФ), """") КАК НомерБланкаСФ,
		|	ПоступленияОбороты.Номенклатура КАК Номенклатура,
		|	ПоступленияОбороты.Регистратор КАК ДокументПоставки,
		|	""Ставка НДС: Стандарт"" КАК СтавкаНДССтрокой,
		|	ПоступленияОбороты.СуммаОборот КАК Сумма,
		|	ПоступленияОбороты.СуммаНДСОборот КАК СуммаНДС,
		|	ПоступленияОбороты.СуммаНСПОборот КАК СуммаНСП,
		|	ПоступленияОбороты.СуммаОборот + ПоступленияОбороты.СуммаНДСОборот + ПоступленияОбороты.СуммаНСПОборот КАК Всего,
		|	1 КАК Порядок
		|ПОМЕСТИТЬ ВременнаяТаблицаОбщая
		|ИЗ
		|	РегистрНакопления.ПоступлениеТоваров.Обороты(&НачалоПериода, &КонецПериода, Авто, Организация = &Организация) КАК ПоступленияОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПоступлении КАК ПоступленияИтоги
		|		ПО (ПоступленияИтоги.ДокументСсылка = ПоступленияОбороты.Регистратор)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаФактурыПолученные КАК СчетаФактурыПолученные
		|		ПО (СчетаФактурыПолученные.Документ = ПоступленияОбороты.Регистратор)
		|ГДЕ
		|	ПоступленияОбороты.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Стандарт)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОбщая.Порядок КАК Порядок,
		|	ВременнаяТаблицаОбщая.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаОбщая.КонтрагентНаименование + ВЫБОР
		|		КОГДА ВременнаяТаблицаОбщая.КонтрагентИНН = """"
		|			ТОГДА """"
		|		ИНАЧЕ "", ""
		|	КОНЕЦ + ВременнаяТаблицаОбщая.КонтрагентИНН КАК КонтрагентНаименованиеИНН,
		|	ВременнаяТаблицаОбщая.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаОбщая.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВременнаяТаблицаОбщая.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаОбщая.Номенклатура ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаОбщая.Номенклатура КАК Справочник.Номенклатура).Наименование
		|		КОГДА ВременнаяТаблицаОбщая.Номенклатура ССЫЛКА Справочник.ОсновныеСредства
		|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаОбщая.Номенклатура КАК Справочник.ОсновныеСредства).Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НоменклатураНаименование,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаОбщая.ДокументПоставки) КАК ДокументПоставкиПредставление,
		|	ВременнаяТаблицаОбщая.Сумма КАК Сумма,
		|	ВременнаяТаблицаОбщая.СуммаНДС КАК СуммаНДС,
		|	ВременнаяТаблицаОбщая.СуммаНСП КАК СуммаНСП,
		|	ВременнаяТаблицаОбщая.Всего КАК Всего,
		|	ВременнаяТаблицаОбщая.СтавкаНДССтрокой КАК СтавкаНДССтрокой,
		|	ВременнаяТаблицаОбщая.ДатаСФ КАК ДатаСФ,
		|	ВременнаяТаблицаОбщая.ДатаПоставки КАК ДатаПоставки,
		|	ВременнаяТаблицаОбщая.СерияБланкаСФ + ВЫБОР
		|		КОГДА ВременнаяТаблицаОбщая.СерияБланкаСФ = """"
		|			ТОГДА """"
		|		ИНАЧЕ "" № ""
		|	КОНЕЦ + (ВЫРАЗИТЬ(ВременнаяТаблицаОбщая.НомерБланкаСФ КАК СТРОКА(8))) КАК СчетФактура
		|ИЗ
		|	ВременнаяТаблицаОбщая КАК ВременнаяТаблицаОбщая";
		
	ТекстГруппировка = "";
	Если Сортировка = "По счет-фактуре" Тогда
		ТекстГруппировка = 
	    "
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	СерияБланкаСФ,
		|	НомерБланкаСФ";	
			
	ИначеЕсли Сортировка = "По контрагенту" Тогда
		ТекстГруппировка =
	    "
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,		
		|	Контрагент";	
	Иначе
		ТекстГруппировка =
	    "
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,		
		|	ДатаПоставки";	
	КонецЕсли;	
			
	Если Группировка = "По контрагенту" ИЛИ НЕ ЗначениеЗаполнено(Группировка) Тогда
		ТекстГруппировка = ТекстГруппировка + 
	    "
		|ИТОГИ
		|	СУММА(Сумма),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаНСП),
		|	СУММА(Всего)
		|ПО
		|	ОБЩИЕ,
		|	СтавкаНДССтрокой,
		|	Контрагент";	
	ИначеЕсли Группировка = "По позициям" Тогда
		ТекстГруппировка = ТекстГруппировка + 
	    "
		|ИТОГИ
		|	СУММА(Сумма),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаНСП),
		|	СУММА(Всего)
		|ПО
		|	ОБЩИЕ,
		|	СтавкаНДССтрокой,
		|	Номенклатура";
	ИначеЕсли Группировка = "По документам" Тогда
		ТекстГруппировка = ТекстГруппировка + 
	    "
		|ИТОГИ
		|	СУММА(Сумма),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаНСП),
		|	СУММА(Всего)
		|ПО
		|	ОБЩИЕ,
		|	СтавкаНДССтрокой,
		|	ДокументПоставки";		
	КонецЕсли;
	
	Запрос.Текст = ОсновнойТекстЗапроса + ТекстГруппировка;
	
	Запрос.УстановитьПараметр("НачалоПериода", 	НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	КонецПериода);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	РезультатЗапроса 		= Запрос.Выполнить();
	ВыборкаПоступлениеИтоги	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Заполнение шапки
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода);
	ТабличныйДокумент.Вывести(ОбластьШапка);	
			
	ОбластьШапкаТаблицы 		=  Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
	
	ВывестиИтоги(ТабличныйДокумент, Макет, ВыборкаПоступлениеИтоги);
	ВыборкаПоступлениеИтоги.Сбросить();
	
	Пока ВыборкаПоступлениеИтоги.Следующий() Цикл		
		ВыборкаПоСтавке = ВыборкаПоступлениеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСтавке.Следующий() Цикл
			ОбластьГруппировка = Макет.ПолучитьОбласть("Группировка");
			ОбластьГруппировка.Параметры.Группировка = ВыборкаПоСтавке.СтавкаНДССтрокой;
			ОбластьГруппировка.Параметры.Заполнить(ВыборкаПоСтавке);
			ТабличныйДокумент.Вывести(ОбластьГруппировка, 2);
		
			ВыборкаПоКонтрагенту = ВыборкаПоСтавке.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоКонтрагенту.Следующий() Цикл
				ОбластьКонтрагент = Макет.ПолучитьОбласть("Группировка");
				Если Группировка = "По контрагенту" ИЛИ НЕ ЗначениеЗаполнено(Группировка) Тогда
					ОбластьКонтрагент.Параметры.Группировка = ВыборкаПоКонтрагенту.Контрагент;
				ИначеЕсли Группировка = "По позициям" Тогда
					ОбластьКонтрагент.Параметры.Группировка = ВыборкаПоКонтрагенту.Номенклатура;
				ИначеЕсли Группировка = "По документам" Тогда
					ОбластьКонтрагент.Параметры.Группировка = ВыборкаПоКонтрагенту.ДокументПоставки;
				КонецЕсли;
				ОбластьКонтрагент.Параметры.Заполнить(ВыборкаПоКонтрагенту);
				ТабличныйДокумент.Вывести(ОбластьКонтрагент, 3);
				
				ВыборкаДетальныеЗаписиПоступление = ВыборкаПоКонтрагенту.Выбрать();			
				Пока ВыборкаДетальныеЗаписиПоступление.Следующий() Цикл
					ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");					
					ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаДетальныеЗаписиПоступление);
					ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы, 4);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
		
	ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();

	РезультатФормирования.Результат = ТабличныйДокумент;
	
КонецПроцедуры

Процедура ВывестиИтоги(ТабличныйДокумент, Макет, ВыборкаПоступлениеИтоги)

	Пока ВыборкаПоступлениеИтоги.Следующий() Цикл
		ОбластьГруппировка = Макет.ПолучитьОбласть("Итог");
		ОбластьГруппировка.Параметры.Заполнить(ВыборкаПоступлениеИтоги);
		ОбластьГруппировка.Параметры.Группировка = "Итог:";
		ТабличныйДокумент.Вывести(ОбластьГруппировка, 1);			
		
		ВыборкаПоСтавке = ВыборкаПоступлениеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСтавке.Следующий() Цикл
			ОбластьГруппировка = Макет.ПолучитьОбласть("Итог");
			ОбластьГруппировка.Параметры.Заполнить(ВыборкаПоСтавке);
			Если ВыборкаПоСтавке.СтавкаНДССтрокой = "Ставка НДС: Стандарт" Тогда
				ОбластьГруппировка.Параметры.Группировка = "в.ч. стоимость мат. ресурсов, приобретенных с НДС";
			ИначеЕсли ВыборкаПоСтавке.СтавкаНДССтрокой = "Ставка НДС: Без НДС" Тогда
				ОбластьГруппировка.Параметры.Группировка = "в.ч. стоимость мат. ресурсов, приобретенных без НДС";
			КонецЕсли;
			ТабличныйДокумент.Вывести(ОбластьГруппировка, 2);		
		КонецЦикла;
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти

#КонецЕсли
