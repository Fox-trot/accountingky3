#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура СформироватьТабличныйДокумент(ТабличныйДокумент, ВидОтчета, СтПериод) Экспорт
			
	Если ВидОтчета = "Обороты" Тогда 
		СхемаКомпоновкиДанных = ЭтотОбъект.ПолучитьМакет("СхемаВалютныйПоВалюте");		
	ИначеЕсли ВидОтчета = "Расшифровка" Тогда 
		СхемаКомпоновкиДанных = ЭтотОбъект.ПолучитьМакет("СхемаРасшифровка");		
	КонецЕсли;

	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	// Параметры
	ПараметрДанныхСтПериод = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("СтПериод");
	ПараметрДанныхСтПериод.Значение = СтПериод;
	ПараметрДанныхСтПериод.Использование = Истина;
	Если ВидОтчета = "Расшифровка" Тогда 
		ПараметрДанныхСубконто1 = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Субконто1");
		ПараметрДанныхСубконто1.Значение = Субконто1;
		ПараметрДанныхСубконто1.Использование = Истина;
	КонецЕсли;
	
	// Отборы	
	НовыйЭлементОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("Организация")).Поле;
	НовыйЭлементОтбора.ПравоеЗначение = Организация;

	КомпановщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпановки   = КомпановщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпановки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпановки.Инициализировать(МакетКомпановки,, ДанныеРасшифровки, Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	
	ПроцессорВывода.НачатьВывод();
	
	//вывод отчета
	Пока Истина Цикл
		ЭлементРезультата = ПроцессорКомпановки.Следующий();
		Если ЭлементРезультата = Неопределено Тогда 
			Прервать;
		Иначе 
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		КонецЕсли;	
	КонецЦикла;	
	
	ПроцессорВывода.ЗакончитьВывод();

КонецПроцедуры

Процедура СформироватьТабличныйДокументМониторИзДокумента(ТабличныйДокумент, СтруктураДанных) Экспорт
			
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.МониторДенежныхСредств.РасшифровкаДокумента");
	
	ОбластьМакета	= Макет.ПолучитьОбласть("Строка");
	Для каждого Поле Из СтруктураДанных Цикл
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Поле);
		ТабличныйДокумент.Вывести(ОбластьМакета);	
	КонецЦикла;

КонецПроцедуры


#КонецЕсли