#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       см. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина);

	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет об использование бланков счет-фактур НДС подробный.'");

	НастройкиВарианта.НастройкиДляПоиска.НаименованияПолей =
		НСтр("ru = 'Индентификационный номер плательщика
		|Наименование предприятия
		|Юридический адрес предприятия
		|№
		|Наименование
		|Кол-во комплек - тов
		|ручное заполнение
		|компьютерное заполнение
		|Номера бланков
		|Руководитель предприятия
		|Главный бухгалтер'");
	НастройкиВарианта.НастройкиДляПоиска.НаименованияПараметровИОтборов =
		НСтр("ru = 'НачалоПериода
		|КонецПериода
		|Организация'");
	НастройкиВарианта.НастройкиДляПоиска.КлючевыеСлова = НСтр("ru = 'Отчет об использование бланков счет-фактур НДС подробный'");
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Процедура формирования отчета.
//
// Параметры:
// ПараметрыОтчета - структура - набор параметров, необходимых для построения отчета.
// 	АдресХранилища - адрес временного хранилища.
Процедура Сформировать(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатФормирования = Новый Структура("Результат, ОписаниеОшибки", Неопределено, "");
	СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования);
	ПоместитьВоВременноеХранилище(РезультатФормирования, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьТабличныйДокумент(СтруктураПараметров, РезультатФормирования) 
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать = Истина;
	
	Организация = СтруктураПараметров.Организация;
	НачалоПериода = СтруктураПараметров.НачалоПериода;
	КонецПериода = СтруктураПараметров.КонецПериода;
	
	Макет = ПолучитьМакет("ПФ_MXL_ОтчетИспользованиеПодробный");

	АдресОрганизации	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Организация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);

	ОбластьШапка		= Макет.ПолучитьОбласть("Шапка");	
	ОбластьШапкаТаблицы	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьГруппы		= Макет.ПолучитьОбласть("Группа");
	ОбластьДетали		= Макет.ПолучитьОбласть("Детали");
	ОбластьПодписи		= Макет.ПолучитьОбласть("Подписи");	
	
	ОбластьШапка.Параметры.НаименованиеОрганизации = Организация.НаименованиеПолное;
	
	ОбластьШапка.Параметры.ИНН 						= Организация.ИНН;
	ОбластьШапка.Параметры.НаименованиеОрганизации 	= Организация.НаименованиеПолное;
	ОбластьШапка.Параметры.Адрес 					= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Организация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.Текст =		
		"ВЫБРАТЬ
		|	СУММА(БланкиСФОстатки.КоличествоОстаток) КАК КоличествоВсего,
		|	БланкиСФОстатки.Серия КАК Серия,
		|	БланкиСФОстатки.Номер КАК БланкНомер
		|ПОМЕСТИТЬ ОстаткиНачало
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Остатки(&НачалоПериода, Организация = &Организация) КАК БланкиСФОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОстатки.Номер,
		|	БланкиСФОстатки.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(БланкиСФОстатки.КоличествоОстаток) КАК КоличествоВсего,
		|	БланкиСФОстатки.Номер КАК БланкНомер,
		|	БланкиСФОстатки.Серия КАК Серия
		|ПОМЕСТИТЬ ОстаткиКонец
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Остатки(&КонецПериода, Организация = &Организация) КАК БланкиСФОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОстатки.Серия,
		|	БланкиСФОстатки.Номер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиНачало.КоличествоВсего КАК КоличествоВсего,
		|	ОстаткиНачало.Серия КАК Серия,
		|	ОстаткиНачало.БланкНомер КАК БланкНомер
		|ИЗ
		|	ОстаткиНачало КАК ОстаткиНачало
		|ГДЕ
		|	ОстаткиНачало.КоличествоВсего > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиКонец.КоличествоВсего КАК КоличествоВсего,
		|	ОстаткиКонец.БланкНомер КАК БланкНомер,
		|	ОстаткиКонец.Серия КАК Серия
		|ИЗ
		|	ОстаткиКонец КАК ОстаткиКонец
		|ГДЕ
		|	ОстаткиКонец.КоличествоВсего > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(БланкиСФОбороты.КоличествоПриход) КАК КоличествоВсего,
		|	БланкиСФОбороты.Номер КАК БланкНомер,
		|	БланкиСФОбороты.Серия КАК Серия
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ПриходБланковСФ)) КАК БланкиСФОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОбороты.Номер,
		|	БланкиСФОбороты.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(БланкиСФОбороты.КоличествоРасход) КАК КоличествоВсего,
		|	БланкиСФОбороты.Номер КАК БланкНомер,
		|	БланкиСФОбороты.Серия КАК Серия
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ИспользованиеБланковСФ)) КАК БланкиСФОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОбороты.Номер,
		|	БланкиСФОбороты.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(БланкиСФОбороты.КоличествоРасход) КАК КоличествоВсего,
		|	БланкиСФОбороты.Номер КАК БланкНомер,
		|	БланкиСФОбороты.Серия КАК Серия
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.УтеряБланковСФ)) КАК БланкиСФОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОбороты.Номер,
		|	БланкиСФОбороты.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(БланкиСФОбороты.КоличествоРасход) КАК КоличествоВсего,
		|	БланкиСФОбороты.Номер КАК БланкНомер,
		|	БланкиСФОбороты.Серия КАК Серия
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ПорчаБланковСФ)) КАК БланкиСФОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОбороты.Номер,
		|	БланкиСФОбороты.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(БланкиСФОбороты.КоличествоРасход) КАК КоличествоВсего,
		|	БланкиСФОбороты.Номер КАК БланкНомер,
		|	БланкиСФОбороты.Серия КАК Серия
		|ИЗ
		|	РегистрНакопления.БланкиСФ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ПроизводственныйБракБланковСФ)) КАК БланкиСФОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	БланкиСФОбороты.Номер,
		|	БланкиСФОбороты.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Серия,
		|	БланкНомер
		|ИТОГИ
		|	СУММА(КоличествоВсего)
		|ПО
		|	Серия";

	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));	
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация", "Истина");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ПараметрыВыборки = Новый Структура;
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[2]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	1);
	ПараметрыВыборки.Вставить("Наименование", 	"Остаток на начало");
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[4]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	2);
	ПараметрыВыборки.Вставить("Наименование", 	"Получено");	
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[5]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	3);
	ПараметрыВыборки.Вставить("Наименование", 	"Использовано");	
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[6]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	4);
	ПараметрыВыборки.Вставить("Наименование", 	"Испорчено");	
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[7]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	5);
	ПараметрыВыборки.Вставить("Наименование", 	"Утеряно");	
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[8]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	6);
	ПараметрыВыборки.Вставить("Наименование", 	"Производственный брак");	
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	ПараметрыВыборки.Вставить("Пакет", 			РезультатЗапроса[3]);
	ПараметрыВыборки.Вставить("НомерСтроки", 	7);
	ПараметрыВыборки.Вставить("Наименование", 	"Остаток на конец");		
	ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки);
	
	СтруктураРуководство = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Организация, КонецПериода);
	Если НЕ СтруктураРуководство.Свойство("Руководитель") = Неопределено Тогда
		ОбластьПодписи.Параметры.Руководитель 	= СтруктураРуководство.Руководитель;
	КонецЕсли;
	Если НЕ СтруктураРуководство.Свойство("ГлавныйБухгалтер") = Неопределено Тогда
		ОбластьПодписи.Параметры.Главбух 		= СтруктураРуководство.ГлавныйБухгалтер;
	КонецЕсли;
		
	ОбластьПодписи.Параметры.ТелефонОрганизации	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Организация, Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	ОбластьПодписи.Параметры.Год				= СтрЗаменить(Строка(Год(КонецПериода)), Символы.НПП, "") + " г.";
	
	ТабличныйДокумент.Вывести(ОбластьПодписи);
	
	РезультатФормирования.Результат = ТабличныйДокумент;
	
КонецПроцедуры

Процедура ОбработкаРезультатаЗапроса(ТабличныйДокумент, Макет, ПараметрыВыборки)
	Пакет 			= ПараметрыВыборки.Пакет;
	ОбластьГруппы 	= Макет.ПолучитьОбласть("Группа");
	ОбластьГруппы.Параметры.Заполнить(ПараметрыВыборки);
	
	ВыборкаГруппы 	= Пакет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	
	Если ВыборкаГруппы.Количество() = 0 Тогда
		ТабличныйДокумент.Вывести(ОбластьГруппы);
	    Возврат;
	КонецЕсли;
	
	Пока ВыборкаГруппы.Следующий() Цикл	
		ОбластьГруппы.Параметры.Заполнить(ВыборкаГруппы);
		ТабличныйДокумент.Вывести(ОбластьГруппы);

		ВыборкаНомера 			= ВыборкаГруппы.Выбрать();		
		Пока ВыборкаНомера.Следующий() Цикл
			ОбластьДетали 	= Макет.ПолучитьОбласть("Детали");
			ОбластьДетали.Параметры.Заполнить(ВыборкаНомера);
			ОбластьДетали.Параметры.СтрокаНомеров = ВыборкаНомера.Серия + " " + ВыборкаНомера.БланкНомер;
			ТабличныйДокумент.Вывести(ОбластьДетали);			
		КонецЦикла;
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
