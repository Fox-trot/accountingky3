#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		
#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчет() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодоходныйНалогОбороты.ФизЛицо КАК ФизЛицо,
		|	ПодоходныйНалогОбороты.ОДПНОборот КАК СуммаВыплаченная,
		|	ПодоходныйНалогОбороты.ПНОборот КАК СуммаПН
		|ПОМЕСТИТЬ ВременнаяТаблицаПН
		|ИЗ
		|	РегистрНакопления.ПодоходныйНалог.Обороты(&ДатаНачала, &ДатаОкончания, , Организация = &Организация) КАК ПодоходныйНалогОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГражданствоФизическихЛицСрезПоследних.ФизЛицо КАК Физлицо,
		|	ГражданствоФизическихЛицСрезПоследних.Страна КАК Страна
		|ПОМЕСТИТЬ ВременнаяТаблицаГражданство
		|ИЗ
		|	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних(
		|			&ДатаОкончания,
		|			Физлицо В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаПН.ФизЛицо
		|				ИЗ
		|					ВременнаяТаблицаПН КАК ВременнаяТаблицаПН)) КАК ГражданствоФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПН.ФизЛицо КАК Физлицо,
		|	ВременнаяТаблицаПН.СуммаВыплаченная КАК СуммаВыплаченная,
		|	ВременнаяТаблицаПН.СуммаПН КАК СуммаПН,
		|	ВременнаяТаблицаГражданство.Страна.Код КАК КодСтраны,
		|	ВременнаяТаблицаПН.ФизЛицо.ИНН КАК НомерНалоговойРегистрации
		|ИЗ
		|	ВременнаяТаблицаПН КАК ВременнаяТаблицаПН
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаГражданство КАК ВременнаяТаблицаГражданство
		|		ПО ВременнаяТаблицаПН.ФизЛицо = ВременнаяТаблицаГражданство.Физлицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	Физлицо
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ДатаОкончания));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Отчет.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
			
КонецПроцедуры // ЗаполнитьТабличнуюЧастьОтчет()

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетГодовой058() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодоходныйНалогОбороты.Физлицо КАК Физлицо,
	|	СУММА(ПодоходныйНалогОбороты.ОДПНОборот) КАК СуммаВыплаченная,
	|	СУММА(ПодоходныйНалогОбороты.ПНОборот) КАК СуммаПН,
	|	ПодоходныйНалогОбороты.Физлицо.ИНН КАК ИНН,
	|	""001"" КАК КодДохода
	|ПОМЕСТИТЬ ВременнаяТаблицаПН
	|ИЗ
	|	РегистрНакопления.ПодоходныйНалог.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			,
	|			Организация = &Организация) КАК ПодоходныйНалогОбороты
	|ГДЕ
	|	ПодоходныйНалогОбороты.ОДПНОборот > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодоходныйНалогОбороты.Физлицо,
	|	ПодоходныйНалогОбороты.Физлицо.ИНН
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ФизЛицо КАК ФизЛицо,
	|	СУММА(Начисления.Результат) КАК СуммаМатВыгоды
	|ПОМЕСТИТЬ ВременнаяТаблицаМатВыгода
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.ПериодРегистрации МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Начисления.Организация = &Организация
	|	И Начисления.ФизЛицо В
	|			(ВЫБРАТЬ
	|				ВременнаяТаблицаПН.Физлицо
	|			ИЗ
	|				ВременнаяТаблицаПН КАК ВременнаяТаблицаПН)
	|	И Начисления.ВидРасчета.Код = ""101""
	|	И Начисления.ВидРасчета.ОблагаетсяПН
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаПН.Физлицо КАК Физлицо,
	|	ВременнаяТаблицаПН.СуммаВыплаченная КАК СуммаВыплаченная,
	|	ВременнаяТаблицаПН.СуммаПН КАК СуммаПН,
	|	ВременнаяТаблицаПН.ИНН КАК ИНН,
	|	ВременнаяТаблицаПН.КодДохода КАК КодДохода,
	|	ВременнаяТаблицаМатВыгода.СуммаМатВыгоды КАК СуммаМатериальнойВыгоды,
	//|	ВЫРАЗИТЬ(ВременнаяТаблицаПН.СуммаПН * (ВременнаяТаблицаМатВыгода.СуммаМатВыгоды * 100 / ВременнаяТаблицаПН.СуммаВыплаченная) / 100 КАК ЧИСЛО(15, 2)) КАК СуммаПНСМатериальнойВыгоды
	|	ВЫРАЗИТЬ(ВременнаяТаблицаМатВыгода.СуммаМатВыгоды * 0.1 КАК ЧИСЛО(15, 2)) КАК СуммаПНСМатериальнойВыгоды
	|ИЗ
	|	ВременнаяТаблицаПН КАК ВременнаяТаблицаПН
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаМатВыгода КАК ВременнаяТаблицаМатВыгода
	|		ПО ВременнаяТаблицаПН.Физлицо = ВременнаяТаблицаМатВыгода.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Физлицо
	|АВТОУПОРЯДОЧИВАНИЕ";
		
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ДатаОкончания));
	Запрос.УстановитьПараметр("Организация", Организация);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Отчет.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
			
КонецПроцедуры // ЗаполнитьТабличнуюЧастьОтчет()

#КонецОбласти

#КонецЕсли