#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьТаблицаБланкиСФ(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК Серия,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК Номер,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ИспользованиеБланковСФ) КАК Операция,		
		|	1 КАК Количество,		
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	НЕ ВременнаяТаблицаШапка.СерияБланкаСФ = """"
		|			И НЕ ВременнаяТаблицаШапка.НомерБланкаСФ = """"";		
		
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаБланкиСФ", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаСотрудники()

Процедура СформироватьТаблицаСчетаФактурыВыписанные(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаТоварыИУслуги.ДокументРеализации КАК ДокументРеализации,
	|	ВременнаяТаблицаТоварыИУслуги.ДокументРеализации.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаТоварыИУслуги.ДокументРеализации.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫРАЗИТЬ(ВременнаяТаблицаТоварыИУслуги.ДокументРеализации.Контрагент КАК Справочник.Контрагенты).СтранаРезидентства КАК СтранаРезидентства,
	|	ВЫРАЗИТЬ(ВременнаяТаблицаТоварыИУслуги.ДокументРеализации.Контрагент КАК Справочник.Контрагенты).СтранаРезидентства.ЕАЭС КАК ЕАЭС,
	|	ВременнаяТаблицаТоварыИУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаТоварыИУслуги.Сумма КАК Сумма,
	|	ВременнаяТаблицаТоварыИУслуги.СуммаНДС КАК СуммаНДС,
	|	ВременнаяТаблицаТоварыИУслуги.СуммаНСП КАК СуммаНСП
	|ПОМЕСТИТЬ ВременнаяТаблицаТоварыИУслугиКонтрагенты
	|ИЗ
	|	ВременнаяТаблицаТоварыИУслуги КАК ВременнаяТаблицаТоварыИУслуги
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаТоварыИУслугиКонтрагенты.ДокументРеализации КАК Документ,
	|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК СерияБланкаСФ,
	|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
	|	ВременнаяТаблицаШапка.Возврат,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаТоварыИУслугиКонтрагенты.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.Киргизия)
	|				ИЛИ ВременнаяТаблицаТоварыИУслугиКонтрагенты.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|		КОГДА ВременнаяТаблицаТоварыИУслугиКонтрагенты.ЕАЭС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|		КОГДА ВременнаяТаблицаШапка.ДокЭкспорт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.Импэкс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ПустаяСсылка)
	|	КОНЕЦ КАК ПризнакСтраны,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.Общая
	|			ТОГДА ВременнаяТаблицаШапка.Контрагент
	|		ИНАЧЕ ВременнаяТаблицаТоварыИУслугиКонтрагенты.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.Общая
	|			ТОГДА ВременнаяТаблицаШапка.ДоговорКонтрагента
	|		ИНАЧЕ ВременнаяТаблицаТоварыИУслугиКонтрагенты.ДоговорКонтрагента
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	ВременнаяТаблицаТоварыИУслугиКонтрагенты.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаШапка.ДатаСФ КАК ДатаСФ,
	|	ВременнаяТаблицаШапка.ВидПоставкиНДС КАК ВидПоставкиНДС,
	|	ВременнаяТаблицаТоварыИУслугиКонтрагенты.Сумма КАК СуммаБезНДС,
	|	ВременнаяТаблицаТоварыИУслугиКонтрагенты.СуммаНДС КАК СуммаНДС,
	|	ВременнаяТаблицаТоварыИУслугиКонтрагенты.СуммаНСП КАК СуммаНеоблагаемая
	|ПОМЕСТИТЬ ВременнаяТаблицаОбщая
	|ИЗ
	|	ВременнаяТаблицаТоварыИУслугиКонтрагенты КАК ВременнаяТаблицаТоварыИУслугиКонтрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОбщая.Период,
	|	ВременнаяТаблицаОбщая.Организация,
	|	ВременнаяТаблицаОбщая.Документ,
	|	ВременнаяТаблицаОбщая.СерияБланкаСФ,
	|	ВременнаяТаблицаОбщая.НомерБланкаСФ,
	|	ВременнаяТаблицаОбщая.Возврат,
	|	ВременнаяТаблицаОбщая.ПризнакСтраны,
	|	ВременнаяТаблицаОбщая.Контрагент,
	|	ВременнаяТаблицаОбщая.ДоговорКонтрагента,
	|	ВременнаяТаблицаОбщая.СтавкаНДС,
	|	ВременнаяТаблицаОбщая.ДатаСФ,
	|	СУММА(ВременнаяТаблицаОбщая.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВременнаяТаблицаОбщая.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВременнаяТаблицаОбщая.СуммаНеоблагаемая) КАК СуммаНеоблагаемая,
	|	ВременнаяТаблицаОбщая.ВидПоставкиНДС
	|ИЗ
	|	ВременнаяТаблицаОбщая КАК ВременнаяТаблицаОбщая
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаОбщая.СтавкаНДС,
	|	ВременнаяТаблицаОбщая.ДоговорКонтрагента,
	|	ВременнаяТаблицаОбщая.Контрагент,
	|	ВременнаяТаблицаОбщая.ДатаСФ,
	|	ВременнаяТаблицаОбщая.Возврат,
	|	ВременнаяТаблицаОбщая.НомерБланкаСФ,
	|	ВременнаяТаблицаОбщая.Период,
	|	ВременнаяТаблицаОбщая.Документ,
	|	ВременнаяТаблицаОбщая.СерияБланкаСФ,
	|	ВременнаяТаблицаОбщая.ПризнакСтраны,
	|	ВременнаяТаблицаОбщая.Организация,
	|	ВременнаяТаблицаОбщая.ВидПоставкиНДС";
		
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСчетаФактурыВыписанные", РезультатЗапроса.Выгрузить());	
		
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СчетФактураВыписанный.Ссылка,
		|	СчетФактураВыписанный.Дата КАК Период,
		|	СчетФактураВыписанный.Организация,
		|	СчетФактураВыписанный.СерияБланкаСФ,
		|	СчетФактураВыписанный.НомерБланкаСФ,
		|	СчетФактураВыписанный.ДатаСФ,
		|	СчетФактураВыписанный.Контрагент,
		|	СчетФактураВыписанный.ДоговорКонтрагента,
		|	СчетФактураВыписанный.ВидПоставкиНДС КАК ВидПоставкиНДС,
		|	СчетФактураВыписанный.ДокЭкспорт,
		|	СчетФактураВыписанный.Возврат,
		|	СчетФактураВыписанный.Общая
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.СчетФактураВыписанный КАК СчетФактураВыписанный
		|ГДЕ
		|	СчетФактураВыписанный.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетФактураВыписанныйТоварыИУслуги.Ссылка,
		|	СчетФактураВыписанныйТоварыИУслуги.Сумма,
		|	СчетФактураВыписанныйТоварыИУслуги.СтавкаНДС,
		|	СчетФактураВыписанныйТоварыИУслуги.СуммаНДС,
		|	СчетФактураВыписанныйТоварыИУслуги.СуммаНСП,
		|	СчетФактураВыписанныйТоварыИУслуги.ДокументРеализации
		|ПОМЕСТИТЬ ВременнаяТаблицаТоварыИУслуги
		|ИЗ
		|	Документ.СчетФактураВыписанный.ТоварыИУслуги КАК СчетФактураВыписанныйТоварыИУслуги
		|ГДЕ
		|	СчетФактураВыписанныйТоварыИУслуги.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("СтавкаНДСПоУмолчанию", СтруктураДополнительныеСвойства.УчетнаяПолитика.СтавкаНДСПоУмолчанию);
	
	Запрос.Выполнить();
	
	СформироватьТаблицаБланкиСФ(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСчетаФактурыВыписанные(ДокументСсылка, СтруктураДополнительныеСвойства);
		
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьСчетФактураНДС(МассивОбъектов, ОбъектыПечати)
			
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураВыписанный";
	ТабличныйДокумент.КлючПараметровПечати = "СчетФактураНДС";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщиеМакеты.СчетФактураНДС");
	
	ТекстЗапроса =
	
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Номер,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ТоварыИУслуги.(
	|		Номенклатура,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество,
	|		Цена,
	|		СуммаНДС,
	|		СуммаНСП,
	|		НомерСтроки,
	|		СкидкаПроцент,
	|		СуммаСкидки,
	|		Сумма КАК Доход,
	|		ТаблицаДокумента.ТоварыИУслуги.Сумма + ТаблицаДокумента.ТоварыИУслуги.СуммаНДС КАК Всего,
	|		ДокументРеализации
	|	),
	|	ТаблицаДокумента.Организация.ИНН КАК ИНН,
	|	ТаблицаДокумента.Организация.ОсновнойБанковскийСчет КАК ОснБанкСчет,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Организация.ЭтоГоловнаяОрганизация = ИСТИНА
	|			ТОГДА ТаблицаДокумента.Организация.ГНС
	|		ИНАЧЕ ТаблицаДокумента.Организация.ГоловнаяОрганизация.ГНС
	|	КОНЕЦ КАК ГНС,
	|	ТаблицаДокумента.Организация.НаименованиеПолное КАК НаименованиеПолное,
	//|	ТаблицаДокумента.ТипПоставкиСФ КАК ТипПоставки,
	//|	ТаблицаДокумента.ТипПоставкиСФ.Наименование КАК ИмяПоставки,
	|	ТаблицаДокумента.ВидПлатежа,
	|	ТаблицаДокумента.Контрагент.ОсновнойБанковскийСчет КАК ОснБанкСчет_Пок,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.Контрагент.КонтактнаяИнформация.(
	|		Город КАК Поле1,
	|		Страна КАК Поле2
	|	) КАК Контрагент1,
	|	ТаблицаДокумента.ДоговорКонтрагента.СтавкаНДС,
	|	СтавкиНДССрезПоследних.Ставка,
	|	СтавкиНСПСрезПоследних.СтавкаПлательщикНДС,
	|	СтавкиНСПСрезПоследних.СтавкаНеПлательщикНДС,
	|	ТаблицаДокумента.Организация.ГоловнаяОрганизация.ИНН КАК ИННГоловнойОрганизации,
	|	ТаблицаДокумента.Организация.ГоловнаяОрганизация.НаименованиеПолное КАК НаименованиеГоловнойОрганизации,
	|	ТаблицаДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	"""" КАК АдресПоставщика
	|ИЗ
	|	Документ.СчетФактураВыписанный КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДС.СрезПоследних КАК СтавкиНДССрезПоследних
	|		ПО ТаблицаДокумента.ДоговорКонтрагента.СтавкаНДС = СтавкиНДССрезПоследних.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних КАК СтавкиНСПСрезПоследних
	|		ПО ТаблицаДокумента.ДоговорКонтрагента.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&СписокДокументов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(Выборка.Дата, Выборка.Организация);
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		
		ВременнаяТаблицаТоварыИУслуги = Выборка.ТоварыИУслуги.Выгрузить();                                        
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		Если ВременнаяТаблицаТоварыИУслуги.Количество() > 0 
			И ЗначениеЗаполнено(ВременнаяТаблицаТоварыИУслуги[0].ДокументРеализации)
			И ТипЗнч(ВременнаяТаблицаТоварыИУслуги[0].ДокументРеализации) = ТИП("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			
			ОбластьМакета.Параметры.Возврат = "Возвратная счет-фактура";					
		КонецЕсли;
		
		//////////////////////Данные шапки НАЧАЛО/////////////////////////////////////
		
		УправлениеЗапасами.ЗаполнитьШапкуСчетФактураНДС(Выборка, ОбластьМакета);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);  
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");	                             
		ТабличныйДокумент.Вывести(ОбластьМакета);
		//////////////////////Данные покупателя КОНЕЦ/////////////////////////////////////
				
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");	 
		Для Каждого Стр Из ВременнаяТаблицаТоварыИУслуги Цикл 
			ОбластьМакета.Параметры.Заполнить(Стр);
			ДанныеПечати.Вставить("Всего",стр.Доход+стр.СуммаНДС);
			ДанныеПечати.Вставить("СтавкаНДС",Выборка.Ставка);
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;	  		
				
		ИтогоСумма 		= ВременнаяТаблицаТоварыИУслуги.Итог("Доход");
		ИтогоСуммаНДС  	= ВременнаяТаблицаТоварыИУслуги.Итог("СуммаНДС");
		ИтогоНСП 		= ВременнаяТаблицаТоварыИУслуги.Итог("СуммаНСП");
		ИтогоСндс       = ИтогоСумма + ИтогоСуммаНДС;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");
		ОбластьМакета.Параметры.ИтогоСумма 		= ИтогоСумма;
		ОбластьМакета.Параметры.ИтогоСуммаНДС	= ИтогоСуммаНДС;		
		ОбластьМакета.Параметры.ИтогоСндс 		= ИтогоСндс; 
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоНСП");
		ОбластьМакета.Параметры.ИтогоНСП = ИтогоНСП;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоВсего");
		ИтогоВсего    = ИтогоСумма + ИтогоСуммаНДС + ИтогоНСП;
		ОбластьМакета.Параметры.ИтогоВсего    =  ИтогоВсего;
		ОбластьМакета.Параметры.СуммаПрописью =  БухгалтерскийУчетСервер.СформироватьСуммуПрописью(ИтогоВсего);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		Руководители  = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Выборка.Организация, КонецДня(Выборка.Дата));
		ОбластьМакета.Параметры.ФИОРуководителя 		= Руководители.Руководитель;
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера 	= Руководители.ГлавныйБухгалтер;
		ТабличныйДокумент.Вывести(ОбластьМакета);   
		
		СуммаСкидки = ВременнаяТаблицаТоварыИУслуги.Итог("СуммаСкидки");
		
		Если СуммаСкидки <> 0 Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Скидка");
			ОбластьМакета.Параметры.СуммаСкидки	= СуммаСкидки;
			ОбластьМакета.Параметры.Сумма		= ИтогоСндс;
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;			
				
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
			
КонецФункции
	
// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактураНДС") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"СчетФактураНДС", НСтр("ru = Счет-фактура по НДС'"), ПечатьСчетФактураНДС(МассивОбъектов, ОбъектыПечати),,
		"ОбщиеМакеты.СчетФактураНДС");
	КонецЕсли;		
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактураНДС";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура НДС'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли