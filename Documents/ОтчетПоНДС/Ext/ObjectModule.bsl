#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = КонецМесяца(ТекущаяДата());
		Если ЕстьДокументВУказанныйПериод(Дата) Тогда
			Дата = Дата('00010101');
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

// В обработчике события ОбработкаПроверкиЗаполнения документа выполняется
// копирование и обнуление проверяемых реквизитов для исключения стандартной
// проверки заполнения платформой и последующей проверки средствами встроенного языка.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

// Выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Функция ЕстьДокументВУказанныйПериод(Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНДС.Ссылка
		|ИЗ
		|	Документ.ОтчетПоНДС КАК ОтчетПоНДС
		|ГДЕ
		|	НЕ ОтчетПоНДС.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ОтчетПоНДС.Дата, МЕСЯЦ) = &Дата";
	
	Запрос.УстановитьПараметр("Дата", Дата);		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 	

КонецФункции 

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетОсновной() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗакрытиеМесяца.Ссылка,
	|	ЗакрытиеМесяца.РасчетНДС
	|ИЗ
	|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
	|ГДЕ
	|	НЕ ЗакрытиеМесяца.ПометкаУдаления
	|	И КОНЕЦПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) = &Дата
	|	И ЗакрытиеМесяца.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗакрытиеМесяцаНДС.Строка,
	|	ЗакрытиеМесяцаНДС.Содержание,
	|	ЗакрытиеМесяцаНДС.Сумма
	|ИЗ
	|	Документ.ЗакрытиеМесяца.НДС КАК ЗакрытиеМесяцаНДС
	|ГДЕ
	|	ЗакрытиеМесяцаНДС.Ссылка.Дата = &Дата
	|	И ЗакрытиеМесяцаНДС.Ссылка.Проведен
	|	И ЗакрытиеМесяцаНДС.Ссылка.Организация = &Организация";		
			
	Запрос.УстановитьПараметр("Организация", 	Организация);	
	Запрос.УстановитьПараметр("Дата", 			КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаЗМ = РезультатЗапроса[0].Выбрать();	
	Если ВыборкаЗМ.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет документа ""Закрытие месяца""! Табличная часть ""Отчет по НДС"" не может быть заполнена'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;	
	ИначеЕсли ВыборкаЗМ.Следующий() И НЕ ВыборкаЗМ.РасчетНДС Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В документе ""Закрытие месяца"" за период ""%1"" расчет НДС не производился! Табличная часть ""Отчет по НДС"" не может быть заполнена'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;	
	КонецЕсли;
	
	ОтчетОсновной.Загрузить(РезультатЗапроса[1].Выгрузить());
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетРеестрПоставок() Экспорт	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаФактурыВыписанные.Документ КАК Документ,
	|	СчетаФактурыВыписанные.Контрагент КАК Контрагент,
	|	СчетаФактурыВыписанные.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетаФактурыВыписанные.Контрагент.ИНН КАК КонтрагентИНН,
	|	СчетаФактурыВыписанные.ДатаСФ КАК ДатаСФ,
	|	СчетаФактурыВыписанные.СерияБланкаСФ КАК СерияБланкаСФ,
	|	СчетаФактурыВыписанные.НомерБланкаСФ КАК НомерБланкаСФ,
	|	1 КАК КоличествоСтрок,
	|	ВЫБОР
	|		КОГДА СчетаФактурыВыписанные.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|			ТОГДА СчетаФактурыВыписанные.Контрагент.ГНС.Код
	|		ИНАЧЕ СчетаФактурыВыписанные.Контрагент.СтранаРезидентства.Код
	|	КОНЕЦ КАК КодГНС,
	|	СчетаФактурыВыписанные.ВидПоставкиНДС.Код КАК КодПоставки,
	|	СчетаФактурыВыписанные.Регистратор КАК СФВыписанныеРегистратор,
	|	СчетаФактурыВыписанные.Документ.Дата КАК ДатаВыписки,
	|	СУММА(СчетаФактурыВыписанные.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(СчетаФактурыВыписанные.СуммаНДС) КАК СуммаНДС,
	|	СУММА(СчетаФактурыВыписанные.СуммаНеоблагаемая) КАК СуммаНеоблагаемая,
	|	СчетаФактурыВыписанные.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаСФВыписанные
	|ИЗ
	|	РегистрСведений.СчетаФактурыВыписанные КАК СчетаФактурыВыписанные
	|ГДЕ
	|	СчетаФактурыВыписанные.ДатаСФ МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаФактурыВыписанные.Документ,
	|	СчетаФактурыВыписанные.Контрагент,
	|	СчетаФактурыВыписанные.ДоговорКонтрагента,
	|	СчетаФактурыВыписанные.ДатаСФ,
	|	СчетаФактурыВыписанные.СерияБланкаСФ,
	|	СчетаФактурыВыписанные.НомерБланкаСФ,
	|	СчетаФактурыВыписанные.ВидПоставкиНДС.Код,
	|	СчетаФактурыВыписанные.Регистратор,
	|	СчетаФактурыВыписанные.Контрагент.ИНН,
	|	СчетаФактурыВыписанные.Документ.Дата,
	|	ВЫБОР
	|		КОГДА СчетаФактурыВыписанные.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|			ТОГДА СчетаФактурыВыписанные.Контрагент.ГНС.Код
	|		ИНАЧЕ СчетаФактурыВыписанные.Контрагент.СтранаРезидентства.Код
	|	КОНЕЦ,
	|	СчетаФактурыВыписанные.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВременнаяТаблицаСФВыписанные.Контрагент, РеализацияОбороты.Контрагент) КАК Контрагент,
	|	РеализацияОбороты.Договор КАК Договор,
	|	РеализацияОбороты.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА РеализацияОбороты.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА ""ДПБУ""
	|		ИНАЧЕ РеализацияИтоги.СерияБланкаСФ
	|	КОНЕЦ КАК СерияСФ,
	|	ВЫБОР
	|		КОГДА РеализацияОбороты.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА РеализацияОбороты.ДокументРеализации.Номер
	|		ИНАЧЕ РеализацияИтоги.НомерБланкаСФ
	|	КОНЕЦ КАК НомерСФ,
	|	РеализацияОбороты.Контрагент.ИНН КАК ИНН,
	|	1 КАК КоличествоСтрок,
	|	ВЫБОР
	|		КОГДА РеализацияОбороты.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|			ТОГДА РеализацияОбороты.Контрагент.ГНС.Код
	|		ИНАЧЕ РеализацияОбороты.Контрагент.СтранаРезидентства.Код
	|	КОНЕЦ КАК КодГНС,
	|	РеализацияИтоги.КодВидаПоставкиНДС КАК КодПоставки,
	|	НАЧАЛОПЕРИОДА(РеализацияОбороты.ПериодСекунда, ДЕНЬ) КАК ДатаПоставки,
	|	СУММА(РеализацияОбороты.ДоходОборот) КАК Отгрузка,
	|	СУММА(РеализацияОбороты.СуммаНДСОборот) КАК СуммаНДС,
	|	СУММА(РеализацияОбороты.СуммаНСПОборот) КАК СуммаНСП,
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(РеализацияИтоги.ДокументРеализации.Дата, РеализацияОбороты.ПериодСекунда), ДЕНЬ) КАК Дата,
	|	СУММА(РеализацияОбороты.ДоходОборот + РеализацияОбороты.СуммаНДСОборот + ВЫБОР
	|			КОГДА &НСПВСумме
	|				ТОГДА РеализацияОбороты.СуммаНСПОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОбщаяСтоимостьСНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаОтгрузкаТекущегоМесяца
	|ИЗ
	|	РегистрНакопления.РеализацияТоваров.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Авто,
	|			Организация = &Организация
	|				И ИСТИНА) КАК РеализацияОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеализацияИтоги КАК РеализацияИтоги
	|		ПО РеализацияОбороты.ДокументРеализации = РеализацияИтоги.ДокументРеализации
	|			И РеализацияОбороты.Договор = РеализацияИтоги.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСФВыписанные КАК ВременнаяТаблицаСФВыписанные
	|		ПО РеализацияОбороты.ДокументРеализации = ВременнаяТаблицаСФВыписанные.Документ
	|ГДЕ
	|	ВременнаяТаблицаСФВыписанные.Документ ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияОбороты.СтавкаНДС,
	|	ЕСТЬNULL(ВременнаяТаблицаСФВыписанные.Контрагент, РеализацияОбороты.Контрагент),
	|	РеализацияОбороты.Договор,
	|	ВЫБОР
	|		КОГДА РеализацияОбороты.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА ""ДПБУ""
	|		ИНАЧЕ РеализацияИтоги.СерияБланкаСФ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеализацияОбороты.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА РеализацияОбороты.ДокументРеализации.Номер
	|		ИНАЧЕ РеализацияИтоги.НомерБланкаСФ
	|	КОНЕЦ,
	|	РеализацияОбороты.Контрагент.ИНН,
	|	ВЫБОР
	|		КОГДА РеализацияОбороты.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|			ТОГДА РеализацияОбороты.Контрагент.ГНС.Код
	|		ИНАЧЕ РеализацияОбороты.Контрагент.СтранаРезидентства.Код
	|	КОНЕЦ,
	|	РеализацияИтоги.КодВидаПоставкиНДС,
	|	НАЧАЛОПЕРИОДА(РеализацияОбороты.ПериодСекунда, ДЕНЬ),
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(РеализацияИтоги.ДокументРеализации.Дата, РеализацияОбороты.ПериодСекунда), ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаСФВыписанные.СФВыписанныеРегистратор.Контрагент,
	|	ВременнаяТаблицаСФВыписанные.СФВыписанныеРегистратор.ДоговорКонтрагента,
	|	ВременнаяТаблицаСФВыписанные.СтавкаНДС,
	|	ВременнаяТаблицаСФВыписанные.СерияБланкаСФ,
	|	ВременнаяТаблицаСФВыписанные.НомерБланкаСФ,
	|	ВременнаяТаблицаСФВыписанные.Контрагент.ИНН,
	|	1,
	|	ВременнаяТаблицаСФВыписанные.КодГНС,
	|	ВременнаяТаблицаСФВыписанные.КодПоставки,
	|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаСФВыписанные.ДатаСФ, ДЕНЬ),
	|	СУММА(ВременнаяТаблицаСФВыписанные.СуммаБезНДС),
	|	СУММА(ВременнаяТаблицаСФВыписанные.СуммаНДС),
	|	СУММА(ВременнаяТаблицаСФВыписанные.СуммаНеоблагаемая),
	|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаСФВыписанные.ДатаВыписки, ДЕНЬ),
	|	СУММА(ВременнаяТаблицаСФВыписанные.СуммаБезНДС + ВременнаяТаблицаСФВыписанные.СуммаНДС + ВЫБОР
	|			КОГДА &НСПВСумме
	|				ТОГДА ВременнаяТаблицаСФВыписанные.СуммаНеоблагаемая
	|			ИНАЧЕ 0
	|		КОНЕЦ)
	|ИЗ
	|	ВременнаяТаблицаСФВыписанные КАК ВременнаяТаблицаСФВыписанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаСФВыписанные.СФВыписанныеРегистратор.Контрагент,
	|	ВременнаяТаблицаСФВыписанные.СФВыписанныеРегистратор.ДоговорКонтрагента,
	|	ВременнаяТаблицаСФВыписанные.Контрагент.ИНН,
	|	ВременнаяТаблицаСФВыписанные.КодГНС,
	|	ВременнаяТаблицаСФВыписанные.КодПоставки,
	|	ВременнаяТаблицаСФВыписанные.СерияБланкаСФ,
	|	ВременнаяТаблицаСФВыписанные.НомерБланкаСФ,
	|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаСФВыписанные.ДатаВыписки, ДЕНЬ),
	|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаСФВыписанные.ДатаСФ, ДЕНЬ),
	|	ВременнаяТаблицаСФВыписанные.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.Договор КАК Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.СерияСФ КАК СерияСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.НомерСФ КАК НомерСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.ИНН КАК ИНН,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяца.КоличествоСтрок) КАК КоличествоСтрок,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.КодГНС КАК КодГНС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.КодПоставки КАК КодПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.ДатаПоставки КАК ДатаПоставки,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяца.Отгрузка) КАК Отгрузка,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяца.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяца.СуммаНСП) КАК СуммаНСП,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.Дата КАК Дата,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяца.ОбщаяСтоимостьСНДС) КАК ОбщаяСтоимостьСНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал
	|ИЗ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца КАК ВременнаяТаблицаОтгрузкаТекущегоМесяца
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.СерияСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.НомерСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.ИНН,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.ДатаПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.КодГНС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.СтавкаНДС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.КодПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяца.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор КАК Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Отгрузка) КАК Отгрузка
	|ПОМЕСТИТЬ ВременнаяТаблицаСписокКонтрагентов
	|ИЗ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал КАК ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансыДоотгрузкаОстатки.Контрагент КАК Контрагент,
	|	АвансыДоотгрузкаОстатки.Договор КАК Договор,
	|	АвансыДоотгрузкаОстатки.ДокументА КАК ДокументАванса,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.ПлатежноеПоручениеВходящее) ССЫЛКА Документ.ПлатежноеПоручениеВходящее
	|			ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.ПлатежноеПоручениеВходящее).Дата
	|		КОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.ПлатежноеПоручениеИсходящее) ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
	|			ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.ПлатежноеПоручениеИсходящее).Дата
	|		КОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.ПриходныйКассовыйОрдер) ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.ПриходныйКассовыйОрдер).Дата
	|		КОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.РасходныйКассовыйОрдер) ССЫЛКА Документ.РасходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОстатки.ДокументА КАК Документ.РасходныйКассовыйОрдер).Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ДатаДокумента,
	|	АвансыДоотгрузкаОстатки.СуммаОстаток КАК АвансыОплата,
	|	АвансыДоотгрузкаОстатки.СуммаНДСОстаток КАК СуммаНДСОстаток,
	|	АвансыДоотгрузкаОстатки.СуммаНСПОстаток КАК СуммаНСПОстаток
	|ПОМЕСТИТЬ ВременнаяТаблицаАвансы
	|ИЗ
	|	РегистрНакопления.АвансыДоотгрузка.Остатки(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И (Контрагент, Договор) В
	|					(ВЫБРАТЬ
	|						ВременнаяТаблицаСписокКонтрагентов.Контрагент,
	|						ВременнаяТаблицаСписокКонтрагентов.Договор
	|					ИЗ
	|						ВременнаяТаблицаСписокКонтрагентов КАК ВременнаяТаблицаСписокКонтрагентов)) КАК АвансыДоотгрузкаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаАвансы.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаАвансы.Договор КАК Договор
	|ПОМЕСТИТЬ ВременнаяТаблицаСписокКонтрагентовСАвансами
	|ИЗ
	|	ВременнаяТаблицаАвансы КАК ВременнаяТаблицаАвансы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаАвансы.Договор,
	|	ВременнаяТаблицаАвансы.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаАвансы.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаАвансы.Договор КАК Договор,
	|	ВременнаяТаблицаАвансы.ДокументАванса КАК ДокументАванса,
	|	ВременнаяТаблицаАвансы.ДатаДокумента КАК ДатаПоставки,
	|	ВременнаяТаблицаАвансы.АвансыОплата КАК АвансыОплата,
	|	ВременнаяТаблицаАвансы.СуммаНДСОстаток КАК СуммаНДСОстаток,
	|	ВременнаяТаблицаАвансы.СуммаНСПОстаток КАК СуммаНСПОстаток,
	|	ВременнаяТаблицаАвансы.Контрагент.Наименование КАК КонтрагентНаименование,
	|	ВременнаяТаблицаАвансы.Контрагент.ИНН КАК ИННКонтрагента,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаАвансы.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|			ТОГДА ""АВАНС-1""
	|		КОГДА ВременнаяТаблицаАвансы.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|			ТОГДА ""АВАНС-2""
	|		ИНАЧЕ ""АВАНС-3""
	|	КОНЕЦ КАК СтранаАванса,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаАвансы.ДокументАванса ССЫЛКА Документ.ПлатежноеПоручениеВходящее
	|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаАвансы.ДокументАванса КАК Документ.ПлатежноеПоручениеВходящее).Номер
	|		КОГДА ВременнаяТаблицаАвансы.ДокументАванса ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
	|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаАвансы.ДокументАванса КАК Документ.ПлатежноеПоручениеИсходящее).Номер
	|		КОГДА ВременнаяТаблицаАвансы.ДокументАванса ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаАвансы.ДокументАванса КАК Документ.ПриходныйКассовыйОрдер).Номер
	|		КОГДА ВременнаяТаблицаАвансы.ДокументАванса ССЫЛКА Документ.РасходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаАвансы.ДокументАванса КАК Документ.РасходныйКассовыйОрдер).Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерДокументаАванса
	|ИЗ
	|	ВременнаяТаблицаАвансы КАК ВременнаяТаблицаАвансы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	Договор,
	|	ДатаПоставки
	|ИТОГИ
	|	МАКСИМУМ(Контрагент),
	|	СУММА(АвансыОплата),
	|	СУММА(СуммаНДСОстаток),
	|	СУММА(СуммаНСПОстаток)
	|ПО
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.Наименование КАК КонтрагентНаименование,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор КАК Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ИНН КАК ИННКонтрагента,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодГНС КАК КодГНС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ДатаПоставки КАК ДатаПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СерияСФ КАК СерияСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.НомерСФ КАК НомерСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Дата КАК ДатаВыписки,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КоличествоСтрок) КАК КоличествоСтрок,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Отгрузка) КАК СтоимостьБезНДС,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СуммаНСП) КАК СуммаНСП,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ОбщаяСтоимостьСНДС) КАК ОбщаяСтоимостьСНДС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодПоставки КАК КодПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КонтрагентСтранаРезидентстваЕАЭС
	|ИЗ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал КАК ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСписокКонтрагентовСАвансами КАК ВременнаяТаблицаСписокКонтрагентовСАвансами
	|		ПО ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор = ВременнаяТаблицаСписокКонтрагентовСАвансами.Договор
	|ГДЕ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Отгрузка <> 0
	|	И НЕ ВременнаяТаблицаСписокКонтрагентовСАвансами.Договор ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ИНН,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодГНС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ДатаПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СерияСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.НомерСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Дата,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.Наименование,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	Договор,
	|	ДатаПоставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.НаименованиеПолное КАК КонтрагентНаименование,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор КАК Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ИНН КАК ИННКонтрагента,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодГНС КАК КодГНС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ДатаПоставки КАК ДатаПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СерияСФ КАК СерияСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.НомерСФ КАК НомерСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Дата КАК ДатаВыписки,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КоличествоСтрок) КАК КоличествоСтрок,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Отгрузка) КАК СтоимостьБезНДС,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СуммаНСП) КАК СуммаНСП,
	|	СУММА(ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ОбщаяСтоимостьСНДС) КАК ОбщаяСтоимостьСНДС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодПоставки КАК КодПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КонтрагентСтранаРезидентстваЕАЭС
	|ИЗ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал КАК ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСписокКонтрагентовСАвансами КАК ВременнаяТаблицаСписокКонтрагентовСАвансами
	|		ПО ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор = ВременнаяТаблицаСписокКонтрагентовСАвансами.Договор
	|ГДЕ
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Отгрузка <> 0
	|	И ВременнаяТаблицаСписокКонтрагентовСАвансами.Договор ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Договор,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ИНН,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодГНС,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.ДатаПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СерияСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.НомерСФ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Дата,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.КодПоставки,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВременнаяТаблицаОтгрузкаТекущегоМесяцаФинал.Контрагент.НаименованиеПолное
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	Договор,
	|	ДатаПоставки";
	
	Запрос.УстановитьПараметр("НачалоПериода", 	НачалоМесяца(Дата));            
	Запрос.УстановитьПараметр("КонецПериода", 	КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", 	Организация); 
	Запрос.УстановитьПараметр("НСПвСумме", 		НСПвСумме); 
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДоговор 				= РезультатЗапроса[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТаблицаОтгрузокДоотгрузок	= РезультатЗапроса[7].Выгрузить();
	ТаблицаОтгрузкиБезАвансов	= РезультатЗапроса[8].Выгрузить();	
	
	Пока ВыборкаДоговор.Следующий() Цикл
		Договор 				= ВыборкаДоговор.Договор;
		Контрагент 				= ВыборкаДоговор.Контрагент; 
		АвансыОплатаИтог 		= ВыборкаДоговор.АвансыОплата;
		ТекущаяОтгрузкаОстатокИтог	= 0;
		ТекущаяОплатаОстатокИтог	= 0;
		
		ВыборкаДокумента = ВыборкаДоговор.Выбрать();
		
		// Начало << Доотгрузка
		
		Отбор = Новый Структура;
		Отбор.Вставить("Договор", ВыборкаДоговор.Договор);		
		ТаблицаОтгрузокКонтрагента 	= ТаблицаОтгрузокДоотгрузок.Скопировать(Отбор);
		
		Пока ВыборкаДокумента.Следующий() 
			И АвансыОплатаИтог > 0 
			И ТаблицаОтгрузокКонтрагента.Количество() > 0 
			И ТаблицаОтгрузокКонтрагента.Итог("СтоимостьБезНДС") > 0 Цикл
			
			НоваяСтрокаАванс = ОтчетРеестрПоставок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаАванс, ВыборкаДокумента);
			НоваяСтрокаАванс.КодПоставки = "111";
			НоваяСтрокаАванс.СерияСФ = ВыборкаДокумента.СтранаАванса;
			НоваяСтрокаАванс.НомерСФ = ВыборкаДокумента.НомерДокументаАванса;
			СтоимостьБезНДСАвансы = 0;
			СуммаНДСАвансы 	= 0;
			СуммаНСПАвансы 	= 0;
			
			ТекущаяОплата = ВыборкаДокумента.АвансыОплата;
			ТекущийОстатокНДС = ВыборкаДокумента.СуммаНДСОстаток;
			ТекущийОстатокНСП = ВыборкаДокумента.СуммаНСПОстаток;
			ПродолжитьОбходТаблицы = Истина;
			Пока ПродолжитьОбходТаблицы Цикл
				Для каждого СтрокаТаблицы Из ТаблицаОтгрузокКонтрагента Цикл
					Если ТекущаяОплата > 0 Тогда
						НоваяСтрокаДоотгрузки = ОтчетРеестрПоставок.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаДоотгрузки, СтрокаТаблицы);
						НоваяСтрокаДоотгрузки.КодПоставки = "112";
						Если ТекущаяОплата >= СтрокаТаблицы.СтоимостьБезНДС Тогда	
							ТекущаяОплата = ТекущаяОплата - СтрокаТаблицы.СтоимостьБезНДС;
							НоваяСтрокаДоотгрузки.СтоимостьБезНДС = -СтрокаТаблицы.СтоимостьБезНДС;
							НоваяСтрокаДоотгрузки.СуммаНДС  = -СтрокаТаблицы.СтоимостьБезНДС * Окр(ВыборкаДокумента.СуммаНДСОстаток / ВыборкаДокумента.АвансыОплата, 2);
							НоваяСтрокаДоотгрузки.СуммаНСП  = -СтрокаТаблицы.СтоимостьБезНДС * Окр(ВыборкаДокумента.СуммаНСПОстаток / ВыборкаДокумента.АвансыОплата, 2);
							НоваяСтрокаДоотгрузки.ОбщаяСтоимостьСНДС  = НоваяСтрокаДоотгрузки.СтоимостьБезНДС	+ НоваяСтрокаДоотгрузки.СуммаНДС + ?(НСПвСумме, НоваяСтрокаДоотгрузки.СуммаНСП, 0);
							СтоимостьБезНДСАвансы = СтоимостьБезНДСАвансы 	+ НоваяСтрокаДоотгрузки.СтоимостьБезНДС;
							СуммаНДСАвансы 	= СуммаНДСАвансы	+ НоваяСтрокаДоотгрузки.СуммаНДС;
							СуммаНСПАвансы 	= СуммаНСПАвансы	+ НоваяСтрокаДоотгрузки.СуммаНСП;							
							ТекущийОстатокНДС 				= ТекущийОстатокНДС - НоваяСтрокаДоотгрузки.СуммаНДС;
							ТекущийОстатокНСП 				= ТекущийОстатокНСП - НоваяСтрокаДоотгрузки.СуммаНСП;							
							ТаблицаОтгрузокКонтрагента.Удалить(СтрокаТаблицы);
						Иначе
							НоваяСтрокаДоотгрузки.СтоимостьБезНДС = -ТекущаяОплата;
							НоваяСтрокаДоотгрузки.СуммаНДС 	= -ТекущийОстатокНДС;
							НоваяСтрокаДоотгрузки.СуммаНСП 	= -ТекущийОстатокНСП;
							НоваяСтрокаДоотгрузки.ОбщаяСтоимостьСНДС  = НоваяСтрокаДоотгрузки.СтоимостьБезНДС	+ НоваяСтрокаДоотгрузки.СуммаНДС + ?(НСПвСумме, НоваяСтрокаДоотгрузки.СуммаНСП, 0);
							СтоимостьБезНДСАвансы = СтоимостьБезНДСАвансы 	+ НоваяСтрокаДоотгрузки.СтоимостьБезНДС;
							СуммаНДСАвансы 	= СуммаНДСАвансы	+ НоваяСтрокаДоотгрузки.СуммаНДС;
							СуммаНСПАвансы 	= СуммаНСПАвансы	+ НоваяСтрокаДоотгрузки.СуммаНСП;							
							СтрокаТаблицы.СтоимостьБезНДС = СтрокаТаблицы.СтоимостьБезНДС - ТекущаяОплата;
							СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.СуммаНДС - ТекущийОстатокНДС;
							СтрокаТаблицы.СуммаНСП = СтрокаТаблицы.СуммаНСП - ТекущийОстатокНСП;
							ТекущаяОплата = 0;
							ПродолжитьОбходТаблицы = Ложь;
							Прервать;
						КонецЕсли;						
						
					Иначе
						ПродолжитьОбходТаблицы = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;		
				Если ТаблицаОтгрузокКонтрагента.Количество() = 0 Тогда
					ПродолжитьОбходТаблицы = Ложь;				
				КонецЕсли;								
			
			КонецЦикла;
			НоваяСтрокаАванс.СтоимостьБезНДС 		= -СтоимостьБезНДСАвансы;
			НоваяСтрокаАванс.СуммаНДС 		= -СуммаНДСАвансы;
			НоваяСтрокаАванс.СуммаНСП 		= -СуммаНСПАвансы;
			НоваяСтрокаАванс.ОбщаяСтоимостьСНДС	= -(СтоимостьБезНДСАвансы + СуммаНДСАвансы + ?(НСПвСумме, СуммаНСПАвансы, 0));
		
		КонецЦикла;
		// Доотгрузка >> Конец
		
		// Остатки не отгрузок после всех доотгрузок по контрагенту
		Для каждого СтрокаТаблицы Из ТаблицаОтгрузокКонтрагента Цикл
			НоваяСтрокаДоотгрузки = ОтчетРеестрПоставок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДоотгрузки, СтрокаТаблицы);
			НоваяСтрокаДоотгрузки.СтоимостьБезНДС = СтрокаТаблицы.СтоимостьБезНДС;
			НоваяСтрокаДоотгрузки.СуммаНДС  = СтрокаТаблицы.СуммаНДС;
			НоваяСтрокаДоотгрузки.СуммаНСП  = СтрокаТаблицы.СуммаНСП;
			НоваяСтрокаДоотгрузки.ОбщаяСтоимостьСНДС  = СтрокаТаблицы.СтоимостьБезНДС 	+ СтрокаТаблицы.СуммаНДС + ?(НСПвСумме, СтрокаТаблицы.СуммаНСП, 0);
			НоваяСтрокаДоотгрузки.КодПоставки  = СокрЛП(СтрокаТаблицы.КодПоставки);
		КонецЦикла;	
		
	КонецЦикла;	
	
	// Отгрузка по контрагентам, у которых не было ни одного аванса
	Для каждого СтрокаТаблицы Из ТаблицаОтгрузкиБезАвансов Цикл
		НоваяСтрокаДоотгрузки = ОтчетРеестрПоставок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаДоотгрузки, СтрокаТаблицы);
		НоваяСтрокаДоотгрузки.СтоимостьБезНДС = СтрокаТаблицы.СтоимостьБезНДС;
		НоваяСтрокаДоотгрузки.СуммаНДС  = СтрокаТаблицы.СуммаНДС;
		НоваяСтрокаДоотгрузки.СуммаНСП  = СтрокаТаблицы.СуммаНСП;
		НоваяСтрокаДоотгрузки.ОбщаяСтоимостьСНДС  = СтрокаТаблицы.ОбщаяСтоимостьСНДС;
		НоваяСтрокаДоотгрузки.КодПоставки  = СокрЛП(СтрокаТаблицы.КодПоставки);
	КонецЦикла;
		
	Если ОтчетРеестрПоставок.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет данных для заполнения отчета ""Реестр поставок""!'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

Процедура РассчитатьИтогиОтчетаРеестрПоставок() Экспорт	
	ИтогоПоРееструБНДС      		= 0;
	ИтогоПоРееструНДС       		= 0;
	ИтогоПоРееструОбщаяСтоимость  	= 0;
	
	ИтогоСуммаБНДСОбл		= 0;
	ИтогоСуммаНДСОбл		= 0;
	ИтогоОбщаяСтоимостьОбл	= 0;
	ИтогоСуммаБНДСНул		= 0;
	ИтогоСуммаБНДСОблЕАЭС	= 0;
	ИтогоСуммаБНДСОСВ		= 0;
	
	Для каждого СтрокаТабличнойЧасти Из ОтчетРеестрПоставок Цикл
		ИтогоПоРееструБНДС      	= ИтогоПоРееструБНДС 	   		+ СтрокаТабличнойЧасти.СтоимостьБезНДС;
		ИтогоПоРееструНДС       	= ИтогоПоРееструНДС  	   		+ СтрокаТабличнойЧасти.СуммаНДС;
		ИтогоПоРееструОбщаяСтоимость= ИтогоПоРееструОбщаяСтоимость	+ СтрокаТабличнойЧасти.ОбщаяСтоимостьСНДС;		
				
		Если НЕ СтрокаТабличнойЧасти.КодПоставки = "111" И НЕ СтрокаТабличнойЧасти.КодПоставки = "112" Тогда
			Если НЕ СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная Тогда
				ИтогоСуммаБНДСОбл      	= ИтогоСуммаБНДСОбл + СтрокаТабличнойЧасти.СтоимостьБезНДС;
				ИтогоСуммаНДСОбл      	= ИтогоСуммаНДСОбл + СтрокаТабличнойЧасти.СуммаНДС;
				ИтогоОбщаяСтоимостьОбл	= ИтогоОбщаяСтоимостьОбл + СтрокаТабличнойЧасти.СтоимостьБезНДС + СтрокаТабличнойЧасти.СуммаНДС;
			КонецЕсли;
			Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Нулевая Тогда
				ИтогоСуммаБНДСНул 		= ИтогоСуммаБНДСНул + СтрокаТабличнойЧасти.СтоимостьБезНДС;			
			КонецЕсли;
			Если СтрокаТабличнойЧасти.КонтрагентСтранаРезидентстваЕАЭС Тогда
				ИтогоСуммаБНДСОблЕАЭС 	= ИтогоСуммаБНДСОблЕАЭС + СтрокаТабличнойЧасти.СтоимостьБезНДС;			
			КонецЕсли;			
			Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная Тогда
				ИтогоСуммаБНДСОСВ      	= ИтогоСуммаБНДСОСВ + СтрокаТабличнойЧасти.СтоимостьБезНДС;
			КонецЕсли;					
		КонецЕсли;		
	КонецЦикла;
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= "ВСЕГО ПО РЕЕСТРУ в том числе:";
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоПоРееструБНДС;
	НоваяСтрокаИтогов.СуммаНДС 				= ИтогоПоРееструНДС;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоПоРееструОбщаяСтоимость;
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= "ОБЛАГАЕМЫЕ ПОСТАВКИ";
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОбл;
	НоваяСтрокаИтогов.СуммаНДС 				= ИтогоСуммаНДСОбл;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоОбщаяСтоимостьОбл;
		
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= "в том числе ПОСТАВКА ТОВАРОВ ПРОМЫШЛЕННОЙ ПЕРЕРАБОТКИ СЕЛЬСКОХОЗЯЙСТВЕННОЙ ПРОДУКЦИИ (с кодом поставки 102)";
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= 0;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= 0;		
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= "2) НУЛЕВЫЕ ПОСТАВКИ";
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСНул;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоСуммаБНДСНул;
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= "в том числе: в государства - члены Евразийского экономического союза";
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОблЕАЭС;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоСуммаБНДСОблЕАЭС;

	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= "ОСВОБОЖДЕННЫЕ И НЕОБЛАГАЕМЫЕ ПОСТАВКИ";
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОСВ;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= 0;
		
КонецПроцедуры

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетРеестрПриобретенных() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	ПоступленияОбороты.Контрагент.НаименованиеПолное КАК КонтрагентНаименование,
	|	ПоступленияОбороты.Контрагент.ИНН КАК ИННКонтрагента,
	|	ПоступленияОбороты.ПериодСекунда КАК ДатаПоставки,
	|	ПоступленияОбороты.Контрагент.ГНС.Код КАК КодГНС,
	|	ПоступленияОбороты.СуммаНДСОборот КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ПоступленияОбороты.Договор.СуммаВключаетНалоги
	|			ТОГДА ПоступленияОбороты.СуммаСебестоимостиОборот - ПоступленияОбороты.СуммаНДСОборот - ПоступленияОбороты.СуммаНеоблагаемаяОборот
	|		ИНАЧЕ ПоступленияОбороты.СуммаСебестоимостиОборот
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ПоступленияОбороты.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
	|				ИЛИ ПоступленияОбороты.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
	|			ТОГДА ПоступленияОбороты.СуммаНДСОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПодлежащаяКЗачетуНДС,
	|	ЕСТЬNULL(СчетаФактурыПолученные.СерияБланкаСФ, ПоступленияИтоги.СерияБланкаСФ) КАК СерияСФ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СчетаФактурыПолученные.СерияБланкаСФ, ПоступленияИтоги.СерияБланкаСФ) = ""ДПБУ""
	|			ТОГДА ВЫБОР
	|					КОГДА ПоступленияИтоги.ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|						ТОГДА ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ПоступлениеТоваровУслуг).Номер
	|					КОГДА ПоступленияИтоги.ДокументПоступления ССЫЛКА Документ.АвансовыйОтчет
	|						ТОГДА ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.АвансовыйОтчет).Номер
	|					КОГДА ПоступленияИтоги.ДокументПоступления ССЫЛКА Документ.ДополнительныеРасходы
	|						ТОГДА ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ДополнительныеРасходы).Номер
	|					КОГДА ПоступленияИтоги.ДокументПоступления ССЫЛКА Документ.ВозвратТоваровПоставщику
	|						ТОГДА ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ВозвратТоваровПоставщику).Номер
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(СчетаФактурыПолученные.НомерБланкаСФ, ПоступленияИтоги.НомерБланкаСФ)
	|	КОНЕЦ КАК НомерСФ,
	|	ВЫБОР
	|		КОГДА ПоступленияИтоги.ДокументПоступления ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ВозвратТоваровПоставщику).СерияБланкаСФ = """"
	|						ТОГДА СчетаФактурыПолученные.СерияБланкаСФ
	|					ИНАЧЕ ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ВозвратТоваровПоставщику).СерияБланкаСФ
	|				КОНЕЦ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КорСерияБланкаСФ,
	|	ВЫБОР
	|		КОГДА ПоступленияИтоги.ДокументПоступления ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ВозвратТоваровПоставщику).НомерБланкаСФ = """"
	|						ТОГДА СчетаФактурыПолученные.НомерБланкаСФ
	|					ИНАЧЕ ВЫРАЗИТЬ(ПоступленияИтоги.ДокументПоступления КАК Документ.ВозвратТоваровПоставщику).НомерБланкаСФ
	|				КОНЕЦ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КорНомерБланкаСФ
	|ИЗ
	|	РегистрНакопления.ПоступлениеТоваров.Обороты(&НачалоПериода, &КонецПериода, Авто, &Организация = Организация) КАК ПоступленияОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоступленияИтоги КАК ПоступленияИтоги
	|		ПО ПоступленияОбороты.Регистратор = ПоступленияИтоги.ДокументПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаФактурыПолученные КАК СчетаФактурыПолученные
	|		ПО ПоступленияОбороты.Регистратор = СчетаФактурыПолученные.Документ
	|ГДЕ
	|	ПоступленияИтоги.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПоставки";
	
	Запрос.УстановитьПараметр("НачалоПериода", 	НачалоМесяца(Дата));            
	Запрос.УстановитьПараметр("КонецПериода", 	КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", 	Организация); 
	
	ОтчетРеестрПриобретенныхМатРессурсов.Загрузить(Запрос.Выполнить().Выгрузить());
		
	Если ОтчетРеестрПриобретенныхМатРессурсов.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет данных для заполнения отчета ""Реестр приобретенных материальных рессурсов""!'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

Процедура РассчитатьИтогиОтчетаРеестрПриобретенных() Экспорт	
	ИтогоПоРееструБНДС      = 0; 	
	ИтогоПоРееструНДС       = 0; 	
	ИтогоПоРееструНДСЗАчет  = 0;
	
	Для каждого СтрокаТабличнойЧасти Из ОтчетРеестрПриобретенныхМатРессурсов Цикл	
		ИтогоПоРееструБНДС      = ИтогоПоРееструБНДС 	   + СтрокаТабличнойЧасти.СтоимостьБезНДС;
		ИтогоПоРееструНДС       = ИтогоПоРееструНДС  	   + СтрокаТабличнойЧасти.СуммаНДС;
		ИтогоПоРееструНДСЗачет  = ИтогоПоРееструНДСЗачет   + СтрокаТабличнойЧасти.СуммаПодлежащаяКЗачетуНДС;		
	КонецЦикла;
	
	НоваяСтрокаИтогов = ОтчетРеестрПриобретенныхМатРессурсовИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 				= "ВСЕГО ПО РЕЕСТРУ :";
	НоваяСтрокаИтогов.СтоимостьБезНДС 			= ИтогоПоРееструБНДС;
	НоваяСтрокаИтогов.СуммаНДС 					= ИтогоПоРееструНДС;
	НоваяСтрокаИтогов.СуммаПодлежащаяКЗачетуНДС = ИтогоПоРееструНДСЗачет;
		
КонецПроцедуры

// Процедура заполняет табличную часть "ОтчетРеестрГТД"
//
Процедура ЗаполнитьТабличнуюЧастьОтчетРеестрГТД() Экспорт

	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеестрВвезенныхОбороты.Контрагент,
		|	РеестрВвезенныхОбороты.Номер КАК ГТДНомер,
		|	РеестрВвезенныхОбороты.ПериодДень КАК ГТДДата,
		|	РеестрВвезенныхОбороты.СуммаБезНДСОборот КАК ГТДСуммаБезНДС,
		|	РеестрВвезенныхОбороты.СуммаНДСОборот КАК ГТДСуммаНДС,
		|	РеестрВвезенныхОбороты.СуммаНДСОборот КАК НДСЗачет
		|ИЗ
		|	РегистрНакопления.ДанныеДляОтчетаРеестрВвезенных.Обороты(&НачалоПериода, &КонецПериода, Авто, Организация = &Организация) КАК РеестрВвезенныхОбороты";
	Запрос.УстановитьПараметр("НачалоПериода", 	НачалоМесяца(Дата));            
	Запрос.УстановитьПараметр("КонецПериода", 	КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	ОтчетРеестрГТД.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли