#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = КонецМесяца(ТекущаяДатаСеанса());
		Если ЕстьДокументВУказанныйПериод(Дата) Тогда
			Дата = Дата('00010101');
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Функция ЕстьДокументВУказанныйПериод(Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНДС.Ссылка
		|ИЗ
		|	Документ.ОтчетПоНДС КАК ОтчетПоНДС
		|ГДЕ
		|	НЕ ОтчетПоНДС.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ОтчетПоНДС.Дата, МЕСЯЦ) = &Дата";
	
	Запрос.УстановитьПараметр("Дата", Дата);		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 	

КонецФункции 

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетОсновной() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗакрытиеМесяца.Ссылка,
		|	ЗакрытиеМесяца.РасчетНДС
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	НЕ ЗакрытиеМесяца.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) = &Дата
		|	И ЗакрытиеМесяца.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНДС.Строка,
		|	ЗакрытиеМесяцаНДС.Содержание,
		|	ЗакрытиеМесяцаНДС.Сумма
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НДС КАК ЗакрытиеМесяцаНДС
		|ГДЕ
		|	ЗакрытиеМесяцаНДС.Ссылка.Дата = &Дата
		|	И ЗакрытиеМесяцаНДС.Ссылка.Проведен
		|	И ЗакрытиеМесяцаНДС.Ссылка.Организация = &Организация";				
	Запрос.УстановитьПараметр("Организация", 	Организация);	
	Запрос.УстановитьПараметр("Дата", 			КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаЗМ = РезультатЗапроса[0].Выбрать();	
	Если ВыборкаЗМ.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет документа ""Закрытие месяца"". Табличная часть ""Отчет по НДС"" не может быть заполнена'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;	
	ИначеЕсли ВыборкаЗМ.Следующий() И НЕ ВыборкаЗМ.РасчетНДС Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В документе ""Закрытие месяца"" за период ""%1"" расчет НДС не производился. Табличная часть ""Отчет по НДС"" не может быть заполнена'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;	
	КонецЕсли;
	
	ОтчетОсновной.Загрузить(РезультатЗапроса[1].Выгрузить());
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетРеестрПоставок(НалоговыйКонтракт, НеУчитыватьЗакупкиБезНДС, ОтчетПоНДСПоПоставке) Экспорт

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент,
		|	ВЫБОР
		|		КОГДА Контрагенты.НаименованиеПолное <> """"
		|			ТОГДА Контрагенты.НаименованиеПолное
		|		ИНАЧЕ Контрагенты.Наименование
		|	КОНЕЦ КАК КонтрагентНаименование,
		|	Контрагенты.ИНН КАК ИННКонтрагента,
		|	ВЫБОР
		|		КОГДА Контрагенты.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|			ТОГДА Контрагенты.ГНС.Код
		|		ИНАЧЕ Контрагенты.СтранаРезидентства.Код
		|	КОНЕЦ КАК КодГНС,
		|	ВЫБОР
		|		КОГДА Контрагенты.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КонтрагентСтранаРезидентстваЕАЭС
		|ПОМЕСТИТЬ ВременнаяТаблицаКонтрагенты
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК ДокументОтгрузки,
		|	АвансыДоотгрузкаОбороты.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаКонтрагенты.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ВременнаяТаблицаКонтрагенты.ИННКонтрагента КАК ИННКонтрагента,
		|	ВременнаяТаблицаКонтрагенты.КодГНС КАК КодГНС,
		|	АвансыДоотгрузкаОбороты.Договор КАК Договор,
		|	АвансыДоотгрузкаОбороты.Договор.СтавкаНДС КАК СтавкаНДС,
		|	АвансыДоотгрузкаОбороты.ДокументА КАК ДокументА,
		|	""АВАНС-1"" КАК СерияСФ,
		|	ВЫБОР
		|		КОГДА АвансыДоотгрузкаОбороты.СуммаПриход > 0
		|			ТОГДА ВЫБОР
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ПлатежноеПоручениеВходящее
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ПлатежноеПоручениеВходящее).Дата
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ВводНачальныхОстатков
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ВводНачальныхОстатков).Дата
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ПриходныйКассовыйОрдер).Дата
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ВозвратТоваровОтПокупателя).Дата
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.СводПоРознице
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.СводПоРознице).Дата
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Дата
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументОтгрузки ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.ПлатежноеПоручениеИсходящее).Дата
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументОтгрузки ССЫЛКА Документ.РасходныйКассовыйОрдер
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.РасходныйКассовыйОрдер).Дата
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.СводПоРознице
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.СводПоРознице).Дата
		|			КОНЕЦ
		|	КОНЕЦ КАК Дата,
		|	ВЫБОР
		|		КОГДА АвансыДоотгрузкаОбороты.СуммаПриход > 0
		|			ТОГДА ВЫБОР
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ПлатежноеПоручениеВходящее
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ПлатежноеПоручениеВходящее).Номер
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ВводНачальныхОстатков
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ВводНачальныхОстатков).Номер
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ПриходныйКассовыйОрдер).Номер
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.ВозвратТоваровОтПокупателя).Номер
		|					КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.СводПоРознице
		|						ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.СводПоРознице).Номер
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|					ТОГДА ВЫБОР
		|							КОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).НомерБланкаСФ <> """"
		|								ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).НомерБланкаСФ
		|							ИНАЧЕ ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Номер
		|						КОНЕЦ
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументОтгрузки ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.ПлатежноеПоручениеИсходящее).Номер
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументОтгрузки ССЫЛКА Документ.РасходныйКассовыйОрдер
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументОтгрузки КАК Документ.РасходныйКассовыйОрдер).Номер
		|				КОГДА АвансыДоотгрузкаОбороты.ДокументА ССЫЛКА Документ.СводПоРознице
		|					ТОГДА ВЫРАЗИТЬ(АвансыДоотгрузкаОбороты.ДокументА КАК Документ.СводПоРознице).Номер
		|			КОНЕЦ
		|	КОНЕЦ КАК НомерСФ,
		|	АвансыДоотгрузкаОбороты.СуммаПриход - АвансыДоотгрузкаОбороты.СуммаНДСПриход - АвансыДоотгрузкаОбороты.СуммаНСППриход КАК СуммаПриход,
		|	АвансыДоотгрузкаОбороты.СуммаРасход - АвансыДоотгрузкаОбороты.СуммаНДСРасход - АвансыДоотгрузкаОбороты.СуммаНСПРасход КАК СуммаРасход,
		|	АвансыДоотгрузкаОбороты.СуммаНДСПриход КАК СуммаНДСПриход,
		|	АвансыДоотгрузкаОбороты.СуммаНДСРасход КАК СуммаНДСРасход,
		|	АвансыДоотгрузкаОбороты.СуммаНСППриход КАК СуммаНСППриход,
		|	АвансыДоотгрузкаОбороты.СуммаНСПРасход КАК СуммаНСПРасход
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансыДоотгрузка
		|ИЗ
		|	РегистрНакопления.АвансыДоотгрузка.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК АвансыДоотгрузкаОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКонтрагенты КАК ВременнаяТаблицаКонтрагенты
		|		ПО (ВЫБОР
		|				КОГДА АвансыДоотгрузкаОбороты.Контрагент.ГоловнойКонтрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|					ТОГДА АвансыДоотгрузкаОбороты.Контрагент = ВременнаяТаблицаКонтрагенты.Контрагент
		|				ИНАЧЕ АвансыДоотгрузкаОбороты.Контрагент.ГоловнойКонтрагент = ВременнаяТаблицаКонтрагенты.Контрагент
		|			КОНЕЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаАвансыДоотгрузка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаАвансыДоотгрузка.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ВременнаяТаблицаАвансыДоотгрузка.ИННКонтрагента КАК ИННКонтрагента,
		|	ВременнаяТаблицаАвансыДоотгрузка.КодГНС КАК КодГНС,
		|	ВременнаяТаблицаАвансыДоотгрузка.Договор КАК Договор,
		|	ВременнаяТаблицаАвансыДоотгрузка.ДокументА КАК ДокументА,
		|	ВременнаяТаблицаАвансыДоотгрузка.СерияСФ КАК СерияСФ,
		|	ВременнаяТаблицаАвансыДоотгрузка.Дата КАК ДатаВыписки,
		|	ВременнаяТаблицаАвансыДоотгрузка.НомерСФ КАК НомерСФ,
		|	111 КАК КодПоставки,
		|	ВременнаяТаблицаАвансыДоотгрузка.СуммаПриход КАК СтоимостьБезНДС,
		|	ВременнаяТаблицаАвансыДоотгрузка.СуммаНДСПриход КАК СуммаНДС,
		|	ВременнаяТаблицаАвансыДоотгрузка.СуммаНСППриход КАК СуммаНСП,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаАвансыДоотгрузка.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Освобожденная)
		|				ИЛИ ВременнаяТаблицаАвансыДоотгрузка.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Необлагаемая)
		|			ТОГДА ВременнаяТаблицаАвансыДоотгрузка.СуммаПриход
		|		КОГДА &НСПВСумме
		|			ТОГДА ВременнаяТаблицаАвансыДоотгрузка.СуммаПриход + ВременнаяТаблицаАвансыДоотгрузка.СуммаНДСПриход + ВременнаяТаблицаАвансыДоотгрузка.СуммаНСППриход
		|		ИНАЧЕ ВременнаяТаблицаАвансыДоотгрузка.СуммаПриход + ВременнаяТаблицаАвансыДоотгрузка.СуммаНДСПриход
		|	КОНЕЦ КАК ОбщаяСтоимостьСНДС,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаАвансыДоотгрузка.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КонтрагентСтранаРезидентстваЕАЭС
		|ИЗ
		|	ВременнаяТаблицаАвансыДоотгрузка КАК ВременнаяТаблицаАвансыДоотгрузка
		|ГДЕ
		|	ВременнаяТаблицаАвансыДоотгрузка.СуммаПриход > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаВыписки,
		|	НомерСФ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаАвансыДоотгрузка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаАвансыДоотгрузка.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ВременнаяТаблицаАвансыДоотгрузка.ИННКонтрагента КАК ИННКонтрагента,
		|	ВременнаяТаблицаАвансыДоотгрузка.КодГНС КАК КодГНС,
		|	ВременнаяТаблицаАвансыДоотгрузка.Договор КАК Договор,
		|	ВременнаяТаблицаАвансыДоотгрузка.ДокументА КАК ДокументА,
		|	ВременнаяТаблицаАвансыДоотгрузка.Дата КАК ДатаВыписки,
		|	ВременнаяТаблицаАвансыДоотгрузка.СерияСФ КАК СерияСФ,
		|	ВременнаяТаблицаАвансыДоотгрузка.НомерСФ КАК НомерСФ,
		|	112 КАК КодПоставки,
		|	-ВременнаяТаблицаАвансыДоотгрузка.СуммаРасход КАК СтоимостьБезНДС,
		|	-ВременнаяТаблицаАвансыДоотгрузка.СуммаНДСРасход КАК СуммаНДС,
		|	-ВременнаяТаблицаАвансыДоотгрузка.СуммаНСПРасход КАК СуммаНСП,
		|	-ВЫБОР
		|		КОГДА ВременнаяТаблицаАвансыДоотгрузка.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Освобожденная)
		|				ИЛИ ВременнаяТаблицаАвансыДоотгрузка.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Необлагаемая)
		|			ТОГДА ВременнаяТаблицаАвансыДоотгрузка.СуммаРасход
		|		КОГДА &НСПВСумме
		|			ТОГДА ВременнаяТаблицаАвансыДоотгрузка.СуммаРасход + ВременнаяТаблицаАвансыДоотгрузка.СуммаНДСРасход + ВременнаяТаблицаАвансыДоотгрузка.СуммаНСПРасход
		|		ИНАЧЕ ВременнаяТаблицаАвансыДоотгрузка.СуммаРасход + ВременнаяТаблицаАвансыДоотгрузка.СуммаНДСРасход
		|	КОНЕЦ КАК ОбщаяСтоимостьСНДС,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаАвансыДоотгрузка.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КонтрагентСтранаРезидентстваЕАЭС
		|ИЗ
		|	ВременнаяТаблицаАвансыДоотгрузка КАК ВременнаяТаблицаАвансыДоотгрузка
		|ГДЕ
		|	ВременнаяТаблицаАвансыДоотгрузка.СуммаРасход > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаВыписки,
		|	НомерСФ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		//|	СчетаФактурыВыписанные.Контрагент КАК Контрагент,
		//|	МАКСИМУМ(СчетаФактурыВыписанные.ДоговорКонтрагента) КАК ДоговорКонтрагента,
		|	СчетаФактурыВыписанные.СтавкаНДС КАК СтавкаНДС,
		|	СчетаФактурыВыписанные.СерияБланкаСФ КАК СерияСФ,
		|	СчетаФактурыВыписанные.НомерБланкаСФ КАК НомерСФ,
		|	СчетаФактурыВыписанные.КорСерияБланкаСФ КАК КорСерияСФ,
		|	СчетаФактурыВыписанные.КорНомерБланкаСФ КАК КорНомерСФ,
		|	ВременнаяТаблицаКонтрагенты.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ВременнаяТаблицаКонтрагенты.ИННКонтрагента КАК ИННКонтрагента,
		|	ВременнаяТаблицаКонтрагенты.КодГНС КАК КодГНС,
		|	ВременнаяТаблицаКонтрагенты.КонтрагентСтранаРезидентстваЕАЭС КАК КонтрагентСтранаРезидентстваЕАЭС,
		|	СчетаФактурыВыписанные.ВидПоставкиНДС.Код КАК КодПоставки,
		|	СчетаФактурыВыписанные.ДатаПоставки КАК ДатаПоставки,
		|	СУММА(СчетаФактурыВыписанные.СуммаБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(СчетаФактурыВыписанные.СуммаНДС) КАК СуммаНДС,
		|	СУММА(СчетаФактурыВыписанные.СуммаНеоблагаемая) КАК СуммаНСП,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(СчетаФактурыВыписанные.Регистратор КАК Документ.СчетФактураВыписанный) ССЫЛКА Документ.СчетФактураВыписанный
		|			ТОГДА ВЫРАЗИТЬ(СчетаФактурыВыписанные.Регистратор КАК Документ.СчетФактураВыписанный).Дата
		|		ИНАЧЕ СчетаФактурыВыписанные.ДатаСФ
		|	КОНЕЦ КАК ДатаВыписки,
		|	СУММА(ВЫБОР
		|			КОГДА СчетаФактурыВыписанные.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Освобожденная)
		|					ИЛИ СчетаФактурыВыписанные.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.Необлагаемая)
		|				ТОГДА СчетаФактурыВыписанные.СуммаБезНДС
		|			КОГДА &НСПВСумме
		|				ТОГДА СчетаФактурыВыписанные.СуммаБезНДС + СчетаФактурыВыписанные.СуммаНДС + СчетаФактурыВыписанные.СуммаНеоблагаемая
		|			ИНАЧЕ СчетаФактурыВыписанные.СуммаБезНДС + СчетаФактурыВыписанные.СуммаНДС
		|		КОНЕЦ) КАК ОбщаяСтоимостьСНДС,
		|	ВЫБОР
		|		КОГДА СчетаФактурыВыписанные.Регистратор ССЫЛКА Документ.СчетФактураВыписанный
		|			ТОГДА ДанныеПервичныхДокументов_СФВыписанные.Номер
		|		ИНАЧЕ ДанныеПервичныхДокументов_Реализации.Номер
		|	КОНЕЦ КАК ДокументНомер
		|ИЗ
		|	РегистрСведений.СчетаФактурыВыписанные КАК СчетаФактурыВыписанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКонтрагенты КАК ВременнаяТаблицаКонтрагенты
		|		ПО (ВЫБОР
		|				КОГДА СчетаФактурыВыписанные.Контрагент.ГоловнойКонтрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|					ТОГДА СчетаФактурыВыписанные.Контрагент = ВременнаяТаблицаКонтрагенты.Контрагент
		|				ИНАЧЕ СчетаФактурыВыписанные.Контрагент.ГоловнойКонтрагент = ВременнаяТаблицаКонтрагенты.Контрагент
		|			КОНЕЦ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов_Реализации
		|		ПО (ДанныеПервичныхДокументов_Реализации.Организация = &Организация)
		|			И СчетаФактурыВыписанные.Документ = ДанныеПервичныхДокументов_Реализации.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов_СФВыписанные
		|		ПО (ДанныеПервичныхДокументов_СФВыписанные.Организация = &Организация)
		|			И СчетаФактурыВыписанные.Регистратор = ДанныеПервичныхДокументов_СФВыписанные.Документ
		|ГДЕ
		|	СчетаФактурыВыписанные.Организация = &Организация
		|	И ВЫБОР
		|			КОГДА &ОтчетПоНДСПоПоставке
		|				ТОГДА СчетаФактурыВыписанные.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|			ИНАЧЕ СчетаФактурыВыписанные.ДатаСФ МЕЖДУ &НачалоПериода И &КонецПериода
		|		КОНЕЦ
		|	И СчетаФактурыВыписанные.СерияБланкаСФ <> """"
		|
		|СГРУППИРОВАТЬ ПО
		|	СчетаФактурыВыписанные.ДатаПоставки,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(СчетаФактурыВыписанные.Регистратор КАК Документ.СчетФактураВыписанный) ССЫЛКА Документ.СчетФактураВыписанный
		|			ТОГДА ВЫРАЗИТЬ(СчетаФактурыВыписанные.Регистратор КАК Документ.СчетФактураВыписанный).Дата
		|		ИНАЧЕ СчетаФактурыВыписанные.ДатаСФ
		|	КОНЕЦ,
		|	СчетаФактурыВыписанные.ВидПоставкиНДС.Код,
		//|	СчетаФактурыВыписанные.Контрагент,
		|	СчетаФактурыВыписанные.СтавкаНДС,
		|	СчетаФактурыВыписанные.КорСерияБланкаСФ,
		|	СчетаФактурыВыписанные.КорНомерБланкаСФ,
		|	СчетаФактурыВыписанные.СерияБланкаСФ,
		|	ВременнаяТаблицаКонтрагенты.КонтрагентНаименование,
		|	ВременнаяТаблицаКонтрагенты.ИННКонтрагента,
		|	ВременнаяТаблицаКонтрагенты.КодГНС,
		|	ВременнаяТаблицаКонтрагенты.КонтрагентСтранаРезидентстваЕАЭС,
		|	СчетаФактурыВыписанные.НомерБланкаСФ,
		|	ВЫБОР
		|		КОГДА СчетаФактурыВыписанные.Регистратор ССЫЛКА Документ.СчетФактураВыписанный
		|			ТОГДА ДанныеПервичныхДокументов_СФВыписанные.Номер
		|		ИНАЧЕ ДанныеПервичныхДокументов_Реализации.Номер
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаВыписки,
		|	НомерСФ";
	Запрос.УстановитьПараметр("НачалоПериода", 				НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", 				КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", 				Организация);  
	Запрос.УстановитьПараметр("НСПВСумме", 					НСПвСумме);
	Запрос.УстановитьПараметр("НеУчитыватьЗакупкиБезНДС", 	НеУчитыватьЗакупкиБезНДС);
	Запрос.УстановитьПараметр("ОтчетПоНДСПоПоставке", 		ОтчетПоНДСПоПоставке);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаАвансы 		= МассивРезультатов[2].Выбрать();
	ВыборкаДоотгрузки 	= МассивРезультатов[3].Выбрать();
	ВыборкаОтгрузки 	= МассивРезультатов[4].Выбрать();
	
	// Начало отгрузки.
	Пока ВыборкаОтгрузки.Следующий() Цикл
		
		Если НалоговыйКонтракт Тогда
			НоваяСтрокаАванс = ОтчетРеестрПоставокНаОсновеНалоговогоКонтракта.Добавить();
		Иначе
			НоваяСтрокаАванс = ОтчетРеестрПоставок.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрокаАванс, ВыборкаОтгрузки);
		
		Если СтрДлина(ВыборкаОтгрузки.НомерСФ) > 6 Тогда
			НоваяСтрокаАванс.НомерСФ = Прав(ВыборкаОтгрузки.НомерСФ, 6);
		КонецЕсли;
	КонецЦикла;	
	// Конец отгрузки.
	
	// Начало Авансы.
	Пока ВыборкаАвансы.Следующий() Цикл
		
		Если НалоговыйКонтракт Тогда
			НоваяСтрокаАванс = ОтчетРеестрПоставокНаОсновеНалоговогоКонтракта.Добавить();
		Иначе
			НоваяСтрокаАванс = ОтчетРеестрПоставок.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрокаАванс, ВыборкаАвансы);
		
		Если СтрДлина(ВыборкаАвансы.НомерСФ) > 6 Тогда
			НоваяСтрокаАванс.НомерСФ = Прав(ВыборкаАвансы.НомерСФ, 6);
		КонецЕсли;
	КонецЦикла;	
	// Конец Авансы.
	
	// Начало Доотгрузки.
	Пока ВыборкаДоотгрузки.Следующий() Цикл
		
		Если НалоговыйКонтракт Тогда
			НоваяСтрокаАванс = ОтчетРеестрПоставокНаОсновеНалоговогоКонтракта.Добавить();
		Иначе
			НоваяСтрокаАванс = ОтчетРеестрПоставок.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрокаАванс, ВыборкаДоотгрузки);
		
		Если СтрДлина(ВыборкаДоотгрузки.НомерСФ) > 6 Тогда
			НоваяСтрокаАванс.НомерСФ = Прав(ВыборкаДоотгрузки.НомерСФ, 6);
		КонецЕсли;
	КонецЦикла;	
	// Конец Доотгрузки.
КонецПроцедуры	

Процедура РассчитатьИтогиОтчетаРеестрПоставок(Структура = Неопределено) Экспорт
	ИтогоПоРееструБНДС      		= 0;
	ИтогоПоРееструНДС       		= 0;
	ИтогоПоРееструОбщаяСтоимость  	= 0;
	
	// ОБЛАГАЕМЫЕ ПОСТАВКИ 
	ИтогоСуммаБНДСОбл			= 0;
	ИтогоСуммаНДСОбл			= 0;
	ИтогоОбщаяСтоимостьОбл		= 0;
	
	// С кодом поставки 102 
	ИтогоСуммаБНДСОбл_102		= 0;
	ИтогоСуммаНДСОбл_102		= 0;
	ИтогоОбщаяСтоимостьОбл_102	= 0;
	
	ИтогоСуммаБНДСНул		= 0;
	ИтогоСуммаБНДСОблЕАЭС	= 0;
	ИтогоСуммаБНДСОСВ		= 0;
	
	ТаблицаСтавок = УчетНДСВызовСервера.ПолучитьСтавкиНДССНулевымиЗначениями(Дата);
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("СтавкаНДС", Справочники.СтавкиНДС.ПустаяСсылка());
	
	Для каждого СтрокаТабличнойЧасти Из ОтчетРеестрПоставок Цикл
		СтруктураПоиска.СтавкаНДС = СтрокаТабличнойЧасти.СтавкаНДС;
		МассивСтавок = ТаблицаСтавок.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтавок.Количество() = 0 Тогда
				ИтогоСуммаБНДСОбл      	= ИтогоСуммаБНДСОбл + СтрокаТабличнойЧасти.СтоимостьБезНДС;
				ИтогоСуммаНДСОбл      	= ИтогоСуммаНДСОбл + СтрокаТабличнойЧасти.СуммаНДС;
				ИтогоОбщаяСтоимостьОбл	= ИтогоОбщаяСтоимостьОбл + СтрокаТабличнойЧасти.СтоимостьБезНДС + СтрокаТабличнойЧасти.СуммаНДС; 
				
				Если СтрокаТабличнойЧасти.КодПоставки = "102" Тогда
					ИтогоСуммаБНДСОбл_102      	= ИтогоСуммаБНДСОбл_102 + СтрокаТабличнойЧасти.СтоимостьБезНДС;
					ИтогоСуммаНДСОбл_102      	= ИтогоСуммаНДСОбл_102 + СтрокаТабличнойЧасти.СуммаНДС;
					ИтогоОбщаяСтоимостьОбл_102	= ИтогоОбщаяСтоимостьОбл_102 + СтрокаТабличнойЧасти.СтоимостьБезНДС + СтрокаТабличнойЧасти.СуммаНДС;	
				КонецЕсли;	
				
		ИначеЕсли МассивСтавок.Количество() > 0 И СтрокаТабличнойЧасти.СтавкаНДС <> Справочники.СтавкиНДС.Нулевая Тогда
			ИтогоСуммаБНДСОСВ = ИтогоСуммаБНДСОСВ + СтрокаТабличнойЧасти.СтоимостьБезНДС;	
		КонецЕсли;
		
		Если СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.Нулевая Тогда
			ИтогоСуммаБНДСНул 		= ИтогоСуммаБНДСНул + СтрокаТабличнойЧасти.СтоимостьБезНДС;
			
			Если СтрокаТабличнойЧасти.КонтрагентСтранаРезидентстваЕАЭС Тогда
				ИтогоСуммаБНДСОблЕАЭС = ИтогоСуммаБНДСОблЕАЭС + СтрокаТабличнойЧасти.СтоимостьБезНДС;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ИтогоПоРееструБНДС      		= ИтогоСуммаБНДСОбл + ИтогоСуммаБНДСНул + ИтогоСуммаБНДСОСВ;
	ИтогоПоРееструНДС       		= ИтогоСуммаНДСОбл;
	ИтогоПоРееструОбщаяСтоимость	= ИтогоОбщаяСтоимостьОбл + ИтогоСуммаБНДСНул + ИтогоСуммаБНДСОСВ;
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= НСтр("ru = 'ВСЕГО ПО РЕЕСТРУ в том числе:'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоПоРееструБНДС;
	НоваяСтрокаИтогов.СуммаНДС 				= ИтогоПоРееструНДС;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоПоРееструОбщаяСтоимость;
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= НСтр("ru = '1) ОБЛАГАЕМЫЕ ПОСТАВКИ'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОбл;
	НоваяСтрокаИтогов.СуммаНДС 				= ИтогоСуммаНДСОбл;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоОбщаяСтоимостьОбл;
		
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= НСтр("ru = 'в том числе ПОСТАВКА ТОВАРОВ ПРОМЫШЛЕННОЙ ПЕРЕРАБОТКИ СЕЛЬСКОХОЗЯЙСТВЕННОЙ ПРОДУКЦИИ (с кодом поставки 102)'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОбл_102;
	НоваяСтрокаИтогов.СуммаНДС 				= ИтогоСуммаНДСОбл_102;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоОбщаяСтоимостьОбл_102;		
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= НСтр("ru = '2) НУЛЕВЫЕ ПОСТАВКИ'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСНул;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоСуммаБНДСНул;
	
	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= НСтр("ru = 'в том числе: в государства - члены Евразийского экономического союза'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОблЕАЭС;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС 	= ИтогоСуммаБНДСОблЕАЭС;

	НоваяСтрокаИтогов = ОтчетРеестрПоставокИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 			= НСтр("ru = '3) ОСВОБОЖДЕННЫЕ И НЕОБЛАГАЕМЫЕ ПОСТАВКИ'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 		= ИтогоСуммаБНДСОСВ;
	НоваяСтрокаИтогов.СуммаНДС 				= 0;
	НоваяСтрокаИтогов.ОбщаяСтоимостьСНДС  	= ИтогоСуммаБНДСОСВ;
	
	// Структура с итогами для заполнения XML файла.
	Если Структура <> Неопределено Тогда
		Структура.F9Total 		= ИтогоПоРееструБНДС;
		Структура.F10Total 		= ИтогоПоРееструНДС;
		Структура.F11Total 		= ИтогоПоРееструОбщаяСтоимость;
		Структура.F9Total1 		= ИтогоСуммаБНДСОбл;
		Структура.F10Total1 	= ИтогоСуммаНДСОбл;
		Структура.F11Total1 	= ИтогоОбщаяСтоимостьОбл;
		Структура.F9Total1_1 	= ИтогоСуммаБНДСОбл_102;
		Структура.F10Total1_1 	= ИтогоСуммаНДСОбл_102;
		Структура.F11Total1_1 	= ИтогоОбщаяСтоимостьОбл_102;
		Структура.F9Total2 		= ИтогоСуммаБНДСНул;
		Структура.F10Total2 	= 0;
		Структура.F11Total2 	= ИтогоСуммаБНДСНул;
		Структура.F9Total2eeu 	= ИтогоСуммаБНДСОблЕАЭС;
		Структура.F10Total2eeu 	= 0;
		Структура.F11Total2eeu 	= ИтогоСуммаБНДСОблЕАЭС;
		Структура.F9Total3 		= ИтогоСуммаБНДСОСВ;
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧастьОтчетРеестрПриобретенных(НалоговыйКонтракт, НеУчитыватьЗакупкиБезНДС) Экспорт
	
	КоэффициентОблагаемыхПоставок = ПолучитьКоэффициентОблагаемыхПоставок();
		
	Запрос = Новый Запрос;
	Запрос.Текст =  
		"ВЫБРАТЬ
		|	РеестрПриобретенныхМатериальныхРесурсов.Регистратор КАК Документ,
		|	РеестрПриобретенныхМатериальныхРесурсов.Контрагент.НаименованиеПолное КАК КонтрагентНаименование,
		|	РеестрПриобретенныхМатериальныхРесурсов.Контрагент.ИНН КАК ИННКонтрагента,
		|	РеестрПриобретенныхМатериальныхРесурсов.Контрагент.ГНС.Код КАК КодГНС,
		|	РеестрПриобретенныхМатериальныхРесурсов.СерияБланкаСФ КАК СерияСФ,
		|	РеестрПриобретенныхМатериальныхРесурсов.НомерБланкаСФ КАК НомерСФ,
		|	РеестрПриобретенныхМатериальныхРесурсов.КорСерияБланкаСФ КАК КорСерияСФ,
		|	РеестрПриобретенныхМатериальныхРесурсов.КорНомерБланкаСФ КАК КорНомерСФ,
		|	РеестрПриобретенныхМатериальныхРесурсов.ПериодДень КАК ДатаПоставки,
		|	РеестрПриобретенныхМатериальныхРесурсов.СуммаОборот КАК СтоимостьБезНДС,
		|	РеестрПриобретенныхМатериальныхРесурсов.СуммаНДСОборот КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА РеестрПриобретенныхМатериальныхРесурсов.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|				ИЛИ РеестрПриобретенныхМатериальныхРесурсов.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|			ТОГДА РеестрПриобретенныхМатериальныхРесурсов.СуммаНДСОборот
		|		КОГДА РеестрПриобретенныхМатериальныхРесурсов.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|			ТОГДА 0
		|		КОГДА РеестрПриобретенныхМатериальныхРесурсов.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Распределение)
		|			ТОГДА РеестрПриобретенныхМатериальныхРесурсов.СуммаНДСОборот * &КоэффициентОблПост
		|	КОНЕЦ КАК СуммаПодлежащаяКЗачетуНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаГотовыеДанные
		|ИЗ
		|	РегистрНакопления.РеестрПриобретенныхМатериальныхРесурсов.Обороты(&НачалоПериода, &КонецПериода, Авто, Организация = &Организация) КАК РеестрПриобретенныхМатериальныхРесурсов
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &НеУчитыватьЗакупкиБезНДС
		|				ТОГДА НЕ РеестрПриобретенныхМатериальныхРесурсов.СуммаНДСОборот = 0
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаГотовыеДанные.Документ КАК Документ,
		|	ВременнаяТаблицаГотовыеДанные.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ВременнаяТаблицаГотовыеДанные.ИННКонтрагента КАК ИННКонтрагента,
		|	ВременнаяТаблицаГотовыеДанные.КодГНС КАК КодГНС,
		|	ВременнаяТаблицаГотовыеДанные.СерияСФ КАК СерияСФ,
		|	ВременнаяТаблицаГотовыеДанные.НомерСФ КАК НомерСФ,
		|	ВременнаяТаблицаГотовыеДанные.КорСерияСФ КАК КорСерияСФ,
		|	ВременнаяТаблицаГотовыеДанные.КорНомерСФ КАК КорНомерСФ,
		|	ВременнаяТаблицаГотовыеДанные.ДатаПоставки КАК ДатаПоставки,
		|	СУММА(ВременнаяТаблицаГотовыеДанные.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ВременнаяТаблицаГотовыеДанные.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВременнаяТаблицаГотовыеДанные.СуммаПодлежащаяКЗачетуНДС) КАК СуммаПодлежащаяКЗачетуНДС
		|ИЗ
		|	ВременнаяТаблицаГотовыеДанные КАК ВременнаяТаблицаГотовыеДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаГотовыеДанные.Документ,
		|	ВременнаяТаблицаГотовыеДанные.КонтрагентНаименование,
		|	ВременнаяТаблицаГотовыеДанные.ИННКонтрагента,
		|	ВременнаяТаблицаГотовыеДанные.ДатаПоставки,
		|	ВременнаяТаблицаГотовыеДанные.КодГНС,
		|	ВременнаяТаблицаГотовыеДанные.КорНомерСФ,
		|	ВременнаяТаблицаГотовыеДанные.КорСерияСФ,
		|	ВременнаяТаблицаГотовыеДанные.НомерСФ,
		|	ВременнаяТаблицаГотовыеДанные.СерияСФ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПоставки";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));            
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("КоэффициентОблПост", КоэффициентОблагаемыхПоставок);
	Запрос.УстановитьПараметр("НеУчитыватьЗакупкиБезНДС", НеУчитыватьЗакупкиБезНДС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если НалоговыйКонтракт Тогда		
		Пока Выборка.Следующий() Цикл		
			СтрокаТабличнойЧасти = ОтчетРеестрПриобретенныхНаОсновеНалоговогоКонтракта.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);		
		КонецЦикла;	
		
		Если ОтчетРеестрПриобретенныхНаОсновеНалоговогоКонтракта.Количество() = 0 Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет данных для заполнения отчета ""Реестр приобретенных на осн. налогового контракта"".'"), 
											ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);		
		КонецЕсли;
		
	Иначе
		Пока Выборка.Следующий() Цикл		
			СтрокаТабличнойЧасти = ОтчетРеестрПриобретенныхМатРесурсов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);			
		КонецЦикла;
		
		Если ОтчетРеестрПриобретенныхМатРесурсов.Количество() = 0 Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет данных для заполнения отчета ""Реестр приобретенных материальных Ресурсов"".'"), 
											ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);		
		КонецЕсли;
		
	КонецЕсли;	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

Процедура РассчитатьИтогиОтчетаРеестрПриобретенных() Экспорт
	ИтогоПоРееструБНДС      = 0; 	
	ИтогоПоРееструНДС       = 0; 	
	ИтогоПоРееструНДСЗАчет  = 0;
	
	Для каждого СтрокаТабличнойЧасти Из ОтчетРеестрПриобретенныхМатРесурсов Цикл	
		ИтогоПоРееструБНДС      = ИтогоПоРееструБНДС 	   + СтрокаТабличнойЧасти.СтоимостьБезНДС;
		ИтогоПоРееструНДС       = ИтогоПоРееструНДС  	   + СтрокаТабличнойЧасти.СуммаНДС;
		ИтогоПоРееструНДСЗачет  = ИтогоПоРееструНДСЗачет   + СтрокаТабличнойЧасти.СуммаПодлежащаяКЗачетуНДС;		
	КонецЦикла;
	
	НоваяСтрокаИтогов = ОтчетРеестрПриобретенныхМатРесурсовИтоги.Добавить();
	НоваяСтрокаИтогов.Содержание 				= НСтр("ru = 'ВСЕГО ПО РЕЕСТРУ :'");
	НоваяСтрокаИтогов.СтоимостьБезНДС 			= ИтогоПоРееструБНДС;
	НоваяСтрокаИтогов.СуммаНДС 					= ИтогоПоРееструНДС;
	НоваяСтрокаИтогов.СуммаПодлежащаяКЗачетуНДС = ИтогоПоРееструНДСЗачет;
		
КонецПроцедуры

// Процедура заполняет табличную часть "ОтчетРеестрВвезенных"
//
Процедура ЗаполнитьТабличнуюЧастьОтчетРеестрВвезенных(НеУчитыватьЗакупкиБезНДС) Экспорт

	КоэффициентОблагаемыхПоставок = ПолучитьКоэффициентОблагаемыхПоставок();
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеестрВвезенныхОбороты.Контрагент КАК Контрагент,
		|	РеестрВвезенныхОбороты.Контрагент.НаименованиеПолное КАК КонтрагентНаименование,
		|	РеестрВвезенныхОбороты.Контрагент.СтранаРезидентства.Код КАК КодСтраныКонтрагента,
		|	РеестрВвезенныхОбороты.Номер КАК ГТДНомер,
		|	РеестрВвезенныхОбороты.ДатаСФ КАК ГТДДата,
		|	СУММА(РеестрВвезенныхОбороты.СуммаНДСОборот) КАК ГТДСуммаНДС,
		|	СУММА(РеестрВвезенныхОбороты.СуммаОборот) + СУММА(РеестрВвезенныхОбороты.СуммаАкцизаОборот) КАК ГТДСуммаБезНДС,
		|	СУММА(ВЫБОР
		|			КОГДА РеестрВвезенныхОбороты.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|				ТОГДА РеестрВвезенныхОбороты.СуммаНДСОборот
		|			КОГДА РеестрВвезенныхОбороты.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				ТОГДА 0
		|			КОГДА РеестрВвезенныхОбороты.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Распределение)
		|				ТОГДА РеестрВвезенныхОбороты.СуммаНДСОборот * &КоэффициентОблПост
		|		КОНЕЦ) КАК НДСЗачет,
		|	ВЫБОР
		|		КОГДА РеестрВвезенныхОбороты.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КонтрагентСтранаРезидентстваЕАЭС
		|ИЗ
		|	РегистрНакопления.ДанныеДляОтчетаРеестрВвезенных.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК РеестрВвезенныхОбороты
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &НеУчитыватьЗакупкиБезНДС
		|				ТОГДА РеестрВвезенныхОбороты.СуммаНДСОборот <> 0
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	РеестрВвезенныхОбороты.Контрагент,
		|	РеестрВвезенныхОбороты.Контрагент.СтранаРезидентства.Код,
		|	РеестрВвезенныхОбороты.Номер,
		|	РеестрВвезенныхОбороты.ДатаСФ,
		|	ВЫБОР
		|		КОГДА РеестрВвезенныхОбороты.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГТДДата";
	Запрос.УстановитьПараметр("НачалоПериода", 				НачалоМесяца(Дата));            
	Запрос.УстановитьПараметр("КонецПериода", 				КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", 				Организация);
	Запрос.УстановитьПараметр("КоэффициентОблПост", 		КоэффициентОблагаемыхПоставок);
	Запрос.УстановитьПараметр("НеУчитыватьЗакупкиБезНДС", 	НеУчитыватьЗакупкиБезНДС);
	
	ОтчетРеестрВвезенныхМатериальныхРесурсовВКР.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если ОтчетРеестрВвезенныхМатериальныхРесурсовВКР.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В период ""%1"" нет данных для заполнения отчета ""Реестр ГТД"".'"), 
										ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата)));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьКоэффициентОблагаемыхПоставок()

	ДанныеУчетнойПолитики = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(Дата, Организация);
	
	СтрокаОтчета = ОтчетОсновной.Найти("053", "Строка");
	
	Если СтрокаОтчета <> Неопределено Тогда
		Сумма053 = СтрокаОтчета.Сумма;
	Иначе
		Сумма053 = 0;
	КонецЕсли;
	
	СтрокаОтчета = ОтчетОсновной.Найти("054", "Строка");
	
	Если СтрокаОтчета <> Неопределено Тогда
		Сумма054 = СтрокаОтчета.Сумма;
	Иначе
		Сумма054 = 0;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗакрытиеМесяцаДоотгрузка.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаДоотгрузка.Договор КАК Договор,
		|	СУММА(ЗакрытиеМесяцаДоотгрузка.Сумма) КАК Сумма,
		|	СУММА(ЗакрытиеМесяцаДоотгрузка.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ЗакрытиеМесяцаДоотгрузка.СуммаНСП) КАК СуммаНСП
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ДоотгрузкаРасшифровка КАК ЗакрытиеМесяцаДоотгрузка
		|ГДЕ
		|	НЕ ЗакрытиеМесяцаДоотгрузка.Ссылка.ПометкаУдаления
		|	И ЗакрытиеМесяцаДоотгрузка.Ссылка.Дата = &Дата
		|	И ЗакрытиеМесяцаДоотгрузка.Ссылка.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗакрытиеМесяцаДоотгрузка.Контрагент,
		|	ЗакрытиеМесяцаДоотгрузка.Договор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаАвансы.СтавкаНДС КАК СтавкаНДС,
		|	ЗакрытиеМесяцаАвансы.СуммаДокумента КАК СуммаДокумента,
		|	ЗакрытиеМесяцаАвансы.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаАвансы.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаАвансы.СуммаНСП КАК СуммаНСП
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АвансыРасшифровка КАК ЗакрытиеМесяцаАвансы
		|ГДЕ
		|	НЕ ЗакрытиеМесяцаАвансы.Ссылка.ПометкаУдаления
		|	И ЗакрытиеМесяцаАвансы.Ссылка.Дата = &Дата
		|	И ЗакрытиеМесяцаАвансы.Ссылка.Организация = &Организация";				
	Запрос.УстановитьПараметр("Организация", 	Организация);	
	Запрос.УстановитьПараметр("Дата", 			КонецМесяца(Дата));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ПараметрыРасчета = Новый Структура;
	
	СуммаДоотгрузки 	= 0;
	СуммаНДСДоотгрузки 	= 0;
	СуммаНСПДоотгрузки 	= 0;
	
	ПараметрыРасчета.Вставить("Таблица", МассивРезультатов[0].Выгрузить());
	СуммаДоотгрузки 	= УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("СуммаДоотгрузки", 	 ПараметрыРасчета).РезультатРасчета;
	СуммаНДСДоотгрузки 	= УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("СуммаНДСДоотгрузки", ПараметрыРасчета).РезультатРасчета;
	СуммаНСПДоотгрузки 	= УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("СуммаНСПДоотгрузки", ПараметрыРасчета).РезультатРасчета;
	
	СуммаАвансы 	= 0;
	СуммаНДСАвансы 	= 0;
	СуммаНСПАвансы 	= 0;
	
	ПараметрыРасчета.Таблица = МассивРезультатов[1].Выгрузить();
	СуммаАвансы 	= УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("СуммаАвансы", 	 ПараметрыРасчета).РезультатРасчета;
	СуммаНДСАвансы 	= УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("СуммаНДСАвансы", ПараметрыРасчета).РезультатРасчета;
	СуммаНСПАвансы 	= УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("СуммаНСПАвансы", ПараметрыРасчета).РезультатРасчета;
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("ПороговыйПроцентОсвобожденныхПоставок", 	ДанныеУчетнойПолитики.ПороговыйПроцентОсвобожденныхПоставок);
	ПараметрыРасчета.Вставить("Сумма053", 								Сумма053);
	ПараметрыРасчета.Вставить("Сумма054", 								Сумма054);
	ПараметрыРасчета.Вставить("СуммаАвансы", 							СуммаАвансы);
	ПараметрыРасчета.Вставить("СуммаНДСАвансы", 						СуммаНДСАвансы);
	ПараметрыРасчета.Вставить("СуммаНСПАвансы", 						СуммаНСПАвансы);
	ПараметрыРасчета.Вставить("СуммаДоотгрузки", 						СуммаДоотгрузки); 
	ПараметрыРасчета.Вставить("СуммаНДСДоотгрузки", 					СуммаНДСДоотгрузки);
	ПараметрыРасчета.Вставить("СуммаНСПДоотгрузки", 					СуммаНСПДоотгрузки);
	КоэффициентОсвобожденныхПоставокРасчетный = УчетНДСВызовСервера.РасчетСуммыОтчетаНДС("КоэффициентОсвобожденныхПоставокРасчетный", ПараметрыРасчета).РезультатРасчета;
	
	Возврат 1 - КоэффициентОсвобожденныхПоставокРасчетный;

КонецФункции // ПолучитьКоэффициентОблагаемыхПоставок()

#КонецОбласти

#КонецЕсли