#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента	

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ)
	
	// Подготовка данных	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Регистратор,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	&Содержание КАК Содержание,
		|	ВременнаяТаблицаШапка.Склад КАК Склад
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимСписка КАК СинонимСписка,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаОС.НомерСтроки КАК НомерСтроки,
		|	ВременнаяТаблицаШапка.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаШапка.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
		|	ВременнаяТаблицаОС.ПервоначальнаяСтоимость КАК Себестоимость,
		|	1 КАК Количество,
		|	ВременнаяТаблицаОС.СчетУчета КАК КорСчетСписания,
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК КорСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Запрос.УстановитьПараметр("Содержание", НСтр("ru = 'Поступление основного средства'")); 
	Запрос.УстановитьПараметр("СинонимСписка", НСтр("ru = 'Товары'"));
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = МассивРезультатов[0].Выгрузить();
	ТаблицаТовары = МассивРезультатов[1].Выгрузить();
	
	ТаблицаСписанныеТовары = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(ТаблицаТовары, ТаблицаРеквизиты, Отказ);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеТовары", ТаблицаСписанныеТовары);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК СчетДт,
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет КАК СчетКт,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт3,
		|	ТаблицаДокумента.ОсновноеСредство КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаДокумента.НакопленнаяАмортизация КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ТаблицаДокумента.НакопленнаяАмортизация КАК ВалютнаяСуммаДт,
		|	ТаблицаДокумента.НакопленнаяАмортизация КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	&СодержаниеНачислениеАмортизации КАК Содержание,
		|	ИСТИНА КАК РаспределятьПоКоэффициентам,
		|	ТаблицаДокумента.СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.НакопленнаяАмортизация > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетУчета,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	0,
		|	&СодержаниеПоступление,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.ПервоначальнаяСтоимость > 0
		|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетУчета,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.ОбъектСтроительства,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	0,
		|	&СодержаниеПоступление,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.ПервоначальнаяСтоимость > 0
		|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)";
		//|
		//|ОБЪЕДИНИТЬ ВСЕ
		//|
		//|ВЫБРАТЬ
		//|	4,
		//|	ВременнаяТаблицаШапка.Дата,
		//|	ВременнаяТаблицаШапка.Организация,
		//|	ТаблицаДокумента.СчетУчета,
		//|	ВременнаяТаблицаШапка.СчетУчета,
		//|	ТаблицаДокумента.ОсновноеСредство,
		//|	НЕОПРЕДЕЛЕНО,
		//|	НЕОПРЕДЕЛЕНО,
		//|	ВременнаяТаблицаШапка.Номенклатура,
		//|	ВременнаяТаблицаШапка.Склад,
		//|	НЕОПРЕДЕЛЕНО,
		//|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		//|	&ВалютаРегламентированногоУчета,
		//|	&ВалютаРегламентированногоУчета,
		//|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		//|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		//|	1,
		//|	""Поступление ОС"",
		//|	ЛОЖЬ,
		//|	НЕОПРЕДЕЛЕНО
		//|ИЗ
		//|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		//|		ПО (ИСТИНА)
		//|ГДЕ
		//|	ТаблицаДокумента.ПервоначальнаяСтоимость > 0
		//|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)";
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);	
	Запрос.УстановитьПараметр("СодержаниеНачислениеАмортизации", НСтр("ru = 'Начисление амортизации'")); 
	Запрос.УстановитьПараметр("СодержаниеПоступление", НСтр("ru = 'Поступление основного средства'")); 
 	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	// Добавляем колонки в ТЗ для записи в рег. бухг.
	ТЗ.Колонки.Добавить("СубконтоДт1");
	ТЗ.Колонки.Добавить("СубконтоДт2");
	ТЗ.Колонки.Добавить("СубконтоДт3");
	
	КэшДанныхСпрСпособыОтрРасхПоАморт = Новый Соответствие();
	
	// Создаем результирующую ТЗ с теми же колонками, что и в запросе.
	ТаблицаХозрасчетный = ТЗ.СкопироватьКолонки();
	
	// Данный цикл перебирает строки ТЗ, полученные из запроса.
	//
	// 1. Если накопленная амортизация больше нуля, тогда по данной строке идет проверка способа отражения 
	// расходов по амортизации. Обращаемся к табличной части данного справочника и в зависимости от количества 
	// строк в ТЧ строка ТЗ дублируется и записывается в результирующую ТЗ. Записывается счет затрат (СчетДт) и в 
	// зависимости от коэффициентов в строках ТЧ распределяется сумма. А также заполняются созданные колонки "СубконтоДт".
	//
	// 2. В остальных случаях распределять сумму по коэффициенам не нужно, поэтому мы просто копируем строку 
	// в исходную ТЗ и заполняем созданные колонки "СубконтоДт" значениями из колонок "ЗначениеСубконтоДт"
	Для Каждого СтрокаТаблицыЗначений Из ТЗ Цикл
		
		Если НЕ СтрокаТаблицыЗначений.РаспределятьПоКоэффициентам Тогда
			СтрокаРезультирующейТЗ = ТаблицаХозрасчетный.Добавить();			
			ЗаполнитьЗначенияСвойств(СтрокаРезультирующейТЗ, СтрокаТаблицыЗначений);
			
			СтрокаРезультирующейТЗ.СубконтоДт1 = СтрокаРезультирующейТЗ.ЗначениеСубконтоДт1;
			СтрокаРезультирующейТЗ.СубконтоДт2 = СтрокаРезультирующейТЗ.ЗначениеСубконтоДт2;
			СтрокаРезультирующейТЗ.СубконтоДт3 = СтрокаРезультирующейТЗ.ЗначениеСубконтоДт3;			
			Продолжить;
		КонецЕсли;
		
		// В соответствие записывается: 
		// Ключ - ссылка на справочник "СпособыОтраженияРасходовПоАмортизации".
		// Значение - ТЗ из запроса к данному элементу справочника.
		// При каждом шаге основного цикла (перебор ТЗ из данных документа) проверяется наличие строки соответствия 
		// по ссылке на справочник, чтобы не обращаться к запросу каждый раз.
		ТЗСпособаОтражения = КэшДанныхСпрСпособыОтрРасхПоАморт.Получить(СтрокаТаблицыЗначений.СпособыОтраженияРасходовПоАмортизации);
		
		Если ТЗСпособаОтражения = Неопределено Тогда
			ТЗСпособаОтражения = УправлениеВнеоборотнымиАктивамиПовтИсп.СпособыОтраженияРасходовПоАмортизации(СтрокаТаблицыЗначений.СпособыОтраженияРасходовПоАмортизации); 
			КэшДанныхСпрСпособыОтрРасхПоАморт.Вставить(СтрокаТаблицыЗначений.СпособыОтраженияРасходовПоАмортизации, ТЗСпособаОтражения);
		КонецЕсли;
		
		МассивКоэфициентов = ТЗСпособаОтражения.ВыгрузитьКолонку("Коэффициент");
		
		МассивСумм = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаТаблицыЗначений.Сумма, МассивКоэфициентов, 2);
		
		Если МассивСумм = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ИндексМассива = 0;
		Для Каждого СтрокаМассива Из МассивСумм Цикл
			СтрокаРезультирующейТЗ = ТаблицаХозрасчетный.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРезультирующейТЗ, СтрокаТаблицыЗначений); 
			
			СтрокаРезультирующейТЗ.Сумма		   = СтрокаМассива;
			СтрокаРезультирующейТЗ.ВалютнаяСуммаДт = СтрокаМассива;
			СтрокаРезультирующейТЗ.ВалютнаяСуммаКт = СтрокаМассива;
			
			СтрокаРезультирующейТЗ.СчетДт		   = ТЗСпособаОтражения[ИндексМассива].СчетЗатрат;
			СтрокаРезультирующейТЗ.СубконтоДт1     = ТЗСпособаОтражения[ИндексМассива].Субконто1;
			СтрокаРезультирующейТЗ.СубконтоДт2     = ТЗСпособаОтражения[ИндексМассива].Субконто2;
			СтрокаРезультирующейТЗ.СубконтоДт3     = ТЗСпособаОтражения[ИндексМассива].Субконто3;
			
			ИндексМассива = ИндексМассива + 1;
		КонецЦикла;
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", ТаблицаХозрасчетный);
КонецПроцедуры // СформироватьТаблицаХозрасчетный()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаМестонахождениеОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.МОЛ КАК МОЛ,
		|	ТаблицаДокумента.Подразделение КАК Подразделение
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМестонахождениеОС", РезультатЗапроса.Выгрузить()); 
КонецПроцедуры // СформироватьТаблицаМестонахождениеОС()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	 	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Дата КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.ГруппаНУ КАК ГруппаНУ,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.СрокСлужбы КАК СрокСлужбы,
		|	ТаблицаДокумента.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
		|	ТаблицаДокумента.СпособыОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПараметрыУчетаОС", РезультатЗапроса.Выгрузить()); 
КонецПроцедуры // СформироватьТаблицаПараметрыУчетаОС()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата как Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ВременнаяТаблицаШапка.СобытиеОС КАК Событие,
		|	ВременнаяТаблицаШапка.Номер КАК НомерДокумента,
		|	ВременнаяТаблицаШапка.Представление КАК НазваниеДокумента
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСобытияОС", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСобытияОС() 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСоставОС(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСоставОС", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСоставОС 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету) КАК Состояние
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСостоянияОС", РезультатЗапроса.Выгрузить()); 
КонецПроцедуры // СформироватьТаблицаСостоянияОС

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСведенияОбИмуществе(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ГруппаИмущества,
		|	ВременнаяТаблицаШапка.Дата КАК ДатаВводаВЭксплуатацию,
		|	ТаблицаДокумента.КодИмущества,
		|	ТаблицаДокумента.МатериалСтен,
		|	ТаблицаДокумента.ЖилаяПлощадь,
		|	ТаблицаДокумента.НежилаяПлощадь,
		|	ТаблицаДокумента.РегиональныйКоэффициент,
		|	ТаблицаДокумента.ЗональныйКоэффициент,
		|	ТаблицаДокумента.ОтраслевойКоэффициент,
		|	ТаблицаДокумента.КатегорияИмущества КАК КатегорияОбъектаИмущества,
		|	ТаблицаДокумента.КодНазначенияИмущества,
		|	ТаблицаДокумента.КодПользователяИмущества
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ1)
		|			ИЛИ ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ2)
		|			ИЛИ ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ3))";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОбИмуществе", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСведенияОбИмуществе

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСведенияОТранспорте(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ГруппаИмущества КАК ГруппаИмущества,
		|	ТаблицаДокумента.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ТаблицаДокумента.ВидТранспорта,
		|	ТаблицаДокумента.ОбъемДвигателя,
		|	ТаблицаДокумента.ГосНомер
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ4)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОТранспорте", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСведенияОТранспорте

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДвижениеОСНУ(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ГруппаНУ,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость КАК Поступление,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость * (12 - МЕСЯЦ(ВременнаяТаблицаШапка.Дата) + 0.5) / 12 КАК ТекущееПоступление
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвижениеОСНУ", РезультатЗапроса.Выгрузить());
КонецПроцедуры 

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.Дата,
		|	ТаблицаДокумента.Номер,
		|	ТаблицаДокумента.Представление,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.ВидОперации,
		|	ТаблицаДокумента.СобытиеОС,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	ТаблицаДокумента.ОбъектСтроительства
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ИнвентарныйНомер,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.НакопленнаяАмортизация,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.СрокСлужбы,
		|	ТаблицаДокумента.СпособНачисленияАмортизации,
		|	ТаблицаДокумента.СпособыОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.МОЛ,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.ГруппаНУ,
		|	ТаблицаДокумента.ГруппаИмущества,
		|	ТаблицаДокумента.КодИмущества,
		|	ТаблицаДокумента.МатериалСтен,
		|	ТаблицаДокумента.ЖилаяПлощадь,
		|	ТаблицаДокумента.НежилаяПлощадь,
		|	ТаблицаДокумента.РегиональныйКоэффициент,
		|	ТаблицаДокумента.ЗональныйКоэффициент,
		|	ТаблицаДокумента.ОтраслевойКоэффициент,
		|	ТаблицаДокумента.КатегорияИмущества,
		|	ТаблицаДокумента.КодНазначенияИмущества,
		|	ТаблицаДокумента.КодПользователяИмущества,
		|	ТаблицаДокумента.ДатаВводаВЭксплуатацию,
		|	ТаблицаДокумента.ВидТранспорта,
		|	ТаблицаДокумента.ОбъемДвигателя,
		|	ТаблицаДокумента.ГосНомер
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить(); 
	
	СформироватьТаблицаМестонахождениеОС(ДокументСсылка, СтруктураДополнительныеСвойства); 
	СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства); 
	СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСоставОС(ДокументСсылка, СтруктураДополнительныеСвойства); 	
	СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОбИмуществе(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОТранспорте(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДвижениеОСНУ(ДокументСсылка, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСписокМОЛ(ВременнаяТаблицаОС)
	Первый = Истина;
	Стр = "";	
	Для каждого СтрокаТаблицыЗначений Из ВременнаяТаблицаОС Цикл
		Стр = Стр + ?(Первый, "", Символы.ПС) + СтрокаТаблицыЗначений.РасшифровкаПодписи;
		Первый = Ложь;
	КонецЦикла; 	
	
	Возврат Стр;
	
КонецФункции // ()

Функция ПолучитьСписокОС(ВременнаяТаблицаОС)
	Первый = Истина;
	Стр = "";	
	Для каждого СтрокаТаблицыЗначений Из ВременнаяТаблицаОС Цикл
		Стр = Стр + ?(Первый, "", ", ") + СтрокаТаблицыЗначений.ОсновноеСредствоНаименование;
		Первый = Ложь;
	КонецЦикла; 	
	
	Возврат Стр;
	
КонецФункции // ()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
//
Функция ПолучитьТекстЗапросаАктПриемкиПередачиОС()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПринятиеКУчетуОС.Ссылка,
	|	ПринятиеКУчетуОС.ВерсияДанных,
	|	ПринятиеКУчетуОС.ПометкаУдаления,
	|	ПринятиеКУчетуОС.Номер КАК НомерДокумента,
	|	ПринятиеКУчетуОС.Дата КАК ДатаДокумента,
	|	ПринятиеКУчетуОС.Проведен,
	|	ПринятиеКУчетуОС.Автор,
	|	ПринятиеКУчетуОС.ВидОперации,
	|	ПринятиеКУчетуОС.ДокументОснование,
	|	ПринятиеКУчетуОС.ДокументОснованиеВид,
	|	ПринятиеКУчетуОС.ДокументОснованиеДата,
	|	ПринятиеКУчетуОС.ДокументОснованиеНомер,
	|	ПринятиеКУчетуОС.Комментарий,
	|	ПринятиеКУчетуОС.Номенклатура,
	|	ПринятиеКУчетуОС.ОбъектСтроительства,
	|	ПринятиеКУчетуОС.Организация,
	|	ПринятиеКУчетуОС.Склад,
	|	ПринятиеКУчетуОС.СобытиеОС,
	|	ПринятиеКУчетуОС.ОС.(
	|		Ссылка,
	|		НомерСтроки,
	|		ОсновноеСредство,
	|		ИнвентарныйНомер,
	|		МОЛ,
	|		Подразделение КАК Получатель,
	|		ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|		СпособНачисленияАмортизации,
	|		СпособыОтраженияРасходовПоАмортизации,
	|		СрокСлужбы КАК СрокСлужбы,
	|		СчетУчета КАК СчетДт,
	|		ГруппаНУ,
	|		ГруппаИмущества,
	|		ЛиквидационнаяСтоимость,
	|		НакопленнаяАмортизация,
	|		КодНазначенияИмущества,
	|		КодПользователяИмущества,
	|		СчетУчета.Представление КАК СчетДтПредставление,
	|		СчетУчета.ПарныйСчет.Представление КАК ПарныйСчетПредставление,
	|		МОЛ.ФИО КАК РасшифровкаПодписи,
	|		ОсновноеСредство.Наименование,
	|		ОсновноеСредство.Код,
	|		ОсновноеСредство.ДатаВыпуска,
	|		МатериалСтен,
	|		ЖилаяПлощадь,
	|		НежилаяПлощадь,
	|		РегиональныйКоэффициент,
	|		ЗональныйКоэффициент,
	|		ОтраслевойКоэффициент,
	|		КодИмущества,
	|		КатегорияИмущества,
	|		ВидТранспорта,
	|		ОбъемДвигателя
	|	),
	|	ПринятиеКУчетуОС.Комиссия.(
	|		Ссылка,
	|		НомерСтроки,
	|		ФизЛицо,
	|		Председатель,
	|		ФизЛицо.ФИО КАК РасшифровкаПодписи
	|	),
	|	ПринятиеКУчетуОС.Организация.НаименованиеПолное,
	|	ПринятиеКУчетуОС.Организация.КодПоОКПО КАК КодПоОКПО
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|ГДЕ
	|	ПринятиеКУчетуОС.Ссылка В(&МассивОбъектов)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ с печатной формой ПКО
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьАктаПриемкиПередачиОС(МассивОбъектов, ОбъектыПечати)
	Перем Ошибки;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати 	= "ПринятиеКУчетуОС_АктПриемкиПередачиОС";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПринятиеКУчетуОС.ПФ_MXL_АктПриемкиПередачи");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ПолучитьТекстЗапросаАктПриемкиПередачиОС();
	
	Запрос.Параметры.Вставить("Дата", );
	РезультатЗапроса 	= Запрос.Выполнить();
	Шапка 				= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.Следующий()  Цикл
		ВременнаяТаблицаОС 		= Шапка.ОС.Выгрузить();
		ТаблицаКомиссия = Шапка.Комиссия.Выгрузить();

		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СтруктураРуководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Шапка.Организация, Шапка.ДатаДокумента);
			
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		Если НЕ СтруктураРуководители.Свойство("Руководитель") = Неопределено Тогда
			ОбластьМакета.Параметры.РуководительДолжность 	= СтруктураРуководители.РуководительДолжность;
			ОбластьМакета.Параметры.РуководительФИО 		= СтруктураРуководители.Руководитель;		
		КонецЕсли;		
		ОбластьМакета.Параметры.СписокМОЛ 		= ПолучитьСписокМОЛ(ВременнаяТаблицаОС);
	    ОбластьМакета.Параметры.НомерДокумента 	= ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента);
		ОбластьМакета.Параметры.ДатаДокумента 	= Формат(Шапка.ДатаДокумента, "ДЛФ=DD");		
	    ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы1");
	    ТабличныйДокумент.Вывести(ОбластьМакета);
				
		Для каждого СтрокаТаблицыЗначений Из ВременнаяТаблицаОС Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы1");
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыЗначений);
		    ТабличныйДокумент.Вывести(ОбластьМакета);		
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Приказ");
		ОбластьМакета.Параметры.НаименованиеОбъекта = ПолучитьСписокОС(ВременнаяТаблицаОС);
	    ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы2");
	    ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Для каждого СтрокаТаблицыЗначений Из ВременнаяТаблицаОС Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы2");
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыЗначений);
			ОбластьМакета.Параметры.ДатаВводаВЭксплуатацию = СтрЗаменить(Строка(Год(Шапка.ДатаДокумента)), Символы.НПП, "") + "-" + Формат(Месяц(Шапка.ДатаДокумента), "ЧЦ=2; ЧВН=");
		    ТабличныйДокумент.Вывести(ОбластьМакета);		
		КонецЦикла;

		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.ОснованиеПеремещения = Шапка.ДокументОснованиеВид 
						+ ?(ЗначениеЗаполнено(Шапка.ДокументОснованиеНомер), ", № " + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.ДокументОснованиеНомер), "") 
						+ ?(ЗначениеЗаполнено(Шапка.ДокументОснованиеДата), ", от " + Формат(Шапка.ДокументОснованиеДата, "ДЛФ=DD"), "");
	    ТабличныйДокумент.Вывести(ОбластьМакета);
				
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы3");
	    ТабличныйДокумент.Вывести(ОбластьМакета);		
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаОборотнойСтороны");
	    ТабличныйДокумент.Вывести(ОбластьМакета);
																		
		Для каждого СтрокаТаблицыЗначений Из ТаблицаКомиссия Цикл
			Если СтрокаТаблицыЗначений.Председатель Тогда
				ЗаголовокРазделаКомиссии = НСтр("ru = 'Председатель комиссии'");
				ОбластьМакета = Макет.ПолучитьОбласть("Комиссия");
				ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыЗначений);
				ОбластьМакета.Параметры.ЗаголовокРазделаКомиссии = ЗаголовокРазделаКомиссии;
			    ТабличныйДокумент.Вывести(ОбластьМакета);
				Прервать;				
			КонецЕсли;
		КонецЦикла;				
		
		ПервыйРядовойЧленКомиссиии = Истина;																
		Для каждого СтрокаТаблицыЗначений Из ТаблицаКомиссия Цикл
			Если НЕ СтрокаТаблицыЗначений.Председатель Тогда
				Если ПервыйРядовойЧленКомиссиии Тогда
					ЗаголовокРазделаКомиссии = НСтр("ru = 'Члены комиссии'");
					ПервыйРядовойЧленКомиссиии = Ложь;				
				Иначе
					ЗаголовокРазделаКомиссии = "";
				КонецЕсли;
				ОбластьМакета = Макет.ПолучитьОбласть("Комиссия");
				ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыЗначений);
				ОбластьМакета.Параметры.ЗаголовокРазделаКомиссии = ЗаголовокРазделаКомиссии;
			    ТабличныйДокумент.Вывести(ОбластьМакета);				
			КонецЕсли;
		КонецЦикла;			
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОССдал");
	    ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Первый = Истина;
		Для каждого СтрокаТаблицыЗначений Из ВременнаяТаблицаОС Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("МОЛ");
			Если Первый Тогда
				ОбластьМакета.Параметры.МП 		= "МП";
				ОбластьМакета.Параметры.Принял 	= "Принял";
				Первый 							= Ложь;
			Иначе
				ОбластьМакета.Параметры.МП 		= "";
				ОбластьМакета.Параметры.Принял 	= "";
			КонецЕсли;
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыЗначений);
			СтруктураМОЛ = ПроведениеРасчетовПоЗарплатеСервер.СведенияОСотруднике(Шапка.ДатаДокумента, Шапка.Организация, СтрокаТаблицыЗначений.МОЛ);
			ОбластьМакета.Параметры.Должность = СтруктураМОЛ.Должность;
			ТабличныйДокумент.Вывести(ОбластьМакета);		
		КонецЦикла;

		
		ОбластьМакета = Макет.ПолучитьОбласть("ПодписьГлавногоБухгалтера");
		Если НЕ СтруктураРуководители.Свойство("ГлавныйБухгалтер") = Неопределено Тогда
			ОбластьМакета.Параметры.БухгалтерФИО = СтруктураРуководители.ГлавныйБухгалтер;		
		КонецЕсли;			
	    ТабличныйДокумент.Вывести(ОбластьМакета);		
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	
	КонецЦикла;
		
	Если НЕ Ошибки = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	КонецЕсли;	
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	// Добавление дополнительных параметров
	БухгалтерскиеОтчетыКлиентСервер.ЗаполнитьДополнительныеПараметрыПечати(ТабличныйДокумент);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемкиПередачиОС") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"АктПриемкиПередачиОС", НСтр("ru = 'Акт приемки-передачи ОС'"), ПечатьАктаПриемкиПередачиОС(МассивОбъектов, ОбъектыПечати),,
			"Документ.ПринятиеКУчетуОС.ПФ_MXL_АктПриемкиПередачи");
	КонецЕсли;	
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктПриемкиПередачиОС";
	КомандаПечати.Представление = НСтр("ru = 'Акт приемки-передачи ОС'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
