#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = КонецМесяца(ТекущаяДатаСеанса());
		Если ЕстьДокументВУказанныйПериод(Дата) Тогда
			Дата = Дата('00010101');
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Функция ЕстьДокументВУказанныйПериод(Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНСП.Ссылка
		|ИЗ
		|	Документ.ОтчетПоНСП КАК ОтчетПоНСП
		|ГДЕ
		|	НЕ ОтчетПоНСП.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ОтчетПоНСП.Дата, МЕСЯЦ) = &Дата";
	
	Запрос.УстановитьПараметр("Дата", Дата);		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 	

КонецФункции 

// Процедура заполняет табличную часть ОтчетОсновной
//
Процедура ЗаполнитьТабличнуюЧастьОтчетПоНСП() Экспорт

	ДанныеУчетнойПолитики = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(Дата, Организация);	
		
	Если ДанныеУчетнойПолитики.ОтчетПоНСПВСекциюБ ИЛИ НЕ ДанныеУчетнойПолитики.ПлательщикНДС Тогда 
		Секция = 2;
	Иначе 
		Секция = 1;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ПродажиОбороты.СуммаОборот КАК Выручка,
		|	ПродажиОбороты.СуммаНСПОборот КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП";	
	Если ВидСубъекта = 0 ИЛИ ВидСубъекта = 1 Тогда
		Запрос.УстановитьПараметр("НачалоПериода", 	НачалоКвартала(Дата));
		Запрос.УстановитьПараметр("КонецПериода", 	КонецКвартала(Дата));
	Иначе 
		Запрос.УстановитьПараметр("НачалоПериода", 	НачалоМесяца(Дата));
		Запрос.УстановитьПараметр("КонецПериода", 	КонецМесяца(Дата));
	КонецЕсли; 
	Запрос.УстановитьПараметр("Организация", Организация);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ДанныеУчетнойПолитики.Контракт Тогда
		
		ТаблицаВыручкиСПродаж = РезультатЗапроса.Выгрузить();
		ТаблицаВыручкиСПродаж.Свернуть("БезналичныйРасчет", "Выручка");
		
		Для Каждого СтрокаТаблицы Из ТаблицаВыручкиСПродаж Цикл
			СтрокаТабличнойЧасти = ОтчетОсновной.Добавить();
			
			СтрокаТабличнойЧасти.Строка 		= ?(СтрокаТаблицы.БезналичныйРасчет, "085", "084");
			СтрокаТабличнойЧасти.Содержание 	= ?(СтрокаТаблицы.БезналичныйРасчет, 
										НСтр("ru = 'В БЕЗНАЛИЧНОЙ ФОРМЕ'"),
										НСтр("ru = 'В НАЛИЧНОЙ ФОРМЕ'"));
			СтрокаТабличнойЧасти.Выручка 	= СтрокаТаблицы.Выручка;
		КонецЦикла;	
		
	Иначе		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаТабличнойЧасти = ОтчетОсновной.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
				СтрокаТабличнойЧасти.Строка 	= "076";
				СтрокаТабличнойЧасти.Содержание = НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
				Продолжить;
				
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.УслугиБанка Тогда
				СтрокаТабличнойЧасти.Строка 	= "079";
				СтрокаТабличнойЧасти.Содержание = НСтр("ru = 'ПО БАНКОВСКОЙ ДЕЯТЕЛЬНОСТИ'");
				Продолжить;
			КонецЕсли;
			
			Если Выборка.БезналичныйРасчет Тогда
				СтрокаТабличнойЧасти.Ставка = 0;
				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "053", "066");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "059", "072");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
			    КонецЕсли;							
			Иначе
				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "050", "063");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "056", "069");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
			    КонецЕсли;					
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ЗаполнитьТабличнуюЧастьОтчетПоНСП()

// Процедура заполняет табличные части Приложения
//
Процедура ЗаполнитьТабличныеЧастиПриложениеКОтчетуПоНСП() Экспорт 

	ДанныеУчетнойПолитики = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(КонецМесяца(Дата), Организация);	
		
	Если ДанныеУчетнойПолитики.ОтчетПоНСПВСекциюБ ИЛИ НЕ ДанныеУчетнойПолитики.ПлательщикНДС Тогда 
		Секция = 2;
	Иначе 
		Секция = 1;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачалоКвартала, КОНЕЦПЕРИОДА(&НачалоКвартала, МЕСЯЦ), , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(КОНЕЦПЕРИОДА(&НачалоКвартала, МЕСЯЦ), Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(НАЧАЛОПЕРИОДА(&ВторойМесяцКвартала, МЕСЯЦ), КОНЕЦПЕРИОДА(&ВторойМесяцКвартала, МЕСЯЦ), , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(КОНЕЦПЕРИОДА(&ВторойМесяцКвартала, МЕСЯЦ), Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(НАЧАЛОПЕРИОДА(&КонецКвартала, МЕСЯЦ), КОНЕЦПЕРИОДА(&КонецКвартала, МЕСЯЦ), , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(КОНЕЦПЕРИОДА(&КонецКвартала, МЕСЯЦ), Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП";	
	Запрос.УстановитьПараметр("НачалоКвартала", 		НачалоКвартала(Дата));	
	Запрос.УстановитьПараметр("ВторойМесяцКвартала", 	ДобавитьМесяц(НачалоКвартала(Дата),+1));	
	Запрос.УстановитьПараметр("КонецКвартала", 			КонецКвартала(Дата));	
	Запрос.УстановитьПараметр("Организация", 			Организация);	
	Запрос.УстановитьПараметр("ПлательщикНДС", 			?(Секция = 2, Ложь, Истина));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();		
	
	Если ДанныеУчетнойПолитики.Контракт Тогда
		
		// Первый месяц
		ТаблцаВыручкиЗаПервыйМесяц = РезультатЗапроса[0].Выгрузить();
		ТаблцаВыручкиЗаПервыйМесяц.Свернуть("БезналичныйРасчет", "Выручка");
		
		Для Каждого СтрокаТаблицы Из ТаблцаВыручкиЗаПервыйМесяц Цикл
			СтрокаТабличнойЧасти = ПриложениеПервыйМесяцКвартала.Добавить();
			
			СтрокаТабличнойЧасти.Строка 		= ?(СтрокаТаблицы.БезналичныйРасчет, "185", "184");
			СтрокаТабличнойЧасти.Содержание 	= ?(СтрокаТаблицы.БезналичныйРасчет, 
										НСтр("ru = 'В БЕЗНАЛИЧНОЙ ФОРМЕ'"),
										НСтр("ru = 'В НАЛИЧНОЙ ФОРМЕ'"));
			СтрокаТабличнойЧасти.Выручка 	= СтрокаТаблицы.Выручка;
		КонецЦикла;
		
		// Второй месяц
		ТаблцаВыручкиЗаВторойМесяц = РезультатЗапроса[1].Выгрузить();
		ТаблцаВыручкиЗаВторойМесяц.Свернуть("БезналичныйРасчет", "Выручка");
		
		Для Каждого СтрокаТаблицы Из ТаблцаВыручкиЗаВторойМесяц Цикл
			СтрокаТабличнойЧасти = ПриложениеВторойМесяцКвартала.Добавить();
			
			СтрокаТабличнойЧасти.Строка 		= ?(СтрокаТаблицы.БезналичныйРасчет, "235", "234");
			СтрокаТабличнойЧасти.Содержание 	= ?(СтрокаТаблицы.БезналичныйРасчет, 
										НСтр("ru = 'В БЕЗНАЛИЧНОЙ ФОРМЕ'"),
										НСтр("ru = 'В НАЛИЧНОЙ ФОРМЕ'"));
			СтрокаТабличнойЧасти.Выручка 	= СтрокаТаблицы.Выручка;
		КонецЦикла;
		
		// Третий месяц
		ТаблцаВыручкиЗаТретийМесяц = РезультатЗапроса[2].Выгрузить();
		ТаблцаВыручкиЗаТретийМесяц.Свернуть("БезналичныйРасчет", "Выручка");
		
		Для Каждого СтрокаТаблицы Из ТаблцаВыручкиЗаТретийМесяц Цикл
			СтрокаТабличнойЧасти = ПриложениеТретийМесяцКвартала.Добавить();
			
			СтрокаТабличнойЧасти.Строка 		= ?(СтрокаТаблицы.БезналичныйРасчет, "285", "284");
			СтрокаТабличнойЧасти.Содержание 	= ?(СтрокаТаблицы.БезналичныйРасчет, 
										НСтр("ru = 'В БЕЗНАЛИЧНОЙ ФОРМЕ'"),
										НСтр("ru = 'В НАЛИЧНОЙ ФОРМЕ'"));
			СтрокаТабличнойЧасти.Выручка 	= СтрокаТаблицы.Выручка;
		КонецЦикла;
		
	Иначе	
		// Первый месяц
		Выборка = РезультатЗапроса[0].Выбрать();	
		Пока Выборка.Следующий() Цикл
			
			СтрокаТабличнойЧасти = ПриложениеПервыйМесяцКвартала.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
					
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
				СтрокаТабличнойЧасти.Строка 	= "176";
				СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
				Продолжить;
				
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.УслугиБанка Тогда
				СтрокаТабличнойЧасти.Строка 	= "179";
				СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО БАНКОВСКОЙ ДЕЯТЕЛЬНОСТИ'");
				Продолжить;
			КонецЕсли;
					
			Если Выборка.БезналичныйРасчет Тогда
				СтрокаТабличнойЧасти.Ставка = 0;

				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "153", "166");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "159", "172");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
			    КонецЕсли;							
			Иначе
				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "150", "163");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "156", "169");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
			    КонецЕсли;					
			КонецЕсли;
		КонецЦикла;

		// Второй месяц
		Выборка = РезультатЗапроса[1].Выбрать();	
		Пока Выборка.Следующий() Цикл
			
			СтрокаТабличнойЧасти = ПриложениеВторойМесяцКвартала.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
					
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
				СтрокаТабличнойЧасти.Строка 	= "226";
				СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
				Продолжить;
				
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.УслугиБанка Тогда
				СтрокаТабличнойЧасти.Строка 	= "229";
				СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО БАНКОВСКОЙ ДЕЯТЕЛЬНОСТИ'");
				Продолжить;
			КонецЕсли;
					
			Если Выборка.БезналичныйРасчет Тогда
				СтрокаТабличнойЧасти.Ставка = 0;

				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "203", "216");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "209", "222");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
			    КонецЕсли;							
			Иначе
				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "200", "213");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "206", "219");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
			    КонецЕсли;					
			КонецЕсли;
		КонецЦикла;

		
		// Третий месяц
		Выборка = РезультатЗапроса[2].Выбрать();	
		Пока Выборка.Следующий() Цикл
			СтрокаТабличнойЧасти = ПриложениеТретийМесяцКвартала.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
				СтрокаТабличнойЧасти.Строка 	= "276";
				СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
				Продолжить;
				
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.УслугиБанка Тогда
				СтрокаТабличнойЧасти.Строка 	= "279";
				СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО БАНКОВСКОЙ ДЕЯТЕЛЬНОСТИ'");
				Продолжить;	
			КонецЕсли;
			
			Если Выборка.БезналичныйРасчет Тогда
				СтрокаТабличнойЧасти.Ставка = 0;

				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "253", "266");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "259", "272");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
				КонецЕсли;							
			Иначе
				Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "250", "263");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
				ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
					СтрокаТабличнойЧасти.Строка 	= ?(Секция = 1, "256", "269");
					СтрокаТабличнойЧасти.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
				КонецЕсли;					
			КонецЕсли;		
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли