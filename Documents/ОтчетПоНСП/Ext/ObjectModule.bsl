#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = КонецМесяца(ТекущаяДатаСеанса());
		Если ЕстьДокументВУказанныйПериод(Дата) Тогда
			Дата = Дата('00010101');
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Функция ЕстьДокументВУказанныйПериод(Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНСП.Ссылка
		|ИЗ
		|	Документ.ОтчетПоНСП КАК ОтчетПоНСП
		|ГДЕ
		|	НЕ ОтчетПоНСП.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ОтчетПоНСП.Дата, МЕСЯЦ) = &Дата";
	
	Запрос.УстановитьПараметр("Дата", Дата);		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 	

КонецФункции 

// Процедура заполняет табличную часть ОтчетОсновной
//
Процедура ЗаполнитьТабличнуюЧастьОтчетПоНСП() Экспорт

	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(КонецМесяца(Дата), Организация);	
		
	Если УПП.ОтчетПоНСПВСекциюБ Тогда 
		Секция = 2;
	Иначе 
		Секция = 1;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачалоМесяца, &КонецМесяца, , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(&КонецМесяца, Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП";	
	Если ВидСубъекта = 1 Тогда
		Запрос.УстановитьПараметр("НачалоМесяца", 	НачалоКвартала(Дата));
		Запрос.УстановитьПараметр("КонецМесяца", 	КонецКвартала(Дата));
	Иначе 
		Запрос.УстановитьПараметр("НачалоМесяца", 	НачалоМесяца(Дата));
		Запрос.УстановитьПараметр("КонецМесяца", 	КонецМесяца(Дата));
	КонецЕсли; 
	Запрос.УстановитьПараметр("Организация", 	Организация);	
	Запрос.УстановитьПараметр("ПлательщикНДС", 	?(Секция = 2, Ложь, Истина));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ОтчетОсновной.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
			НоваяСтрока.Строка 	= "076";
			НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
			Продолжить;
		КонецЕсли;
		
		Если Выборка.БезналичныйРасчет Тогда
			НоваяСтрока.Ставка = 0;
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "053", "066");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "059", "072");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
		    КонецЕсли;							
		Иначе
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "050", "063");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "056", "069");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
		    КонецЕсли;					
		КонецЕсли;

	КонецЦикла;
КонецПроцедуры // ЗаполнитьТабличнуюЧастьОтчетПоНСП()

// Процедура заполняет табличные части Приложения
//
Процедура ЗаполнитьТабличныеЧастиПриложениеКОтчетуПоНСП() Экспорт 

	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(КонецМесяца(Дата), Организация);	
		
	Если УПП.ОтчетПоНСПВСекциюБ Тогда 
		Секция = 2;
	Иначе 
		Секция = 1;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачалоКвартала, КОНЕЦПЕРИОДА(&НачалоКвартала, МЕСЯЦ), , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(КОНЕЦПЕРИОДА(&НачалоКвартала, МЕСЯЦ), Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(НАЧАЛОПЕРИОДА(&ВторойМесяцКвартала, МЕСЯЦ), КОНЕЦПЕРИОДА(&ВторойМесяцКвартала, МЕСЯЦ), , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(КОНЕЦПЕРИОДА(&ВторойМесяцКвартала, МЕСЯЦ), Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродажиОбороты.БезналичныйРасчет КАК БезналичныйРасчет,
		|	СУММА(ПродажиОбороты.СуммаОборот) КАК Выручка,
		|	СУММА(ПродажиОбороты.СуммаНСПОборот) КАК СуммаНСП,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка,
		|	ПродажиОбороты.СтавкаНСП КАК СтавкаНСП
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(НАЧАЛОПЕРИОДА(&КонецКвартала, МЕСЯЦ), КОНЕЦПЕРИОДА(&КонецКвартала, МЕСЯЦ), , Организация = &Организация) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(КОНЕЦПЕРИОДА(&КонецКвартала, МЕСЯЦ), Организация = &Организация) КАК СтавкиНСПСрезПоследних
		|		ПО ПродажиОбороты.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНСПСрезПоследних.Ставка,
		|	ПродажиОбороты.БезналичныйРасчет,
		|	ПродажиОбороты.СтавкаНСП";	
	Запрос.УстановитьПараметр("НачалоКвартала", 		НачалоКвартала(Дата));	
	Запрос.УстановитьПараметр("ВторойМесяцКвартала", 	ДобавитьМесяц(НачалоКвартала(Дата),+1));	
	Запрос.УстановитьПараметр("КонецКвартала", 			КонецКвартала(Дата));	
	Запрос.УстановитьПараметр("Организация", 			Организация);	
	Запрос.УстановитьПараметр("ПлательщикНДС", 			?(Секция = 2, Ложь, Истина));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();		
	
	// Первый месяц
	Выборка = РезультатЗапроса[0].Выбрать();	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ПриложениеПервыйМесяцКвартала.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
		Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
			НоваяСтрока.Строка 	= "176";
			НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
			Продолжить;
		КонецЕсли;
				
		Если Выборка.БезналичныйРасчет Тогда
			НоваяСтрока.Ставка = 0;

			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "153", "166");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "159", "172");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
		    КонецЕсли;							
		Иначе
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "150", "163");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "156", "169");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
		    КонецЕсли;					
		КонецЕсли;
	КонецЦикла;

	// Второй месяц
	Выборка = РезультатЗапроса[1].Выбрать();	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ПриложениеВторойМесяцКвартала.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
		Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
			НоваяСтрока.Строка 	= "226";
			НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
			Продолжить;
		КонецЕсли;
				
		Если Выборка.БезналичныйРасчет Тогда
			НоваяСтрока.Ставка = 0;

			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "203", "216");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "209", "222");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
		    КонецЕсли;							
		Иначе
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "200", "213");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "206", "219");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
		    КонецЕсли;					
		КонецЕсли;
	КонецЦикла;

	
	// Третий месяц
	Выборка = РезультатЗапроса[2].Выбрать();	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ПриложениеТретийМесяцКвартала.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.СотоваяСвязь Тогда
			НоваяСтрока.Строка 	= "276";
			НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ В СФЕРЕ СОТОВОЙ СВЯЗИ'");
			Продолжить;
		КонецЕсли;
		
		Если Выборка.БезналичныйРасчет Тогда
			НоваяСтрока.Ставка = 0;

			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "253", "266");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "259", "272");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В БЕЗНАЛИЧНОЙ ФОРМЕ'");						
			КонецЕсли;							
		Иначе
			Если Выборка.СтавкаНСП = Справочники.СтавкиНСП.Торговля Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "250", "263");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");					
			ИначеЕсли Выборка.СтавкаНСП = Справочники.СтавкиНСП.Прочее Тогда
				НоваяСтрока.Строка 	= ?(Секция = 1, "256", "269");
				НоваяСтрока.Содержание 	= НСтр("ru = 'ПО ДЕЯТЕЛЬНОСТИ, ЗА ИСКЛЮЧЕНИЕМ ТОРГОВОЙ ДЕЯТЕЛЬНОСТИ, ОПЛАЧЕННЫХ: В НАЛИЧНОЙ ФОРМЕ'");						
			КонецЕсли;					
		КонецЕсли;		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли