#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета КАК СчетДт,
		|	ВременнаяТаблицаШапка.Касса.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.Касса КАК СубконтоКт1,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция КАК Содержание,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.ВалютаДоговора
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета.Валютный
		|			ТОГДА ВЫБОР
		|					КОГДА ВременнаяТаблицаШапка.Курс >= ВременнаяТаблицаШапка.КурсДоговора
		|						ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
		|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа / ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаКт
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	(ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ОплатаПоставщику)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВозвратПокупателю)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.РасчетыПоЗаймам))
		|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета,
		|	ВременнаяТаблицаШапка.Касса.СчетУчета,
		|	ВременнаяТаблицаШапка.БанковскийСчет,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Касса,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,
		|	ВременнаяТаблицаШапка.СуммаДокумента * ВременнаяТаблицаШапка.Курс,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.СуммаДокумента
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.СуммаДокумента
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВзносВБанк)
		|	И ВременнаяТаблицаШапка.СуммаДокумента <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СчетУчета,
		|	ВременнаяТаблицаШапка.Касса.СчетУчета,
		|	ВременнаяТаблицаШапка.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Касса,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,
		|	ВременнаяТаблицаШапка.СуммаДокумента * ВременнаяТаблицаШапка.Курс,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.СуммаДокумента
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.СуммаДокумента
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.Инкассация)
		|	И ВременнаяТаблицаШапка.СуммаДокумента <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПрочийРасход.СчетУчета,
		|	ВременнаяТаблицаШапка.Касса.СчетУчета,
		|	ВременнаяТаблицаПрочийРасход.Субконто1,
		|	ВременнаяТаблицаПрочийРасход.Субконто2,
		|	ВременнаяТаблицаПрочийРасход.Субконто3,
		|	ВременнаяТаблицаШапка.Касса,
		|	ВременнаяТаблицаПрочийРасход.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,
		|	ВременнаяТаблицаПрочийРасход.СуммаПлатежа * ВременнаяТаблицаШапка.Курс,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаПрочийРасход.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаПрочийРасход.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаПрочийРасход.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаПрочийРасход.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаПрочийРасход КАК ВременнаяТаблицаПрочийРасход
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаПрочийРасход.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ПрочийРасход)
		|	И ВременнаяТаблицаПрочийРасход.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СчетУчета,
		|	ВременнаяТаблицаШапка.Касса.СчетУчета,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Касса,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств,
		|	ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа
		|ИЗ
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ВременнаяТаблицаВыплатаЗаработнойПлаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаВыплатаЗаработнойПлаты.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗП)
		|	И ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СчетУчета,
		|	ВременнаяТаблицаШапка.Касса.СчетУчета,
		|	ВременнаяТаблицаШапка.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Касса,
		|	ВременнаяТаблицаВыдачаВПодотчет.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,
		|	ВременнаяТаблицаВыдачаВПодотчет.СуммаПлатежа * ВременнаяТаблицаШапка.Курс,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаВыдачаВПодотчет.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Касса.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаВыдачаВПодотчет.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаВыдачаВПодотчет КАК ВременнаяТаблицаВыдачаВПодотчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаВыдачаВПодотчет.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыдачаПодотчетнику)
		|	И ВременнаяТаблицаВыдачаВПодотчет.СуммаПлатежа <> 0";
						
	//Операционная курсовая разница
	ТекстОКР = "";
	Если НЕ Константы.НеСчитатьОперационныеКурсовыеРазницы.Получить() Тогда
		Если ДокументСсылка.ВалютаДенежныхСредств <> ВалютаРегламентированногоУчета И ДокументСсылка.ВалютаДоговора <> ВалютаРегламентированногоУчета Тогда
			ТекстОКР = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте) 
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|	КОНЕЦ КАК СчетДт,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте)
			|	КОНЕЦ КАК СчетКт,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.КурсовыеРазницы)
			|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент
			|	КОНЕЦ КАК СубконтоДт1,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|	КОНЕЦ КАК СубконтоДт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА ВременнаяТаблицаШапка.Контрагент
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК СубконтоКт1,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК СубконтоКт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
			|	ВременнаяТаблицаШапка.Дата,
			|	ВременнаяТаблицаШапка.Организация,
			|	ВременнаяТаблицаШапка.СодержаниеОКР КАК Содержание,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс
			|		ИНАЧЕ -((ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс)
			|	КОНЕЦ КАК Сумма,						
			|	ВременнаяТаблицаШапка.ВалютаДоговора,
			|	&ВалютаРегламентированногоУчета,
			|	0 КАК Поле4,
			|	0 КАК Поле7
			|ИЗ
			|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
			|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
			|ГДЕ
			|	НЕ (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс = 0
			|	И НЕ ВременнаяТаблицаШапка.ВалютаДоговора = ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
			|	И НЕ ВременнаяТаблицаШапка.КурсДоговора = ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов";			
	
		Иначе
			ТекстОКР = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте) 
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|	КОНЕЦ КАК СчетДт,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте)
			|	КОНЕЦ КАК СчетКт,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.КурсовыеРазницы)
			|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент
			|	КОНЕЦ КАК СубконтоДт1,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|	КОНЕЦ КАК СубконтоДт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ВременнаяТаблицаШапка.Контрагент
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК СубконтоКт1,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК СубконтоКт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
			|	ВременнаяТаблицаШапка.Дата,
			|	ВременнаяТаблицаШапка.Организация,
			|	ВременнаяТаблицаШапка.СодержаниеОКР КАК Содержание,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора
			|		ИНАЧЕ -(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора)
			|	КОНЕЦ КАК Сумма,						
			|	ВременнаяТаблицаШапка.ВалютаДоговора,
			|	&ВалютаРегламентированногоУчета,
			|	0 КАК Поле4,
			|	0 КАК Поле7
			|ИЗ
			|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
			|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
			|ГДЕ
			|	НЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора = 0
			|	И НЕ ВременнаяТаблицаШапка.ВалютаДоговора = ВременнаяТаблицаШапка.Касса.ВалютаДенежныхСредств
			|	И НЕ ВременнаяТаблицаШапка.КурсДоговора = ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов"			
			
		КонецЕсли;
		ТекстОКР = 
		"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|"
		+ ТекстОКР;	
			
	КонецЕсли;

	Запрос.Текст = Запрос.Текст + ТекстОКР;	
	
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ДвиженияДенежныхСредств.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДДС(СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК Статья,
		|	-ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Дата КАК Период
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	НЕ ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
		|	И (ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ОплатаПоставщику)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВозвратПокупателю)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.РасчетыПоЗаймам))
		|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПрочийРасход.СтатьяДвиженияДенежныхСредств,
		|	ВременнаяТаблицаПрочийРасход.СуммаПлатежа,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Дата
		|ИЗ
		|	ВременнаяТаблицаПрочийРасход КАК ВременнаяТаблицаПрочийРасход
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаПрочийРасход.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	НЕ ВременнаяТаблицаПрочийРасход.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
		|	И ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ПрочийРасход)
		|	И ВременнаяТаблицаПрочийРасход.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаВыдачаВПодотчет.СтатьяДвиженияДенежныхСредств,
		|	ВременнаяТаблицаВыдачаВПодотчет.СуммаПлатежа * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Дата
		|ИЗ
		|	ВременнаяТаблицаВыдачаВПодотчет КАК ВременнаяТаблицаВыдачаВПодотчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаВыдачаВПодотчет.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	НЕ ВременнаяТаблицаВыдачаВПодотчет.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
		|	И ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыдачаПодотчетнику)
		|	И ВременнаяТаблицаВыдачаВПодотчет.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СтатьяДвиженияДенежныхСредств,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Дата
		|ИЗ
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ВременнаяТаблицаВыплатаЗаработнойПлаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаВыплатаЗаработнойПлаты.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	НЕ ВременнаяТаблицаВыплатаЗаработнойПлаты.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
		|	И ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗП)
		|	И ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа <> 0";
			
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДДС", РезультатЗапроса.Выгрузить());	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВыплаченнаяЗарплата(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаШапка.Дата КАК Период,
		|	ТаблицаШапка.Организация,
		|	ВедомостьНаВыплатуЗарплатыЗарплата.ФизЛицо,
		|	ТаблицаДокумента.Ведомость,
		|	ВЫРАЗИТЬ(ТаблицаДокумента.Ведомость КАК Документ.ВедомостьЗП).ПериодРегистрации КАК ПериодРегистрации,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыВыплатыЗарплаты.ЧерезКассу) КАК ВидВыплаты,
		|	ВедомостьНаВыплатуЗарплатыЗарплата.СуммаКВыплате КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ТаблицаДокумента
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВедомостьЗП.Зарплата КАК ВедомостьНаВыплатуЗарплатыЗарплата
		|			ПО ТаблицаДокумента.Ведомость = ВедомостьНаВыплатуЗарплатыЗарплата.Ссылка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗП)
		|	И ТаблицаШапка.ЧерезПодотчетника
		|	И ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Ведомость) = ТИП(Документ.ВедомостьЗП)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.Дата КАК Период,
		|	ТаблицаШапка.Организация,
		|	ВыплатаЗарплатыЗарплата.ФизЛицо,
		|	ТаблицаДокумента.Ведомость,
		|	ВЫРАЗИТЬ(ТаблицаДокумента.Ведомость КАК Документ.ВыплатаЗП).ПериодРегистрации КАК ПериодРегистрации,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыВыплатыЗарплаты.ЧерезКассу) КАК ВидВыплаты,
		|	ВыплатаЗарплатыЗарплата.СуммаКВыплате КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ТаблицаДокумента
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыплатаЗП.Зарплата КАК ВыплатаЗарплатыЗарплата
		|			ПО ТаблицаДокумента.Ведомость = ВыплатаЗарплатыЗарплата.Ссылка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗП)
		|	И ТаблицаШапка.ЧерезПодотчетника
		|	И ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Ведомость) = ТИП(Документ.ВыплатаЗП)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.Дата,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаШапка.Ведомость,
		|   ВЫБОР
		|		КОГДА ТаблицаШапка.Ведомость Ссылка Документ.ВедомостьЗП
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаШапка.Ведомость КАК Документ.ВедомостьЗП).ПериодРегистрации
		|       ИНАЧЕ ВЫРАЗИТЬ(ТаблицаШапка.Ведомость КАК Документ.ВыплатаЗП).ПериодРегистрации
		|   КОНЕЦ, 
		|	ЗНАЧЕНИЕ(Перечисление.ВидыВыплатыЗарплаты.ЧерезКассу),
		|	ТаблицаДокумента.СуммаПлатежа
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗП)
		|	И НЕ ТаблицаШапка.ЧерезПодотчетника";
		
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыплаченнаяЗарплата", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаВыплаченнаяЗарплата()

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ДанныеРеестраГТД.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДанныеРеестраГТД(ДокументСсылка,СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаРасшифровкаПлатежа.ДокументРасчетов КАК Документ,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	(ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ОплатаПоставщику)
		|				И (ВЫРАЗИТЬ(ВременнаяТаблицаРасшифровкаПлатежа.ДокументРасчетов КАК Документ.ГТДПоИмпорту) ССЫЛКА Документ.ГТДПоИмпорту
		|					И НЕ (ВЫРАЗИТЬ(ВременнаяТаблицаРасшифровкаПлатежа.ДокументРасчетов КАК Документ.ГТДПоИмпорту)) = ЗНАЧЕНИЕ(Документ.ГТДПоИмпорту.ПустаяСсылка))
		|			ИЛИ ВЫРАЗИТЬ(ВременнаяТаблицаРасшифровкаПлатежа.ДокументРасчетов КАК Документ.ПоступлениеТоваровУслуг) ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|				И НЕ (ВЫРАЗИТЬ(ВременнаяТаблицаРасшифровкаПлатежа.ДокументРасчетов КАК Документ.ПоступлениеТоваровУслуг)) = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка))
		|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа <> 0";
	
		
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДанныеРеестраГТД", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру "АвансыПодотчетника".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаАвансыПодотчетника(ДокументСсылка,СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Документ,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаШапка.ВалютаДенежныхСредств КАК Валюта,
		|	ВременнаяТаблицаШапка.СуммаДокумента КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииРКО.ВыдачаДенежныхСредствПодотчетнику)";	
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыПодотчетника", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.ВерсияДанных,
		|	ТаблицаДокумента.ПометкаУдаления,
		|	ТаблицаДокумента.Номер,
		|	ТаблицаДокумента.Дата,
		|	ТаблицаДокумента.Проведен,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.Операция,
		|	ТаблицаДокумента.Касса,
		|	ТаблицаДокумента.ВалютаДенежныхСредств,
		|	ТаблицаДокумента.ВалютаДоговора,
		|	ТаблицаДокумента.СтатьяДвиженияДенежныхСредств,
		|	ТаблицаДокумента.Контрагент,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.БанковскийСчет,
		|	ТаблицаДокумента.Курс,
		|	ТаблицаДокумента.КурсДоговора,
		|	ТаблицаДокумента.ДокументОснование,
		|	ТаблицаДокумента.СуммаДокумента,
		|	ТаблицаДокумента.Комментарий,
		|	ТаблицаДокумента.Автор,
		|	ТаблицаДокумента.НазначениеПлатежа,
		|	ТаблицаДокумента.Выдать,
		|	ТаблицаДокумента.Основание,
		|	ТаблицаДокумента.Приложение,
		|	ТаблицаДокумента.ПоДокументу,
		|	ТаблицаДокумента.ЧерезПодотчетника,
		|	&СодержаниеОКР КАК СодержаниеОКР,
		|	ТаблицаДокумента.Ведомость
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.НомерСтроки,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчета,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаПлатежа,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.КурсВзаиморасчетов,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.ПрямойКурс,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Комментарий,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов,
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.ДокументРасчетов
		|ПОМЕСТИТЬ ВременнаяТаблицаРасшифровкаПлатежа
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
		|ГДЕ
		|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходныйКассовыйОрдерПрочиеРасходы.Ссылка,
		|	РасходныйКассовыйОрдерПрочиеРасходы.НомерСтроки,
		|	РасходныйКассовыйОрдерПрочиеРасходы.СчетУчета,
		|	РасходныйКассовыйОрдерПрочиеРасходы.СтатьяДвиженияДенежныхСредств,
		|	РасходныйКассовыйОрдерПрочиеРасходы.СуммаПлатежа,
		|	РасходныйКассовыйОрдерПрочиеРасходы.Субконто1,
		|	РасходныйКассовыйОрдерПрочиеРасходы.Субконто2,
		|	РасходныйКассовыйОрдерПрочиеРасходы.Субконто3
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочийРасход
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер.ПрочиеРасходы КАК РасходныйКассовыйОрдерПрочиеРасходы
		|ГДЕ
		|	РасходныйКассовыйОрдерПрочиеРасходы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходныйКассовыйОрдерВыдачаВПодотчет.Ссылка,
		|	РасходныйКассовыйОрдерВыдачаВПодотчет.НомерСтроки,
		|	РасходныйКассовыйОрдерВыдачаВПодотчет.СтатьяДвиженияДенежныхСредств,
		|	РасходныйКассовыйОрдерВыдачаВПодотчет.СуммаПлатежа
		|ПОМЕСТИТЬ ВременнаяТаблицаВыдачаВПодотчет
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер.ВыдачаВПодотчет КАК РасходныйКассовыйОрдерВыдачаВПодотчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
		|		ПО РасходныйКассовыйОрдерВыдачаВПодотчет.Ссылка = РасходныйКассовыйОрдер.Ссылка
		|ГДЕ
		|	РасходныйКассовыйОрдер.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка,
		|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.НомерСтроки,
		|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость,
		|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.ФизЛицо,
		|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.СтатьяДвиженияДенежныхСредств,
		|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.СуммаПлатежа,
		|	РасходныйКассовыйОрдер.ЧерезПодотчетника
		|ПОМЕСТИТЬ ВременнаяТаблицаВыплатаЗаработнойПлаты
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер.ВыплатаЗаработнойПлаты КАК РасходныйКассовыйОрдерВыплатаЗаработнойПлаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
		|		ПО РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка = РасходныйКассовыйОрдер.Ссылка
		|ГДЕ
		|	РасходныйКассовыйОрдер.Ссылка = &Ссылка";
		
		
	Запрос.УстановитьПараметр("Ссылка", 		ДокументСсылка);
	Запрос.УстановитьПараметр("СодержаниеОКР", 	?(ДокументСсылка.РасшифровкаПлатежа.Количество() > 0, "ОКР. Курс НБКР = " + ДокументСсылка.Курс, ""));	
	
	Запрос.Выполнить();
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДДС(СтруктураДополнительныеСвойства);
	СформироватьТаблицаВыплаченнаяЗарплата(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДанныеРеестраГТД(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаАвансыПодотчетника(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьСтрокуСчетовДт(ВыборкаРасшифровкаПлатежа)

	Первый = Истина;
	СтрокаСчетаДт = "";
	
	Пока ВыборкаРасшифровкаПлатежа.Следующий()  Цикл
		СтрокаСчетаДт = СтрокаСчетаДт + ?(Первый, ВыборкаРасшифровкаПлатежа.СчетУчета, ", " + ВыборкаРасшифровкаПлатежа.СчетУчета);                    
		Первый = Ложь;
	КонецЦикла;
	
	ВыборкаРасшифровкаПлатежа.Сбросить();
	
	Возврат СтрокаСчетаДт;
	
КонецФункции // ()

Функция СформироватьСтрокуКодовАналитическогоУчета(ВыборкаПрочиеРасходы)

	Первый = Истина;
	Строка = "";
	
	Пока ВыборкаПрочиеРасходы.Следующий()  Цикл
		Код = ВыборкаПрочиеРасходы.КодАналитическогоУчета;
		Если ЗначениеЗаполнено(Код) Тогда
			Строка = Строка + ?(Первый, Код, ", " + Код);                    
			Первый = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ВыборкаПрочиеРасходы.Сбросить();
	
	Возврат Строка;
	
КонецФункции // ()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
//
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыРКО()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Ссылка,
	|	РасходныйКассовыйОрдер.ВерсияДанных,
	|	РасходныйКассовыйОрдер.ПометкаУдаления,
	|	РасходныйКассовыйОрдер.Номер,
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.Проведен,
	|	РасходныйКассовыйОрдер.Организация,
	|	РасходныйКассовыйОрдер.Операция,
	|	РасходныйКассовыйОрдер.Касса,
	|	РасходныйКассовыйОрдер.ВалютаДенежныхСредств,
	|	РасходныйКассовыйОрдер.СтатьяДвиженияДенежныхСредств,
	|	РасходныйКассовыйОрдер.Контрагент,
	|	РасходныйКассовыйОрдер.СчетУчета КАК Корреспонденция,
	|	РасходныйКассовыйОрдер.ФизЛицо,
	|	РасходныйКассовыйОрдер.БанковскийСчет,
	|	РасходныйКассовыйОрдер.Курс,
	|	РасходныйКассовыйОрдер.ДокументОснование,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	РасходныйКассовыйОрдер.Комментарий,
	|	РасходныйКассовыйОрдер.Автор,
	|	РасходныйКассовыйОрдер.НазначениеПлатежа,
	|	РасходныйКассовыйОрдер.Выдать,
	|	РасходныйКассовыйОрдер.Основание,
	|	РасходныйКассовыйОрдер.Приложение,
	|	РасходныйКассовыйОрдер.ПоДокументу,
	|	РасходныйКассовыйОрдер.Касса.СчетУчета КАК Кредит,
	|	ВЫБОР
	|		КОГДА РасходныйКассовыйОрдер.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыдачаПодотчетнику)
	|			ТОГДА РасходныйКассовыйОрдер.ФизЛицо.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ТабельныйНомерФизЛица,
	|	РасходныйКассовыйОрдер.БанковскийСчет.СчетУчета КАК СчетУчетаБанковскогоСчета,
	|	"""" КАК КодПодразделения
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер.ВыдачаВПодотчет КАК РасходныйКассовыйОрдерВыдачаВПодотчет
	|		ПО РасходныйКассовыйОрдер.Ссылка = РасходныйКассовыйОрдерВыдачаВПодотчет.Ссылка
	|ГДЕ
	|	РасходныйКассовыйОрдер.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка,
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.НомерСтроки,
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость,
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.ФизЛицо,
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.СтатьяДвиженияДенежныхСредств,
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.СуммаПлатежа,
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.ФизЛицо.Код КАК ТабельныйНомерФизЛица,
	|	ВЫБОР
	|		КОГДА РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость ССЫЛКА Документ.ВедомостьЗП
	|			ТОГДА РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость.Номер
	|		КОГДА РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость ССЫЛКА Документ.ВыплатаЗП
	|			ТОГДА РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость.Номер
	|		ИНАЧЕ РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость
	|	КОНЕЦ КАК ВедомостьНомер
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.ВыплатаЗаработнойПлаты КАК РасходныйКассовыйОрдерВыплатаЗаработнойПлаты
	|ГДЕ
	|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗП)
	|	И РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчета
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	РасходныйКассовыйОрдерПрочиеРасходы.Ссылка,
	|	РасходныйКассовыйОрдерПрочиеРасходы.НомерСтроки,
	|	РасходныйКассовыйОрдерПрочиеРасходы.СчетУчета,
	|	РасходныйКассовыйОрдерПрочиеРасходы.СтатьяДвиженияДенежныхСредств,
	|	РасходныйКассовыйОрдерПрочиеРасходы.СуммаПлатежа,
	|	РасходныйКассовыйОрдерПрочиеРасходы.Субконто1,
	|	РасходныйКассовыйОрдерПрочиеРасходы.Субконто2,
	|	РасходныйКассовыйОрдерПрочиеРасходы.Субконто3,
	|	ВЫБОР
	|		КОГДА РасходныйКассовыйОрдерПрочиеРасходы.Субконто1 ССЫЛКА Справочник.ФизическиеЛица
	|				ИЛИ РасходныйКассовыйОрдерПрочиеРасходы.Субконто1 ССЫЛКА Справочник.Контрагенты
	|			ТОГДА РасходныйКассовыйОрдерПрочиеРасходы.Субконто1.Код
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КодАналитическогоУчета
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.ПрочиеРасходы КАК РасходныйКассовыйОрдерПрочиеРасходы
	|ГДЕ
	|	РасходныйКассовыйОрдерПрочиеРасходы.Ссылка В(&МассивОбъектов)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ с печатной формой ПКО
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьРКО(МассивОбъектов, ОбъектыПечати)
	Перем ПодразделениеОтветственныхЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "РасходныйКассовыйОрдер_КассовыйОрдер";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыРКО();
	
	РезультатЗапроса 			= Запрос.ВыполнитьПакет();
	Шапка 						= РезультатЗапроса[0].Выбрать();
	ВыборкаРасшифровкаПлатежа	= РезультатЗапроса[2].Выбрать();
	ВыборкаПрочийРасход			= РезультатЗапроса[3].Выбрать();
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ИмяМакета = "КО2";		
		Макет = Документы.РасходныйКассовыйОрдер.ПолучитьМакет(ИмяМакета);
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
				
		Валютный = Шапка.ВалютаДенежныхСредств <> ВалютаРегламентированногоУчета;
		
		ОбластьМакетаШапка 	= Макет.ПолучитьОбласть("Шапка");
		
		ОбластьМакетаШапка.Параметры.Валюта = Шапка.ВалютаДенежныхСредств;				
		ОбластьМакетаШапка.Параметры.Заполнить(Шапка);
		
		НашаОрганизация = Шапка.Организация;
				
		ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации = ?(ЗначениеЗаполнено(НашаОрганизация.НаименованиеПолное), НашаОрганизация.НаименованиеПолное, НашаОрганизация.Наименование);
		
		ОбластьМакетаШапка.Параметры.ОрганизацияПоОКПО = НашаОрганизация.КодПоОКПО;
		ОбластьМакетаШапка.Параметры.ДатаДокумента     = Формат(Шапка.Дата, "ДФ = 'дд.ММ.гггг'")+" г.";
		ОбластьМакетаШапка.Параметры.НомерДокумента    = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер); 		
				
		Если Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП Тогда
			
			ОбластьМакетаШапка.Параметры.ДебетСубСчет 			= Шапка.Корреспонденция;
			ОбластьМакетаШапка.Параметры.КредитСубСчет 			= Шапка.Кредит;
			ОбластьМакетаШапка.Параметры.Сумма 					= Шапка.СуммаДокумента;
		Иначе
			Если Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.ВзносВБанк Тогда
				ОбластьМакетаШапка.Параметры.ДебетСубСчет 				= Шапка.СчетУчетаБанковскогоСчета;
			ИначеЕсли Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику 
					ИЛИ Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю 
					ИЛИ Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыПоЗаймам Тогда				
				ОбластьМакетаШапка.Параметры.ДебетСубСчет 			= СформироватьСтрокуСчетовДт(ВыборкаРасшифровкаПлатежа);					
			ИначеЕсли Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.ПрочийРасход Тогда
				ОбластьМакетаШапка.Параметры.ДебетСубСчет 			= СформироватьСтрокуСчетовДт(ВыборкаПрочийРасход);				
			Иначе
				ОбластьМакетаШапка.Параметры.ДебетСубСчет 			= Шапка.Корреспонденция;
			КонецЕсли;
			
			ОбластьМакетаШапка.Параметры.КредитСубСчет 			= Шапка.Кредит;
			
			// << ХБ
			Если Шапка.Операция.ВидОперации = Перечисления.ВидыОперацийРКО.ПрочийРасход  Тогда
				ОбластьМакетаШапка.Параметры.КодАналитическогоУчета 	= СформироватьСтрокуКодовАналитическогоУчета(ВыборкаПрочийРасход);
			Иначе
				ОбластьМакетаШапка.Параметры.КодАналитическогоУчета 	= Шапка.ТабельныйНомерФизЛица;
			КонецЕсли;
			
			ОбластьМакетаШапка.Параметры.Сумма 					= Шапка.СуммаДокумента;

		КонецЕсли;
		
		
		ОбластьМакетаШапка.Параметры.СуммаПрописью     = РаботаСКурсамиВалют.СформироватьСуммуПрописью(Шапка.СуммаДокумента, Шапка.ВалютаДенежныхСредств);		
 
		ОбластьМакетаШапка.Параметры.СуммаПолучил  = РаботаСКурсамиВалют.СформироватьСуммуПрописью(Шапка.СуммаДокумента, Шапка.ВалютаДенежныхСредств);
				
		Руководители 	= БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Шапка.Организация, КонецДня(Шапка.Дата));
		Руководитель 	= Руководители.Руководитель;
		РуководительДолжность = Руководители.РуководительДолжность;
		Бухгалтер    	= Руководители.ГлавныйБухгалтер;
		Кассир    		= Руководители.Кассир;

		ОбластьМакетаШапка.Параметры.Приложение            = Шапка.Приложение;
		
		ОбластьМакетаШапка.Параметры.ФИОРуководителя       = Руководитель;
		ОбластьМакетаШапка.Параметры.ДолжностьРуководителя = РуководительДолжность;
		
		ОбластьМакетаШапка.Параметры.ФИОГлавногоБухгалтера = Бухгалтер;
		ОбластьМакетаШапка.Параметры.ФИОКассира            = Кассир; 
		
		ОбластьМакетаШапка.Параметры.Основание             = Шапка.Основание;
		ОбластьМакетаШапка.Параметры.Приложение            = Шапка.Приложение;
		
		ОбластьМакетаШапка.Параметры.ФИОПолучателя      	= Шапка.Выдать;
		
		ОбластьМакетаШапка.Параметры.РеквизитыДокументаУдостоверяющегоЛичность = Шапка.ПоДокументу;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		                                               НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КО2") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"КО2", НСтр("ru = 'Печать РКО'"), ПечатьРКО(МассивОбъектов, ОбъектыПечати),,
			"Документ.РасходныйКассовыйОрдер.КО2");
	КонецЕсли;
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КО2";
	КомандаПечати.Представление = НСтр("ru = 'Печать РКО'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли