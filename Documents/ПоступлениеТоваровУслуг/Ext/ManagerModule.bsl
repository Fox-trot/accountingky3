#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	СчетаУчетаНоменклатуры.СчетБУ КАК СчетБУ,
	|	СчетаУчетаНоменклатуры.СчетПередачи КАК СчетПередачи,
	|	СчетаУчетаНоменклатуры.СчетДохода КАК СчетДохода,
	|	СчетаУчетаНоменклатуры.СубконтоДоходов1 КАК СубконтоДоходов1,
	|	СчетаУчетаНоменклатуры.СубконтоДоходов2 КАК СубконтоДоходов2,
	|	СчетаУчетаНоменклатуры.СубконтоДоходов3 КАК СубконтоДоходов3,
	|	СчетаУчетаНоменклатуры.ВЭД КАК ВЭД,
	|	СчетаУчетаНоменклатуры.СчетУчетаЗабалансовый КАК СчетУчетаЗабалансовый,
	|	СчетаУчетаНоменклатуры.СчетПередачиЗабалансовый КАК СчетПередачиЗабалансовый,
	|	СчетаУчетаНоменклатуры.СчетРасхода КАК СчетРасхода,
	|	СчетаУчетаНоменклатуры.СубконтоРасходов2 КАК СубконтоРасходов2,
	|	СчетаУчетаНоменклатуры.СубконтоРасходов3 КАК СубконтоРасходов3,
	|	СчетаУчетаНоменклатуры.СтавкаНСП КАК СтавкаНСП,
	|	СчетаУчетаНоменклатуры.Организация КАК Организация
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетаУчетаМБП
	|ИЗ
	|	РегистрСведений.СчетаУчетаНоменклатуры КАК СчетаУчетаНоменклатуры
	|ГДЕ
	|	(СчетаУчетаНоменклатуры.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ СчетаУчетаНоменклатуры.Организация В
	|				(ВЫБРАТЬ
	|					ВременнаяТаблицаШапка.Организация
	|				ИЗ
	|					ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка))
	|	И СчетаУчетаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.МБП)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период КАК Период,
	|	ВременнаяТаблицаШапка.Организация КАК Организация,
	|	ВременнаяТаблицаТовары.СчетУчета КАК СчетДт,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика КАК СчетКт,
	|	ВременнаяТаблицаТовары.Номенклатура КАК СубконтоДт1,
	|	ВременнаяТаблицаШапка.Склад КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.Новый)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,
	|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс КАК Сумма,
	|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
	|	ВременнаяТаблицаТовары.Сумма КАК ВалютнаяСуммаДт,
	|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
	|	ВременнаяТаблицаТовары.Сумма КАК ВалютнаяСуммаКт,
	|	""Поступление ТМЗ - "" + ВременнаяТаблицаШапка.ВидОперации.Наименование КАК Содержание,
	|	ВременнаяТаблицаТовары.Количество КАК КоличествоДт,
	|	ВременнаяТаблицаТовары.Количество КАК КоличествоКт
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ НЕ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Оприходование)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ПлательщикНДС
	|				И ВременнаяТаблицаШапка.УказыватьПризнакЗачетаНДСПриПоступлении
	|				И ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	|			ТОГДА ВременнаяТаблицаТовары.СчетУчета
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВременнаяТаблицаШапка.НДСНеПодтвержден
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_НеПодтвержденный)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаТовары.СуммаНДС * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаТовары.СуммаНДС,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаТовары.СуммаНДС,
	|	""НДС"",
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаШапка.ПлательщикНДС
	|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Покупка)
	|	И ВременнаяТаблицаТовары.СуммаНДС <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаУслуги.СчетУчета,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика,
	|	ВременнаяТаблицаУслуги.Субконто1,
	|	ВременнаяТаблицаУслуги.Субконто2,
	|	ВременнаяТаблицаУслуги.Субконто3,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаУслуги.Сумма * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаУслуги.Сумма,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаУслуги.Сумма,
	|	""Поступление услуг - "" + ВременнаяТаблицаШапка.ВидОперации.Наименование,
	|	ВременнаяТаблицаУслуги.Количество,
	|	ВременнаяТаблицаУслуги.Количество
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаУслуги.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ПлательщикНДС
	|				И ВременнаяТаблицаШапка.УказыватьПризнакЗачетаНДСПриПоступлении
	|				И ВременнаяТаблицаУслуги.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	|			ТОГДА ВременнаяТаблицаУслуги.СчетУчета
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВременнаяТаблицаШапка.НДСНеПодтвержден
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_НеПодтвержденный)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаУслуги.СуммаНДС * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаУслуги.СуммаНДС,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаУслуги.СуммаНДС,
	|	""НДС"",
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаУслуги.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаШапка.ПлательщикНДС
	|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Покупка)
	|	И ВременнаяТаблицаУслуги.СуммаНДС <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаОС.СчетУчета,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика,
	|	ВременнаяТаблицаОС.ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаОС.Сумма,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаОС.Сумма,
	|	""Поступление ОС - "" + ВременнаяТаблицаШапка.ВидОперации.Наименование,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ПлательщикНДС
	|				И ВременнаяТаблицаШапка.УказыватьПризнакЗачетаНДСПриПоступлении
	|				И ВременнаяТаблицаОС.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	|			ТОГДА ВременнаяТаблицаОС.СчетУчета
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВременнаяТаблицаШапка.НДСНеПодтвержден
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_НеПодтвержденный)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаОС.СуммаНДС * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаОС.СуммаНДС,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаОС.СуммаНДС,
	|	""НДС"",
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаШапка.ПлательщикНДС
	|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Покупка)
	|	И ВременнаяТаблицаОС.СуммаНДС <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	СчетаУчетаНоменклатуры.СчетРасхода,
	|	ВременнаяТаблицаШапка.СчетРасчетовПоставщика,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВременнаяТаблицаШапка.Склад,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.Новый)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	|	ВременнаяТаблицаТовары.СуммаОтклонение * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаТовары.СуммаОтклонение,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаТовары.СуммаОтклонение,
	|	""Отклонение при пересчете стоимости МБП"",
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетаУчетаМБП КАК СчетаУчетаНоменклатуры
	|			ПО (ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.Организация = СчетаУчетаНоменклатуры.Организация
	|						ТОГДА ИСТИНА
	|					КОГДА СчетаУчетаНоменклатуры.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ)
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаТовары.СуммаОтклонение <> 0
	|	И ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
	|	И НЕ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Оприходование)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ВременнаяТаблицаТовары.СчетУчета
	|		ИНАЧЕ ВременнаяТаблицаШапка.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.СчетДоходов = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ВременнаяТаблицаТовары.СчетДоходов
	|		ИНАЧЕ ВременнаяТаблицаШапка.СчетДоходов
	|	КОНЕЦ,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВременнаяТаблицаШапка.Склад,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.СчетДоходов = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ВременнаяТаблицаТовары.СтатьяДоходов
	|		ИНАЧЕ ВременнаяТаблицаШапка.СтатьяДоходов
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаТовары.Сумма,
	|	ВременнаяТаблицаШапка.ВалютаДокумента,
	|	ВременнаяТаблицаТовары.Сумма,
	|	""Поступление ТМЗ - "" + ВременнаяТаблицаШапка.ВидОперации.Наименование,
	|	ВременнаяТаблицаТовары.Количество,
	|	ВременнаяТаблицаТовары.Количество
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Оприходование)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВременнаяТаблицаШапка.Ссылка КАК Партия,
	|	ВременнаяТаблицаШапка.Склад,
	|	ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.Новый) КАК Статус,
	|	ВременнаяТаблицаТовары.Количество,
	|	ВременнаяТаблицаТовары.СчетУчета
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМБПСклад", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

Процедура СформироватьТаблицаПоступления(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.Ссылка КАК ДокументПоступления,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,	
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВременнаяТаблицаТовары.СтавкаНДС,
	|	ВременнаяТаблицаТовары.ЗачетНДС КАК ЗачетНДС,
	|	ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс КАК СуммаСебестоимости,
	|	ВременнаяТаблицаТовары.Сумма КАК СуммаСебестоимостиВал,
	|	ВременнаяТаблицаТовары.СуммаНСП * ВременнаяТаблицаШапка.Курс КАК СуммаНеоблагаемая,
	|	ВременнаяТаблицаТовары.СуммаНСП КАК СуммаНеоблагаемаяВал,
	|	ВременнаяТаблицаТовары.СуммаНДС * ВременнаяТаблицаШапка.Курс КАК СуммаНДС,
	|	ВременнаяТаблицаТовары.СуммаНДС КАК СуммаНДСВал,
	|	ВременнаяТаблицаТовары.Количество
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.Ссылка,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,	
	|	ВременнаяТаблицаУслуги.Номенклатура,
	|	ВременнаяТаблицаУслуги.СтавкаНДС,
	|	ВременнаяТаблицаУслуги.ЗачетНДС,
	|	ВременнаяТаблицаУслуги.Сумма * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаУслуги.Сумма,
	|	ВременнаяТаблицаУслуги.СуммаНСП * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаУслуги.СуммаНСП,
	|	ВременнаяТаблицаУслуги.СуммаНДС * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаУслуги.СуммаНДС,
	|	ВременнаяТаблицаУслуги.Количество
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаУслуги.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.Ссылка,
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента,	
	|	ВременнаяТаблицаОС.ОсновноеСредство,
	|	ВременнаяТаблицаОС.СтавкаНДС,
	|	ВременнаяТаблицаОС.ЗачетНДС,
	|	ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаОС.Сумма,
	|	ВременнаяТаблицаОС.СуммаНСП * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаОС.СуммаНСП,
	|	ВременнаяТаблицаОС.СуммаНДС * ВременнаяТаблицаШапка.Курс,
	|	ВременнаяТаблицаОС.СуммаНДС,
	|	1
	|ИЗ
	|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоступления", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

Процедура СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	ОсновнойСпособОценкиЗапасов = СтруктураДополнительныеСвойства.ДляПроведения.ОсновнойСпособОценкиЗапасов;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаТовары.СчетУчета,
	|	ВременнаяТаблицаШапка.Склад,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ ВременнаяТаблицаТовары.СчетУчета.Количественный
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА СпособыОценкиЗапасов.СчетУчета ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА &ОсновнойСпособОценкиЗапасов = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ВременнаяТаблицаШапка.Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СпособыОценкиЗапасов.СпособОценки = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ ВременнаяТаблицаШапка.Ссылка
	|			КОНЕЦ
	|	КОНЕЦ КАК Партия,
	|	СУММА(ВЫБОР
	|			КОГДА ВременнаяТаблицаТовары.СчетУчета.Количественный
	|				ТОГДА ВременнаяТаблицаТовары.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Количество,
	|	ВременнаяТаблицаТовары.Цена * ВременнаяТаблицаШапка.Курс КАК Цена,
	|	СУММА(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОценкиЗапасов КАК СпособыОценкиЗапасов
	|		ПО ВременнаяТаблицаТовары.СчетУчета = СпособыОценкиЗапасов.СчетУчета
	|ГДЕ
	|	НЕ ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаТовары.СчетУчета,
	|	ВременнаяТаблицаШапка.Склад,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВременнаяТаблицаТовары.Цена * ВременнаяТаблицаШапка.Курс,
	|	ВЫБОР
	|		КОГДА НЕ ВременнаяТаблицаТовары.СчетУчета.Количественный
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА СпособыОценкиЗапасов.СчетУчета ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА &ОсновнойСпособОценкиЗапасов = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ВременнаяТаблицаШапка.Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СпособыОценкиЗапасов.СпособОценки = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ ВременнаяТаблицаШапка.Ссылка
	|			КОНЕЦ
	|	КОНЕЦ"; 
	// РВ если мбп тогда не будем делать запись в РН Запасы
	
	Запрос.Параметры.Вставить("ОсновнойСпособОценкиЗапасов", ОсновнойСпособОценкиЗапасов);
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

Процедура СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	 	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	ВременнаяТаблицаОС.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаОС.Сумма КАК ПервоначальнаяСтоимость
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО Истина";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПараметрыУчетаОС", РезультатЗапроса.Выгрузить()); 
		
КонецПроцедуры

Процедура СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Период КАК ДатаСостояния,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Поступило) КАК Состояние,
		|	НЕОПРЕДЕЛЕНО КАК ДатаСписания
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСостоянияОС", РезультатЗапроса.Выгрузить()); 

КонецПроцедуры

Процедура СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШапкаДокумента.Период КАК Период,
	|	ШапкаДокумента.Организация,
	|	ШапкаДокумента.СобытиеОС КАК Событие,
	|	ШапкаДокумента.Номер КАК НомерДокумента,
	|	ШапкаДокумента.Ссылка КАК НазваниеДокумента,
	|	ТабличнаяЧастьДокумента.ОсновноеСредство
	|ИЗ
	|	ВременнаяТаблицаОС КАК ТабличнаяЧастьДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ШапкаДокумента
	|		ПО ТабличнаяЧастьДокумента.Ссылка = ШапкаДокумента.Ссылка
	|ГДЕ
	|	НЕ ШапкаДокумента.СобытиеОС = ЗНАЧЕНИЕ(Справочник.СобытияОС.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСобытияОС", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицаМестонахождениеОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	ВременнаяТаблицаШапка.Склад
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМестонахождениеОС", РезультатЗапроса.Выгрузить()); 

КонецПроцедуры

Процедура СформироватьТаблицаПоступленияИтоги(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.Ссылка КАК ДокументПоступления,
	|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК Договор,
	|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК СерияБланкаСФ,
	|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
	|	ВременнаяТаблицаШапка.ДатаСФ КАК ДатаСФ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Импорт)	
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыУчетаНДС.Импорт)
	|		КОГДА ВременнаяТаблицаШапка.НДСНеПодтвержден
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыУчетаНДС.НДСНеПодтвержден)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыУчетаНДС.ПустаяСсылка)
	|	КОНЕЦ КАК ВидУчета,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.Контрагент.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.Киргизия)
	|				ИЛИ ВременнаяТаблицаШапка.Контрагент.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|		КОГДА ВременнаяТаблицаШапка.Контрагент.СтранаРезидентства.ЕАЭС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.Импорт)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.Импэкс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ПустаяСсылка)
	|	КОНЕЦ КАК ПризнакСтраны
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоступленияИтоги", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(
			СтруктураДополнительныеСвойства.ДляПроведения.Дата, 
			СтруктураДополнительныеСвойства.ДляПроведения.Организация
		);	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц 	= СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Дата КАК Период,
	|	ПоступлениеТоваровУслуг.Организация,
	|	ПоступлениеТоваровУслуг.ВидОперации,
	|	ПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ПоступлениеТоваровУслуг.Склад,
	|	ПоступлениеТоваровУслуг.Контрагент,
	|	ПоступлениеТоваровУслуг.ДоговорКонтрагента,
	|	ПоступлениеТоваровУслуг.Курс,
	|	ПоступлениеТоваровУслуг.Комментарий,
	|	ПоступлениеТоваровУслуг.СуммаДокумента,
	|	ПоступлениеТоваровУслуг.СчетРасчетовПоставщика,
	|	ПоступлениеТоваровУслуг.СерияБланкаСФ,
	|	ПоступлениеТоваровУслуг.НомерБланкаСФ,
	|	ПоступлениеТоваровУслуг.ДатаСФ,
	|	ПоступлениеТоваровУслуг.НеВключатьВРеестрСФ,
	|	ПоступлениеТоваровУслуг.НДСНеПодтвержден,
	|	&ПлательщикНДС,
	|	&УказыватьПризнакЗачетаНДСПриПоступлении,
	|	ПоступлениеТоваровУслуг.Подотчетник,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Номер,
	|	ПоступлениеТоваровУслуг.СобытиеОС,
	|	ПоступлениеТоваровУслуг.ХарактерСделки,
	|	ПоступлениеТоваровУслуг.КоммерческийДокумент,
	|	ПоступлениеТоваровУслуг.ВидТранспорта,
	|	ПоступлениеТоваровУслуг.ПоказательИмпорта,
	|	ПоступлениеТоваровУслуг.ИмпортОсвобожденныйОтНДС,
	|	ПоступлениеТоваровУслуг.Транспортировка,
	|	ПоступлениеТоваровУслуг.СчетУчета,
	|	ПоступлениеТоваровУслуг.СчетДоходов,
	|	ПоступлениеТоваровУслуг.СтатьяДоходов
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Ссылка,
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Количество,
	|	ПоступлениеТоваровУслугТовары.Цена,
	|	ПоступлениеТоваровУслугТовары.Сумма,
	|	ПоступлениеТоваровУслугТовары.СчетУчета,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНСП,
	|	ПоступлениеТоваровУслугТовары.Всего,
	|	ПоступлениеТоваровУслугТовары.ЗачетНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаОтклонение,
	|	ПоступлениеТоваровУслугТовары.Вес,
	|	ПоступлениеТоваровУслугТовары.СуммаАкциза,
	|	ПоступлениеТоваровУслугТовары.СчетДоходов,
	|	ПоступлениеТоваровУслугТовары.СтатьяДоходов
	|ПОМЕСТИТЬ ВременнаяТаблицаТовары
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугУслуги.Ссылка,
	|	ПоступлениеТоваровУслугУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугУслуги.Количество,
	|	ПоступлениеТоваровУслугУслуги.Цена,
	|	ПоступлениеТоваровУслугУслуги.Сумма,
	|	ПоступлениеТоваровУслугУслуги.СчетУчета,
	|	ПоступлениеТоваровУслугУслуги.СуммаНДС,
	|	ПоступлениеТоваровУслугУслуги.СтавкаНДС,
	|	ПоступлениеТоваровУслугУслуги.ЗачетНДС,
	|	ПоступлениеТоваровУслугУслуги.СуммаНСП,
	|	ПоступлениеТоваровУслугУслуги.Субконто1,
	|	ПоступлениеТоваровУслугУслуги.Субконто2,
	|	ПоступлениеТоваровУслугУслуги.Субконто3
	|ПОМЕСТИТЬ ВременнаяТаблицаУслуги
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугУслуги.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОС.Ссылка,
	|	ПоступлениеТоваровУслугОС.ОсновноеСредство,
	|	ПоступлениеТоваровУслугОС.Сумма,
	|	ПоступлениеТоваровУслугОС.Сумма КАК ПервоначальнаяСтоимость,
	|	ПоступлениеТоваровУслугОС.СчетУчета,
	|	ПоступлениеТоваровУслугОС.СтавкаНДС,
	|	ПоступлениеТоваровУслугОС.СуммаНДС,
	|	ПоступлениеТоваровУслугОС.СуммаНСП,
	|	ПоступлениеТоваровУслугОС.ЗачетНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаОС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОС КАК ПоступлениеТоваровУслугОС
	|ГДЕ
	|	ПоступлениеТоваровУслугОС.Ссылка = &ДокументСсылка";	
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("ПлательщикНДС", УПП.ПлательщикНДС);
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДСПриПоступлении", УПП.УказыватьПризнакЗачетаНДСПриПоступлении);
	Запрос.Выполнить();    		
		
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);	
	СформироватьТаблицаПоступления(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства); 
	СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства);	
	СформироватьТаблицаМестонахождениеОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПоступленияИтоги(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати 	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_Накладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Накладная");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Поступление.Ссылка КАК Ссылка,
	|	Поступление.Номер КАК Номер,
	|	Поступление.Контрагент.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	Поступление.Организация.НаименованиеПолное КАК ПредставлениеПолучателя,
	|	Поступление.Склад КАК Склад,
	|	Поступление.СуммаНДС КАК ВсегоНДС,
	|	Поступление.СуммаНСП КАК ВсегоНСП,
	|	Поступление.ДоговорКонтрагента.ВалютаРасчетов,
	|	КурсыВалют.Курс КАК Курс,
	|	Поступление.Товары. (
	|	Номенклатура.Код КАК Код,
	|	Номенклатура КАК Товар,
	|	Количество КАК Количество,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Цена  КАК Цена,
	|	Сумма КАК Сумма,
	|	Всего КАК СуммаСНДС
	|	),
	|	Поступление.Услуги. (
	|	Номенклатура.Код КАК Код,
	|	Номенклатура КАК Товар,
	|	Количество КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения,
	|	Цена КАК Цена,
	|	Сумма КАК Сумма,
	|	Всего КАК СуммаСНДС
	|	),
	|	Поступление.ОС. (
	|	ОсновноеСредство.Код КАК Код,
	|	ОсновноеСредство КАК Товар,
	|	1 КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения,
	|	Сумма КАК Цена,
	|	Сумма КАК Сумма,
	|	Всего КАК СуммаСНДС
	|	)
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК Поступление
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
	|		ПО Поступление.ДоговорКонтрагента.ВалютаРасчетов = КурсыВалют.Валюта
	|ГДЕ
	|	Поступление.Ссылка В(&МассивОбъектов)";
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Параметры.Вставить("Дата", МассивОбъектов[0].Дата);

	Результат = Запрос.Выполнить();
	Шапка = Результат.Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть ("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтогоНДС = Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьИтогоСНДС = Макет.ПолучитьОбласть("ИтогоСНДС");
	ОбластьИтогоНСП = Макет.ПолучитьОбласть("ИтогоНСП");
	ОбластьСуммаПрописью = Макет.ПолучитьОбласть ("СуммаПрописью");
	ОбластьПодписи = Макет.ПолучитьОбласть ("Подписи");
	
	ТабличныйДокумент.Очистить();
		 
	Пока Шапка.Следующий() Цикл 
		ОбластьЗаголовок.Параметры.Заполнить(Шапка);
		ОбластьПоставщик.Параметры.Заполнить(Шапка);                     
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
			ВыборкаСтрокТовары = Шапка.Товары.Выбрать();              
			НомерСтроки = 1;
			Пока ВыборкаСтрокТовары.Следующий() Цикл 
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрокТовары);
				
				Если Шапка.Курс <> 1 Тогда
				Цена = ВыборкаСтрокТовары.Цена * Шапка.Курс;
				Сумма = ВыборкаСтрокТовары.Сумма * Шапка.Курс;
				СуммаСНДС = ВыборкаСтрокТовары.СуммаСНДС * Шапка.Курс;
				
				СтрокаТоварыВСомах = Новый Структура();
				СтрокаТоварыВСомах.Вставить("Цена", Цена);
				СтрокаТоварыВСомах.Вставить("Сумма", Сумма);
				СтрокаТоварыВСомах.Вставить("СуммаСНДС", СуммаСНДС);
				
				ОбластьСтрока.Параметры.Заполнить(СтрокаТоварыВСомах);
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
			ВыборкаСтрокУслуги = Шапка.Услуги.Выбрать();
			Пока ВыборкаСтрокУслуги.Следующий() Цикл 
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрокУслуги);
				
				Если Шапка.Курс <> 1 Тогда
				Цена = ВыборкаСтрокУслуги.Цена * Шапка.Курс;
				Сумма = ВыборкаСтрокУслуги.Сумма * Шапка.Курс;
				СуммаСНДС = ВыборкаСтрокУслуги.СуммаСНДС * Шапка.Курс;
				
				СтрокаУслугиВСомах = Новый Структура();
				СтрокаУслугиВСомах.Вставить("Цена", Цена);
				СтрокаУслугиВСомах.Вставить("Сумма", Сумма);
				СтрокаУслугиВСомах.Вставить("СуммаСНДС", СуммаСНДС);
				
				ОбластьСтрока.Параметры.Заполнить(СтрокаУслугиВСомах);
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
			ВыборкаСтрокОС = Шапка.ОС.Выбрать();
			Пока ВыборкаСтрокОС.Следующий() Цикл 
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрокОС);
				
				Если Шапка.Курс <> 1 Тогда
				Цена = ВыборкаСтрокОС.Цена * Шапка.Курс;
				Сумма = ВыборкаСтрокОС.Сумма * Шапка.Курс;
				СуммаСНДС = ВыборкаСтрокОС.СуммаСНДС * Шапка.Курс;
				
				СтрокаОСВСомах = Новый Структура();
				СтрокаОСВСомах.Вставить("Цена", Цена);
				СтрокаОСВСомах.Вставить("Сумма", Сумма);
				СтрокаОСВСомах.Вставить("СуммаСНДС", СуммаСНДС);
				
				ОбластьСтрока.Параметры.Заполнить(СтрокаОСВСомах);
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
		Если Шапка.Курс <> 1 Тогда
			СуммаДокументаБезНДС = Шапка.Курс * (Шапка.Товары.Выгрузить().Итог("Сумма") + Шапка.Услуги.Выгрузить().Итог("Сумма")+ Шапка.ОС.Выгрузить().Итог("Сумма"));
			СуммаДокументаСНДС = Шапка.Курс * (Шапка.Товары.Выгрузить().Итог("СуммаСНДС") + Шапка.Услуги.Выгрузить().Итог("СуммаСНДС")+ Шапка.ОС.Выгрузить().Итог("СуммаСНДС"));
			СуммаДокументаПрописью = БухгалтерскийУчетСервер.СформироватьСуммуПрописью(СуммаДокументаСНДС);
			ИтоговаяСтрока = "Всего наименований " + (НомерСтроки-1) + ", на сумму " + Формат(СуммаДокументаСНДС, "ЧЦ=15; ЧДЦ=2") + " сом";
			СуммаНДС = Шапка.Курс * Шапка.ВсегоНДС;
			СуммаНСП = Шапка.Курс * Шапка.ВсегоНСП;
		Иначе
			СуммаДокументаБезНДС = Шапка.Товары.Выгрузить().Итог("Сумма") + Шапка.Услуги.Выгрузить().Итог("Сумма")+ Шапка.ОС.Выгрузить().Итог("Сумма");
			СуммаДокументаСНДС = Шапка.Товары.Выгрузить().Итог("СуммаСНДС") + Шапка.Услуги.Выгрузить().Итог("СуммаСНДС")+ Шапка.ОС.Выгрузить().Итог("СуммаСНДС");
			СуммаДокументаПрописью = БухгалтерскийУчетСервер.СформироватьСуммуПрописью(СуммаДокументаСНДС);
			ИтоговаяСтрока = "Всего наименований " + (НомерСтроки-1) + ", на сумму " + Формат(СуммаДокументаСНДС, "ЧЦ=15; ЧДЦ=2") + " сом";
			СуммаНДС = Шапка.ВсегоНДС;
			СуммаНСП = Шапка.ВсегоНСП;
		КонецЕсли;
		
		
		
		ОбластьИтого.Параметры.ВсегоБезНДС  			 = СуммаДокументаБезНДС;
		ОбластьИтогоНДС.Параметры.ВсегоНДС				 = СуммаНДС;
		ОбластьИтогоСНДС.Параметры.ИтогоСНДС			 = СуммаДокументаСНДС;
		ОбластьИтогоНСП.Параметры.ВсегоНСП   			 = СуммаНСП;
		ОбластьСуммаПрописью.Параметры.Суммапрописью	 = СуммаДокументаПрописью;
		ОбластьСуммаПрописью.Параметры.ИтоговаяСтрока 	 = ИтоговаяСтрока;

		ТабличныйДокумент.Вывести(ОбластьИтого);
		ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		ТабличныйДокумент.Вывести(ОбластьИтогоСНДС);
		ТабличныйДокумент.Вывести(ОбластьИтогоНСП);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
			
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.     
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;      
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		 //Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"Накладная", НСтр("ru = Накладная'"), ПечатьНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
			"Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Накладная");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 
	КомандаПечати.Порядок = 1;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
