#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// ОС 
	// 1. Распределение расходов по амортизации за месяц
	// 2. Списание накопленной амортизации
	// 3. Списание остаточной стоимости
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК СчетДт,
		|	ВременнаяТаблицаОС.СчетУчета.ПарныйСчет КАК СчетКт,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт3,
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаОС.АмортизацияЗаМесяц КАК Сумма,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК ВалютнаяСуммаКт,
		|	&СодержаниеРасходыПоАмортизации КАК Содержание,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт,
		|	ВременнаяТаблицаОС.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ИСТИНА КАК РаспределятьПоКоэффициентам
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаОС.АмортизацияЗаМесяц = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.СчетУчета.ПарныйСчет,
		|	ВременнаяТаблицаОС.СчетУчета,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.НакопленнаяАмортизация + ВременнаяТаблицаОС.АмортизацияЗаМесяц,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0,
		|	&СодержаниеСписаниеАмортизации,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаОС.НакопленнаяАмортизация + ВременнаяТаблицаОС.АмортизацияЗаМесяц = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.СчетРасхода,
		|	ВременнаяТаблицаОС.СчетУчета,
		|	ВременнаяТаблицаОС.СтатьяЗатрат,
		|	ВременнаяТаблицаШапка.Подразделение,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.ПервоначальнаяСтоимость - ВременнаяТаблицаОС.НакопленнаяАмортизация - ВременнаяТаблицаОС.АмортизацияЗаМесяц,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0,
		|	&СодержаниеСписаниеОстаточнойСтоимости,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаОС.ПервоначальнаяСтоимость - ВременнаяТаблицаОС.НакопленнаяАмортизация - ВременнаяТаблицаОС.АмортизацияЗаМесяц > 0";
	Запрос.УстановитьПараметр("СодержаниеРасходыПоАмортизации", НСтр("ru = 'Расходы по амортизации ОС за месяц'")); 
	Запрос.УстановитьПараметр("СодержаниеСписаниеАмортизации", НСтр("ru = 'Расходы по амортизации'")); 
	Запрос.УстановитьПараметр("СодержаниеСписаниеОстаточнойСтоимости", НСтр("ru = 'Списание стоимости ОС'")); 
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаДвижений = РезультатЗапроса.Выгрузить();
	
	// Добавление колонк в таблицу для записи в рег. бухг.
	ТаблицаДвижений.Колонки.Добавить("СубконтоДт1");
	ТаблицаДвижений.Колонки.Добавить("СубконтоДт2");
	ТаблицаДвижений.Колонки.Добавить("СубконтоДт3");
	
	ТаблицаХозрасчетный = УправлениеВнеоборотнымиАктивами.РаспределениеПоСпособуОтраженияРасходовПоАмортизации(ТаблицаДвижений);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", ТаблицаХозрасчетный);	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета) КАК Состояние
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСостоянияОС", РезультатЗапроса.Выгрузить()); 
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Списание) КАК Событие,
		|	ВременнаяТаблицаШапка.Номер КАК НомерДокумента,
		|	""Списание ОС"" КАК НазваниеДокумента
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСобытияОС", РезультатЗапроса.Выгрузить());
КонецПроцедуры     

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДвижениеОСНУ(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ПараметрыУчетаОССрезПоследних.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаОС.ПервоначальнаяСтоимость КАК Выбытие,
		|	ВременнаяТаблицаОС.ПервоначальнаяСтоимость * (12 - МЕСЯЦ(ВременнаяТаблицаШапка.Дата) + 0.5) / 12 КАК ТекущееВыбытие
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|				&Период,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаОС КАК ВременнаяТаблицаОС)) КАК ПараметрыУчетаОССрезПоследних
		|		ПО ВременнаяТаблицаОС.ОсновноеСредство = ПараметрыУчетаОССрезПоследних.ОсновноеСредство";
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвижениеОСНУ", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаКорректировкиНУ(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Счет
		|ПОМЕСТИТЬ ВременнаяТаблицаСчетов
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Временный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаХозрасчетный.Период КАК Период,
		|	ТаблицаХозрасчетный.Организация КАК Организация,
		|	ТаблицаХозрасчетный.СчетДт КАК СчетДт,
		|	ТаблицаХозрасчетный.СчетКт КАК СчетКт,
		|	ТаблицаХозрасчетный.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаХозрасчетный
		|ИЗ
		|	&ТаблицаХозрасчетный КАК ТаблицаХозрасчетный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаХозрасчетный.Период КАК Период,
		|	ВременнаяТаблицаХозрасчетный.Организация КАК Организация,
		|	ВременнаяТаблицаХозрасчетный.СчетДт КАК Счет,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ВР) КАК ВидУчета,
		|	ВременнаяТаблицаХозрасчетный.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаКорректировкиНУ
		|ИЗ
		|	ВременнаяТаблицаХозрасчетный КАК ВременнаяТаблицаХозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов
		|		ПО ВременнаяТаблицаХозрасчетный.СчетДт = ВременнаяТаблицаСчетов.Счет
		|ГДЕ
		|	ВременнаяТаблицаХозрасчетный.СчетДт В
		|			(ВЫБРАТЬ
		|				ВременнаяТаблицаСчетов.Счет
		|			ИЗ
		|				ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаХозрасчетный.Период,
		|	ВременнаяТаблицаХозрасчетный.Организация,
		|	ВременнаяТаблицаХозрасчетный.СчетКт,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ВР),
		|	ВременнаяТаблицаХозрасчетный.Сумма
		|ИЗ
		|	ВременнаяТаблицаХозрасчетный КАК ВременнаяТаблицаХозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов
		|		ПО ВременнаяТаблицаХозрасчетный.СчетКт = ВременнаяТаблицаСчетов.Счет
		|ГДЕ
		|	ВременнаяТаблицаХозрасчетный.СчетКт В
		|			(ВЫБРАТЬ
		|				ВременнаяТаблицаСчетов.Счет
		|			ИЗ
		|				ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаКорректировкиНУ.Период КАК Период,
		|	ВременнаяТаблицаКорректировкиНУ.Организация КАК Организация,
		|	ВременнаяТаблицаКорректировкиНУ.Счет КАК Счет,
		|	ВременнаяТаблицаКорректировкиНУ.ВидУчета КАК ВидУчета,
		|	СУММА(ВременнаяТаблицаКорректировкиНУ.Сумма) КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаКорректировкиНУ КАК ВременнаяТаблицаКорректировкиНУ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаКорректировкиНУ.Счет,
		|	ВременнаяТаблицаКорректировкиНУ.ВидУчета,
		|	ВременнаяТаблицаКорректировкиНУ.Организация,
		|	ВременнаяТаблицаКорректировкиНУ.Период";
	Запрос.УстановитьПараметр("ТаблицаХозрасчетный", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаХозрасчетный);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаКорректировкиНУ", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСоставОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаОС.СостояниеВСоставеОС КАК СостояниеВСоставеОС
		|ПОМЕСТИТЬ ВременнаяТаблицаОСВсе
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	СоставОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СоставОССрезПоследних.ОсновноеСредствоВСоставеКомплекта КАК ОсновноеСредствоВСоставеКомплекта,
		|	ВременнаяТаблицаОСВсе.СостояниеВСоставеОС КАК СостояниеВСоставеОС
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставОС.СрезПоследних(
		|				&Период,
		|				ОсновноеСредствоВСоставеКомплекта В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаОСВсе.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаОСВсе КАК ВременнаяТаблицаОСВсе)
		|					И НЕ Регистратор = &ДокументСсылка) КАК СоставОССрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОСВсе КАК ВременнаяТаблицаОСВсе
		|			ПО СоставОССрезПоследних.ОсновноеСредствоВСоставеКомплекта = ВременнаяТаблицаОСВсе.ОсновноеСредство
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Комплектация)
		|			ИЛИ СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Добавление))";
	Запрос.Параметры.Вставить("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);	
	Запрос.Параметры.Вставить("ДокументСсылка", ДокументСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСоставОС", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	ТаблицаОСВходящихВКомплекты = ТаблицаОСВходящихВКомплекты(ДокументСсылка, СтруктураДополнительныеСвойства);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.Номер КАК Номер,
		|	ТаблицаДокумента.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.СписаниеОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.ОстаточнаяСтоимость КАК ОстаточнаяСтоимость,
		|	ТаблицаДокумента.НакопленнаяАмортизация КАК НакопленнаяАмортизация,
		|	ТаблицаДокумента.АмортизацияЗаМесяц КАК АмортизацияЗаМесяц,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.СчетРасхода КАК СчетРасхода,
		|	ТаблицаДокумента.СтатьяЗатрат КАК СтатьяЗатрат
		|ПОМЕСТИТЬ ВременнаяТаблицаОСКомплектов
		|ИЗ
		|	&ТаблицаОСВходящихВКомплекты КАК ТаблицаДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.ОстаточнаяСтоимость КАК ОстаточнаяСтоимость,
		|	ТаблицаДокумента.НакопленнаяАмортизация КАК НакопленнаяАмортизация,
		|	ТаблицаДокумента.АмортизацияЗаМесяц КАК АмортизацияЗаМесяц,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.СчетРасхода КАК СчетРасхода,
		|	ТаблицаДокумента.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.ВыводИзКомплекта) КАК СостояниеВСоставеОС
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	Документ.СписаниеОС.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И НЕ ТаблицаДокумента.ОсновноеСредство.ЭтоКомплект
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.ОстаточнаяСтоимость,
		|	ТаблицаДокумента.НакопленнаяАмортизация,
		|	ТаблицаДокумента.АмортизацияЗаМесяц,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.СчетРасхода,
		|	ТаблицаДокумента.СтатьяЗатрат,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Ликвидация)
		|ИЗ
		|	ВременнаяТаблицаОСКомплектов КАК ТаблицаДокумента";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ТаблицаОСВходящихВКомплекты", ТаблицаОСВходящихВКомплекты);
	Запрос.Выполнить();      	
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);			 
	СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства); 	
	СформироватьТаблицаДвижениеОСНУ(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаКорректировкиНУ(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСоставОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьФормыОС4(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СписаниеОС.Ссылка КАК Ссылка,
		|	СписаниеОС.Дата КАК Дата,
		|	СписаниеОС.Организация КАК Организация,
		|	СписаниеОС.Организация.Наименование КАК ОрганизацияНаименование,
		|	СписаниеОС.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	СписаниеОС.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
		|	СписаниеОС.МОЛ КАК МОЛ,
		|	СписаниеОС.МОЛ.ФИО КАК МОЛ_ФИО,
		|	СписаниеОС.ТехническоеСостояние КАК ТехническоеСостояние,
		|	СписаниеОС.ДокументОснованиеВид КАК ДокументОснованиеВид,
		|	СписаниеОС.ДокументОснованиеДата КАК ДокументОснованиеДата,
		|	СписаниеОС.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
		|	СписаниеОС.ОС.(
		|		НомерСтроки КАК НП,
		|		ОсновноеСредство КАК ОсновноеСредство,
		|		СчетУчета КАК СчетДт,
		|		СчетРасхода КАК СчетКт,
		|		ПервоначальнаяСтоимость КАК НачальнаяСтоимость,
		|		СписаниеОС.ОС.НакопленнаяАмортизация + СписаниеОС.ОС.АмортизацияЗаМесяц КАК НачальнаяАмортизация,
		|		ИнвентарныйНомер КАК ИнвентарныйНомер
		|	) КАК ОС,
		|	СписаниеОС.Комиссия.(
		|		ФизЛицо КАК ФизЛицо,
		|		ФизЛицо.ФИО КАК ФизЛицоФИО,
		|		Председатель КАК Председатель
		|	) КАК Комиссия
		|ИЗ
		|	Документ.СписаниеОС КАК СписаниеОС";	
	Запрос = Новый Запрос(ТекстЗапроса);	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);		
	РезультатЗапроса = Запрос.Выполнить();
	Шапка = РезультатЗапроса.Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати 	= "СписаниеОС_ФормаОС4";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ФормаОС4");
	
	Пока Шапка.Следующий() Цикл		
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ДанныеПечати = Новый Структура;
		
		ДанныеПечати.Вставить("ПредставлениеОрганизации", ?(ЗначениеЗаполнено(Шапка.ОрганизацияНаименованиеПолное), Шапка.ОрганизацияНаименованиеПолное, Шапка.ОрганизацияНаименование));
		ДанныеПечати.Вставить("ОрганизацияПоОКПО", Шапка.ОрганизацияПоОКПО);
		ДанныеПечати.Вставить("ДатаДокумента", Шапка.Дата);
		
		ДанныеПечати.Вставить("ДокументОснованиеВид", Шапка.ДокументОснованиеВид);
		Если Шапка.ДокументОснованиеДата <> Дата(1, 1, 1) Тогда
			ДанныеПечати.Вставить("ДокументОснованиеДата", Шапка.ДокументОснованиеДата);
		Иначе
			ДанныеПечати.Вставить("ДокументОснованиеДата", "");
		КонецЕсли;	
		ДанныеПечати.Вставить("ДокументОснованиеНомер", Шапка.ДокументОснованиеНомер);
		ДанныеПечати.Вставить("ТехническоеСостояние", Шапка.ТехническоеСостояние);
		
		Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Шапка.Организация, Шапка.Дата);
		ДанныеПечати.Вставить("РасшифровкаПодписиГлавБух", Руководители.ГлавныйБухгалтер);

		ВременнаяТаблицаКомиссия = Шапка.Комиссия.Выгрузить();
		
		МассивФизЛиц = ВременнаяТаблицаКомиссия.ВыгрузитьКолонку("ФизЛицо");
		МассивФизЛиц.Добавить(Шапка.МОЛ);
		
		Запрос = Новый Запрос();
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
			|	СотрудникиСрезПоследних.Должность КАК Должность
			|ИЗ
			|	РегистрСведений.Сотрудники.СрезПоследних(
			|			&Период,
			|			Организация = &Организация
			|				И ФизЛицо В (&ФизЛицо)) КАК СотрудникиСрезПоследних";
		Запрос.УстановитьПараметр("Период", 	 Шапка.Дата);
		Запрос.УстановитьПараметр("Организация", Шапка.Организация);
		Запрос.УстановитьПараметр("ФизЛицо", 	 МассивФизЛиц);
		ТаблицаДолжности = Запрос.Выполнить().Выгрузить();
		ТаблицаДолжности.Индексы.Добавить("ФизЛицо");

		ОбластьМакета = Макет.ПолучитьОбласть("Комиссия");
		ДанныеПечати.Вставить("ЗаголовокРазделаКомиссии", НСтр("ru = 'Председатель комиссии'"));
		
		СтрокаФизЛицо  = ВременнаяТаблицаКомиссия.Найти(Истина, "Председатель");
		Если ТаблицаДолжности.Количество() > 0 И СтрокаФизЛицо <> Неопределено Тогда
			СтрокаДожность = ТаблицаДолжности.Найти(СтрокаФизЛицо.ФизЛицо, "ФизЛицо");
			Если СтрокаДожность <> Неопределено Тогда 
				ДанныеПечати.Вставить("Должность", СтрокаДожность.Должность);
			Иначе
				ДанныеПечати.Вставить("Должность", "");	
			КонецЕсли;	
		Иначе
			ДанныеПечати.Вставить("Должность", "");
		КонецЕсли;	
		
		Если СтрокаФизЛицо <> Неопределено Тогда
			ДанныеПечати.Вставить("РасшифровкаПодписи", СтрокаФизЛицо.ФизЛицоФИО);
		Иначе 
			ДанныеПечати.Вставить("РасшифровкаПодписи", "");
		КонецЕсли;	
		
		// Инициализация массива областей макета
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Шапка");
		МассивОбластейМакета.Добавить("ЗаголовокТаблицы1");
		МассивОбластейМакета.Добавить("СтрокаТаблицы1");
		МассивОбластейМакета.Добавить("ИтогоТаблицы1");
		МассивОбластейМакета.Добавить("ЗаголовокТаблицы2");
		МассивОбластейМакета.Добавить("Комиссия");
		МассивОбластейМакета.Добавить("ЗаголовокТаблицы3");
		МассивОбластейМакета.Добавить("СтрокаТаблицы3");
		МассивОбластейМакета.Добавить("ИтогоТаблицы3");
		МассивОбластейМакета.Добавить("Подвал");
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти = "ЗаголовокТаблицы1"
				ИЛИ ИмяОбласти = "ЗаголовокТаблицы3"
				ИЛИ ИмяОбласти = "СтрокаТаблицы3"
				ИЛИ ИмяОбласти = "ИтогоТаблицы3" Тогда
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			ИначеЕсли ИмяОбласти = "Шапка"
				ИЛИ ИмяОбласти = "ИтогоТаблицы1"
				ИЛИ ИмяОбласти = "ЗаголовокТаблицы2"
				ИЛИ ИмяОбласти = "Подвал" Тогда
				
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			ИначеЕсли ИмяОбласти = "СтрокаТаблицы1" Тогда
				
				ВыборкаОС = Шапка.ОС.Выбрать();
				
				ИтогоНачальнаяАмортизация = 0;
				СтрокаОсновныхСредств = "";
				ОСДляПринятияКУчету = Неопределено;
				ПервыйШаг = Истина;
				
				Пока ВыборкаОС.Следующий() Цикл		
					ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы1");
					ОбластьМакета.Параметры.Заполнить(ВыборкаОС);			
					ТабличныйДокумент.Вывести(ОбластьМакета);
					
					ИтогоНачальнаяАмортизация = ИтогоНачальнаяАмортизация + ВыборкаОС.НачальнаяАмортизация;
					
					Если ОСДляПринятияКУчету = Неопределено Тогда
						ОСДляПринятияКУчету = ВыборкаОС.ОсновноеСредство;
					КонецЕсли;	
					
					Если ПервыйШаг Тогда
						СтрокаОсновныхСредств = Строка(ВыборкаОС.ОсновноеСредство);
					Иначе	
						СтрокаОсновныхСредств = СтрШаблон("%1, %2", СтрокаОсновныхСредств, ВыборкаОС.ОсновноеСредство);
					КонецЕсли;
					ПервыйШаг = Ложь;
				КонецЦикла;
				
				ДанныеПечати.Вставить("ИтогоНачальнаяАмортизация", ИтогоНачальнаяАмортизация);
				ДанныеПечати.Вставить("ОсновноеСредство", СтрокаОсновныхСредств);
				
				СтруктураОтбора = Новый Структура();
				СтруктураОтбора.Вставить("Организация", Шапка.Организация);
				СтруктураОтбора.Вставить("ОсновноеСредство", ОСДляПринятияКУчету);
				
				ТаблицаЗначений = РегистрыСведений.ПараметрыУчетаОС.СрезПоследних(Шапка.Дата, СтруктураОтбора);
				
				Если ТаблицаЗначений.Количество() > 0 Тогда
					ДанныеПечати.Вставить("ДатаВводаВЭксплуатацию", ТаблицаЗначений[0].ДатаВводаВЭксплуатацию);
				КонецЕсли;
				
			ИначеЕсли ИмяОбласти = "Комиссия" Тогда
				
				Для РазделыКомиссии = 1 По 3 Цикл
					
					Если РазделыКомиссии = 2 Тогда
						ДанныеПечати.ЗаголовокРазделаКомиссии = НСтр("ru = 'Члены комиссии'");
						ДанныеПечати.Должность = "";
						ДанныеПечати.РасшифровкаПодписи = "";
						
					ИначеЕсли РазделыКомиссии = 3 Тогда
						ДанныеПечати.ЗаголовокРазделаКомиссии = НСтр("ru = 'Материально-ответственное лицо'");
						
						Если ТаблицаДолжности.Количество() > 0 И Шапка.Мол <> Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
							СтрокаДожность = ТаблицаДолжности.Найти(Шапка.Мол, "ФизЛицо");
							Если СтрокаДожность <> Неопределено Тогда 
								ДанныеПечати.Должность = СтрокаДожность.Должность;
							Иначе
								ДанныеПечати.Должность = "";	
							КонецЕсли;
						Иначе
							ДанныеПечати.Должность = "";	
						КонецЕсли;
						
						Если Шапка.Мол <> Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
							ДанныеПечати.РасшифровкаПодписи = Шапка.МОЛ_ФИО;	
						Иначе
							ДанныеПечати.РасшифровкаПодписи = "";
						КонецЕсли;	
					КонецЕсли;	
						
					ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
					ТабличныйДокумент.Вывести(ОбластьМакета);		
				КонецЦикла;
			КонецЕсли;		
		КонецЦикла;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);													   
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
	
КонецФункции // ПечатнаяФорма()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаОС4") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"ФормаОС4", 
			НСтр("ru = 'Форма ОС-4'"), 
			ПечатьФормыОС4(МассивОбъектов, ОбъектыПечати),,
			"Документ.СписаниеОС.ПФ_MXL_ФормаОС4");
	КонецЕсли;

КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ФормаОС4";
	КомандаПечати.Представление = НСтр("ru = 'Форма ОС-4'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "РеестрСписаниеОС";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Списание ОС""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаОСВходящихВКомплекты(ДокументСсылка, СтруктураДополнительныеСвойства)
	ДатаДокумента = СтруктураДополнительныеСвойства.ДляПроведения.Дата;
	Организация = СтруктураДополнительныеСвойства.ДляПроведения.Организация;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СчетРасхода КАК СчетРасхода,
		|	ТаблицаДокумента.СтатьяЗатрат КАК СтатьяЗатрат
		|ПОМЕСТИТЬ ВременнаяТаблицаКомплекты
		|ИЗ
		|	Документ.СписаниеОС.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.ОсновноеСредство.ЭтоКомплект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставОССрезПоследних.ОсновноеСредствоВСоставеКомплекта КАК ОсновноеСредство,
		|	0 КАК ПервоначальнаяСтоимость,
		|	0 КАК ЛиквидационнаяСтоимость,
		|	0 КАК ОстаточнаяСтоимость,
		|	0 КАК НакопленнаяАмортизация,
		|	0 КАК АмортизацияЗаМесяц,
		|	ВременнаяТаблицаКомплекты.СчетУчета КАК СчетУчета,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияРасходовПоАмортизации.ПустаяСсылка) КАК СпособОтраженияРасходовПоАмортизации,
		|	ВременнаяТаблицаКомплекты.СчетРасхода КАК СчетРасхода,
		|	ВременнаяТаблицаКомплекты.СтатьяЗатрат КАК СтатьяЗатрат
		|ИЗ
		|	ВременнаяТаблицаКомплекты КАК ВременнаяТаблицаКомплекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставОС.СрезПоследних(
		|				&МомВрем,
		|				ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаКомплекты.ОсновноеСредство КАК ОсновноеСредство
		|					ИЗ
		|						ВременнаяТаблицаКомплекты КАК ВременнаяТаблицаКомплекты)) КАК СоставОССрезПоследних
		|		ПО ВременнаяТаблицаКомплекты.ОсновноеСредство = СоставОССрезПоследних.ОсновноеСредство
		|			И (СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Комплектация)
		|				ИЛИ СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Добавление))";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Параметры.Вставить("МомВрем", Новый Граница(ДокументСсылка.МоментВремени(), ВидГраницы.Исключая));
	ТаблицаОС = Запрос.Выполнить().Выгрузить();
	
	МассивОсновныхСредств = ТаблицаОС.ВыгрузитьКолонку("ОсновноеСредство");
	УправлениеВнеоборотнымиАктивами.ЗаполнитьДанныеОсновныхСредствВТабличнойЧасти(ДокументСсылка, ДатаДокумента, Организация, ТаблицаОС, МассивОсновныхСредств);
	
	Возврат ТаблицаОС;

КонецФункции

#КонецОбласти

#КонецЕсли