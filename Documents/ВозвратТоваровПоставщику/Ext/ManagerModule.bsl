#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ)
	
	ПрямыеПроводки = ДокументСсылка.ПрямыеПроводки;
	
	// Подготовка данных	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Регистратор,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаВзаиморасчетов,
		|	&Содержание КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимСписка КАК СинонимСписка,
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ВременнаяТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	0 КАК Себестоимость,
		|	ВременнаяТаблицаТовары.Количество КАК Количество,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК КорСчетСписания,
		|	ВременнаяТаблицаШапка.Контрагент КАК КорСубконто1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
		|	0 КАК СуммаСписания,
		|	&Содержание КАК Содержание,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ВременнаяТаблицаТовары.Сумма - ВременнаяТаблицаТовары.СуммаАкциза
		|		ИНАЧЕ ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ВЫРАЗИТЬ((ВременнаяТаблицаТовары.Сумма - ВременнаяТаблицаТовары.СуммаАкциза) / ВременнаяТаблицаШапка.Курс * ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ ВременнаяТаблицаТовары.Сумма
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ИСТИНА КАК СебестоимостьУказанаВДокументе
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Запрос.УстановитьПараметр("Содержание", НСтр("ru = 'Возврат товаров поставщику'")); 
	Запрос.УстановитьПараметр("СинонимСписка", НСтр("ru = 'Товары'"));
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = МассивРезультатов[0].Выгрузить(); 
	ТаблицаТовары = МассивРезультатов[1].Выгрузить();
	
	ТаблицаСписанныеТовары = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(ТаблицаТовары, ТаблицаРеквизиты, Отказ);
	
	МассивЧисло = Новый Массив;
	МассивЧисло.Добавить(Тип("Число"));
	
	ТаблицаСписанныеТовары.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов(МассивЧисло, Новый КвалификаторыЧисла(15, 2)));
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаТовары.СуммаАкциза КАК СуммаАкциза
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|ГДЕ
		|	ВременнаяТаблицаТовары.СуммаАкциза > 0";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		Пока Выборка.Следующий() Цикл	
			МассивСтрок = ТаблицаСписанныеТовары.НайтиСтроки(Новый Структура("Номенклатура", Выборка.Номенклатура));
			
			МассивКоэффициентов = Новый Массив();
			
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				МассивКоэффициентов.Добавить(СтрокаМассива.СуммаСписания);	
			КонецЦикла;	
				
			МассивСумм 	= ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(Выборка.СуммаАкциза, МассивКоэффициентов);
			Индекс 		= 0;
			
			Для Каждого СтрокаМассива Из МассивСумм Цикл
				МассивСтрок[Индекс].СуммаАкциза = СтрокаМассива;
				МассивСтрок[Индекс].СуммаСписания = МассивСтрок[Индекс].СуммаСписания - СтрокаМассива;
				Индекс = Индекс + 1;
			КонецЦикла;	
		КонецЦикла;	
	КонецЕсли;
	
	// Если проводки не прямые, т.е. 1610-3110 то суммы записываются с минусом, т.к. это возврат.
	// Также необходимо поменять местами счета и их субконто, это делается в общем модуле 
	// "УчетТоваров", процедура "СформироватьДвиженияВозвратТоваровПоставщику".
	Если НЕ ПрямыеПроводки Тогда	
		Для Каждого СтрокаТаблицы Из ТаблицаСписанныеТовары Цикл	
			СтрокаТаблицы.СуммаСписания = -СтрокаТаблицы.СуммаСписания;
			СтрокаТаблицы.Количество	= -СтрокаТаблицы.Количество;
		КонецЦикла;		
	КонецЕсли;	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТовары", ТаблицаТовары);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеТовары", ТаблицаСписанныеТовары);

	// Корректировка себестоимости и НДС
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		// 1 и 2. Распределение сумм на себестоимость и сумму НДС		
		// 3. Итог сумм по пункту 1-2
		// 4. СуммаСписания из сформированной таблиц "ТаблицаСписанныеТовары" 
		// 5 и 6. СуммаСписания из временных таблиц "ТаблицаСписанныеТоварыПредварительно" и "ТаблицаСписанныеУслуги"
		// 7. Итог по СуммаСписания из 4-5 пункта
		// 8. Соединение пункта 3 и данных из шапки документа
		// 9. Формирование данных для проводок по возврату услуг, корректировки себестоимости возвращаемых товаров и по НДС
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВременнаяТаблицаТовары.Сумма + ВременнаяТаблицаТовары.СуммаНДС + ВременнаяТаблицаТовары.СуммаНСП
		|		ИНАЧЕ ВременнаяТаблицаТовары.Сумма + ВременнаяТаблицаТовары.СуммаНСП
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаТовары.СуммаНДС
		|	КОНЕЦ КАК СуммаНДС,
		|	ВременнаяТаблицаТовары.СуммаАкциза КАК СуммаАкциза
		|ПОМЕСТИТЬ ВременнаяТаблицаРаспределенныеСуммы
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаУслуги.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВременнаяТаблицаУслуги.Сумма + ВременнаяТаблицаУслуги.СуммаНДС + ВременнаяТаблицаУслуги.СуммаНСП
		|		ИНАЧЕ ВременнаяТаблицаУслуги.Сумма + ВременнаяТаблицаУслуги.СуммаНСП
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаУслуги.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаУслуги.СуммаНДС
		|	КОНЕЦ,
		|	ВременнаяТаблицаУслуги.СуммаАкциза
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВременнаяТаблицаСуммы.Сумма) КАК Сумма,
		|	СУММА(ВременнаяТаблицаСуммы.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВременнаяТаблицаСуммы.СуммаАкциза) КАК СуммаАкциза
		|ПОМЕСТИТЬ ВременнаяТаблицаГотовыеСуммы
		|ИЗ
		|	ВременнаяТаблицаРаспределенныеСуммы КАК ВременнаяТаблицаСуммы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСписанныеТовары.СуммаСписания КАК СуммаСписания,
		|	ТаблицаСписанныеТовары.СуммаАкциза КАК СуммаАкциза,
		|	ТаблицаСписанныеТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаСписанныеТовары.СчетУчета КАК СчетУчета
		|ПОМЕСТИТЬ ВременнаяТаблицаСписанныеТоварыПредварительно
		|ИЗ
		|	&ТаблицаСписанныеТовары КАК ТаблицаСписанныеТовары		
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИСТИНА КАК Товар,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ТаблицаСписанныеТовары.СуммаСписания
		|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаСписанныеТовары.СуммаСписания / ВременнаяТаблицаШапка.Курс * ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15,2))
		|	КОНЕЦ КАК Сумма,
		|	ТаблицаСписанныеТовары.СуммаАкциза КАК СуммаАкциза,
		|	ТаблицаСписанныеТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаСписанныеТовары.СчетУчета КАК Счет,
		|	НЕОПРЕДЕЛЕНО КАК Субконто1,
		|	НЕОПРЕДЕЛЕНО КАК Субконто2,
		|	НЕОПРЕДЕЛЕНО КАК Субконто3
		|ПОМЕСТИТЬ ВременнаяТаблицаСписанныеТовары
		|ИЗ
		|	ВременнаяТаблицаСписанныеТоварыПредварительно КАК ТаблицаСписанныеТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ТаблицаСписанныеУслуги.Сумма - ТаблицаСписанныеУслуги.СуммаАкциза
		|					ИНАЧЕ ТаблицаСписанныеУслуги.Сумма
		|				КОНЕЦ 
		|		ИНАЧЕ 
		|			ВЫБОР
		|				КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -(ТаблицаСписанныеУслуги.Сумма - ТаблицаСписанныеУслуги.СуммаАкциза)
		|				ИНАЧЕ - ТаблицаСписанныеУслуги.Сумма
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ТаблицаСписанныеУслуги.СуммаАкциза,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаСписанныеУслуги.СчетРасхода,
		|	ТаблицаСписанныеУслуги.Субконто1,
		|	ТаблицаСписанныеУслуги.Субконто2,
		|	ТаблицаСписанныеУслуги.Субконто3
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ТаблицаСписанныеУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВременнаяТаблицаСписанныеТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаСуммаСписанныхТоваров
		|ИЗ
		|	ВременнаяТаблицаСписанныеТовары КАК ВременнаяТаблицаСписанныеТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетРасчетов,
		|	ВременнаяТаблицаШапка.ПризнакСтраны КАК ПризнакСтраны,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоВозвратамПоставщикам) КАК СчетРасходов,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК Валюта,
		|	ВременнаяТаблицаГотовыеСуммы.Сумма - ВременнаяТаблицаГотовыеСуммы.СуммаАкциза КАК Сумма,
		|	ВременнаяТаблицаШапка.Курс КАК Курс,
		|	ВременнаяТаблицаШапка.Кратность КАК Кратность,
		|	&СодержаниеКорректировка КАК Содержание
		|ПОМЕСТИТЬ ВременнаяТаблицаСуммаВозвратаСДанными
		|ИЗ
		|	ВременнаяТаблицаГотовыеСуммы КАК ВременнаяТаблицаГотовыеСуммы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ВременнаяТаблицаШапка.СчетРасчетов
		|		ИНАЧЕ ВременнаяТаблицаУслуги.СчетРасхода
		|	КОНЕЦ КАК СчетДт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ВременнаяТаблицаУслуги.СчетРасхода
		|		ИНАЧЕ ВременнаяТаблицаШапка.СчетРасчетов
		|	КОНЕЦ КАК СчетКт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ВременнаяТаблицаШапка.Контрагент
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт1,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ВременнаяТаблицаШапка.ДоговорКонтрагента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент
		|	КОНЕЦ КАК СубконтоКт1,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВременнаяТаблицаШапка.ДоговорКонтрагента
		|	КОНЕЦ КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ВременнаяТаблицаУслуги.Сумма - ВременнаяТаблицаУслуги.СуммаАкциза
		|					ИНАЧЕ ВЫРАЗИТЬ(ВременнаяТаблицаУслуги.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|				КОНЕЦ
		|		ИНАЧЕ 
		|			ВЫБОР
		|				КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -(ВременнаяТаблицаУслуги.Сумма - ВременнаяТаблицаУслуги.СуммаАкциза)
		|				ИНАЧЕ -ВЫРАЗИТЬ(ВременнаяТаблицаУслуги.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ВЫРАЗИТЬ((ВременнаяТаблицаУслуги.Сумма - ВременнаяТаблицаУслуги.СуммаАкциза) / ВременнаяТаблицаШапка.Курс * ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|					ИНАЧЕ ВременнаяТаблицаУслуги.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 
		|			ВЫБОР
		|				КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -ВЫРАЗИТЬ((ВременнаяТаблицаУслуги.Сумма - ВременнаяТаблицаУслуги.СуммаАкциза) / ВременнаяТаблицаШапка.Курс * ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|				ИНАЧЕ - ВременнаяТаблицаУслуги.Сумма
		|			КОНЕЦ
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ВЫРАЗИТЬ((ВременнаяТаблицаУслуги.Сумма - ВременнаяТаблицаУслуги.СуммаАкциза) / ВременнаяТаблицаШапка.Курс * ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|					ИНАЧЕ ВременнаяТаблицаУслуги.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 
		|			ВЫБОР
		|				КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -ВЫРАЗИТЬ((ВременнаяТаблицаУслуги.Сумма - ВременнаяТаблицаУслуги.СуммаАкциза) / ВременнаяТаблицаШапка.Курс * ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|				ИНАЧЕ - ВременнаяТаблицаУслуги.Сумма
		|			КОНЕЦ
		|	КОНЕЦ КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратУслуг КАК Содержание,
		|	ВременнаяТаблицаУслуги.Количество КАК КоличествоДт,
		|	ВременнаяТаблицаУслуги.Количество КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаУслуги.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|		
		|ВЫБРАТЬ
		|	ТаблицаСуммаВозвратаСДанными.Период КАК Период,
		|	ТаблицаСуммаВозвратаСДанными.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ТаблицаСуммаВозвратаСДанными.СчетРасчетов
		|		ИНАЧЕ ТаблицаСуммаВозвратаСДанными.СчетРасходов
		|	КОНЕЦ КАК СчетДт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ТаблицаСуммаВозвратаСДанными.СчетРасходов
		|		ИНАЧЕ ТаблицаСуммаВозвратаСДанными.СчетРасчетов
		|	КОНЕЦ КАК СчетКт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ТаблицаСуммаВозвратаСДанными.Контрагент
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт1,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА ТаблицаСуммаВозвратаСДанными.ДоговорКонтрагента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаСуммаВозвратаСДанными.Контрагент
		|	КОНЕЦ КАК СубконтоКт1,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаСуммаВозвратаСДанными.ДоговорКонтрагента
		|	КОНЕЦ КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаСуммаВозвратаСДанными.Валюта КАК ВалютаДт,
		|	ТаблицаСуммаВозвратаСДанными.Валюта КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ТаблицаСуммаВозвратаСДанными.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма
		|					ИНАЧЕ ВЫРАЗИТЬ((ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма) * ТаблицаСуммаВозвратаСДанными.Курс / ТаблицаСуммаВозвратаСДанными.Кратность КАК ЧИСЛО(15, 2))
		|				КОНЕЦ
		|		ИНАЧЕ
		|			ВЫБОР
		|				КОГДА ТаблицаСуммаВозвратаСДанными.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -(ТаблицаСуммаВозвратаСДанными.Сумма + ТаблицаСуммаСписанныхТоваров.Сумма)
		|				ИНАЧЕ -ВЫРАЗИТЬ((ТаблицаСуммаВозвратаСДанными.Сумма + ТаблицаСуммаСписанныхТоваров.Сумма) * ТаблицаСуммаВозвратаСДанными.Курс / ТаблицаСуммаВозвратаСДанными.Кратность КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ТаблицаСуммаВозвратаСДанными.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ВЫРАЗИТЬ((ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма) / ТаблицаСуммаВозвратаСДанными.Курс * ТаблицаСуммаВозвратаСДанными.Кратность КАК ЧИСЛО(15, 2))
		|					ИНАЧЕ ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 
		|			ВЫБОР
		|				КОГДА ТаблицаСуммаВозвратаСДанными.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -ВЫРАЗИТЬ((ТаблицаСуммаВозвратаСДанными.Сумма + ТаблицаСуммаСписанныхТоваров.Сумма) / ТаблицаСуммаВозвратаСДанными.Курс * ТаблицаСуммаВозвратаСДанными.Кратность КАК ЧИСЛО(15, 2))
		|				ИНАЧЕ -(ТаблицаСуммаВозвратаСДанными.Сумма + ТаблицаСуммаСписанныхТоваров.Сумма)
		|			КОНЕЦ
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА &ПрямыеПроводки
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ТаблицаСуммаВозвратаСДанными.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|						ТОГДА ВЫРАЗИТЬ((ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма) / ТаблицаСуммаВозвратаСДанными.Курс * ТаблицаСуммаВозвратаСДанными.Кратность КАК ЧИСЛО(15, 2))
		|					ИНАЧЕ ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 
		|			ВЫБОР
		|				КОГДА ТаблицаСуммаВозвратаСДанными.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|					ТОГДА -ВЫРАЗИТЬ((ТаблицаСуммаВозвратаСДанными.Сумма + ТаблицаСуммаСписанныхТоваров.Сумма) / ТаблицаСуммаВозвратаСДанными.Курс * ТаблицаСуммаВозвратаСДанными.Кратность КАК ЧИСЛО(15, 2))
		|				ИНАЧЕ -(ТаблицаСуммаВозвратаСДанными.Сумма + ТаблицаСуммаСписанныхТоваров.Сумма)
		|			КОНЕЦ
		|	КОНЕЦ КАК ВалютнаяСуммаКт,
		|	ТаблицаСуммаВозвратаСДанными.Содержание КАК Содержание,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаСуммаВозвратаСДанными КАК ТаблицаСуммаВозвратаСДанными
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСуммаСписанныхТоваров КАК ТаблицаСуммаСписанныхТоваров
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ПрямыеПроводки
		|				ТОГДА ТаблицаСуммаВозвратаСДанными.Сумма - ТаблицаСуммаСписанныхТоваров.Сумма <> 0
		|			ИНАЧЕ ТаблицаСуммаСписанныхТоваров.Сумма + ТаблицаСуммаВозвратаСДанными.Сумма <> 0
		|		КОНЕЦ 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетов,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению),
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ВременнаяТаблицаШапка.КонтрагентГНС
		|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ВременнаяТаблицаШапка.ДоговорКонтрагентаГНС
		|		ИНАЧЕ ВременнаяТаблицаШапка.ДоговорКонтрагента
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА &ВалютаРегламентированногоУчета
		|		ИНАЧЕ ВременнаяТаблицаШапка.ВалютаДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА &ВалютаРегламентированногоУчета
		|		ИНАЧЕ ВременнаяТаблицаШапка.ВалютаДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|			ТОГДА ВременнаяТаблицаГотовыеСуммы.СуммаНДС
		|		ИНАЧЕ ВЫРАЗИТЬ(ВременнаяТаблицаГотовыеСуммы.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|	КОНЕЦ,
		|	ВременнаяТаблицаГотовыеСуммы.СуммаНДС,
		|	ВременнаяТаблицаГотовыеСуммы.СуммаНДС,
		|	&СодержаниеНДС,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаГотовыеСуммы КАК ВременнаяТаблицаГотовыеСуммы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаГотовыеСуммы.СуммаНДС = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаСписанныеТовары.Счет,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы),
		|	ВременнаяТаблицаСписанныеТовары.Субконто1,
		|	ВременнаяТаблицаСписанныеТовары.Субконто2,
		|	ВременнаяТаблицаСписанныеТовары.Субконто3,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	-ВременнаяТаблицаСписанныеТовары.СуммаАкциза,
		|	-ВременнаяТаблицаСписанныеТовары.СуммаАкциза,
		|	-ВременнаяТаблицаСписанныеТовары.СуммаАкциза,
		|	&СодержаниеАкциз,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаСписанныеТовары КАК ВременнаяТаблицаСписанныеТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаСписанныеТовары.СуммаАкциза = 0
		|	И НЕ ВременнаяТаблицаСписанныеТовары.Товар
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаСписанныеТовары.Счет,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы),
		|	ВременнаяТаблицаСписанныеТовары.Номенклатура,
		|	ВременнаяТаблицаШапка.Склад,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	-ВременнаяТаблицаСписанныеТовары.СуммаАкциза,
		|	-ВременнаяТаблицаСписанныеТовары.СуммаАкциза,
		|	-ВременнаяТаблицаСписанныеТовары.СуммаАкциза,
		|	&СодержаниеАкциз,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаСписанныеТовары КАК ВременнаяТаблицаСписанныеТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаСписанныеТовары.СуммаАкциза = 0
		|	И ВременнаяТаблицаСписанныеТовары.Товар";
	Запрос.УстановитьПараметр("ТаблицаСписанныеТовары", ТаблицаСписанныеТовары);
	Запрос.УстановитьПараметр("ПрямыеПроводки", ПрямыеПроводки);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("СодержаниеКорректировка", НСтр("ru = 'Корректировка суммы возврата'"));
	Запрос.УстановитьПараметр("СодержаниеВозвратУслуг", НСтр("ru = 'Возврат услуг'"));
	Запрос.УстановитьПараметр("СодержаниеНДС", НСтр("ru = 'Возврат товаров поставщику (НДС)'"));
	Запрос.УстановитьПараметр("СодержаниеАкциз", НСтр("ru = 'Акциз'"));
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПоступлениеТоваров(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК Договор,
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНСП,
		|	ВременнаяТаблицаШапка.БезналичныйРасчет,
		|	-ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	-ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	-ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСП,
		|	-ВременнаяТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоступлениеТоваров", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСведенияОПоступлении(СтруктураДополнительныеСвойства) 
	                   
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК ДокументСсылка,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.Дата КАК ДатаПоставки,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВременнаяТаблицаШапка.ДатаСФ КАК ДатаСФ,
		|	ВременнаяТаблицаШапка.ПризнакСтраны КАК ПризнакСтраны,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаСтроки
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ,
		|	ВременнаяТаблицаШапка.ДатаСФ,
		|	ВременнаяТаблицаШапка.ПризнакСтраны,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаУслуги.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВременнаяТаблицаУслуги.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВременнаяТаблицаУслуги.СуммаНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСтроки.ДокументСсылка КАК ДокументСсылка,
		|	ВременнаяТаблицаСтроки.Организация КАК Организация,
		|	ВременнаяТаблицаСтроки.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаСтроки.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаСтроки.ДатаПоставки КАК ДатаПоставки,
		|	ВременнаяТаблицаСтроки.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаСтроки.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВременнаяТаблицаСтроки.ДатаСФ КАК ДатаСФ,
		|	ВременнаяТаблицаСтроки.ПризнакСтраны КАК ПризнакСтраны,
		|	-СУММА(ВременнаяТаблицаСтроки.Сумма) КАК Сумма,
		|	-СУММА(ВременнаяТаблицаСтроки.СуммаНДС) КАК СуммаНДС,
		|	-СУММА(ВременнаяТаблицаСтроки.СуммаНСП) КАК СуммаНСП
		|ИЗ
		|	ВременнаяТаблицаСтроки КАК ВременнаяТаблицаСтроки
		|ГДЕ
		|	ВременнаяТаблицаСтроки.ПризнакСтраны <> ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ИмпортЭкспорт)
		|	И ВременнаяТаблицаСтроки.СерияБланкаСФ <> """"
		|	И (ВременнаяТаблицаСтроки.Сумма <> 0
		|			ИЛИ ВременнаяТаблицаСтроки.СуммаНДС <> 0
		|			ИЛИ ВременнаяТаблицаСтроки.СуммаНСП <> 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСтроки.ДокументСсылка,
		|	ВременнаяТаблицаСтроки.Организация,
		|	ВременнаяТаблицаСтроки.Контрагент,
		|	ВременнаяТаблицаСтроки.ДоговорКонтрагента,
		|	ВременнаяТаблицаСтроки.ДатаПоставки,
		|	ВременнаяТаблицаСтроки.СерияБланкаСФ,
		|	ВременнаяТаблицаСтроки.НомерБланкаСФ,
		|	ВременнаяТаблицаСтроки.ДатаСФ,
		|	ВременнаяТаблицаСтроки.ПризнакСтраны";		
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОПоступлении", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРеестрПриобретенныхМатериальныхРесурсов(СтруктураДополнительныеСвойства) 
	                   
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.ДатаСФ КАК ДатаПоставки,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВременнаяТаблицаШапка.КорСерияБланкаСФ КАК КорСерияБланкаСФ,
		|	ВременнаяТаблицаШапка.КорНомерБланкаСФ КАК КорНомерБланкаСФ,
		|	-СУММА(ВременнаяТаблицаТовары.Сумма) КАК Сумма,
		|	-СУММА(ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВЫБОР
		|					КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|					ИНАЧЕ ВременнаяТаблицаТовары.ЗачетНДС
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|	КОНЕЦ КАК ЗачетНДС
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.ДатаСФ,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ,
		|	ВременнаяТаблицаШапка.КорСерияБланкаСФ,
		|	ВременнаяТаблицаШапка.КорНомерБланкаСФ,
		|	ВЫБОР
		|		КОГДА &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВЫБОР
		|					КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|					ИНАЧЕ ВременнаяТаблицаТовары.ЗачетНДС
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|	КОНЕЦ";		
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрПриобретенныхМатериальныхРесурсов", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРеестрВвезенных(СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Номер КАК Номер,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА НЕ &УказыватьПризнакЗачетаНДС
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Распределение)
		|		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|		ИНАЧЕ ВременнаяТаблицаТовары.ЗачетНДС
		|	КОНЕЦ КАК ЗачетНДС,
		|	-СУММА(ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаНДС
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|	И ВременнаяТаблицаТовары.СуммаНДС <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Номер,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаТовары.ЗачетНДС";		
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрВвезенных", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНДСНаИмпорт(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ИмпортОсвобожденныйОтНДС <> ЗНАЧЕНИЕ(Перечисление.ИмпортОсвобожденныйОтНДС.ПустаяСсылка)
		|			ТОГДА ВременнаяТаблицаШапка.ИмпортОсвобожденныйОтНДС
		|		ИНАЧЕ ВременнаяТаблицаТовары.ИмпортОсвобожденныйОтНДС
		|	КОНЕЦ КАК ИмпортОСвобожденныйОтНДС,
		|	ВременнаяТаблицаШапка.ПоказательИмпорта КАК ПоказательИмпорта,
		|	-СУММА(ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаНДС,
		|	-СУММА(ВременнаяТаблицаТовары.Сумма) КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|	И ВременнаяТаблицаШапка.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ИмпортОсвобожденныйОтНДС <> ЗНАЧЕНИЕ(Перечисление.ИмпортОсвобожденныйОтНДС.ПустаяСсылка)
		|			ТОГДА ВременнаяТаблицаШапка.ИмпортОсвобожденныйОтНДС
		|		ИНАЧЕ ВременнаяТаблицаТовары.ИмпортОсвобожденныйОтНДС
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.ПоказательИмпорта";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНДСНаИмпорт", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРеестрЗакупок(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНСП,
		|	-ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	-ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	-ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСП,
		|	-ВременнаяТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПризнакСтраны <> ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ИмпортЭкспорт)";
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрЗакупок", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьСведенияПоПоказателямИмпорта(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Дата,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Ссылка КАК ДокументСсылка,
		|	ВременнаяТаблицаШапка.Контрагент КАК Поставщик,
		|	ВременнаяТаблицаШапка.Страна КАК Страна,
		|	ВременнаяТаблицаШапка.ХарактерСделки КАК ХарактерСделки,
		|	ВременнаяТаблицаШапка.КоммерческийДокумент КАК КоммерческиеДокументы,
		|	ВременнаяТаблицаШапка.Транспортировка КАК Транспортировка,
		|	ВременнаяТаблицаШапка.ДополнительнаяИнформация КАК ДополнительнаяИнформация,
		|	ВременнаяТаблицаШапка.ВидТранспорта КАК ВидТранспорта,
		|	ВременнаяТаблицаШапка.НомерКонтракта КАК НомерКонтракта,
		|	ВременнаяТаблицаШапка.ДатаКонтракта КАК ДатаКонтракта,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	-СУММА(ВременнаяТаблицаТовары.Вес) КАК Вес,
		|	-СУММА(ВременнаяТаблицаТовары.Количество) КАК Количество,
		|	-СУММА(ВременнаяТаблицаТовары.Сумма) КАК Сумма,
		|	-СУММА(ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма / ВременнаяТаблицаКурсДоллара.Курс * ВременнаяТаблицаКурсДоллара.Кратность КАК ЧИСЛО(15, 2))) КАК СуммаВДолларах
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсДоллара КАК ВременнаяТаблицаКурсДоллара
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|	И ВременнаяТаблицаШапка.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Транспортировка,
		|	ВременнаяТаблицаШапка.Ссылка,
		|	ВременнаяТаблицаШапка.КоммерческийДокумент,
		|	ВременнаяТаблицаШапка.ДополнительнаяИнформация,
		|	ВременнаяТаблицаШапка.НомерКонтракта,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.ДатаКонтракта,
		|	ВременнаяТаблицаШапка.ВидТранспорта,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Страна,
		|	ВременнаяТаблицаШапка.ХарактерСделки,
		|	ВременнаяТаблицаТовары.Номенклатура";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияПоПоказателямИмпорта", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц 	= СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка КАК Ссылка,
		|	ВозвратТоваровПоставщику.Организация КАК Организация,
		|	ВозвратТоваровПоставщику.Дата КАК Дата,
		|	ВозвратТоваровПоставщику.Номер КАК Номер,
		|	ВозвратТоваровПоставщику.ВидОперации КАК ВидОперации,
		|	ВозвратТоваровПоставщику.Контрагент КАК Контрагент,
		|	ВозвратТоваровПоставщику.ВалютаДокумента КАК ВалютаДокумента,
		|	ВозвратТоваровПоставщику.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	&КонтрагентГНС КАК КонтрагентГНС,
		|	&ДоговорКонтрагентаГНС КАК ДоговорКонтрагентаГНС,
		|	ВозвратТоваровПоставщику.Склад КАК Склад,
		|	ВозвратТоваровПоставщику.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ВозвратТоваровПоставщику.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП,
		|	ВозвратТоваровПоставщику.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ВозвратТоваровПоставщику.ДокументОснование КАК ДокументОснование,
		|	ВозвратТоваровПоставщику.Курс КАК Курс,
		|	ВозвратТоваровПоставщику.Кратность КАК Кратность,
		|	ВозвратТоваровПоставщику.СчетРасчетов КАК СчетРасчетов,
		|	ВозвратТоваровПоставщику.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВозвратТоваровПоставщику.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВозвратТоваровПоставщику.ДатаСФ КАК ДатаСФ,
		|	ВозвратТоваровПоставщику.ПризнакСтраны КАК ПризнакСтраны,
		|	ПоступлениеТоваровУслуг.СерияБланкаСФ КАК КорСерияБланкаСФ,
		|	ПоступлениеТоваровУслуг.НомерБланкаСФ КАК КорНомерБланкаСФ,
		|	ПоступлениеТоваровУслуг.ИмпортОсвобожденныйОтНДС КАК ИмпортОсвобожденныйОтНДС,
		|	ПоступлениеТоваровУслуг.ПоказательИмпорта КАК ПоказательИмпорта,
		|	ВозвратТоваровПоставщику.Контрагент.СтранаРезидентства КАК Страна,
		|	ПоступлениеТоваровУслуг.ХарактерСделки КАК ХарактерСделки,
		|	ПоступлениеТоваровУслуг.КоммерческийДокумент КАК КоммерческийДокумент,
		|	ПоступлениеТоваровУслуг.Транспортировка КАК Транспортировка,
		|	ПоступлениеТоваровУслуг.ДополнительнаяИнформация КАК ДополнительнаяИнформация,
		|	ПоступлениеТоваровУслуг.ВидТранспорта КАК ВидТранспорта,
		|	ПоступлениеТоваровУслуг.НомерКонтракта КАК НомерКонтракта,
		|	ПоступлениеТоваровУслуг.ДатаКонтракта КАК ДатаКонтракта
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ПО ВозвратТоваровПоставщику.ДокументОснование = ПоступлениеТоваровУслуг.Ссылка
		|ГДЕ
		|	ВозвратТоваровПоставщику.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщикуТовары.НомерСтроки КАК НомерСтроки,
		|	ВозвратТоваровПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	ВозвратТоваровПоставщикуТовары.Количество КАК Количество,
		|	ВозвратТоваровПоставщикуТовары.Сумма КАК Сумма,
		|	ВозвратТоваровПоставщикуТовары.СуммаНДС КАК СуммаНДС,
		|	ВозвратТоваровПоставщикуТовары.СуммаНСП КАК СуммаНСП,
		|	ВозвратТоваровПоставщикуТовары.СуммаАкциза КАК СуммаАкциза,
		|	ВозвратТоваровПоставщикуТовары.СчетУчета КАК СчетУчета,
		|	ВозвратТоваровПоставщикуТовары.Всего КАК Всего,
		|	ВозвратТоваровПоставщикуТовары.ЗачетНДС КАК ЗачетНДС,
		|	ВозвратТоваровПоставщикуТовары.ИмпортОсвобожденныйОтНДС КАК ИмпортОсвобожденныйОтНДС,
		|	ВозвратТоваровПоставщикуТовары.Вес КАК Вес
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратТоваровПоставщикуТовары
		|ГДЕ
		|	ВозвратТоваровПоставщикуТовары.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщикуУслуги.НомерСтроки КАК НомерСтроки,
		|	ВозвратТоваровПоставщикуУслуги.Номенклатура КАК Номенклатура,
		|	ВозвратТоваровПоставщикуУслуги.Количество КАК Количество,
		|	ВозвратТоваровПоставщикуУслуги.Сумма КАК Сумма,
		|	ВозвратТоваровПоставщикуУслуги.СуммаНДС КАК СуммаНДС,
		|	ВозвратТоваровПоставщикуУслуги.СуммаНСП КАК СуммаНСП,
		|	ВозвратТоваровПоставщикуУслуги.СуммаАкциза КАК СуммаАкциза,
		|	ВозвратТоваровПоставщикуУслуги.Всего КАК Всего,
		|	ВозвратТоваровПоставщикуУслуги.ЗачетНДС КАК ЗачетНДС,
		|	ВозвратТоваровПоставщикуУслуги.СчетРасхода КАК СчетРасхода,
		|	ВозвратТоваровПоставщикуУслуги.Субконто1 КАК Субконто1,
		|	ВозвратТоваровПоставщикуУслуги.Субконто2 КАК Субконто2,
		|	ВозвратТоваровПоставщикуУслуги.Субконто3 КАК Субконто3
		|ПОМЕСТИТЬ ВременнаяТаблицаУслуги
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Услуги КАК ВозвратТоваровПоставщикуУслуги
		|ГДЕ
		|	ВозвратТоваровПоставщикуУслуги.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Курс КАК Курс,
		|	КурсыВалютСрезПоследних.Кратность КАК Кратность
		|ПОМЕСТИТЬ ВременнаяТаблицаКурсДоллара
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта.Код = ""840"") КАК КурсыВалютСрезПоследних";	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(ДокументСсылка.ДокументОснование.Дата), 
									ДокументСсылка.ДокументОснование.Дата, СтруктураДополнительныеСвойства.ДляПроведения.Дата));
	
	// Параметры для отражения НДС.
	КонтрагентГНС = СтруктураДополнительныеСвойства.ДляПроведения.Организация.КонтрагентГНС;
	
	СписокВидовДоговора = Новый СписокЗначений;
	СписокВидовДоговора.Добавить(Перечисления.ВидыДоговоровКонтрагентов.Прочее);
	ДоговорКонтрагентаГНС = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(КонтрагентГНС, 
		СтруктураДополнительныеСвойства.ДляПроведения.Организация, СписокВидовДоговора);
	
	Запрос.УстановитьПараметр("КонтрагентГНС", КонтрагентГНС);
	Запрос.УстановитьПараметр("ДоговорКонтрагентаГНС", ДоговорКонтрагентаГНС);
	
	Запрос.Выполнить();    		
		
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ);	
	СформироватьТаблицаПоступлениеТоваров(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОПоступлении(СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрПриобретенныхМатериальныхРесурсов(СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрВвезенных(СтруктураДополнительныеСвойства);
	СформироватьТаблицаНДСНаИмпорт(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрЗакупок(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьСведенияПоПоказателямИмпорта(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует табличный документ с печатной формой ПечатьНакладной
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка КАК Ссылка,
		|	ВозвратТоваровПоставщику.Номер КАК Номер,
		|	ВозвратТоваровПоставщику.Дата КАК Дата,
		|	ВозвратТоваровПоставщику.Организация КАК Организация,
		|	ВозвратТоваровПоставщику.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ВозвратТоваровПоставщику.ВалютаДокумента КАК ВалютаДокумента,
		|	ВозвратТоваровПоставщику.Контрагент КАК Контрагент,
		|	ВозвратТоваровПоставщику.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
		|	ВозвратТоваровПоставщику.Склад КАК Склад,
		|	ПРЕДСТАВЛЕНИЕ(ВозвратТоваровПоставщику.Склад) КАК СкладПредставление,
		|	ВозвратТоваровПоставщику.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
		|		ПРЕДСТАВЛЕНИЕ(ВозвратТоваровПоставщику.Товары.Номенклатура.ЕдиницаИзмерения) КАК ЕИ,
		|		Количество КАК Количество,
		|		Цена КАК Цена,
		|		Сумма КАК Сумма,
		|		СуммаНДС КАК СуммаНДС,
		|		СуммаНСП КАК СуммаНСП
		|	) КАК Товары,
		|	ВозвратТоваровПоставщику.Услуги.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
		|		Номенклатура.ЕдиницаИзмерения.Представление КАК ЕИ,
		|		Количество КАК Количество,
		|		Цена КАК Цена,
		|		Сумма КАК Сумма,
		|		СуммаНДС КАК СуммаНДС,
		|		СуммаНСП КАК СуммаНСП
		|	) КАК Услуги
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ГДЕ
		|	ВозвратТоваровПоставщику.Ссылка В(&СписокДокументов)";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ВозвратТоваровПоставщику_Накладная";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратТоваровПоставщику.ПФ_MXL_Накладная");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'Накладная на возврат поставщику'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ОрганизацияНаименованиеПолное", Шапка.ОрганизацияНаименованиеПолное);
		ДанныеПечати.Вставить("СкладПредставление", Шапка.СкладПредставление);
		ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", Шапка.КонтрагентНаименованиеПолное);
		
		ТаблицаТовары = Шапка.Товары.Выгрузить();
		ТаблицаУслуги = Шапка.Услуги.Выгрузить();

		Всего = ТаблицаТовары.Итог("Сумма") 		+ ТаблицаУслуги.Итог("Сумма");
		ВсегоНДС = ТаблицаТовары.Итог("СуммаНДС")	+ ТаблицаУслуги.Итог("СуммаНДС");
		ВсегоНСП = ТаблицаТовары.Итог("СуммаНСП")	+ ТаблицаУслуги.Итог("СуммаНСП");
		КоличествоНаименований = ТаблицаТовары.Количество();		
		
		ДанныеПечати.Вставить("Всего", Всего);
		ДанныеПечати.Вставить("ВсегоНДС", ВсегоНДС);
		ДанныеПечати.Вставить("ВсегоНСП", ВсегоНСП);
		ДанныеПечати.Вставить("ИтоговаяСтрока", 
			СтрШаблон(НСтр("ru = 'Всего наименований %1, на сумму %2'"), 
			Формат(КоличествоНаименований, "ЧН=0; ЧГ=0"), Формат(ДанныеПечати.Всего, "ЧЦ=15; ЧДЦ=2")));
		ДанныеПечати.Вставить("СуммаПрописью", БухгалтерскийУчетСервер.СформироватьСуммуПрописью(ДанныеПечати.Всего, Шапка.ВалютаДокумента));

		// Области
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("Строка");
		МассивОбластейМакета.Добавить("Подвал");
		МассивОбластейМакета.Добавить("Итоги");
		МассивОбластейМакета.Добавить("ИтогиНДС");
		МассивОбластейМакета.Добавить("ИтогиНСП");
		МассивОбластейМакета.Добавить("СуммаПрописью");
		МассивОбластейМакета.Добавить("Подписи");

		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Строка" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			ИначеЕсли ИмяОбласти = "Строка" Тогда
				НомерСтроки = 1;
				Для Каждого СтрокаТаблицы Из ТаблицаТовары Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ТабличныйДокумент.Вывести(ОбластьМакета);
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;
				Для Каждого СтрокаТаблицы Из ТаблицаУслуги Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ТабличныйДокумент.Вывести(ОбластьМакета);
					НомерСтроки = НомерСтроки + 1
				КонецЦикла;				
			КонецЕсли;	
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"Накладная", НСтр("ru = 'Накладная'"), ПечатьНакладной(МассивОбъектов, ОбъектыПечати),,
			"Документ.ВозвратТоваровПоставщику.ПФ_MXL_Накладная");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "РеестрВозвратТоваровПоставщику";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Возврат товаров поставщику""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Дата          - Дата - дата документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа.
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "")
	
	ДанныеДокумента = Новый Структура("Номер,Дата,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа.
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = СтрНайти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер);
	Возврат СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),
		НазваниеДокумента, НомерНаПечать, Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	
КонецФункции

#КонецОбласти

#КонецЕсли