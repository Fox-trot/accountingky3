#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТабличнуюЧастьОтчет() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(Начисления.ОтработаноЧасов) КАК ОтработаноЧасов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиСрезПоследних.ФизЛицо) КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаЗаКвартал
	|ИЗ
	|	РегистрСведений.Сотрудники.СрезПоследних(, Период МЕЖДУ &НачалоПериода И &КонецПериода) КАК СотрудникиСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО СотрудникиСрезПоследних.ФизЛицо = Начисления.ФизЛицо
	|ГДЕ
	|	СотрудникиСрезПоследних.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|	И Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(Начисления.ОтработаноЧасов) КАК ОтработаноЧасовСНачалаГода,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиСрезПоследних.ФизЛицо) КАК КоличествоЗаГод
	|ПОМЕСТИТЬ ВременнаяТаблицаЗаГод
	|ИЗ
	|	РегистрСведений.Сотрудники.СрезПоследних(, Период МЕЖДУ &НачалоГода И &КонецПериода) КАК СотрудникиСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО СотрудникиСрезПоследних.ФизЛицо = Начисления.ФизЛицо
	|ГДЕ
	|	СотрудникиСрезПоследних.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|	И Начисления.ПериодРегистрации МЕЖДУ &НачалоГода И &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Среднесписочная численность сотрудников,человек"" КАК Наименование,
	|	ВременнаяТаблицаЗаКвартал.Количество КАК ЗаКвартал,
	|	ВременнаяТаблицаЗаГод.КоличествоЗаГод КАК СНачалаГода
	|ИЗ
	|	ВременнаяТаблицаЗаГод КАК ВременнаяТаблицаЗаГод,
	|	ВременнаяТаблицаЗаКвартал КАК ВременнаяТаблицаЗаКвартал
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Количество отработанных человеко-часов,тыс."",
	|	ВременнаяТаблицаЗаКвартал.ОтработаноЧасов,
	|	ВременнаяТаблицаЗаГод.ОтработаноЧасовСНачалаГода
	|ИЗ
	|	ВременнаяТаблицаЗаКвартал КАК ВременнаяТаблицаЗаКвартал,
	|	ВременнаяТаблицаЗаГод КАК ВременнаяТаблицаЗаГод";

	
	Запрос.УстановитьПараметр("НачалоГода",НачалоГода(ДатаНачала));
	Запрос.УстановитьПараметр("НачалоПериода",ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(ДатаОкончания));
	
	Отчет.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

#КонецОбласти

#КонецЕсли