#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УникальныйИдентификаторФормыВладельца = Параметры.УникальныйИдентификаторФормыВладельца;
	
	Организация = Параметры.Организация;
	ЭтоВозврат = Параметры.ЭтоВозврат;
	ЭтоУслуги = Параметры.ЭтоУслуги;
	Контрагент = Параметры.Контрагент;
	ДоговорКонтрагента = Параметры.ДоговорКонтрагента;
	КодПоставкиНДС = Параметры.КодПоставкиНДС;
	Курс = Параметры.Курс;
	ФормаОплаты = Параметры.ФормаОплаты;
	
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	
	ЗаполнитьТаблицуДокументов();
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РезультатВыбора, "НачалоПериода,КонецПериода");

	ЗаполнитьТаблицуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивСсылок = Новый Массив();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДокументов Цикл		
		Если СтрокаТаблицы.Подобрать Тогда
			МассивСсылок.Добавить(СтрокаТаблицы.Документ);	
		КонецЕсли;	
	КонецЦикла;	
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("МассивСсылок", МассивСсылок);  
	Оповестить("ПодборДокументовЭСФ", СтруктураВозврата, УникальныйИдентификаторФормыВладельца);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	ЗаполнитьТаблицуДокументов();
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	ЗаполнитьТаблицуДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	МассивСсылок = Новый Массив();
	
	Для Каждого СтрокаМассива Из Значение Цикл
		МассивСсылок.Добавить(ТаблицаДокументов.Получить(СтрокаМассива).Документ);	
	КонецЦикла;	
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("МассивСсылок", МассивСсылок);  
	Оповестить("ПодборДокументовЭСФ", СтруктураВозврата, УникальныйИдентификаторФормыВладельца);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуДокументов()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	
		// 1. Данные ТЧ "Товары" документа "Реализация товаров и услуг".
		// 2. Данные ТЧ "ОС" документа "Реализация товаров и услуг".
		// 3. Данные ТЧ "Услуги" документа "Реализация товаров и услуг".
		// 4. Данные ТЧ "Товары" документа "Возврат товаров от покупателя".
		// 5. Данные ТЧ "ОС" документа "Возврат товаров от покупателя".
		// 6. Данные ТЧ "Услуги" документа "Возврат товаров от покупателя".
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Документ
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслуг.Ссылка
		|ГДЕ
		|	РеализацияТоваровУслуг.Проведен
		|	И НЕ РеализацияТоваровУслугТовары.НомерСтроки ЕСТЬ NULL
		|	И НЕ &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И РеализацияТоваровУслуг.Организация = &Организация
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|	И РеализацияТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И РеализацияТоваровУслуг.КодПоставкиНДС = &КодПоставкиНДС
		|	И РеализацияТоваровУслуг.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.БезвозмезднаяПоставка)
		|				ТОГДА РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА НЕ РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|						И РеализацияТоваровУслуг.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|						И НЕ РеализацияТоваровУслуг.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслуг.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.ОС КАК РеализацияТоваровУслугОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО РеализацияТоваровУслугОС.Ссылка = РеализацияТоваровУслуг.Ссылка
		|ГДЕ
		|	РеализацияТоваровУслуг.Проведен
		|	И НЕ РеализацияТоваровУслугОС.НомерСтроки ЕСТЬ NULL
		|	И НЕ &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И РеализацияТоваровУслуг.Организация = &Организация
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|	И РеализацияТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И РеализацияТоваровУслуг.КодПоставкиНДС = &КодПоставкиНДС
		|	И РеализацияТоваровУслуг.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.БезвозмезднаяПоставка)
		|				ТОГДА РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА НЕ РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|						И РеализацияТоваровУслуг.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|						И НЕ РеализацияТоваровУслуг.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслуг.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО РеализацияТоваровУслугУслуги.Ссылка = РеализацияТоваровУслуг.Ссылка
		|ГДЕ
		|	РеализацияТоваровУслуг.Проведен
		|	И НЕ РеализацияТоваровУслугУслуги.НомерСтроки ЕСТЬ NULL
		|	И НЕ &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И РеализацияТоваровУслуг.Организация = &Организация
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|	И РеализацияТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И РеализацияТоваровУслуг.КодПоставкиНДС = &КодПоставкиНДС
		|	И РеализацияТоваровУслуг.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.БезвозмезднаяПоставка)
		|				ТОГДА РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА НЕ РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|						И РеализацияТоваровУслуг.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ РеализацияТоваровУслуг.БезвозмезднаяПоставка
		|						И НЕ РеализацияТоваровУслуг.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслуг.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.Проведен
		|	И НЕ ВозвратТоваровОтПокупателяТовары.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И ВозвратТоваровОтПокупателя.Организация = &Организация
		|	И ВозвратТоваровОтПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВозвратТоваровОтПокупателя.Контрагент = &Контрагент
		|	И ВозвратТоваровОтПокупателя.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ВозвратТоваровОтПокупателя.КодПоставкиНДС = &КодПоставкиНДС
		|	И ВозвратТоваровОтПокупателя.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.БезвозмезднаяПоставка)
		|				ТОГДА ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА НЕ ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|						И ВозвратТоваровОтПокупателя.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|						И НЕ ВозвратТоваровОтПокупателя.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровОтПокупателя.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.ОС КАК ВозвратТоваровОтПокупателяОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ПО ВозвратТоваровОтПокупателяОС.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.Проведен
		|	И НЕ ВозвратТоваровОтПокупателяОС.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И ВозвратТоваровОтПокупателя.Организация = &Организация
		|	И ВозвратТоваровОтПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВозвратТоваровОтПокупателя.Контрагент = &Контрагент
		|	И ВозвратТоваровОтПокупателя.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ВозвратТоваровОтПокупателя.КодПоставкиНДС = &КодПоставкиНДС
		|	И ВозвратТоваровОтПокупателя.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.БезвозмезднаяПоставка)
		|				ТОГДА ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА НЕ ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|						И ВозвратТоваровОтПокупателя.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|						И НЕ ВозвратТоваровОтПокупателя.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровОтПокупателя.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Услуги КАК ВозвратТоваровОтПокупателяУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ПО ВозвратТоваровОтПокупателяУслуги.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.Проведен
		|	И НЕ ВозвратТоваровОтПокупателяУслуги.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И ВозвратТоваровОтПокупателя.Организация = &Организация
		|	И ВозвратТоваровОтПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВозвратТоваровОтПокупателя.Контрагент = &Контрагент
		|	И ВозвратТоваровОтПокупателя.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ВозвратТоваровОтПокупателя.КодПоставкиНДС = &КодПоставкиНДС
		|	И ВозвратТоваровОтПокупателя.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.БезвозмезднаяПоставка)
		|				ТОГДА ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА НЕ ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|						И ВозвратТоваровОтПокупателя.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ВозвратТоваровОтПокупателя.БезвозмезднаяПоставка
		|						И НЕ ВозвратТоваровОтПокупателя.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровОтПокупателя.Ссылка";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЭтоВозврат", ЭтоВозврат);
	Запрос.УстановитьПараметр("ЭтоУслуги", ЭтоУслуги);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("КодПоставкиНДС", КодПоставкиНДС);
	Запрос.УстановитьПараметр("Курс", Курс);
	Запрос.УстановитьПараметр("ФормаОплаты", ФормаОплаты);
	
	ТаблицаДокументов.Загрузить(Запрос.Выполнить().Выгрузить());	
КонецПроцедуры

#КонецОбласти
