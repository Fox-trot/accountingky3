#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСотрудники(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.ФизЛицо,
	|	СотрудникиСрезПоследних.Подразделение,
	|	СотрудникиСрезПоследних.Должность,
	|	СотрудникиСрезПоследних.ЗанимаемыхСтавок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение) КАК ВидСобытия,
	|	СотрудникиСрезПоследних.ГрафикРаботы,
	|	СотрудникиСрезПоследних.Регистратор КАК Ссылка,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), СЕКУНДА, -1) КАК ДатаОкончания
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудникиСрезПоследних КАК СотрудникиСрезПоследних
	|		ПО (ИСТИНА)";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДокументСсылка.ДатаУвольнения);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСотрудники", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСотрудники()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПлановыеНачисленияОкончание(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// 1. Таблица по всем видам начисления
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВременнаяТаблицаШапка.Период, ДЕНЬ), СЕКУНДА, -1) КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.ФизЛицо,
		|	ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально.ВидРасчета,
		|	ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально.Размер,
		|	ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально.Регистратор КАК ДокументСсылка,
		|	ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально.Основной
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально КАК ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлановыеНачисленияОкончание", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаПлановыеНачисленияОкончание()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПлановыеУдержания(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.ФизЛицо,
	|	ПлановыеУдержанияСрезПоследних.ВидРасчета,
	|	ПлановыеУдержанияСрезПоследних.Регистратор КАК ДокументСсылка,
	|	ПлановыеУдержанияСрезПоследних.Размер,
	|	ПлановыеУдержанияСрезПоследних.Размер КАК ПрежнийРазмер,
	|	ЛОЖЬ КАК Актуальность
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПлановыеУдержанияСрезПоследних КАК ПлановыеУдержанияСрезПоследних
	|		ПО (ИСТИНА)";
	Запрос.УстановитьПараметр("Период", ДокументСсылка.ДатаУвольнения);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлановыеУдержания", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаПлановыеНачисленияИУдержания()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВзаиморасчетыССотрудниками(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.ФизЛицо,
	|	ТаблицаДокумента.Результат КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисленияИУдержания КАК ТаблицаДокумента
	|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзаиморасчетыССотрудниками", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаВзаиморасчетыССотрудниками()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаУвольнения КАК Период,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.ВидРасчета,
		|	ТаблицаДокумента.ДнейКомпенсацииУдержанияОтпуска
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ВидРасчета,
		|	ТаблицаДокумента.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисленияИУдержания
		|ИЗ
		|	Документ.Увольнение.НачисленияИУдержания КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение,
		|	СотрудникиСрезПоследних.Должность,
		|	СотрудникиСрезПоследних.ЗанимаемыхСтавок,
		|	СотрудникиСрезПоследних.ГрафикРаботы,
		|	СотрудникиСрезПоследних.Регистратор
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиСрезПоследних
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&Период,
		|			(Организация, ФизЛицо) В
		|					(ВЫБРАТЬ
		|						ТаблицаДокумента.Организация,
		|						ТаблицаДокумента.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаШапка КАК ТаблицаДокумента)
		|				И НЕ Регистратор = &Ссылка) КАК СотрудникиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеНачисленияНачалоСрезПоследних.Регистратор,
		|	ПлановыеНачисленияНачалоСрезПоследних.ВидРасчета,
		|	ПлановыеНачисленияНачалоСрезПоследних.Размер,
		|	ПлановыеНачисленияНачалоСрезПоследних.Основной
		|ПОМЕСТИТЬ ВременнаяТаблицаСрезПлановыеНачисленияНачалоАктуально
		|ИЗ
		|	РегистрСведений.ПлановыеНачисленияНачало.СрезПоследних(
		|			&Период,
		|			(Организация, ФизЛицо) В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаШапка.Организация,
		|						ВременнаяТаблицаШапка.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка)
		|				И НЕ Регистратор = &Ссылка) КАК ПлановыеНачисленияНачалоСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияОкончание.СрезПоследних(
		|				&Период,
		|				(Организация, ФизЛицо) В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаШапка.Организация,
		|							ВременнаяТаблицаШапка.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка)
		|					И НЕ Регистратор = &Ссылка) КАК ПлановыеНачисленияОкончаниеСрезПоследних
		|		ПО ПлановыеНачисленияНачалоСрезПоследних.Организация = ПлановыеНачисленияОкончаниеСрезПоследних.Организация
		|			И ПлановыеНачисленияНачалоСрезПоследних.ФизЛицо = ПлановыеНачисленияОкончаниеСрезПоследних.ФизЛицо
		|			И ПлановыеНачисленияНачалоСрезПоследних.ВидРасчета = ПлановыеНачисленияОкончаниеСрезПоследних.ВидРасчета
		|			И ПлановыеНачисленияНачалоСрезПоследних.Регистратор = ПлановыеНачисленияОкончаниеСрезПоследних.ДокументСсылка
		|ГДЕ
		|	ПлановыеНачисленияОкончаниеСрезПоследних.Организация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеУдержанияСрезПоследних.Регистратор,
		|	ПлановыеУдержанияСрезПоследних.ФизЛицо,
		|	ПлановыеУдержанияСрезПоследних.ВидРасчета,
		|	ПлановыеУдержанияСрезПоследних.Размер
		|ПОМЕСТИТЬ ВременнаяТаблицаПлановыеУдержанияСрезПоследних
		|ИЗ
		|	РегистрСведений.ПлановыеУдержания.СрезПоследних(
		|			&Период,
		|			(Организация, ФизЛицо) В
		|					(ВЫБРАТЬ
		|						ТаблицаДокумента.Организация,
		|						ТаблицаДокумента.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаШапка КАК ТаблицаДокумента)
		|				И НЕ Регистратор = &Ссылка) КАК ПлановыеУдержанияСрезПоследних
		|ГДЕ
		|	ПлановыеУдержанияСрезПоследних.Актуальность";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДокументСсылка.ДатаУвольнения);
	
	Запрос.Выполнить();
	
	СформироватьТаблицаСотрудники(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПлановыеНачисленияОкончание(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПлановыеУдержания(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаВзаиморасчетыССотрудниками(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ИнтерфейсПечати

// Сформировать печатные формы объектов
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
	Перем Ошибки;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриказОбУвольнении";
	
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Увольнение_ПриказОбУвольнении";     
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Увольнение.ПФ_MXL_ПриказОбУвольнении");
		
		Запрос = Новый Запрос;	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Увольнение.Дата КАК ДатаПриказа,
		|	Увольнение.Номер КАК НомерПриказа,
		|	Увольнение.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
		|	Увольнение.Организация.КодПоОКПО КАК КодПоОКПО,
		|	Увольнение.Организация,
		|	Увольнение.ФизЛицо КАК Сотрудник,
		|	Увольнение.ФизЛицо.Код КАК ТабНом,
		|	Увольнение.ОснованиеУвольнения,
		|	Увольнение.СтатьяТрудовогоКодекса,
		|	СотрудникиСрезПоследних.Подразделение,
		|	СотрудникиСрезПоследних.Должность,
		|	Увольнение.Комментарий КАК Дополнительно,
		|	Увольнение.ДатаУвольнения,
		|	Увольнение.РабочийГодПо,
		|	Увольнение.РабочийГодС,
		|	Увольнение.ДнейКомпенсацииУдержанияОтпуска КАК Дней,
		|	Увольнение.ПризнакКомпенсацииУдержанияОтпуска
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники.СрезПоследних(&Дата, ) КАК СотрудникиСрезПоследних
		|		ПО Увольнение.ФизЛицо = СотрудникиСрезПоследних.ФизЛицо
		|ГДЕ
		|	Увольнение.Ссылка = &ТекущийДокумент";
		
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.УстановитьПараметр("Дата", ТекущийДокумент.Дата);
		Выборка=Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.НомерПриказа = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.НомерПриказа);
		ОбластьМакета.Параметры.ДатаПриказа = Формат(Выборка.ДатаПриказа,"ДЛФ=ДД");
		ОбластьМакета.Параметры.СтатьяТрудовогоКодекса = Выборка.СтатьяТрудовогоКодекса;
		ОбластьМакета.Параметры.ОснованиеСтатьи		   = Выборка.СтатьяТрудовогоКодекса.Основание;
		ОбластьМакета.Параметры.ДатаУвольнения 			 = Формат(Выборка.ДатаУвольнения,"ДЛФ=ДД");
		ОбластьМакета.Параметры.Дополнительно 			 = СокрЛП(Выборка.Дополнительно);
		
		//Дни 
		Если Выборка.Дней > 1 Тогда
			ПредставлениеДней = НСтр("ru = 'день'")+ "," + НСтр("ru = 'дня'")  + "," + НСтр("ru = 'дней'");
			ПродолжительностьДни = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Выборка.Дней, ПредставлениеДней) + " ";
			
			Если Выборка.ПризнакКомпенсацииУдержанияОтпуска = Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные Тогда
				Если ЗначениеЗаполнено(Выборка.РабочийГодПо) Тогда 
					ОбластьМакета.Параметры.ПериодДниКомпенсация = ПродолжительностьДни + "с " + Формат(Выборка.РабочийГодС,"ДЛФ=D") + " по " + Формат(Выборка.РабочийГодПо,"ДЛФ=D");
				Иначе 
					ОбластьМакета.Параметры.ПериодДниКомпенсация = ПродолжительностьДни;
				КонецЕсли;
			ИначеЕсли Выборка.ПризнакКомпенсацииУдержанияОтпуска = Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом Тогда 
				Если ЗначениеЗаполнено(Выборка.РабочийГодПо) Тогда 
					ОбластьМакета.Параметры.ПериодДниУдержание = ПродолжительностьДни + "с " + Формат(Выборка.РабочийГодС,"ДЛФ=D") + " по " + Формат(Выборка.РабочийГодПо,"ДЛФ=D");
				Иначе 
					ОбластьМакета.Параметры.ПериодДниУдержание = ПродолжительностьДни;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
		Структура = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Выборка.Организация, Выборка.ДатаПриказа);
		Если НЕ Структура.Свойство("Руководитель") = Неопределено Тогда
			ОбластьМакета.Параметры.ДолжностьРуководителя 	= Структура.РуководительДолжность;
			ОбластьМакета.Параметры.ФИОРуководителя 		= Структура.Руководитель;		
		КонецЕсли;

		ТабличныйДокумент.Вывести(ОбластьМакета);
			
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
	КонецЦикла;
	
	Если НЕ Ошибки = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	КонецЕсли;	
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	// Добавление дополнительных параметров
	БухгалтерскиеОтчетыКлиентСервер.ЗаполнитьДополнительныеПараметрыПечати(ТабличныйДокумент);
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриказОбУвольнении") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"ПриказОбУвольнении", 
		"Приказ об увольнении", 
		ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "ПриказОбУвольнении"));
	КонецЕсли;

КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПриказОбУвольнении";
	КомандаПечати.Представление = НСтр("ru = 'Приказ об увольнении'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;

КонецПроцедуры

#КонецОбласти
	
#КонецЕсли