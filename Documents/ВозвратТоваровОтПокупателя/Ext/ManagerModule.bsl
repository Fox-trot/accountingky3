#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка,СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СчетДохода КАК СчетДт,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетКт,
		|	ВременнаяТаблицаШапка.СубконтоДоходов КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Возврат ТМЗ от покупателя - доходы"" КАК Содержание,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.КонтрСчетУчетаСкидок = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.ДоходСоСкидкой * ВременнаяТаблицаШапка.Курс
		|		ИНАЧЕ ВременнаяТаблицаОбщиеСуммыДоходы.Доход * ВременнаяТаблицаШапка.Курс
		|	КОНЕЦ КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.КонтрСчетУчетаСкидок = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.ДоходСоСкидкой
		|		ИНАЧЕ ВременнаяТаблицаОбщиеСуммыДоходы.Доход
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.КонтрСчетУчетаСкидок = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.ДоходСоСкидкой
		|		ИНАЧЕ ВременнаяТаблицаОбщиеСуммыДоходы.Доход
		|	КОНЕЦ КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаОбщиеСуммыДоходы КАК ВременнаяТаблицаОбщиеСуммыДоходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаОбщиеСуммыДоходы.Доход <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СчетРасчетов,
		|	ВременнаяТаблицаШапка.КонтрСчетУчетаСкидок,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.СубконтоДоходов,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Возврат ТМЗ от покупателя - скидка"",
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СуммаСкидки * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СуммаСкидки,
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СуммаСкидки,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаОбщиеСуммыДоходы КАК ВременнаяТаблицаОбщиеСуммыДоходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СуммаСкидки <> 0
		|	И ВременнаяТаблицаШапка.КонтрСчетУчетаСкидок <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СчетУчетаНДС,
		|	ВременнаяТаблицаШапка.СчетРасчетов,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Возврат ТМЗ от покупателя - НДС"",
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНДССоСкидкой * ВременнаяТаблицаШапка.Курс
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНДССоСкидкой
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНДССоСкидкой
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаОбщиеСуммыДоходы КАК ВременнаяТаблицаОбщиеСуммыДоходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНДССоСкидкой <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СчетУчетаНСП,
		|	ВременнаяТаблицаШапка.СчетРасчетов,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Возврат ТМЗ от покупателя - НСП"",
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПлательщикНСП
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНСПСоСкидкой * ВременнаяТаблицаШапка.Курс
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПлательщикНСП
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНСПСоСкидкой
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ПлательщикНСП
		|			ТОГДА ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНСПСоСкидкой
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаОбщиеСуммыДоходы КАК ВременнаяТаблицаОбщиеСуммыДоходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаОбщиеСуммыДоходы.СуммаНСПСоСкидкой <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТоварыСГруппировкой.СчетУчета,
		|	ВременнаяТаблицаТоварыСГруппировкой.СчетСебестоимости,
		|	ВременнаяТаблицаТоварыСГруппировкой.Номенклатура,
		|	ВременнаяТаблицаШапка.СкладРасход,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТоварыСГруппировкой.предСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВременнаяТаблицаТоварыСГруппировкой.предСтатус
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.СубконтоСписанияСебестоимости,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Возврат ТМЗ от покупателя - списание себестоимости"",
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаТоварыСГруппировкой.Количество * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаТоварыСГруппировкой.Количество,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаТоварыСГруппировкой.Количество,
		|	ВременнаяТаблицаТоварыСГруппировкой.Количество,
		|	ВременнаяТаблицаТоварыСГруппировкой.Количество
		|ИЗ
		|	ВременнаяТаблицаТоварыСГруппировкой КАК ВременнаяТаблицаТоварыСГруппировкой
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТоварыСГруппировкой.Ссылка = ВременнаяТаблицаШапка.Ссылка,
		|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
		|			&Дата,
		|			Организация В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаШапка.Организация
		|				ИЗ
		|					ВременнаяТаблицаШапка)) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|ГДЕ
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость <> 0
		|	И ВЫБОР
		|			КОГДА ВременнаяТаблицаТоварыСГруппировкой.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
		|				ТОГДА УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриВводе)
		|						ИЛИ ВременнаяТаблицаТоварыСГруппировкой.предСтатус = ЗНАЧЕНИЕ(перечисление.СтатусыМБП.Новый)
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТоварыСГруппировкой.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленныйИзносМБП),
		|	ВременнаяТаблицаТоварыСГруппировкой.Номенклатура,
		|	ВременнаяТаблицаШапка.СкладРасход,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТоварыСГруппировкой.предСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВременнаяТаблицаТоварыСГруппировкой.предСтатус
		|	КОНЕЦ,
		|	ВременнаяТаблицаТоварыСГруппировкой.Номенклатура,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВЫБОР
		|		КОГДА УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриСписании)
		|			ТОГДА ""Возврат: Износ МБП - 100% при списании""
		|		ИНАЧЕ ""Возврат: Износ МБП - 50% при списании""
		|	КОНЕЦ,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость,
		|	ВременнаяТаблицаТоварыСГруппировкой.Количество,
		|	ВременнаяТаблицаТоварыСГруппировкой.Количество
		|ИЗ
		|	ВременнаяТаблицаТоварыСГруппировкой КАК ВременнаяТаблицаТоварыСГруппировкой
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
		|					&Дата,
		|					Организация В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаШапка.Организация
		|						ИЗ
		|							ВременнаяТаблицаШапка)) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|			ПО (ИСТИНА)
		|		ПО ВременнаяТаблицаТоварыСГруппировкой.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость <> 0
		|	И ВЫБОР
		|			КОГДА ВременнаяТаблицаТоварыСГруппировкой.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
		|				ТОГДА (УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриСписании)
		|						ИЛИ УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПоловинуПриВводе))
		|						И НЕ ВременнаяТаблицаТоварыСГруппировкой.предСтатус = ЗНАЧЕНИЕ(перечисление.СтатусыМБП.Новый)
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленныйИзносМБП),
		|	ВременнаяТаблицаТоварыСГруппировкой.СчетСебестоимости,
		|	ВременнаяТаблицаТоварыСГруппировкой.Номенклатура,
		|	ВременнаяТаблицаШапка.СкладРасход,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаТоварыСГруппировкой.Номенклатура,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТоварыСГруппировкой.предСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВременнаяТаблицаТоварыСГруппировкой.предСтатус
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Возврат: Списан накопленный износ МБП"",
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаТоварыСГруппировкой.Количество * ВременнаяТаблицаШапка.Курс * ВЫБОР
		|		КОГДА УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриСписании)
		|			ТОГДА 1
		|		ИНАЧЕ 0.5
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаТоварыСГруппировкой.Количество * ВЫБОР
		|		КОГДА УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриСписании)
		|			ТОГДА 1
		|		ИНАЧЕ 0.5
		|	КОНЕЦ,
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость * ВременнаяТаблицаТоварыСГруппировкой.Количество * ВЫБОР
		|		КОГДА УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриСписании)
		|			ТОГДА 1
		|		ИНАЧЕ 0.5
		|	КОНЕЦ,
		|	ВременнаяТаблицаТоварыСГруппировкой.Количество,
		|	ВременнаяТаблицаТоварыСГруппировкой.Количество
		|ИЗ
		|	ВременнаяТаблицаТоварыСГруппировкой КАК ВременнаяТаблицаТоварыСГруппировкой
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
		|					&Дата,
		|					Организация В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаШапка.Организация
		|						ИЗ
		|							ВременнаяТаблицаШапка)) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|			ПО (ИСТИНА)
		|		ПО ВременнаяТаблицаТоварыСГруппировкой.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТоварыСГруппировкой.Себестоимость <> 0
		|	И ВЫБОР
		|			КОГДА ВременнаяТаблицаТоварыСГруппировкой.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
		|				ТОГДА (УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПриСписании)
		|						ИЛИ УчетнаяПолитикаОрганизацийСрезПоследних.ВидУчетаИзносаМБП = ЗНАЧЕНИЕ(перечисление.ВидыУчетаИзносаМБП.ПоловинуПриВводе))
		|						И НЕ ВременнаяТаблицаТоварыСГруппировкой.предСтатус = ЗНАЧЕНИЕ(перечисление.СтатусыМБП.Новый)
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Дата", ДокументСсылка.Дата);
		
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
	БухгалтерскийУчетСервер.ПолучитьПроводкиВозвратСебестоимостиПоРБХ(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // СформироватьТаблицаХозрасчетный()

Процедура СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства)

	Запрос = новый Запрос;    
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаШапка.Организация КАК Организация,
	|	ВременнаяТаблицаШапка.СкладРасход КАК Склад,
	|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаТовары.Партия КАК Партия,
	|	ВременнаяТаблицаТовары.ИндивидуальныйНомер КАК ИнвНомер,
	|	СУММА(ВременнаяТаблицаТовары.Количество) КАК Количество,
	|	СУММА(ВременнаяТаблицаТовары.Себестоимость * ВременнаяТаблицаТовары.Количество) КАК Сумма,
	|	ВременнаяТаблицаТовары.предСтатус КАК Статус
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка,
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|ГДЕ
	|	ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.СкладРасход,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаТовары.Партия,
	|	ВременнаяТаблицаТовары.ИндивидуальныйНомер,
	|	ВременнаяТаблицаТовары.предСтатус";
	РезультатЗапроса = Запрос.Выполнить();	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМБПСклад", РезультатЗапроса.Выгрузить());

КонецПроцедуры

Процедура СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	БухгалтерскийУчетСервер.СформироватьТаблицаЗапасыВозвратОтПокупателя(ДокументСсылка, СтруктураДополнительныеСвойства);	
			
КонецПроцедуры

Процедура СформироватьТаблицаРеализация(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК Договор,		
		|	ВременнаяТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ВременнаяТаблицаТовары.СтавкаНСП КАК СтавкаНСП,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	-ВременнаяТаблицаТовары.ДоходСоСкидкой КАК Доход,
		|	-ВременнаяТаблицаТовары.СуммаНДССоСкидкой КАК СуммаНДС,
		|	-ВременнаяТаблицаТовары.СуммаНСПСоСкидкой КАК СуммаНСП,
		|	-ВременнаяТаблицаТовары.СуммаСкидки КАК СуммаСкидки,
		|	-ВременнаяТаблицаТовары.Себестоимость КАК Себестоимость,
		|	-ВременнаяТаблицаТовары.Количество КАК Количество,
		|	ВременнаяТаблицаШапка.Ссылка КАК ДокументРеализации
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.Доход <> 0";
		
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеализация", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРеализация(ДокументСсылка, СтруктураДополнительныеСвойства) 

Процедура СформироватьТаблицаНДС(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период КАК Период,
	|	ВременнаяТаблицаШапка.Организация КАК Организация,
	|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	-ВременнаяТаблицаТовары.ДоходСоСкидкой КАК СуммаБезНДС,
	|	-ВременнаяТаблицаТовары.СуммаНДССоСкидкой КАК СуммаНДС,
	|	-ВременнаяТаблицаТовары.СуммаНСПСоСкидкой КАК СуммаНСП
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВременнаяТаблицаТовары.Доход <> 0";
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНДС", РезультатЗапроса.Выгрузить());

	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Организация,
	|	ВозвратТоваровОтПокупателя.ВалютаДокумента,
	|	ВозвратТоваровОтПокупателя.Курс,
	|	ВозвратТоваровОтПокупателя.Склад КАК СкладРасход,
	|	НЕОПРЕДЕЛЕНО КАК СкладПриход,
	|	ВозвратТоваровОтПокупателя.СчетРасчетов,
	|	ВозвратТоваровОтПокупателя.Контрагент,
	|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента,
	|	ВозвратТоваровОтПокупателя.Дата КАК Период,
	|	ВозвратТоваровОтПокупателя.СубконтоДоходов,
	|	ВозвратТоваровОтПокупателя.СубконтоСписанияСебестоимости,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате) КАК СчетУчетаНДС,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогСПродаж) КАК СчетУчетаНСП,
	|	&ПлательщикНДС,
	|	&ПлательщикНСП,
	|	ВозвратТоваровОтПокупателя.КонтрСчетУчетаСкидок
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.Количество,
	|	ВозвратТоваровОтПокупателяТовары.Доход,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДС,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНСП,
	|	ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНДС * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2)) КАК СуммаНДССоСкидкой,
	|	ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНСП * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2)) КАК СуммаНСПСоСкидкой,
	|	ВозвратТоваровОтПокупателяТовары.Итого - (ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНДС * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2))) - (ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНСП * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2))) КАК ДоходСоСкидкой,
	|	ВозвратТоваровОтПокупателяТовары.СчетУчета,
	|	ВозвратТоваровОтПокупателяТовары.СчетДохода,
	|	ВозвратТоваровОтПокупателяТовары.СчетСебестоимости,
	|	ВозвратТоваровОтПокупателяТовары.СуммаСкидки,
	|	ВозвратТоваровОтПокупателяТовары.НомерСтроки,
	|	ВозвратТоваровОтПокупателяТовары.Цена,
	|	ВозвратТоваровОтПокупателяТовары.Себестоимость,
	|	ВозвратТоваровОтПокупателяТовары.предСтатусМБП КАК предСтатус,
	|	ВозвратТоваровОтПокупателяТовары.Партия,
	|	ВозвратТоваровОтПокупателяТовары.ИндивидуальныйНомер,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНСП
	|ПОМЕСТИТЬ ВременнаяТаблицаТовары
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Доход) КАК Доход,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаСкидки) КАК СуммаСкидки,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНСП) КАК СуммаНСП,
	|	ВозвратТоваровОтПокупателяТовары.СчетУчета,
	|	МАКСИМУМ(ВозвратТоваровОтПокупателяТовары.СчетДохода) КАК СчетДохода,
	|	МАКСИМУМ(ВозвратТоваровОтПокупателяТовары.СчетСебестоимости) КАК СчетСебестоимости,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	МАКСИМУМ(ВозвратТоваровОтПокупателяТовары.Себестоимость) КАК Себестоимость,
	|	ВозвратТоваровОтПокупателяТовары.предСтатусМБП КАК предСтатус
	|ПОМЕСТИТЬ ВременнаяТаблицаТоварыСГруппировкой
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.Ссылка,
	|	ВозвратТоваровОтПокупателяТовары.СчетУчета,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.предСтатусМБП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Доход) КАК Доход,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНСП) КАК СуммаНСП,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаСкидки) КАК СуммаСкидки,
	|	ВозвратТоваровОтПокупателяТовары.СчетДохода,
	|	СУММА(ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНДС * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2))) КАК СуммаНДССоСкидкой,
	|	СУММА(ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНСП * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2))) КАК СуммаНСПСоСкидкой,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Итого - (ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНДС * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2))) - (ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.СуммаНСП * (1 - ВозвратТоваровОтПокупателяТовары.СуммаСкидки / (ВозвратТоваровОтПокупателяТовары.Доход + ВозвратТоваровОтПокупателяТовары.СуммаНДС + ВозвратТоваровОтПокупателяТовары.СуммаНСП)) КАК ЧИСЛО(15, 2)))) КАК ДоходСоСкидкой,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС
	|ПОМЕСТИТЬ ТаблицыСГруппировкойПоДоходам
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.Ссылка,
	|	ВозвратТоваровОтПокупателяТовары.СчетДохода,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицыСГруппировкой.Ссылка,
	|	СУММА(ТаблицыСГруппировкой.Доход) КАК Доход,
	|	СУММА(ТаблицыСГруппировкой.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицыСГруппировкой.СуммаНСП) КАК СуммаНСП,
	|	СУММА(ТаблицыСГруппировкой.СуммаСкидки) КАК СуммаСкидки,
	|	ТаблицыСГруппировкой.СчетДохода,
	|	СУММА(ТаблицыСГруппировкой.СуммаНДССоСкидкой) КАК СуммаНДССоСкидкой,
	|	СУММА(ТаблицыСГруппировкой.СуммаНСПСоСкидкой) КАК СуммаНСПСоСкидкой,
	|	СУММА(ТаблицыСГруппировкой.ДоходСоСкидкой) КАК ДоходСоСкидкой,
	|	ТаблицыСГруппировкой.СтавкаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаОбщиеСуммыДоходы
	|ИЗ
	|	ТаблицыСГруппировкойПоДоходам КАК ТаблицыСГруппировкой
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицыСГруппировкой.Ссылка,
	|	ТаблицыСГруппировкой.СчетДохода,
	|	ТаблицыСГруппировкой.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыОбороты.СчетУчета,
	|	ЗапасыОбороты.Номенклатура,
	|	ЗапасыОбороты.Партия,
	|	СУММА(ЗапасыОбороты.КоличествоПриход) КАК Количество,
	|	ЗапасыОбороты.Цена
	|ПОМЕСТИТЬ ВременнаяТаблицаУжеВозвращенныхТоваров
	|ИЗ
	|	РегистрНакопления.Запасы.Обороты(
	|			,
	|			,
	|			Авто,
	|			Организация = &Организация
	|				И (Склад = &Склад
	|					ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ВременнаяТаблицаТовары.Номенклатура
	|					ИЗ
	|						ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары)) КАК ЗапасыОбороты
	|ГДЕ
	|	ВЫРАЗИТЬ(ЗапасыОбороты.Регистратор КАК Документ.ВозвратТоваровПоставщику).ДокументОснование = &ДокументОснование
	|	И (ВЫРАЗИТЬ(ЗапасыОбороты.Регистратор КАК Документ.ВозвратТоваровПоставщику)) <> &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОбороты.СчетУчета,
	|	ЗапасыОбороты.Партия,
	|	ЗапасыОбороты.Номенклатура,
	|	ЗапасыОбороты.Цена";
		
	Запрос.УстановитьПараметр("Ссылка", 				ДокументСсылка);
	Запрос.УстановитьПараметр("ПлательщикНДС", 			СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикНДС);
	Запрос.УстановитьПараметр("ПлательщикНСП", 			СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикНСП);
	Запрос.УстановитьПараметр("Организация", 			СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("Склад", 					СтруктураДополнительныеСвойства.ДляПроведения.СкладРасход);
	Запрос.УстановитьПараметр("ДокументОснование", 		СтруктураДополнительныеСвойства.ДляПроведения.ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();

	СформироватьТаблицаХозрасчетный(ДокументСсылка,СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеализация(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНДС(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьНакладной(МассивОбъектов, ОбъектыПечати)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати 	= "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратТоваровОтПокупателя_Накладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратТоваровОтПокупателя.Накладная");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Дата,
	|	ВозвратТоваровОтПокупателя.Номер,
	|	ВозвратТоваровОтПокупателя.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	ВозвратТоваровОтПокупателя.Контрагент.НаименованиеПолное КАК ПредставлениеПолучателя,
	|	ВозвратТоваровОтПокупателя.Товары. (
	|		Номенклатура,
	|		Количество,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Цена,
	|		СуммаНДС,
	|		СуммаНСП,
	|		Всего
	|	)
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка В(&МассивОбъектов)";
	  
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);

	Результат = Запрос.Выполнить();
	Шапка = Результат.Выбрать();
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель	 	= Макет.ПолучитьОбласть("Покупатель");
	ОбластьШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого 			= Макет.ПолучитьОбласть("Итого");
	ОбластьИтогоНДС 		= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьИтогоНСП 		= Макет.ПолучитьОбласть("ИтогоНСП");
	ОбластьСуммаПрописью 	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	ТабличныйДокумент.Очистить();
		 
	Пока Шапка.Следующий() Цикл 
		ОбластьЗаголовок.Параметры.Заполнить(Шапка);
		ОбластьПоставщик.Параметры.Заполнить(Шапка);  
		ОбластьПокупатель.Параметры.Заполнить(Шапка);
		ОбластьШапкаТаблицы.Параметры.Заполнить(Шапка);
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
			ВыборкаСтрокТовары = Шапка.Товары.Выбрать();              
			НомерСтроки = 1;
			Пока ВыборкаСтрокТовары.Следующий() Цикл  
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрокТовары);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
		ВсегоНДС 				= Шапка.Товары.Выгрузить().Итог("СуммаНДС");
		ВсегоНСП 				= Шапка.Товары.Выгрузить().Итог("СуммаНСП");
		Всего	 				= Шапка.Товары.Выгрузить().Итог("Всего");	
		СуммаДокументаПрописью 	= БухгалтерскийУчетСервер.СформироватьСуммуПрописью(Всего);
		ИтоговаяСтрока 			= "Всего наименований " + (НомерСтроки-1) + ", на сумму " + Формат(Всего, "ЧЦ=15; ЧДЦ=2") + " сом";
		
		ОбластиМакетаНижеТаблицы = Новый Структура();
		ОбластиМакетаНижеТаблицы.Вставить("Всего", Всего);
		ОбластиМакетаНижеТаблицы.Вставить("СуммаНДС", ВсегоНДС);
		ОбластиМакетаНижеТаблицы.Вставить("СуммаНСП", ВсегоНСП);
		ОбластиМакетаНижеТаблицы.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		ОбластиМакетаНижеТаблицы.Вставить("СуммаДокументаПрописью", СуммаДокументаПрописью);
		
		ОбластьИтого.Параметры.Заполнить(ОбластиМакетаНижеТаблицы);
		ОбластьИтогоНДС.Параметры.Заполнить(ОбластиМакетаНижеТаблицы);
		ОбластьИтогоНСП.Параметры.Заполнить(ОбластиМакетаНижеТаблицы);
		ОбластьСуммаПрописью.Параметры.Заполнить(ОбластиМакетаНижеТаблицы);

		ТабличныйДокумент.Вывести(ОбластьИтого);
		ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		ТабличныйДокумент.Вывести(ОбластьИтогоНСП);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьСчетФактураНДС(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратТоваровОтПокупателя";
	ТабличныйДокумент.КлючПараметровПечати = "СчетФактураНДС";

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщиеМакеты.СчетФактураНДС");
	
	ТекстЗапроса =
	
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Номер,
	|	ВозвратТоваровОтПокупателя.Дата,
	|	ВозвратТоваровОтПокупателя.Организация,
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Контрагент,
	|	ВозвратТоваровОтПокупателя.Склад,
	|	ВозвратТоваровОтПокупателя.Товары.(
	|		Номенклатура,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество,
	|		Цена,
	|		Всего,
	|		СуммаНДС,
	|		СуммаНСП,
	|		НомерСтроки,
	|		СуммаСкидки
	|	),
	|	ВозвратТоваровОтПокупателя.Организация.ИНН КАК ИНН,
	|	ВозвратТоваровОтПокупателя.Организация.ОсновнойБанковскийСчет КАК ОснБанкСчет,
	|	ВозвратТоваровОтПокупателя.Организация.ГНС КАК ГНС ,                        //ГНСГоловнойОрганизации КАК ГНС,
	|	ВозвратТоваровОтПокупателя.Организация.НаименованиеПолное КАК НаименованиеПолное,
	|	НЕОПРЕДЕЛЕНО КАК Поле1,
	|	ВозвратТоваровОтПокупателя.Контрагент.ОсновнойБанковскийСчет КАК ОснБанкСчет_Пок,
	|	НЕОПРЕДЕЛЕНО КАК Поле2,
	|	ВозвратТоваровОтПокупателя.Контрагент.КонтактнаяИнформация.(
	|		Ссылка КАК Поле1,
	|		НомерСтроки КАК Поле2,
	|		Тип КАК Поле3,
	|		Вид КАК Поле4,
	|		Представление КАК Поле5,
	|		ЗначенияПолей КАК Поле6,
	|		Страна КАК Поле7,
	|		Регион КАК Поле8,
	|		Город КАК Поле9,
	|		АдресЭП КАК Поле10,
	|		ДоменноеИмяСервера КАК Поле11,
	|		НомерТелефона КАК Поле12,
	|		НомерТелефонаБезКодов КАК Поле13
	|	) КАК Контрагент1,
	|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента.СтавкаНДС,
	|	СтавкиНДССрезПоследних.Ставка,
	|	СтавкиНСПСрезПоследних.СтавкаПлательщикНДС КАК СтавкаНСП,
	|	СтавкиНСПСрезПоследних.СтавкаНеПлательщикНДС КАК СтавкаНСПне,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента,
	|	ВозвратТоваровОтПокупателя.Организация.ОсновнойБанковскийСчет КАК БанковскийСчет,
	|	ВозвратТоваровОтПокупателя.ДокументОснование.СкидкаПроцент КАК ПроцентСкидки,
	|	ВозвратТоваровОтПокупателя.ДокументОснование.Товары.СтавкаНСП
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДС.СрезПоследних(, ) КАК СтавкиНДССрезПоследних
	|		ПО ВозвратТоваровОтПокупателя.ДоговорКонтрагента.СтавкаНДС = СтавкиНДССрезПоследних.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНСП.СрезПоследних(, ) КАК СтавкиНСПСрезПоследних
	|		ПО ВозвратТоваровОтПокупателя.ДокументОснование.Товары.СтавкаНСП = СтавкиНСПСрезПоследних.СтавкаНСП
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка В(&СписокДокументов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		
		ВременнаяТаблицаТовары = Выборка.Товары.Выгрузить();                                        
		
		ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
		
		//////////////////////Данные поставщика НАЧАЛО/////////////////////////////////////
		
		ОбластьМакетаШапка.Параметры.Номер                     = Выборка.Номер;
		ДатаСФ                         					 	   = Строка(Выборка.Дата);
		ОбластьМакетаШапка.Параметры.Дата1                     = Сред(ДатаСФ,1,1);
		ОбластьМакетаШапка.Параметры.Дата2                     = Сред(ДатаСФ,2,1);
		ОбластьМакетаШапка.Параметры.Дата4                     = Сред(ДатаСФ,4,1);
		ОбластьМакетаШапка.Параметры.Дата5                     = Сред(ДатаСФ,5,1);
		ОбластьМакетаШапка.Параметры.Дата7                     = Сред(ДатаСФ,7,1);
		ОбластьМакетаШапка.Параметры.Дата8                     = Сред(ДатаСФ,8,1);
		ОбластьМакетаШапка.Параметры.Дата9                     = Сред(ДатаСФ,9,1);
		ОбластьМакетаШапка.Параметры.Дата10                    = Сред(ДатаСФ,10,1);
		
		ОбластьМакетаШапка.Параметры.ДП1                       = Сред(ДатаСФ,1,1);
		ОбластьМакетаШапка.Параметры.ДП2                       = Сред(ДатаСФ,2,1);
		ОбластьМакетаШапка.Параметры.ДП4                       = Сред(ДатаСФ,4,1);
		ОбластьМакетаШапка.Параметры.ДП5                       = Сред(ДатаСФ,5,1);
		ОбластьМакетаШапка.Параметры.ДП7                       = Сред(ДатаСФ,7,1);
		ОбластьМакетаШапка.Параметры.ДП8                       = Сред(ДатаСФ,8,1);
		ОбластьМакетаШапка.Параметры.ДП9                       = Сред(ДатаСФ,9,1);
		ОбластьМакетаШапка.Параметры.ДП10                      = Сред(ДатаСФ,10,1);
		
		ИННнаш = Выборка.ИНН;	
		ОбластьМакетаШапка.Параметры.ИННПост1					= Сред(ИННнаш,1,1);	
		ОбластьМакетаШапка.Параметры.ИННПост2					= Сред(ИННнаш,2,1);	
		ОбластьМакетаШапка.Параметры.ИННПост3					= Сред(ИННнаш,3,1);
		ОбластьМакетаШапка.Параметры.ИННПост4					= Сред(ИННнаш,4,1);
		ОбластьМакетаШапка.Параметры.ИННПост5					= Сред(ИННнаш,5,1);
		ОбластьМакетаШапка.Параметры.ИННПост6					= Сред(ИННнаш,6,1);
		ОбластьМакетаШапка.Параметры.ИННПост7					= Сред(ИННнаш,7,1);
		ОбластьМакетаШапка.Параметры.ИННПост8					= Сред(ИННнаш,8,1);
		ОбластьМакетаШапка.Параметры.ИННПост9					= Сред(ИННнаш,9,1);
		ОбластьМакетаШапка.Параметры.ИННПост10					= Сред(ИННнаш,10,1);
		ОбластьМакетаШапка.Параметры.ИННПост11					= Сред(ИННнаш,11,1);
		ОбластьМакетаШапка.Параметры.ИННПост12					= Сред(ИННнаш,12,1);
		ОбластьМакетаШапка.Параметры.ИННПост13					= Сред(ИННнаш,13,1);
		ОбластьМакетаШапка.Параметры.ИННПост14					= Сред(ИННнаш,14,1);
		
		БанкНашСсылка  	= Выборка.БанковскийСчет;
		БИКнаш	 = БанкНашСсылка.Банк.Код;
		ОбластьМакетаШапка.Параметры.БИКПоставщика1				= Сред(БИКнаш,1,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика2				= Сред(БИКнаш,2,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика3		        = Сред(БИКнаш,3,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика4				= Сред(БИКнаш,4,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика5				= Сред(БИКнаш,5,1);	
		ОбластьМакетаШапка.Параметры.БИКПоставщика6				= Сред(БИКнаш,6,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика7				= Сред(БИКнаш,7,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика8				= Сред(БИКнаш,8,1);
		ОбластьМакетаШапка.Параметры.БИКПоставщика9				= Сред(БИКнаш,9,1);
		
		
		РСчетНаш = ?(не ЗначениеЗаполнено(БанкНашСсылка.НомерСчета),БанкНашСсылка.Наименование,БанкНашСсылка.НомерСчета);
		ОбластьМакетаШапка.Параметры.СчетПоставщика1			= Сред(РСчетНаш,1,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика2			= Сред(РСчетНаш,2,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика3			= Сред(РСчетНаш,3,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика4			= Сред(РСчетНаш,4,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика5			= Сред(РСчетНаш,5,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика6			= Сред(РСчетНаш,6,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика7			= Сред(РСчетНаш,7,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика8			= Сред(РСчетНаш,8,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика9			= Сред(РСчетНаш,9,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика10			= Сред(РСчетНаш,10,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика11			= Сред(РСчетНаш,11,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика12			= Сред(РСчетНаш,12,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика13			= Сред(РСчетНаш,13,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика14			= Сред(РСчетНаш,14,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика15			= Сред(РСчетНаш,15,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика16			= Сред(РСчетНаш,16,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика17			= Сред(РСчетНаш,17,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика18			= Сред(РСчетНаш,18,1);
		ОбластьМакетаШапка.Параметры.СчетПоставщика19			= Сред(РСчетНаш,19,1);
		
		ОбластьМакетаШапка.Параметры.БанкПост    			    = БанкНашСсылка.Банк.Наименование;
		
		ГНСнаш 													= Выборка.ГНС;
		
		ОбластьМакетаШапка.Параметры.ГНСПоставщика1				= Сред(ГНСнаш.Код,1,1);
		ОбластьМакетаШапка.Параметры.ГНСПоставщика2				= Сред(ГНСнаш.Код,2,1);
		ОбластьМакетаШапка.Параметры.ГНСПоставщика3				= Сред(ГНСнаш.Код,3,1);
		
		ОбластьМакетаШапка.Параметры.НаименованиеГНСПоставщика  = ГНСнаш.Наименование;
		ОбластьМакетаШапка.Параметры.ПредставлениеПоставщика    = СокрЛП(Выборка.НаименованиеПолное);
		
		ОбластьМакетаШапка.Параметры.АдресПоставщика 			= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Выборка.Организация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации); 			
		//////////////////////Данные поставщика КОНЕЦ/////////////////////////////////////
		
		//////////////////////Данные покупателя НАЧАЛО/////////////////////////////////////
		ОбластьМакетаШапка.Параметры.АдресПокупателя 			= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Выборка.Контрагент, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
		
		ИННпокупателя 											= Выборка.Контрагент.ИНН;
		ОбластьМакетаШапка.Параметры.ИННПок1					= Сред(ИННпокупателя,1,1);
		ОбластьМакетаШапка.Параметры.ИННПок2					= Сред(ИННпокупателя,2,1);
		ОбластьМакетаШапка.Параметры.ИННПок3					= Сред(ИННпокупателя,3,1);
		ОбластьМакетаШапка.Параметры.ИННПок4					= Сред(ИННпокупателя,4,1);
		ОбластьМакетаШапка.Параметры.ИННПок5					= Сред(ИННпокупателя,5,1);
		ОбластьМакетаШапка.Параметры.ИННПок6					= Сред(ИННпокупателя,6,1);
		ОбластьМакетаШапка.Параметры.ИННПок7					= Сред(ИННпокупателя,7,1);
		ОбластьМакетаШапка.Параметры.ИННПок8					= Сред(ИННпокупателя,8,1);
		ОбластьМакетаШапка.Параметры.ИННПок9					= Сред(ИННпокупателя,9,1);
		ОбластьМакетаШапка.Параметры.ИННПок10					= Сред(ИННпокупателя,10,1);
		ОбластьМакетаШапка.Параметры.ИННПок11					= Сред(ИННпокупателя,11,1);
		ОбластьМакетаШапка.Параметры.ИННПок12					= Сред(ИННпокупателя,12,1);
		ОбластьМакетаШапка.Параметры.ИННПок13					= Сред(ИННпокупателя,13,1);
		ОбластьМакетаШапка.Параметры.ИННПок14					= Сред(ИННпокупателя,14,1);
		
		ОбластьМакетаШапка.Параметры.ПредставлениеПокупателя    = СокрЛП(Выборка.Контрагент.НаименованиеПолное);
		
		КодГНСПок          										= Выборка.Контрагент.ГНС.Код;
		ОбластьМакетаШапка.Параметры.ГНСПок1					= Сред(КодГНСПок,1,1);
		ОбластьМакетаШапка.Параметры.ГНСПок2					= Сред(КодГНСПок,2,1);
		ОбластьМакетаШапка.Параметры.ГНСПок3					= Сред(КодГНСПок,3,1);
		
		ОбластьМакетаШапка.Параметры.НаименованиеГНСПок 		 = Выборка.Контрагент.ГНС.Наименование;
		
		БанкСсылкаПок  											= Выборка.ОснБанкСчет_Пок;
		СчетПок 												= ?(не ЗначениеЗаполнено(БанкСсылкаПок.НомерСчета),
																БанкСсылкаПок.Наименование,БанкСсылкаПок.НомерСчета);
		ОбластьМакетаШапка.Параметры.СчетПок1					= Сред(СчетПок,1,1);
		ОбластьМакетаШапка.Параметры.СчетПок2					= Сред(СчетПок,2,1);
		ОбластьМакетаШапка.Параметры.СчетПок3					= Сред(СчетПок,3,1);
		ОбластьМакетаШапка.Параметры.СчетПок4					= Сред(СчетПок,4,1);
		ОбластьМакетаШапка.Параметры.СчетПок5					= Сред(СчетПок,5,1);
		ОбластьМакетаШапка.Параметры.СчетПок6					= Сред(СчетПок,6,1);
		ОбластьМакетаШапка.Параметры.СчетПок7					= Сред(СчетПок,7,1);
		ОбластьМакетаШапка.Параметры.СчетПок8					= Сред(СчетПок,8,1);
		ОбластьМакетаШапка.Параметры.СчетПок9					= Сред(СчетПок,9,1);
		ОбластьМакетаШапка.Параметры.СчетПок10	  				= Сред(СчетПок,10,1);
		ОбластьМакетаШапка.Параметры.СчетПок11					= Сред(СчетПок,11,1);
		ОбластьМакетаШапка.Параметры.СчетПок12					= Сред(СчетПок,12,1);
		ОбластьМакетаШапка.Параметры.СчетПок13					= Сред(СчетПок,13,1);
		ОбластьМакетаШапка.Параметры.СчетПок14					= Сред(СчетПок,14,1);
		ОбластьМакетаШапка.Параметры.СчетПок15					= Сред(СчетПок,15,1);
		ОбластьМакетаШапка.Параметры.СчетПок16					= Сред(СчетПок,16,1);
		ОбластьМакетаШапка.Параметры.СчетПок17					= Сред(СчетПок,17,1);
		ОбластьМакетаШапка.Параметры.СчетПок18					= Сред(СчетПок,18,1);
		ОбластьМакетаШапка.Параметры.СчетПок19					= Сред(СчетПок,19,1);
		
		БИКПок = БанкСсылкаПок.Банк.Код;   
		ОбластьМакетаШапка.Параметры.БИКПок1					= Сред(БИКПок,1,1);
		ОбластьМакетаШапка.Параметры.БИКПок2					= Сред(БИКПок,2,1);
		ОбластьМакетаШапка.Параметры.БИКПок3					= Сред(БИКПок,3,1);
		ОбластьМакетаШапка.Параметры.БИКПок4					= Сред(БИКПок,4,1);
		ОбластьМакетаШапка.Параметры.БИКПок5					= Сред(БИКПок,5,1);
		ОбластьМакетаШапка.Параметры.БИКПок6					= Сред(БИКПок,6,1);
		ОбластьМакетаШапка.Параметры.БИКПок7					= Сред(БИКПок,7,1);
		ОбластьМакетаШапка.Параметры.БИКПок8					= Сред(БИКПок,8,1);
		ОбластьМакетаШапка.Параметры.БИКПок9					= Сред(БИКПок,9,1);
		
		ОбластьМакетаШапка.Параметры.БанкПок     	= БанкСсылкаПок.Банк.Наименование;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);  
		
		ОбластьМакетаЗаголовоТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");	                             
		ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовоТаблицы);
		//////////////////////Данные покупателя КОНЕЦ/////////////////////////////////////
		
		//////////////////////ТабЧасть НАЧАЛО/////////////////////////////////////
		
		ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка");	 
		ВыборкаТовары = Выборка.Товары.Выбрать();
		
		СуммаБезНДСПоДокументу = 0;
		Пока ВыборкаТовары.Следующий()Цикл  
			
			ОбластьМакетаСтрока.Параметры.Заполнить(ВыборкаТовары);
			ОбластьМакетаСтрока.Параметры.Доход  = ВыборкаТовары.Всего - ВыборкаТовары.СуммаНДС;
			ОбластьМакетаСтрока.Параметры.СтавкаНДС = Выборка.Ставка; 
			
			ТабличныйДокумент.Вывести(ОбластьМакетаСтрока);
			
			СуммаБезНДСПоДокументу = СуммаБезНДСПоДокументу + ВыборкаТовары.Всего - ВыборкаТовары.СуммаНДС;

		КонецЦикла; 
		
		ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаСтрока.Параметры.Номенклатура = "Скидка" + Выборка.ПроцентСкидки + "%";
		ОбластьМакетаСтрока.Параметры.Доход  = ВыборкаТовары.СуммаСкидки;
		ОбластьМакетаСтрока.Параметры.Всего = ВыборкаТовары.СуммаСкидки;
			
		ТабличныйДокумент.Вывести(ОбластьМакетаСтрока);
			
		СуммаБезНДСПоДокументу = СуммаБезНДСПоДокументу - ВыборкаТовары.СуммаСкидки;
		
		ОбластьМакетаИтого 			= Макет.ПолучитьОбласть("Итого");
		ОбластьМакетаИтогоНСП		= Макет.ПолучитьОбласть("ИтогоНСП");
		ОбластьМакетаИтогоВсего	 	= Макет.ПолучитьОбласть("ИтогоВсего");
		
		СуммаКоличестоПоДокументу 	= Выборка.Товары.Выгрузить().Итог("Количество");
		СуммаНДСПоДокументу 		= Выборка.Товары.Выгрузить().Итог("СуммаНДС");
		СуммаВсегоПоДокументу 		= Выборка.Товары.Выгрузить().Итог("Всего") - ВыборкаТовары.СуммаСкидки;
		СуммаНСППоДокументу 		= Выборка.Товары.Выгрузить().Итог("СуммаНСП");
		СуммаПоДокументуСНСП    	= СуммаВсегоПоДокументу;
		
		СуммаДокументаПрописью 		= БухгалтерскийУчетСервер.СформироватьСуммуПрописью(СуммаПоДокументуСНСП);
		
		ОбластиМакетаИтоговДляЗаполнения = Новый Структура();
		ОбластиМакетаИтоговДляЗаполнения.Вставить("ИтогоКоличество", СуммаКоличестоПоДокументу);
		ОбластиМакетаИтоговДляЗаполнения.Вставить("ИтогоСумма", СуммаБезНДСПоДокументу);
		ОбластиМакетаИтоговДляЗаполнения.Вставить("ИтогоСуммаНДС", СуммаНДСПоДокументу);
		ОбластиМакетаИтоговДляЗаполнения.Вставить("ИтогоСндс", СуммаВсегоПоДокументу);
		ОбластиМакетаИтоговДляЗаполнения.Вставить("СтавкаНСП", "" + Выборка.СтавкаНСП + "%");
		ОбластиМакетаИтоговДляЗаполнения.Вставить("ИтогоНСП", СуммаНСППоДокументу);
		ОбластиМакетаИтоговДляЗаполнения.Вставить("ИтогоВсего", СуммаПоДокументуСНСП);
		ОбластиМакетаИтоговДляЗаполнения.Вставить("СуммаПрописью", СуммаДокументаПрописью);

		ОбластьМакетаИтого.Параметры.Заполнить(ОбластиМакетаИтоговДляЗаполнения);
		ОбластьМакетаИтогоНСП.Параметры.Заполнить(ОбластиМакетаИтоговДляЗаполнения);
		ОбластьМакетаИтогоВсего.Параметры.Заполнить(ОбластиМакетаИтоговДляЗаполнения);
		
		ТабличныйДокумент.Вывести(ОбластьМакетаИтого);
		ТабличныйДокумент.Вывести(ОбластьМакетаИтогоНСП);
		ТабличныйДокумент.Вывести(ОбластьМакетаИтогоВсего);
		
		ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
		Руководители 	= БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Выборка.Организация, КонецДня(Выборка.Дата));
		ОбластьМакетаПодвал.Параметры.ФИОРуководителя 	= Руководители.Руководитель;
		ОбластьМакетаПодвал.Параметры.ФИОГлавногоБухгалтера    	= Руководители.ГлавныйБухгалтер;
		ТабличныйДокумент.Вывести(ОбластьМакетаПодвал);   
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
		
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов – Массив – ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати – Структура – дополнительные настройки печати;
//  КоллекцияПечатныхФорм – ТаблицаЗначений – сформированные табличные документы (выходной параметр)
//  ОбъектыПечати – СписокЗначений – значение – ссылка на объект;
//                                            представление – имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода – Структура – дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
				"Накладная", НСтр("ru = 'Накладная'"), ПечатьНакладной(МассивОбъектов, ОбъектыПечати),,
				"Документ.ВозвратТоваровПоставщику.ПФ_MXL_Накладная");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"СчетФактура", НСтр("ru = 'Счет-фактура'"), ПечатьСчетФактураНДС(МассивОбъектов, ОбъектыПечати),,
			"ОбщиеМакеты.СчетФактураНДС");
	КонецЕсли;
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 2;
	
КонецПроцедуры

#КонецОбласти

	
#КонецЕсли

