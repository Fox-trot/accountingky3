#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Контрагент 			= Параметры.Контрагент;
	ДоговорКонтрагента 	= Параметры.ДоговорКонтрагента;
	Организация 		= Параметры.Организация;
	ДоступнаТабличнаяЧасть = Параметры.ДоступнаТабличнаяЧасть;
	Инициатор 			= Параметры.Инициатор;
	
	ПолучитьТаблицыЗначений();
	
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеВнешнимВидом

&НаСервере
// Процедура устанавливает видимость и доступность элементов.
//
Процедура УстановитьВидимостьДоступностьЭлементов()	
	
	Элементы.Товары.Доступность = ДоступнаТабличнаяЧасть;

КонецПроцедуры // УстановитьВидимостьДоступностьЭлементов()    

#КонецОбласти

#Область ОбработчикиТабличныхЧастей

&НаКлиенте
Процедура ТаблицаДокументовПриАктивизацииСтроки(Элемент)
	Если НЕ Элементы.ТаблицаДокументов.ТекущиеДанные = Неопределено  Тогда	
		ФиксированнаяСтруктура 		= Новый ФиксированнаяСтруктура("ДокументРеализации",Элементы.ТаблицаДокументов.ТекущиеДанные.ДокументРеализации);
		Элементы.Товары.ОтборСтрок 	= ФиксированнаяСтруктура; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ДокументРеализации = Элементы.ТаблицаДокументов.ТекущиеДанные.ДокументРеализации;
	СтруктураВозврата = Новый Структура();
	АдресПодобраннойНоменклатурыВХранилище = ПоместитьПодобраннуюНоменклатуруВХранилище(ДокументРеализации);
	СтруктураВозврата.Вставить("АдресПодобраннойНоменклатурыВХранилище", АдресПодобраннойНоменклатурыВХранилище);	
	СтруктураВозврата.Вставить("ДокументРеализации", 	ДокументРеализации);
	СтруктураВозврата.Вставить("Инициатор", 			Инициатор);
	Оповестить("ПодборИзТабличнойЧастиТоварыДокументаРеализации", СтруктураВозврата);
	Закрыть()

КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтруктураВозврата = Новый Структура();
	АдресПодобраннойНоменклатурыВХранилище = ПоместитьПодобраннуюНоменклатуруВХранилище(Значение);
	СтруктураВозврата.Вставить("АдресПодобраннойНоменклатурыВХранилище", АдресПодобраннойНоменклатурыВХранилище);	
	СтруктураВозврата.Вставить("ДокументРеализации", 	Элементы.ТаблицаДокументов.ТекущиеДанные.ДокументРеализации);
	СтруктураВозврата.Вставить("Инициатор", 			Инициатор);
	Оповестить("ПодборИзТабличнойЧастиТоварыДокументаРеализации", СтруктураВозврата);
	Закрыть()

КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
Функция ПоместитьПодобраннуюНоменклатуруВХранилище(Массив)
		
	ТаблицаВременная = Новый ТаблицаЗначений;
	ТаблицаВременная.Колонки.Добавить("Номенклатура");
	ТаблицаВременная.Колонки.Добавить("Количество");
	ТаблицаВременная.Колонки.Добавить("Цена");
	ТаблицаВременная.Колонки.Добавить("Всего");
	ТаблицаВременная.Колонки.Добавить("Доход");
	ТаблицаВременная.Колонки.Добавить("СкидкаПроцент");
	ТаблицаВременная.Колонки.Добавить("СуммаСкидки");
	ТаблицаВременная.Колонки.Добавить("СчетУчета");
	ТаблицаВременная.Колонки.Добавить("СчетДохода");
	ТаблицаВременная.Колонки.Добавить("СчетСебестоимости");
	ТаблицаВременная.Колонки.Добавить("Итого");
	ТаблицаВременная.Колонки.Добавить("СпособОценки");
	ТаблицаВременная.Колонки.Добавить("СтавкаНДС");
	
	Если ТипЗнч(Массив) = ТИП("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ДокументРеализации = Массив;	
		ПараметрыОтбора = Новый Структура;
	    ПараметрыОтбора.Вставить("ДокументРеализации", ДокументРеализации);
		МассивСтрок = Товары.НайтиСтроки(ПараметрыОтбора);
		Для каждого СтрокаТаблицыЗначений Из МассивСтрок Цикл
			Если СтрокаТаблицыЗначений.ДоступноеКоличество > 0 Тогда
				НоваяСтрока = ТаблицаВременная.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыЗначений);
				НоваяСтрока.Количество = СтрокаТаблицыЗначений.ДоступноеКоличество;
			КонецЕсли;		
		КонецЦикла;		
	Иначе
		Для каждого Эл Из Массив Цикл
			Если Товары[Эл].ДоступноеКоличество > 0 Тогда
				НоваяСтрока = ТаблицаВременная.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Товары[Эл]);
				НоваяСтрока.Количество = Товары[Эл].ДоступноеКоличество;
			КонецЕсли;
		КонецЦикла;		
	КонецЕсли;

	АдресПодобраннойНоменклатурыВХранилище = ПоместитьВоВременноеХранилище(ТаблицаВременная, УникальныйИдентификатор);
	
	Возврат АдресПодобраннойНоменклатурыВХранилище;
	
КонецФункции

&НаСервере
Процедура ПолучитьТаблицыЗначений()
	Документ = РеквизитФормыВЗначение("Объект");
	Запрос = Новый Запрос;
 	Запрос.Текст = Документ.ПолучитьТекстЗапросаПодбора();

	Запрос.УстановитьПараметр("Контрагент", 		Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("ДокументРеализации", ДокументРеализации);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();	
	ТаблицаДокументов.Загрузить(РезультатЗапроса[4].Выгрузить());
	Товары.Загрузить(РезультатЗапроса[5].Выгрузить());
	
КонецПроцедуры // ()

#КонецОбласти