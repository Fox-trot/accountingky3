#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = КонецГода(ТекущаяДатаСеанса());
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры // ПриЧтенииНаСервере()

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Объект.Дата = КонецГода(Объект.Дата);
	Если ЕстьДокументВУказанныйПериод(Объект.Дата) Тогда
		ТекстСообщения = НСтр("ru = 'Для указанного периода уже существует документ ""Отчет по налогу на имущество"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.Дата");
		// Возвращаем значение до изменения
		Объект.Дата	= ДатаДокумента;
	    Возврат;
	КонецЕсли;

	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда		
		Объект.Дата = КонецМесяца(Объект.Дата);		
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;				
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	// Обработка события изменения организации.
	Объект.Номер = "";
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
		
	Если Объект.Недвижимость.Количество() > 0 ИЛИ Объект.Недвижимость.Количество() > 0 Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросЗаполнить", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные табличной части документа будут перезаполнены! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	Иначе
		ЗаполнитьНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросЗаполнить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаСервере();
	КонецЕсли; 
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервере
Функция ЕстьДокументВУказанныйПериод(Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНалогуНаИмущество.Ссылка
		|ИЗ
		|	Документ.ОтчетПоНалогуНаИмущество КАК ОтчетПоНалогуНаИмущество
		|ГДЕ
		|	НЕ ОтчетПоНалогуНаИмущество.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ОтчетПоНалогуНаИмущество.Дата, ГОД) = &Дата";
	
	Запрос.УстановитьПараметр("Дата", Дата);		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 	

КонецФункции 

&НаСервере
Процедура ЗаполнитьНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияПоНалогуНаИмущество.Активность,
		|	СведенияПоНалогуНаИмущество.Организация,
		|	СведенияПоНалогуНаИмущество.ОсновноеСредство,
		|	СведенияПоНалогуНаИмущество.Год,
		|	СведенияПоНалогуНаИмущество.ГруппаИмущества,
		|	СведенияПоНалогуНаИмущество.ДатаВводаВЭксплуатацию,
		|	СведенияПоНалогуНаИмущество.ЖилаяПлощадь,
		|	СведенияПоНалогуНаИмущество.НежилаяПлощадь,
		|	СведенияПоНалогуНаИмущество.КодИмущества,
		|	СведенияПоНалогуНаИмущество.КатегорияОбъектаИмущества,
		|	СведенияПоНалогуНаИмущество.КодНазначенияИмущества,
		|	СведенияПоНалогуНаИмущество.КодПользователяИмущества,
		|	СведенияПоНалогуНаИмущество.КоличествоМесяцев,
		|	СведенияПоНалогуНаИмущество.СуммаНалога,
		|	СведенияПоНалогуНаИмущество.ОценочнаяСтоимость,
		|	СведенияПоНалогуНаИмущество.ОбщаяПлощадь,
		|	СведенияПоНалогуНаИмущество.СтоимостьНУ,
		|	СведенияПоНалогуНаИмущество.МатериалСтен,
		|	СведенияПоНалогуНаИмущество.ЗемНалог,
		|	СведенияПоНалогуНаИмущество.Ставка,
		|	СведенияПоНалогуНаИмущество.Кр,
		|	СведенияПоНалогуНаИмущество.Кз,
		|	СведенияПоНалогуНаИмущество.Ко,
		|	СведенияПоНалогуНаИмущество.АБВ,
		|	СведенияПоНалогуНаИмущество.ОбъемДвигателя,
		|	СведенияПоНалогуНаИмущество.ВидТранспорта,
		|	СведенияПоНалогуНаИмущество.ОтЛет,
		|	СведенияПоНалогуНаИмущество.ДоЛет
		|ПОМЕСТИТЬ ВременнаяТаблицаЗаГод
		|ИЗ
		|	РегистрСведений.СведенияПоНалогуНаИмущество КАК СведенияПоНалогуНаИмущество
		|ГДЕ
		|	СведенияПоНалогуНаИмущество.Год = &Год
		|	И СведенияПоНалогуНаИмущество.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗаГод.Организация КАК Организация,
		|	ВременнаяТаблицаЗаГод.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаЗаГод.Год КАК Год,
		|	ВременнаяТаблицаЗаГод.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаЗаГод.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаЗаГод.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ВременнаяТаблицаЗаГод.НежилаяПлощадь КАК НежилаяПлощадь,
		|	ВременнаяТаблицаЗаГод.КодИмущества КАК КодИмущества,
		|	ВременнаяТаблицаЗаГод.КатегорияОбъектаИмущества КАК КатегорияОбъектаИмущества,
		|	ВременнаяТаблицаЗаГод.КодНазначенияИмущества КАК КодНазначенияИмущества,
		|	ВременнаяТаблицаЗаГод.КодПользователяИмущества КАК КодПользователяИмущества,
		|	ВременнаяТаблицаЗаГод.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ВременнаяТаблицаЗаГод.СуммаНалога КАК СуммаНалога,
		|	ВременнаяТаблицаЗаГод.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаЗаГод.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаЗаГод.СтоимостьНУ КАК СтоимостьНУ,
		|	ВременнаяТаблицаЗаГод.МатериалСтен КАК МатериалСтен,
		|	ВременнаяТаблицаЗаГод.ЗемНалог КАК ЗемНалог,
		|	ВременнаяТаблицаЗаГод.Ставка КАК Ставка,
		|	ВременнаяТаблицаЗаГод.Кр КАК Кр,
		|	ВременнаяТаблицаЗаГод.Кз КАК Кз,
		|	ВременнаяТаблицаЗаГод.Ко КАК Ко,
		|	ВременнаяТаблицаЗаГод.ОтЛет,
		|	ВременнаяТаблицаЗаГод.ДоЛет
		|ИЗ
		|	ВременнаяТаблицаЗаГод КАК ВременнаяТаблицаЗаГод
		|ГДЕ
		|	ВременнаяТаблицаЗаГод.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ2)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗаГод.Организация,
		|	ВременнаяТаблицаЗаГод.ОсновноеСредство,
		|	ВременнаяТаблицаЗаГод.Год,
		|	ВременнаяТаблицаЗаГод.ГруппаИмущества,
		|	ВременнаяТаблицаЗаГод.ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаЗаГод.ЖилаяПлощадь,
		|	ВременнаяТаблицаЗаГод.НежилаяПлощадь,
		|	ВременнаяТаблицаЗаГод.КодИмущества,
		|	ВременнаяТаблицаЗаГод.КатегорияОбъектаИмущества,
		|	ВременнаяТаблицаЗаГод.КодНазначенияИмущества,
		|	ВременнаяТаблицаЗаГод.КодПользователяИмущества,
		|	ВременнаяТаблицаЗаГод.КоличествоМесяцев,
		|	ВременнаяТаблицаЗаГод.СуммаНалога,
		|	ВременнаяТаблицаЗаГод.ОценочнаяСтоимость,
		|	ВременнаяТаблицаЗаГод.ОбщаяПлощадь,
		|	ВременнаяТаблицаЗаГод.СтоимостьНУ,
		|	ВременнаяТаблицаЗаГод.МатериалСтен,
		|	ВременнаяТаблицаЗаГод.ЗемНалог,
		|	ВременнаяТаблицаЗаГод.Ставка,
		|	ВременнаяТаблицаЗаГод.Кр,
		|	ВременнаяТаблицаЗаГод.Кз,
		|	ВременнаяТаблицаЗаГод.Ко,
		|	NULL,
		|	NULL
		|ИЗ
		|	ВременнаяТаблицаЗаГод КАК ВременнаяТаблицаЗаГод
		|ГДЕ
		|	ВременнаяТаблицаЗаГод.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ3)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗаГод.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаЗаГод.ГруппаИмущества,
		|	ВременнаяТаблицаЗаГод.ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаЗаГод.КоличествоМесяцев,
		|	ВременнаяТаблицаЗаГод.СуммаНалога,
		|	ВременнаяТаблицаЗаГод.Ставка,
		|	ВременнаяТаблицаЗаГод.АБВ,
		|	ВременнаяТаблицаЗаГод.ОбъемДвигателя,
		|	ВременнаяТаблицаЗаГод.ВидТранспорта,
		|	ВременнаяТаблицаЗаГод.ОтЛет,
		|	ВременнаяТаблицаЗаГод.ДоЛет
		|ИЗ
		|	ВременнаяТаблицаЗаГод КАК ВременнаяТаблицаЗаГод
		|ГДЕ
		|	ВременнаяТаблицаЗаГод.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ4)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновноеСредство";
	
	Запрос.УстановитьПараметр("Год", 			ДатаДокумента);
	Запрос.УстановитьПараметр("Организация", 	Объект.Организация);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Объект.Недвижимость.Загрузить(РезультатЗапроса[1].Выгрузить());
	Объект.Транспорт.Загрузить(РезультатЗапроса[2].Выгрузить());	

КонецПроцедуры // ЗаполнитьНаСервере()

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

