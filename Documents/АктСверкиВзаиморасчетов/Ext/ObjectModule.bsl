#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если СверкаПоСоцФонду Тогда  
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");		
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДаннымБухгалтерскогоУчета() Экспорт
	
	Если СверкаПоСоцФонду Тогда 
		ЗапросТекст = 
			"ВЫБРАТЬ
			|	ХозрасчетныйОборотыДтКт.Регистратор КАК ДокументСверки,
			|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК СуммаДт,
			|	0 КАК СуммаКт
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СписокСчетов), , , , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ХозрасчетныйОборотыДтКт.Регистратор,
			|	0,
			|	ХозрасчетныйОборотыДтКт.СуммаОборот
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, , , СчетКт В (&СписокСчетов), , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт";
		// Временно.
		// Нужно переделать запросы ниже под общий формат.
		Запрос = Новый Запрос;
		Запрос.Текст = ЗапросТекст;
		Запрос.УстановитьПараметр("Организация", 			Организация);
		Запрос.УстановитьПараметр("НачалоПериода", 			НачалоДня(ДатаНачала));
		Запрос.УстановитьПараметр("КонецПериода", 			КонецДня(ДатаОкончания));
		Запрос.УстановитьПараметр("СписокСчетов", 			СписокСчетов.Выгрузить().ВыгрузитьКолонку("СчетУчета"));
		Запрос.УстановитьПараметр("УчитыватьВзаимозачеты", 	УчитыватьВзаимозачеты);

		ПоДаннымОрганизации.Загрузить(Запрос.Выполнить().Выгрузить());
		
		Возврат;
		
	Иначе
		// Список контрагентов.
		СписокКонтрагентов = Новый СписокЗначений;
		СписокКонтрагентов.Добавить(Контрагент);
		Если ПоГоловномуКонтрагенту Тогда 
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Контрагенты.Ссылка КАК Контрагент
				|ИЗ
				|	Справочник.Контрагенты КАК Контрагенты
				|ГДЕ
				|	Контрагенты.ГоловнойКонтрагент = &Контрагент
				|	И НЕ Контрагенты.ПометкаУдаления";
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СписокКонтрагентов.Добавить(ВыборкаДетальныеЗаписи.Контрагент);	
			КонецЦикла;
		КонецЕсли;	
		
		ЗапросТекст =
			"ВЫБРАТЬ
			|	ХозрасчетныйОборотыДтКт.ПериодСекунда КАК Дата,
			|	ХозрасчетныйОборотыДтКт.Регистратор КАК Документ,
			|	ХозрасчетныйОборотыДтКт.СубконтоДт2 КАК Договор,
			|	СУММА(ХозрасчетныйОборотыДтКт.ВалютнаяСуммаОборотДт) КАК Дебет,
			|	СУММА(0) КАК Кредит,
			|	1 КАК Группа
			|ПОМЕСТИТЬ ВременнаяТаблицаДанные
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			Авто,
			|			СчетДт В (&СписокСчетов),
			|			,
			|			,
			|			,
			|			Организация = &Организация
			|				И ВалютаДт = &Валюта
			|				И СубконтоДт1 В (&СписокКонтрагентов)
			|				И СубконтоДт2 = &Договор) КАК ХозрасчетныйОборотыДтКт
			|ГДЕ
			|	ВЫБОР
			|		КОГДА &УчитыватьВзаимозачеты
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ НЕ (ХозрасчетныйОборотыДтКт.СчетДт В (&СписокСчетов) 
			|			И ХозрасчетныйОборотыДтКт.СчетКт В (&СписокСчетов)
			|			И ХозрасчетныйОборотыДтКт.СубконтоДт1 = ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СубконтоКт1, НЕОПРЕДЕЛЕНО)
			|			И ХозрасчетныйОборотыДтКт.СубконтоДт2 = ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СубконтоКт2, НЕОПРЕДЕЛЕНО))
			|	КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОборотыДтКт.Регистратор,
			|	ХозрасчетныйОборотыДтКт.ПериодСекунда,
			|	ХозрасчетныйОборотыДтКт.СубконтоДт2
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ХозрасчетныйОборотыДтКт.ПериодСекунда,
			|	ХозрасчетныйОборотыДтКт.Регистратор,
			|	ХозрасчетныйОборотыДтКт.СубконтоКт2,
			|	СУММА(0),
			|	СУММА(ХозрасчетныйОборотыДтКт.ВалютнаяСуммаОборотКт),
			|	2
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			Авто,
			|			,
			|			,
			|			СчетКт В (&СписокСчетов),
			|			,
			|			Организация = &Организация
			|				И ВалютаКт = &Валюта
			|				И СубконтоКт1 В (&СписокКонтрагентов)
			|				И СубконтоКт2 = &Договор) КАК ХозрасчетныйОборотыДтКт
			|ГДЕ
			|	ВЫБОР
			|		КОГДА &УчитыватьВзаимозачеты
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ НЕ (ХозрасчетныйОборотыДтКт.СчетДт В (&СписокСчетов) 
			|			И ХозрасчетныйОборотыДтКт.СчетКт В (&СписокСчетов)
			|			И ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СубконтоДт1, НЕОПРЕДЕЛЕНО) = ХозрасчетныйОборотыДтКт.СубконтоКт1
			|			И ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СубконтоДт2, НЕОПРЕДЕЛЕНО) = ХозрасчетныйОборотыДтКт.СубконтоКт2)
			|	КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОборотыДтКт.ПериодСекунда,
			|	ХозрасчетныйОборотыДтКт.Регистратор,
			|	ХозрасчетныйОборотыДтКт.СубконтоКт2
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВременнаяТаблицаДанные.Дата КАК Дата,
			|	ВременнаяТаблицаДанные.Документ КАК Документ,
			|	ВременнаяТаблицаДанные.Договор КАК Договор,
			|	СУММА(ВременнаяТаблицаДанные.Дебет) КАК Дебет,
			|	СУММА(ВременнаяТаблицаДанные.Кредит) КАК Кредит,
			|	ВременнаяТаблицаДанные.Группа КАК Группа
			|ИЗ
			|	ВременнаяТаблицаДанные КАК ВременнаяТаблицаДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ВременнаяТаблицаДанные.Дата,
			|	ВременнаяТаблицаДанные.Документ,
			|	ВременнаяТаблицаДанные.Группа,
			|	ВременнаяТаблицаДанные.Договор
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Документ,
			|	Группа";			
		
		Если ДоговорКонтрагента.Пустая() Тогда
			ЗапросТекст = СтрЗаменить(ЗапросТекст, "И СубконтоДт2 = &Договор", "");
			ЗапросТекст = СтрЗаменить(ЗапросТекст, "И СубконтоКт2 = &Договор", "");
		КонецЕсли;
		
		Если ВалютаСверки.Пустая() Тогда
			ЗапросТекст = СтрЗаменить(ЗапросТекст, "И ВалютаКт = &Валюта", "");
			ЗапросТекст = СтрЗаменить(ЗапросТекст, "И ВалютаДт = &Валюта", "");
		КонецЕсли;		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ЗапросТекст;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокКонтрагентов", СписокКонтрагентов);
	Запрос.УстановитьПараметр("Договор", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Валюта", ВалютаСверки);
	Запрос.УстановитьПараметр("СписокСчетов", СписокСчетов.Выгрузить().ВыгрузитьКолонку("СчетУчета"));
	Запрос.УстановитьПараметр("УчитыватьВзаимозачеты", УчитыватьВзаимозачеты);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Если СверкаПоСоцФонду Тогда 
			ТекстСообщения = НСтр("ru = 'Для введенных данных за указанный период нет никаких операций.'");
		Иначе 
			ТекстСообщения = НСтр("ru = 'Проверьте правильность заполнения контрагента и договора, для введенных данных за указанный период нет никаких операций.'");
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		Выборка = РезультатЗапроса.Выбрать();		
	
		Пока Выборка.Следующий() Цикл 
			СТЧ = ПоДаннымОрганизации.Добавить();
			СТЧ.ДокументСверки 	= Выборка.Документ;
			СТЧ.Договор 		= Выборка.Договор;
			
			Если Выборка.Дебет-Выборка.Кредит > 0 Тогда 
				СТЧ.СуммаДт = Выборка.Дебет-Выборка.Кредит;
			Иначе 
				СТЧ.СуммаКт = Выборка.Кредит-Выборка.Дебет;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли