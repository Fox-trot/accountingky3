#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымБухгалтерскогоУчета() Экспорт
	Отказ = Ложь;
	ФильтрСписокСчетов = Новый массив();
	Для каждого СтрокаСчета Из СписокСчетов Цикл
		Если Не ЗначениеЗаполнено(СтрокаСчета.СчетУчета) 
			или СтрокаСчета.УчаствуетВРасчетах = Ложь Тогда
			Продолжить;
		Иначе
			ФильтрСписокСчетов.Добавить(СтрокаСчета.СчетУчета);
		КонецЕсли; 
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	
	Если СверкаПоСоцФонду Тогда 
		ЗапросТекст = 
			"ВЫБРАТЬ
			|	СУММА(ОстОб.СуммаНачальныйОстаток) КАК НачальныйОстаток,
			|	СУММА(ОстОб.СуммаКонечныйОстаток) КАК КонечныйОстаток
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			,
			|			,
			|			Счет В (&СписокСчетов),
			|			,
			|			Организация = &Организация
			|				И Валюта = &Валюта) КАК ОстОб
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда КАК Дата,
			|	ХозрасчетныйОстаткиИОбороты.Регистратор КАК Документ,
			|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаОборотДт) КАК Дебет,
			|	СУММА(0) КАК Кредит,
			|	1 КАК Группа
			|ПОМЕСТИТЬ ВременнаяТаблицаДанные
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			Авто,
			|			,
			|			Счет В (&СписокСчетов),
			|			,
			|			Организация = &Организация
			|				И Валюта = &Валюта) КАК ХозрасчетныйОстаткиИОбороты
			|ГДЕ
			|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт <> 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда,
			|	ХозрасчетныйОстаткиИОбороты.Регистратор
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда,
			|	ХозрасчетныйОстаткиИОбороты.Регистратор,
			|	СУММА(0),
			|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт),
			|	2
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			Авто,
			|			,
			|			Счет В (&СписокСчетов),
			|			,
			|			Организация = &Организация
			|				И Валюта = &Валюта) КАК ХозрасчетныйОстаткиИОбороты
			|ГДЕ
			|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт <> 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОстаткиИОбороты.Регистратор,
			|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВременнаяТаблицаДанные.Дата КАК Дата,
			|	ВременнаяТаблицаДанные.Документ КАК Документ,
			|	СУММА(ВременнаяТаблицаДанные.Дебет) КАК Дебет,
			|	СУММА(ВременнаяТаблицаДанные.Кредит) КАК Кредит,
			|	ВременнаяТаблицаДанные.Группа КАК Группа
			|ИЗ
			|	ВременнаяТаблицаДанные КАК ВременнаяТаблицаДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ВременнаяТаблицаДанные.Документ,
			|	ВременнаяТаблицаДанные.Дата,
			|	ВременнаяТаблицаДанные.Группа
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Документ,
			|	Группа";

	Иначе 

		АналитикаРасчетов = новый Массив();
		АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
		АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
		ЗапросТекст = 
		"ВЫБРАТЬ
		|	СУММА(ОстОб.ВалютнаяСуммаНачальныйОстаток) КАК НачальныйОстаток,
		|	СУММА(ОстОб.ВалютнаяСуммаКонечныйОстаток) КАК КонечныйОстаток
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			,
		|			,
		|			Счет В (&СписокСчетов),
		|			&ВидыСубконто,
		|			Организация = &Организация
		|				И Валюта = &Валюта
		|				И Субконто1 = &Контрагент
		|				И Субконто2 = &Договор) КАК ОстОб
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда КАК Дата,
		|	ХозрасчетныйОстаткиИОбороты.Регистратор КАК Документ,
		|	СУММА(ХозрасчетныйОстаткиИОбороты.ВалютнаяСуммаОборотДт) КАК Дебет,
		|	СУММА(0) КАК Кредит,
		|	1 КАК Группа
		|ПОМЕСТИТЬ ВременнаяТаблицаДанные
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			Авто,
		|			,
		|			,
		|			&ВидыСубконто,
		|			Организация = &Организация
		|				И Валюта = &Валюта
		|				И Субконто1 = &Контрагент
		|				И Субконто2 = &Договор) КАК ХозрасчетныйОстаткиИОбороты
		|ГДЕ
		|	ХозрасчетныйОстаткиИОбороты.ВалютнаяСуммаОборотДт <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстаткиИОбороты.Регистратор,
		|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда,
		|	ХозрасчетныйОстаткиИОбороты.Регистратор,
		|	СУММА(0),
		|	СУММА(ХозрасчетныйОстаткиИОбороты.ВалютнаяСуммаОборотКт),
		|	2
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			Авто,
		|			,
		|			,
		|			&ВидыСубконто,
		|			Организация = &Организация
		|				И Валюта = &Валюта
		|				И Субконто1 = &Контрагент
		|				И Субконто2 = &Договор) КАК ХозрасчетныйОстаткиИОбороты
		|ГДЕ
		|	ХозрасчетныйОстаткиИОбороты.ВалютнаяСуммаОборотКт <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстаткиИОбороты.Регистратор,
		|	ХозрасчетныйОстаткиИОбороты.ПериодСекунда
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДанные.Дата КАК Дата,
		|	ВременнаяТаблицаДанные.Документ КАК Документ,
		|	СУММА(ВременнаяТаблицаДанные.Дебет) КАК Дебет,
		|	СУММА(ВременнаяТаблицаДанные.Кредит) КАК Кредит,
		|	ВременнаяТаблицаДанные.Группа КАК Группа
		|ИЗ
		|	ВременнаяТаблицаДанные КАК ВременнаяТаблицаДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДанные.Дата,
		|	ВременнаяТаблицаДанные.Документ,
		|	ВременнаяТаблицаДанные.Группа
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Документ,
		|	Группа";
			
		Если ДоговорКонтрагента.Пустая() Тогда
			ЗапросТекст = СтрЗаменить(ЗапросТекст, "И Субконто2 = &Договор", "");
		КонецЕсли;
		
		Если ВалютаСверки.Пустая() Тогда
			ЗапросТекст = СтрЗаменить(ЗапросТекст, "И Валюта = &Валюта", "");		
		КонецЕсли;	
		
	КонецЕсли;
		Запрос.Текст = ЗапросТекст;
		Запрос.УстановитьПараметр("Организация", 	Организация);
		Запрос.УстановитьПараметр("Контрагент", 	Контрагент);
		Запрос.УстановитьПараметр("Договор", 		ДоговорКонтрагента);
		Запрос.УстановитьПараметр("ДатаНачала", 	НачалоДня(ДатаНачала));
		Запрос.УстановитьПараметр("ДатаОкончания", 	КонецДня(ДатаОкончания));
		Запрос.УстановитьПараметр("СписокСчетов",	ФильтрСписокСчетов);
		Запрос.УстановитьПараметр("ВидыСубконто",   АналитикаРасчетов);
		Запрос.УстановитьПараметр("Валюта",   		ВалютаСверки);
		
		Результат = Запрос.ВыполнитьПакет();
		
		ВыборкаОст = Результат[0].Выбрать();
		Пока ВыборкаОст.Следующий() Цикл 
			ОстатокНаНачало = ВыборкаОст.НачальныйОстаток;
			ОстатокНаКонец	= ВыборкаОст.КонечныйОстаток;
		КонецЦикла;
		
		ВыборкаОб = Результат[2].Выбрать();	
		Пока ВыборкаОб.Следующий() Цикл 
			СТЧ = ПоДаннымОрганизации.Добавить();
			СТЧ.ДокументСверки = ВыборкаОб.Документ;
			
			Если ВыборкаОб.Дебет-ВыборкаОб.Кредит > 0 Тогда 
				СТЧ.СуммаДт = ВыборкаОб.Дебет-ВыборкаОб.Кредит;
			Иначе 
				СТЧ.СуммаКт = ВыборкаОб.Кредит-ВыборкаОб.Дебет;
			КонецЕсли;
			
		КонецЦикла;
		Если Результат[2].Пустой() Тогда 
			ТекстСообщения = НСтр("ru = 'Проверьте правильность заполнения контрагента и договора, для введенных данных за указанный период нет никаких операций.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
		
		ПоДаннымОрганизации.Сортировать("ДокументСверки");

КонецПроцедуры

#КонецОбласти

#КонецЕсли