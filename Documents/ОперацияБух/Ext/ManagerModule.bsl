#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует табличный документ с печатной формой БухгалтерскаяСправка
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьБухгалтерскаяСправка(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб				= Истина;
	ТабДокумент.ПолеСверху				= 10;
	ТабДокумент.ПолеСлева				= 0;
	ТабДокумент.ПолеСнизу				= 0;
	ТабДокумент.ПолеСправа				= 0;
	ТабДокумент.РазмерКолонтитулаСверху	= 10;
	ТабДокумент.ОриентацияСтраницы		= ОриентацияСтраницы.Ландшафт;
	ТабДокумент.КлючПараметровПечати	= "ОперацияБух_БухгалтерскаяСправка";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТиповойДвиженияССубконто.НомерСтроки КАК НомерСтроки,
		|	ТиповойДвиженияССубконто.СчетДт КАК СчетДт,
		|	ТиповойДвиженияССубконто.СубконтоДт1 КАК СубконтоДт1,
		|	ТиповойДвиженияССубконто.СубконтоДт2 КАК СубконтоДт2,
		|	ТиповойДвиженияССубконто.СубконтоДт3 КАК СубконтоДт3,
		|	ТиповойДвиженияССубконто.СчетКт КАК СчетКт,
		|	ТиповойДвиженияССубконто.СубконтоКт1 КАК СубконтоКт1,
		|	ТиповойДвиженияССубконто.СубконтоКт2 КАК СубконтоКт2,
		|	ТиповойДвиженияССубконто.СубконтоКт3 КАК СубконтоКт3,
		|	ТиповойДвиженияССубконто.ВалютаДт КАК ВалютаДт,
		|	ТиповойДвиженияССубконто.ВалютаКт КАК ВалютаКт,
		|	ТиповойДвиженияССубконто.Сумма КАК Сумма,
		|	ТиповойДвиженияССубконто.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
		|	ТиповойДвиженияССубконто.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
		|	ТиповойДвиженияССубконто.КоличествоДт КАК КоличествоДт,
		|	ТиповойДвиженияССубконто.КоличествоКт КАК КоличествоКт,
		|	ТиповойДвиженияССубконто.Содержание КАК Содержание,
		|	ТиповойДвиженияССубконто.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВременнаяТаблицаТиповой
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор В (&МассивОбъектов), , ) КАК ТиповойДвиженияССубконто
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОперацияБух.Ссылка КАК Ссылка,
		|	ОперацияБух.Номер КАК Номер,
		|	ОперацияБух.Дата КАК Дата,
		|	ОперацияБух.Содержание КАК СодержаниеОперации,
		|	ВременнаяТаблицаТиповой.НомерСтроки КАК НомерСтроки,
		|	ВременнаяТаблицаТиповой.СчетДт КАК СчетДт,
		|	ВременнаяТаблицаТиповой.СубконтоДт1 КАК СубконтоДт1,
		|	ВременнаяТаблицаТиповой.СубконтоДт2 КАК СубконтоДт2,
		|	ВременнаяТаблицаТиповой.СубконтоДт3 КАК СубконтоДт3,
		|	ВременнаяТаблицаТиповой.СчетКт КАК СчетКт,
		|	ВременнаяТаблицаТиповой.СубконтоКт1 КАК СубконтоКт1,
		|	ВременнаяТаблицаТиповой.СубконтоКт2 КАК СубконтоКт2,
		|	ВременнаяТаблицаТиповой.СубконтоКт3 КАК СубконтоКт3,
		|	ОперацияБух.Организация КАК Организация,
		|	ВременнаяТаблицаТиповой.ВалютаДт КАК ВалютаДт,
		|	ВременнаяТаблицаТиповой.ВалютаКт КАК ВалютаКт,
		|	ЕСТЬNULL(ВременнаяТаблицаТиповой.Сумма, 0) КАК Сумма,
		|	ВременнаяТаблицаТиповой.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаТиповой.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
		|	ВременнаяТаблицаТиповой.КоличествоДт КАК КоличествоДт,
		|	ВременнаяТаблицаТиповой.КоличествоКт КАК КоличествоКт,
		|	ВременнаяТаблицаТиповой.Содержание КАК Содержание
		|ИЗ
		|	Документ.ОперацияБух КАК ОперацияБух
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаТиповой КАК ВременнаяТаблицаТиповой
		|		ПО ОперацияБух.Ссылка = ВременнаяТаблицаТиповой.Регистратор
		|ГДЕ
		|	ОперацияБух.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОперацияБух.Дата,
		|	ОперацияБух.Ссылка,
		|	НомерСтроки";
	ВыборкаДвижений = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;

	Пока ВыборкаДвижений.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ЕстьОшибки = Ложь;	
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		// Зададим параметры макета по умолчанию
		ТабДокумент.РазмерКолонтитулаСверху = 0;
		ТабДокумент.РазмерКолонтитулаСнизу  = 0;
		ТабДокумент.АвтоМасштаб             = Истина;
		ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОперацияБух.ПФ_MXL_БухгалтерскаяСправка");
		// Получаем области макета для вывода в табличный документ.
		ШапкаДокумента   = Макет.ПолучитьОбласть("Шапка");
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		СтрокаТаблицы    = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ПодвалТаблицы    = Макет.ПолучитьОбласть("ПодвалТаблицы");
		ПодвалДокумента  = Макет.ПолучитьОбласть("Подвал");
		
		// Выведем шапку документа.
		СведенияОбОрганизации = БухгалтерскийУчетСервер.ПолучитьСведенияОбОрганизации(ВыборкаДвижений.Организация, ВыборкаДвижений.Дата);
		
		ШапкаДокумента.Параметры.Организация    = СведенияОбОрганизации.НаименованиеПолное;
		ШапкаДокумента.Параметры.НомерДокумента = ВыборкаДвижений.Номер;
		ШапкаДокумента.Параметры.ДатаДокумента  = Формат(ВыборкаДвижений.Дата, "ДЛФ=D");
		ШапкаДокумента.Параметры.Содержание     = ВыборкаДвижений.СодержаниеОперации;
		
		ТабДокумент.Вывести(ШапкаДокумента);
		
		// Выведем заголовок таблицы.
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		СуммаПоДокументу  = 0;
		КоличествоСтрокВОперации = 0;

		// Выведем строки документа.
		Пока ВыборкаДвижений.Следующий() Цикл
			
			СтрокаТаблицы.Параметры.Заполнить(ВыборкаДвижений);
			
			АналитикаДт = Строка(ВыборкаДвижений.СубконтоДт1) + Символы.ПС
			+ Строка(ВыборкаДвижений.СубконтоДт2) + Символы.ПС
			+ Строка(ВыборкаДвижений.СубконтоДт3);
			
			АналитикаКт = Строка(ВыборкаДвижений.СубконтоКт1) + Символы.ПС
			+ Строка(ВыборкаДвижений.СубконтоКт2) + Символы.ПС
			+ Строка(ВыборкаДвижений.СубконтоКт3);
			
			СтрокаТаблицы.Параметры.АналитикаДт = АналитикаДт;
			СтрокаТаблицы.Параметры.АналитикаКт = АналитикаКт;
			
			СуммаПоДокументу = СуммаПоДокументу + ВыборкаДвижений.Сумма;
			КоличествоСтрокВОперации = КоличествоСтрокВОперации + 1;			
			
			//// Проверим, помещается ли строка с подвалом.
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(СтрокаТаблицы);
			СтрокаСПодвалом.Добавить(ПодвалТаблицы);
			СтрокаСПодвалом.Добавить(ПодвалДокумента);
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда
				ТабДокумент.Вывести(ПодвалТаблицы);
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;	
			
		ТабДокумент.Вывести(СтрокаТаблицы);
			
		КонецЦикла;
		
		ПодвалТаблицы.Параметры.ИтоговаяСтрока = СтрШаблон(НСтр("ru = 'Всего корреспонденций: %1, на сумму %2'"), 
													КоличествоСтрокВОперации, 
													БухгалтерскийУчетСервер.СформироватьСуммуПрописью(СуммаПоДокументу));
		
		// Выведем подвал таблицы.
		ТабДокумент.Вывести(ПодвалТаблицы);
		
		ТабДокумент.Вывести(ПодвалДокумента);
		
	КонецЦикла;

	Возврат ТабДокумент;

КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Печать бух. справки
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "БухгалтерскаяСправка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"БухгалтерскаяСправка",
			НСтр("ru = 'Бухгалтерская справка'"),
			ПечатьБухгалтерскаяСправка(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ОперацияБух.ПФ_MXL_БухгалтерскаяСправка");
	КонецЕсли;

	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Бухгалтерская справка
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "БухгалтерскаяСправка";
	КомандаПечати.Представление = НСтр("ru = 'Бухгалтерская справка'");
	КомандаПечати.Порядок = 50;
	
	// Настраиваемый комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "БухгалтерскаяСправка";
	КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Настраиваемый комплект'");
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;
	КомандаПечати.Порядок = 75;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли