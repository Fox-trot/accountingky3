#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ТаблицаДокумента.Период,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ТаблицаДокумента.СчетКт,
		|	ТаблицаДокумента.СубконтоДт1,
		|	ТаблицаДокумента.СубконтоДт2,
		|	ТаблицаДокумента.СубконтоДт3,
		|	ТаблицаДокумента.СубконтоКт1,
		|	ТаблицаДокумента.СубконтоКт2,
		|	ТаблицаДокумента.СубконтоКт3,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютнаяСуммаДт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютнаяСуммаКт,
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.Содержание,
		|	ТаблицаДокумента.КоличествоДт,
		|	ТаблицаДокумента.КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаХозрасчетный КАК ТаблицаДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ТаблицаДокумента.БанковскийСчетКасса,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
		|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
		|			ТОГДА ТаблицаДокумента.СуммаВал
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	""Ввод остатков по денежным средствам"",
		|	NULL,
		|	NULL
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаДенежныеСредства КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ВалютнаяСумма,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	""Ввод остатков (прочее)"",
		|	ТаблицаДокумента.Количество,
		|	0
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПрочее КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.ВидСальдо = ""СальдоДт""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ТаблицаДокумента.СчетУчета,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ВалютнаяСумма,
		|	ТаблицаДокумента.Сумма,
		|	""Ввод остатков (прочее)"",
		|	0,
		|	ТаблицаДокумента.Количество
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПрочее КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.ВидСальдо = ""СальдоКт""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.Сумма,
		|	""Ввод остатков по начислению заработной платы"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРасчетыСПерсоналом КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);		
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
КонецПроцедуры

Процедура СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Период КАК ДатаВводаВЭксплуатацию,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ОсновноеСредство,
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.ГруппаНУ,
	|	ТаблицаДокумента.СпособНачисленияАмортизации,
	|	ТаблицаДокумента.СрокСлужбы КАК СрокСлужбы,
	|	ТаблицаДокумента.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаДокумента.КоэффициентАмортизации,
	|	ТаблицаДокумента.КоэффициентУскорения,
	|	ТаблицаДокумента.ЛиквидационнаяСтоимость,
	|	ТаблицаДокумента.ИнвентарныйНомер,
	|	ТаблицаДокумента.СпособПоступления,
	|	ТаблицаДокумента.ПервоначальнаяСтоимость,
	|	ТаблицаДокумента.ПараметрВыработки,
	|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации,
	|	ТаблицаДокумента.НормаАмортизации
	|ИЗ
	|	ВременнаяТаблицаПараметрыУчетаОС КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПараметрыУчетаОС", РезультатЗапроса.Выгрузить()); 
	
КонецПроцедуры

Процедура СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ОсновноеСредство,
	|	ТаблицаДокумента.Состояние,
	|	ТаблицаДокумента.ДатаСписания
	|ИЗ
	|	ВременнаяТаблицаСостоянияОС КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСостоянияОС", РезультатЗапроса.Выгрузить()); 
	
КонецПроцедуры

Процедура СформироватьТаблицаМестонахождениеОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ОсновноеСредство,
	|	ТаблицаДокумента.Склад,	
	|	ТаблицаДокумента.МОЛ,
	|	ТаблицаДокумента.Местонахождение КАК Подразделение
	|ИЗ
	|	ВременнаяТаблицаМестонахождениеОС КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМестонахождениеОС", РезультатЗапроса.Выгрузить()); 
	
КонецПроцедуры

Процедура СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ОсновноеСредство,
	|	ТаблицаДокумента.Событие,
	|	ТаблицаДокумента.НомерДокумента,
	|	ТаблицаДокумента.НазваниеДокумента
	|ИЗ
	|	ВременнаяТаблицаСобытияОС КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСобытияОС", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаСоставОС(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Активность,
	|	ТаблицаДокумента.ОсновноеСредство,
	|	ТаблицаДокумента.ВСоставеОС,
	|	ТаблицаДокумента.СостояниеВСоставеОС
	|ИЗ
	|	ВременнаяТаблицаСоставОС КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСоставОС", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Год,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ГруппаНУ,
	|	ТаблицаДокумента.ОсновноеСредство,
	|	ТаблицаДокумента.БалансоваяСтоимостьНаКонецГода,
	|	ТаблицаДокумента.АмортизацияЗаГод,
	|	ТаблицаДокумента.СуммаПоступленияОстаток,
	|	ТаблицаДокумента.СуммаВыбытияОстаток
	|ИЗ
	|	ВременнаяТаблицаНалоговаяАмортизация КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяАмортизация", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаНалоговыеДекларации(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Убыток,
	|	ТаблицаДокумента.УбытокОтРеализацииЦенныхБумаг,
	|	ТаблицаДокумента.ПокрытыйУбытокПрошлыхЛет,
	|	ТаблицаДокумента.Прибыль
	|ИЗ
	|	ВременнаяТаблицаНалоговыеДекларации КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговыеДекларации", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры      

Процедура СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	СпособОценки = СтруктураДополнительныеСвойства.ДляПроведения.СпособОценки;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Склад,
	|	ВЫБОР
	|		КОГДА &СпособОценки = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|				ИЛИ НЕ ВременнаяТаблицаШапка.СчетУчета.Количественный
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаДокумента.Ссылка
	|	КОНЕЦ КАК Партия,
	|	ТаблицаДокумента.Номенклатура,
	|	ВременнаяТаблицаШапка.СчетУчета,
	|	ТаблицаДокумента.Цена,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.СчетУчета.Количественный
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ТаблицаДокумента.Сумма
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТМЗСклад КАК ТаблицаДокумента
	|		ПО (ИСТИНА)";
	
	Запрос.Параметры.Вставить("СпособОценки", СпособОценки);	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.ИнвНомер,
	|	ТаблицаДокумента.Количество,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.Партия,
	|	ТаблицаДокумента.Статус,
	|	ТаблицаДокумента.Склад
	|ИЗ
	|	ВременнаяТаблицаТМЗСклад КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМБПСклад", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаМБПЭксплуатация(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.ИнвНомер,
	|	ТаблицаДокумента.Количество,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.Партия,
	|	ТаблицаДокумента.МОЛ,
	|	ТаблицаДокумента.ДатаВыдачи
	|ИЗ
	|	ВременнаяТаблицаТМЗСклад КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники.СрезПоследних(&Дата, ) КАК СотрудникиСрезПоследних
	|		ПО ТаблицаДокумента.МОЛ = СотрудникиСрезПоследних.ФизЛицо";
	
	Запрос.Параметры.Вставить("Дата", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМБПЭксплуатация", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВзаиморасчетыССотрудниками(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаШапка.Дата КАК Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ТаблицаДокумента.ФизЛицо,
	|	ТаблицаДокумента.Сумма
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРасчетыСПерсоналом КАК ТаблицаДокумента
	|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзаиморасчетыССотрудниками", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСотрудники()

Процедура СформироватьТаблицаСведенияОбИмуществе(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ГруппаИмущества,
		|	ТаблицаДокумента.ДатаВводаВЭксплуатацию,
		|	ТаблицаДокумента.КодИмущества,
		|	ТаблицаДокумента.МатериалСтен,
		|	ТаблицаДокумента.ЖилаяПлощадь,
		|	ТаблицаДокумента.НежилаяПлощадь,
		|	ТаблицаДокумента.РегиональныйКоэффициент,
		|	ТаблицаДокумента.ЗональныйКоэффициент,
		|	ТаблицаДокумента.ОтраслевойКоэффициент,
		|	ТаблицаДокумента.КатегорияИмущества КАК КатегорияОбъектаИмущества,
		|	ТаблицаДокумента.КодНазначенияИмущества,
		|	ТаблицаДокумента.КодПользователяИмущества
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ1)
		|			ИЛИ ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ2)
		|			ИЛИ ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ3))";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОбИмуществе", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСведенияОбИмуществе

Процедура СформироватьТаблицаСведенияОТранспорте(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ГруппаИмущества,
		|	ТаблицаДокумента.ДатаВводаВЭксплуатацию,
		|	ТаблицаДокумента.ВидТранспорта,
		|	ТаблицаДокумента.ОбъемДвигателя,
		|	ТаблицаДокумента.ГосНомер
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ4)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОТранспорте", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСведенияОТранспорте

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.РазделУчета,
		|	ТаблицаДокумента.МБП,
		|	ТаблицаДокумента.СтатусМБП
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ВводНачальныхОстатков КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПерсоналом
		|ИЗ
		|	Документ.ВводНачальныхОстатков.РасчетыСПерсоналом КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Ссылка.РазделУчета = ""РасчетыСПерсоналом""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.БанковскийСчетКасса,
		|	ТаблицаДокумента.БанковскийСчетКасса.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.ВалютаДенежныхСредств,
		|	ТаблицаДокумента.СуммаВал,
		|	ТаблицаДокумента.Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаДенежныеСредства
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ДенежныеСредства КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Ссылка.РазделУчета = ""ДенежныеСредства""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата КАК Период,
		|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
		|	ТаблицаДокумента.СчетУчета КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
		|	ТаблицаДокумента.ОсновноеСредство КАК СубконтоДт1,
		|	ТаблицаДокумента.МОЛ КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаДокумента.ПервоначальнаяСтоимостьБУ КАК Сумма,
		|	""Ввод остатков по ОС (Перв/стоимость)"" КАК Содержание,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКТ
		|ПОМЕСТИТЬ ВременнаяТаблицаХозрасчетный
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет,
		|	ТаблицаДокумента.МОЛ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.НакопленнаяАмортизацияБУ,
		|	""Ввод остатков по ОС (начисл/амортизация)"",
		|	0,
		|	0
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.СчетНачисленияАмортизацииБУ <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|	И ТаблицаДокумента.НакопленнаяАмортизацияБУ <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.Ссылка.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	ТаблицаДокумента.СостояниеЗапчасти,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	""Ввод начальных остатков ТМЗ"",
		|	ТаблицаДокумента.Количество,
		|	ТаблицаДокумента.Количество
		|ИЗ
		|	Документ.ВводНачальныхОстатков.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный),
		|	ТаблицаДокумента.Ссылка.СчетУчета.ПарныйСчет,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаИзноса,
		|	""Ввод начальных остататков МБП с износом"",
		|	ТаблицаДокумента.Количество,
		|	ТаблицаДокумента.Количество
		|ИЗ
		|	Документ.ВводНачальныхОстатков.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка.МБП
		|	И ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.СуммаИзноса <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаПринятияКУчетуРегл КАК Период,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.ГруппаНУ,
		|	ТаблицаДокумента.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
		|	ТаблицаДокумента.СтоимостьДляВычисленияАмортизацииБУ КАК СтоимостьДляВычисленияАмортизации,
		|	ТаблицаДокумента.СрокСлужбы КАК СрокСлужбы,
		|	ТаблицаДокумента.ОбъемПродукцииРаботДляВычисленияАмортизацииБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ТаблицаДокумента.КоэффициентУскорения,
		|	ТаблицаДокумента.КоэффициентАмортизации,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.ИнвентарныйНомерРегл КАК ИнвентарныйНомер,
		|	ТаблицаДокумента.ПараметрВыработкиБУ КАК ПараметрВыработки,
		|	ТаблицаДокумента.СпособПоступленияРегл КАК СпособПоступления,
		|	ТаблицаДокумента.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ПорядокПогашенияСтоимостиБУ КАК ПорядокПогашенияСтоимости,
		|	ТаблицаДокумента.СпособыОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.НормаАмортизации
		|ПОМЕСТИТЬ ВременнаяТаблицаПараметрыУчетаОС
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаПринятияКУчетуРегл КАК ДатаСостояния,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СостояниеПринятияКУчетуРегл.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Поступление)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Поступило)
		|		КОГДА ТаблицаДокумента.СостояниеПринятияКУчетуРегл.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Передача)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
		|	КОНЕЦ КАК Состояние,
		|	ТаблицаДокумента.ДатаПринятияКУчетуРегл КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК ДатаСписания
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОС
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаПринятияКУчетуРегл КАК Период,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.Ссылка.Склад КАК Склад,
		|	ТаблицаДокумента.МОЛ,
		|	ТаблицаДокумента.Подразделение КАК Местонахождение
		|ПОМЕСТИТЬ ВременнаяТаблицаМестонахождениеОС
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаПринятияКУчетуРегл КАК Период,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.СостояниеПринятияКУчетуРегл КАК Событие,
		|	ТаблицаДокумента.НомерДокументаПринятияКУчетуРегл КАК НомерДокумента,
		|	ТаблицаДокумента.НазваниеДокументаПринятияКУчетуРегл КАК НазваниеДокумента
		|ПОМЕСТИТЬ ВременнаяТаблицаСобытияОС
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаПринятияКУчетуРегл КАК Период,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ИСТИНА КАК Активность,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.РодительскоеОС КАК ВСоставеОС,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийВСоставеОС.ВключеноВСостав) КАК СостояниеВСоставеОС
		|ПОМЕСТИТЬ ВременнаяТаблицаСоставОС
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.РодительскоеОС <> ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата КАК Год,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.ГруппаНУ,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.БалансоваяСтоимостьНаКонецГода,
		|	ТаблицаДокумента.АмортизацияЗаГод,
		|	ТаблицаДокумента.СуммаПоступленияОстаток,
		|	ТаблицаДокумента.СуммаВыбытияОстаток
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяАмортизация
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ГруппаНалоговогоУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата КАК Период,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.Убыток,
		|	ТаблицаДокумента.УбытокОтРеализацииЦенныхБумаг,
		|	ТаблицаДокумента.ПокрытыйУбытокПрошлыхЛет,
		|	ТаблицаДокумента.Прибыль
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговыеДекларации
		|ИЗ
		|	Документ.ВводНачальныхОстатков.НалоговаяДекларация КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВводНачальныхОстатковПрочее.СчетУчета,
		|	ВводНачальныхОстатковПрочее.Субконто1,
		|	ВводНачальныхОстатковПрочее.Субконто2,
		|	ВводНачальныхОстатковПрочее.Субконто3,
		|	ВводНачальныхОстатковПрочее.Количество,
		|	ВводНачальныхОстатковПрочее.Сумма,
		|	ВводНачальныхОстатковПрочее.Валюта,
		|	ВводНачальныхОстатковПрочее.Курс,
		|	ВводНачальныхОстатковПрочее.ВалютнаяСумма,
		|	ВводНачальныхОстатковПрочее.ВидСальдо
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочее
		|ИЗ
		|	Документ.ВводНачальныхОстатков.Прочее КАК ВводНачальныхОстатковПрочее
		|ГДЕ
		|	ВводНачальныхОстатковПрочее.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.Ссылка.Дата КАК Период,
		|	ТаблицаДокумента.Ссылка.Организация,
		|	ТаблицаДокумента.ИнвНомер,
		|	ТаблицаДокумента.Количество,
		|	ТаблицаДокумента.Цена,
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Ссылка.СтатусМБП КАК Статус,
		|	ТаблицаДокумента.Ссылка КАК Партия,
		|	ТаблицаДокумента.Склад КАК Склад,
		|	ТаблицаДокумента.МОЛ,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.ДатаВыдачи,
		|	ТаблицаДокумента.ДатаПрихода
		|ПОМЕСТИТЬ ВременнаяТаблицаТМЗСклад
		|ИЗ
		|	Документ.ВводНачальныхОстатков.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ГруппаИмущества,
		|	ТаблицаДокумента.КодИмущества,
		|	ТаблицаДокумента.МатериалСтен,
		|	ТаблицаДокумента.ОсновнаяПлощадь КАК ЖилаяПлощадь,
		|	ТаблицаДокумента.НежилаяПлощадь,
		|	ТаблицаДокумента.РегиональныйКоэффициент,
		|	ТаблицаДокумента.ЗональныйКоэффициент,
		|	ТаблицаДокумента.ОтраслевойКоэффициент,
		|	ТаблицаДокумента.КатегорияИмущества КАК КатегорияОбъектаИмущества,
		|	ТаблицаДокумента.КодНазначенияИмущества,
		|	ТаблицаДокумента.КодПользователяИмущества,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ДатаВводаВЭксплуатацию = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ТаблицаДокумента.Ссылка.Дата
		|		ИНАЧЕ ТаблицаДокумента.ДатаВводаВЭксплуатацию
		|	КОНЕЦ КАК ДатаВводаВЭксплуатацию,
		|	ТаблицаДокумента.КатегорияИмущества,
		|	ТаблицаДокумента.ВидТранспорта,
		|	ТаблицаДокумента.ОбъемДвигателя,
		|	ТаблицаДокумента.ГосНомер
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	Документ.ВводНачальныхОстатков.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();      	
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);			 
	СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства); 
	СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаМестонахождениеОС(ДокументСсылка, СтруктураДополнительныеСвойства); 
	СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСоставОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговыеДекларации(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаВзаиморасчетыССотрудниками(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаМБПЭксплуатация(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОбИмуществе(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОТранспорте(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ИнтерфейсПечати

// Сформировать печатные формы объектов
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати)
	Перем Ошибки;
	
	ТекстЗапроса =
	
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Номер,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация.НаименованиеПолное КАК ОрганизацияПолноеНаименование,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.ОС.(
	|		НомерСтроки,
	|		ВыработкаКоличествоБУ,
	|		ДатаПринятияКУчетуРегл,
	|		ИнвентарныйНомерРегл,
	|		МОЛ,
	|		НазваниеДокументаПринятияКУчетуРегл,
	|		НакопленнаяАмортизацияБУ,
	|		ОбъемПродукцииРаботДляВычисленияАмортизацииБУ,
	|		ОсновноеСредство,
	|		ПараметрВыработкиБУ,
	|		ПервоначальнаяСтоимостьБУ,
	|		СостояниеПринятияКУчетуРегл,
	|		СпособПоступленияРегл,
	|		СрокСлужбы,
	|		СчетНачисленияАмортизацииБУ,
	|		СчетУчета,
	|		СтоимостьДляВычисленияАмортизацииБУ,
	|		ГруппаНУ,
	|		КоэффициентАмортизации,
	|		КоэффициентУскорения,
	|		ГруппаИмущества
	|	)
	|ИЗ
	|	Документ.ВводНачальныхОстатков КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&СписокДокументов)";		
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ВводНачальныхОстатков";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВводНачальныхОстатков.ПФ_MXL_ОписьОС");
	
	Пока Выборка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		
		ВременнаяТаблицаОС = Выборка.ОС.Выгрузить();                                      
		
		ДанныеПечати.Вставить("ДатаДокумента", Формат(Выборка.Дата,"ДЛФ=DD"));
		ДанныеПечати.Вставить("НомерДокумента", Выборка.Номер);
		ДанныеПечати.Вставить("ПредставлениеОрганизации", Выборка.ОрганизацияПолноеНаименование);
		ДанныеПечати.Вставить("ПервоначальнаяСтоимостьИтого", ВременнаяТаблицаОС.Итог("ПервоначальнаяСтоимостьБУ"));
		ДанныеПечати.Вставить("СтоимостьВычАморИтого", ВременнаяТаблицаОС.Итог("СтоимостьДляВычисленияАмортизацииБУ"));
		ДанныеПечати.Вставить("НакопленнаяАмортизацияИтого", ВременнаяТаблицаОС.Итог("НакопленнаяАмортизацияБУ"));
		
		МассивОбластейМакета = Новый Массив;  
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("ДеталиТаблицы");
		МассивОбластейМакета.Добавить("ИтогиТаблицы");
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			
			Если ИмяОбласти = "ДеталиТаблицы" Тогда
				Для Каждого СтрокаТабличнойЧасти Из ВременнаяТаблицаОС Цикл 
					ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;	
			Иначе
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЕсли;
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб=Истина;
	ТабличныйДокумент.ОриентацияСтраницы= ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВводНачальныхОстатковПоОС") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"ВводНачальныхОстатковПоОС", 
		"Ввод начальных остатков по ОС", 
		ПечатнаяФорма(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт		
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ВводНачальныхОстатковПоОС";
	КомандаПечати.Представление = НСтр("ru = 'Ввод начальных остатков по ОС'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

