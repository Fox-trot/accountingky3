#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	Если НЕ ЗначениеЗаполнено(ПорядокОкругления) Тогда
		ПорядокОкругления = Перечисления.ПорядкиОкругления.Окр0_01;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПериодРегистрации) Тогда
		Если ЗначениеЗаполнено(Дата) Тогда
			ПериодРегистрации = НачалоМесяца(Дата);
		Иначе
			ПериодРегистрации = НачалоМесяца(ТекущаяДатаСеанса());		
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ПроверяемыеРеквизиты.Добавить("Зарплата");
	
	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Зарплата.Итог("СуммаПоБанковскомуСчету") + Зарплата.Итог("СуммаПоКартСчету") + Зарплата.Итог("СуммаПоКассе");
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличную часть "Зарплата"
//
Процедура ЗаполнитьТабличнуюЧасть() Экспорт 
	
	ВидЗаполнения = ДополнительныеСвойства.ВидЗаполненияТабличнойЧасти;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Сотрудники, признак увольнения для выплаты всего остатка
	// Карт счета для определения банка
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ТекстЗапроса =
		// 1. Выбираются все сотрудники по подразделению (или просто все) + с признаком "Уволен".
		// 2. Из первой таблицы выбираются сотрудники с карт счетами + данные их карт счетов.
		// 3. Из первой таблицы выбираются сотрудники с банковскими счетами + данные их банковских счетов.
		// 4. В зависимости от значений реквизитов документа подбирается список необходимых сотруников.
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение,
		|	ВЫБОР
		|		КОГДА СотрудникиСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Уволен
		|ПОМЕСТИТЬ ВременнаяТаблицаПолныйСписокСотрудников
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СотрудникиСрезПоследних
		|ГДЕ
		|	СотрудникиСрезПоследних.Подразделение = &Подразделение
		|;
		|
		|/////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Подразделение,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Уволен,
		|   КартСчетаСотрудников.Банк КАК БанкКартСчета,
		|   КартСчетаСотрудников.Ссылка КАК КартСчет
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиСКартСчетами
		|ИЗ
		|	ВременнаяТаблицаПолныйСписокСотрудников КАК ВременнаяТаблицаПолныйСписокСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КартСчетаСотрудников КАК КартСчетаСотрудников
		|		ПО ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо = КартСчетаСотрудников.Владелец
		|;
		|
		|/////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Подразделение,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Уволен,
		|	БанковскиеСчета.Ссылка КАК БанковскийСчет,
		|	БанковскиеСчета.Банк КАК БанкБанковскогоСчета
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиСБанковскимиСчетами
		|ИЗ
		|	ВременнаяТаблицаПолныйСписокСотрудников КАК ВременнаяТаблицаПолныйСписокСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ПО ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо = БанковскиеСчета.Владелец
		|;
		|
		|/////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо КАК ФизЛицо,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Подразделение КАК Подразделение,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Уволен КАК Уволен,
		|	НЕОПРЕДЕЛЕНО КАК КартСчет,
		|   НЕОПРЕДЕЛЕНО КАК БанкКартСчета,
		|   НЕОПРЕДЕЛЕНО КАК БанковскийСчет,
		|   НЕОПРЕДЕЛЕНО КАК БанкБанковскогоСчета
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	ВременнаяТаблицаПолныйСписокСотрудников КАК ВременнаяТаблицаПолныйСписокСотрудников
		|ГДЕ
		|	&СотрудникиПоКассе
		|	И НЕ ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо В (ВЫБРАТЬ
		|												ВременнаяТаблицаСотрудникиСКартСчетами.ФизЛицо
		|											ИЗ
		|                                         		ВременнаяТаблицаСотрудникиСКартСчетами КАК ВременнаяТаблицаСотрудникиСКартСчетами
		|
		|											ОБЪЕДИНИТЬ ВСЕ
		|
		|	 										ВЫБРАТЬ
		|												ВременнаяТаблицаСотрудникиСБанковскимиСчетами.ФизЛицо
		|											ИЗ
		|                                         		ВременнаяТаблицаСотрудникиСБанковскимиСчетами КАК ВременнаяТаблицаСотрудникиСБанковскимиСчетами)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиСКартСчетами.ФизЛицо,
		|   ВременнаяТаблицаСотрудникиСКартСчетами.Подразделение,
		|   ВременнаяТаблицаСотрудникиСКартСчетами.Уволен,
		|	ВременнаяТаблицаСотрудникиСКартСчетами.КартСчет,
		|   ВременнаяТаблицаСотрудникиСКартСчетами.БанкКартСчета,
		|   НЕОПРЕДЕЛЕНО,
		|   НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаСотрудникиСКартСчетами КАК ВременнаяТаблицаСотрудникиСКартСчетами
		|ГДЕ
		|	&СотрудникиСКартСчетами
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиСБанковскимиСчетами.ФизЛицо,
		|   ВременнаяТаблицаСотрудникиСБанковскимиСчетами.Подразделение,
		|   ВременнаяТаблицаСотрудникиСБанковскимиСчетами.Уволен,
		|	НЕОПРЕДЕЛЕНО,
		|   НЕОПРЕДЕЛЕНО,
		|   ВременнаяТаблицаСотрудникиСБанковскимиСчетами.БанковскийСчет,
		|   ВременнаяТаблицаСотрудникиСБанковскимиСчетами.БанкБанковскогоСчета
		|ИЗ
		|	ВременнаяТаблицаСотрудникиСБанковскимиСчетами КАК ВременнаяТаблицаСотрудникиСБанковскимиСчетами
		|ГДЕ
		|	&СотрудникиСБанковскимиСчетами
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПолныйСписокСотрудников.ФизЛицо,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Подразделение,
		|   ВременнаяТаблицаПолныйСписокСотрудников.Уволен,
		|	НЕОПРЕДЕЛЕНО,
		|   НЕОПРЕДЕЛЕНО,
		|   НЕОПРЕДЕЛЕНО,
		|   НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаПолныйСписокСотрудников КАК ВременнаяТаблицаПолныйСписокСотрудников
		|ГДЕ
		|	&ВсеСотрудники";		
	Если ЗначениеЗаполнено(Подразделение) Тогда  
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Иначе 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СотрудникиСрезПоследних.Подразделение = &Подразделение", "ИСТИНА");	
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	ВсеСотрудники 					= Ложь;
	СотрудникиПоКассе 				= Ложь;
	СотрудникиСБанковскимиСчетами 	= Ложь;
	СотрудникиСКартСчетами 			= Ложь;
	
	Если ВидВедомости = Перечисления.ВидыВедомости.Все Тогда
		ВсеСотрудники = Истина;
	ИначеЕсли ВидВедомости = Перечисления.ВидыВедомости.Касса Тогда
		СотрудникиПоКассе = Истина;
	ИначеЕсли ВидВедомости = Перечисления.ВидыВедомости.Банк Тогда
		СотрудникиСБанковскимиСчетами = Истина;
	ИначеЕсли ВидВедомости = Перечисления.ВидыВедомости.ЗППроект Тогда
		СотрудникиСКартСчетами = Истина;
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("КонецПериода", 					КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация", 					Организация);
	Запрос.УстановитьПараметр("ВсеСотрудники", 					ВсеСотрудники);
	Запрос.УстановитьПараметр("СотрудникиПоКассе", 				СотрудникиПоКассе);
	Запрос.УстановитьПараметр("СотрудникиСБанковскимиСчетами", 	СотрудникиСБанковскимиСчетами);
	Запрос.УстановитьПараметр("СотрудникиСКартСчетами", 		СотрудникиСКартСчетами);
	
	РезультатЗапроса = Запрос.Выполнить();

	// Остатки по счету 3520
	Если ВидЗаполнения = "ЗаполнитьПоОстаткам" Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Субконто1 КАК ФизЛицо,
			|	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаКВыплате
			|ПОМЕСТИТЬ ВременнаяТаблицаРассчитанныеСуммы
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(
			|			&КонецПериода,
			|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций),
			|			Организация = &Организация
			|				И Субконто1 В
			|					(ВЫБРАТЬ
			|						ВременнаяТаблицаСотрудники.ФизЛицо
			|					ИЗ
			|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)) КАК ХозрасчетныйОстатки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
			|		ПО ХозрасчетныйОстатки.Субконто1 = ВременнаяТаблицаСотрудники.ФизЛицо
			|ГДЕ
			|	ХозрасчетныйОстатки.СуммаОстатокКт > 0";
		
	// Обороты по счету 3520 для не уволенных
	// Остатки по счету 3520 для уволенных
	ИначеЕсли ВидЗаполнения = "ЗаполнитьПоОстаткамЗаМесяц" Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ХозрасчетныйОбороты.Субконто1 КАК ФизЛицо,
			|	ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт КАК СуммаКВыплате
			|ПОМЕСТИТЬ ВременнаяТаблицаРассчитанныеСуммы
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Обороты(
			|			&НачалоПериода,
			|			&КонецПериода,
			|			,
			|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций),
			|			Организация = &Организация
			|				И Субконто1 В
			|					(ВЫБРАТЬ
			|						ВременнаяТаблицаСотрудники.ФизЛицо
			|					ИЗ
			|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
			|					ГДЕ
			|						НЕ ВременнаяТаблицаСотрудники.Уволен),
			|			,
			|			) КАК ХозрасчетныйОбороты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
			|		ПО ХозрасчетныйОбороты.Субконто1 = ВременнаяТаблицаСотрудники.ФизЛицо
			|ГДЕ
			|	ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт > 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Субконто1,
			|	ХозрасчетныйОстатки.СуммаОстатокКт
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(
			|			&КонецПериода,
			|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций),
			|			Организация = &Организация
			|				И Субконто1 В
			|					(ВЫБРАТЬ
			|						ВременнаяТаблицаСотрудники.ФизЛицо
			|					ИЗ
			|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
			|					ГДЕ
			|						ВременнаяТаблицаСотрудники.Уволен)) КАК ХозрасчетныйОстатки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
			|		ПО ХозрасчетныйОстатки.Субконто1 = ВременнаяТаблицаСотрудники.ФизЛицо
			|ГДЕ
			|	ХозрасчетныйОстатки.СуммаОстатокКт > 0";			
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации) + 1);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаРассчитанныеСуммы.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаСотрудники.Подразделение КАК Подразделение,
		|	ВременнаяТаблицаСотрудники.Уволен,
		|	ВременнаяТаблицаРассчитанныеСуммы.СуммаКВыплате КАК СуммаКВыплате,
		|	ВременнаяТаблицаСотрудники.КартСчет,
		|	ВременнаяТаблицаСотрудники.БанкКартСчета,
		|	ВременнаяТаблицаСотрудники.БанковскийСчет,
		|	ВременнаяТаблицаСотрудники.БанкБанковскогоСчета
		|ИЗ
		|	ВременнаяТаблицаРассчитанныеСуммы КАК ВременнаяТаблицаРассчитанныеСуммы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаРассчитанныеСуммы.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаРассчитанныеСуммы.ФизЛицо.Наименование";
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбщаяСуммаПоБанковскимСчетам = 0;
	ОбщаяСуммаПоКассе 			 = 0;
	ОбщаяСуммаПоАО 				 = 0;
	
	Пока Выборка.Следующий() Цикл
		СтрокаТабличнойЧасти = Зарплата.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
		
		СуммаВыборки = Выборка.СуммаКВыплате;
		
		Если СотрудникиСБанковскимиСчетами Тогда
			СтрокаТабличнойЧасти.СуммаПоБанковскомуСчету = СуммаВыборки;
			ОбщаяСуммаПоБанковскимСчетам = ОбщаяСуммаПоБанковскимСчетам + СуммаВыборки;
			
		ИначеЕсли СотрудникиСКартСчетами Тогда
			СтрокаТабличнойЧасти.СуммаПоКартСчету = СуммаВыборки;
			
		ИначеЕсли СотрудникиПоКассе ИЛИ ВсеСотрудники Тогда
			СтрокаТабличнойЧасти.СуммаПоКассе = СуммаВыборки;
			ОбщаяСуммаПоКассе = ОбщаяСуммаПоКассе + СуммаВыборки;
		КонецЕсли;
		
		Если Выборка.Уволен Тогда 
			Продолжить;
		КонецЕсли;
		
		// Округление
		Если СотрудникиСКартСчетами Тогда
			СтрокаТабличнойЧасти.СуммаПоКартСчету = БухгалтерскийУчетСервер.ОкруглитьЦену(СтрокаТабличнойЧасти.СуммаПоКартСчету, ПорядокОкругления, Истина);
		ИначеЕсли СотрудникиСБанковскимиСчетами Тогда
			СтрокаТабличнойЧасти.СуммаПоБанковскомуСчету = БухгалтерскийУчетСервер.ОкруглитьЦену(СтрокаТабличнойЧасти.СуммаПоБанковскомуСчету, ПорядокОкругления, Истина);
		ИначеЕсли СотрудникиПоКассе ИЛИ ВсеСотрудники Тогда
			СтрокаТабличнойЧасти.СуммаПоКассе = БухгалтерскийУчетСервер.ОкруглитьЦену(СтрокаТабличнойЧасти.СуммаПоКассе, ПорядокОкругления, Истина);
		КонецЕсли;
	КонецЦикла; 
	
	МенеджерВременныхТаблиц.Закрыть();
		
	Если СотрудникиСКартСчетами Тогда
		ЗаполнитьТаблицуВыплатПоЗППроектам(Зарплата.Выгрузить());
	КонецЕсли;
	
	Если СотрудникиСБанковскимиСчетами Тогда
		ЗаполнитьТаблицуВыплатПоБанковскимСчетам(ОбщаяСуммаПоБанковскимСчетам);
	КонецЕсли;
	
	Если СотрудникиПоКассе ИЛИ ВсеСотрудники Тогда
		ЗаполнитьТаблицуВыплатПоКассе(ОбщаяСуммаПоКассе);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура создает документы выплаты.
//
// Параметры:
//	ТЗЗарплата - ТаблицаЗначений, выгруженная ТЧ "Зарплата".
//
Процедура ЗаполнитьТаблицуВыплатПоЗППроектам(ТЗЗарплата) Экспорт 

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаЗарплата.БанкКартСчета КАК Банк,
		|	ТаблицаЗарплата.СуммаПоКартСчету КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаЗарплата
		|ИЗ
		|	&ТЗЗарплата КАК ТаблицаЗарплата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗарплата.Банк КАК Банк,
		|	ВременнаяТаблицаЗарплата.Сумма КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаЗарплата КАК ВременнаяТаблицаЗарплата
		|ГДЕ
		|	НЕ ВременнаяТаблицаЗарплата.Банк = ЗНАЧЕНИЕ(Справочник.Банки.ПустаяСсылка)
		|
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	Банк";
	Запрос.УстановитьПараметр("ТЗЗарплата", ТЗЗарплата);
	ВыборкаПоГруппировкам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоличествоЗпПроектов = ВыборкаПоГруппировкам.Количество();	
	НомерПроекта = 1;
	
	СуммаОстаткаПоБС = ПолучитьОстатокПоКассеИлиБанковскомуСчету(Ложь);
	
	Пока ВыборкаПоГруппировкам.Следующий() Цикл
		Если ВыборкаПоГруппировкам.Сумма <> 0 Тогда
			СтрокаТабличнойЧасти = ТаблицаВыплат.Добавить();
			
			Если КоличествоЗпПроектов = 1 Тогда
				Наименование = НСтр("ru = 'ЗП проект'");
			Иначе
				Наименование = СтрШаблон(НСтр("ru = 'ЗП проект%1'"), НомерПроекта);
			КонецЕсли;
			
			СтрокаТабличнойЧасти.Наименование = Наименование;
			СтрокаТабличнойЧасти.Банк		  = ВыборкаПоГруппировкам.Банк;
			СтрокаТабличнойЧасти.Сумма 		  = ВыборкаПоГруппировкам.Сумма;
			СтрокаТабличнойЧасти.СуммаОстаток = СуммаОстаткаПоБС;
			
			НомерПроекта = НомерПроекта + 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура создает документы выплаты.
//
// Параметры:
//	Сумма - Число, общая сумма по колонке "СуммаПоБанковскомуСчету" из ТЧ "Зарплата".
//
Процедура ЗаполнитьТаблицуВыплатПоБанковскимСчетам(Сумма) Экспорт 
	
	Если Сумма <> 0 Тогда	
		СтрокаТабличнойЧасти = ТаблицаВыплат.Добавить();
		СтрокаТабличнойЧасти.Наименование = НСтр("ru = 'Банковские счета'");
		СтрокаТабличнойЧасти.Сумма 		  = Сумма;
		СтрокаТабличнойЧасти.СуммаОстаток = ПолучитьОстатокПоКассеИлиБанковскомуСчету(Ложь);
	КонецЕсли;	
		
КонецПроцедуры

// Процедура создает документы выплаты.
//
// Параметры:
//	Сумма - Число, общая сумма по колонке "СуммаПоКассе" из ТЧ "Зарплата".
//
Процедура ЗаполнитьТаблицуВыплатПоКассе(Сумма) Экспорт 
	
	Если Сумма <> 0 Тогда
		СтрокаТабличнойЧасти = ТаблицаВыплат.Добавить();
		СтрокаТабличнойЧасти.Наименование = "Касса";
		СтрокаТабличнойЧасти.Сумма 		  = Сумма;
		СтрокаТабличнойЧасти.СуммаОстаток = ПолучитьОстатокПоКассеИлиБанковскомуСчету(Истина);
	КонецЕсли;
		
КонецПроцедуры

// Процедура создает документы выплаты.
//
// Параметры:
//	ПолучитьОстатокПоКассе - Булево, параметр для определения по кассе или банку получать сумму остаток.
//
// Возвращаемое значение: Число, сумма остаток по РБ "Хозрасчетный".
//
Функция ПолучитьОстатокПоКассеИлиБанковскомуСчету(ПолучитьОстатокПоКассе) Экспорт 
		
	Запрос = Новый Запрос();
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период, 
		|			Счет = &Счет, 
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДенежныеСредства), 
		|			Организация = &Организация
		|				И Субконто1 = &БанковскийСчетКасса) КАК ХозрасчетныйОстатки";
	Запрос.УстановитьПараметр("Период", КонецМесяца(ПериодРегистрации) + 1);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ПолучитьОстатокПоКассе Тогда
		Запрос.УстановитьПараметр("Счет", Касса.СчетУчета);
		Запрос.УстановитьПараметр("БанковскийСчетКасса", Касса);
	Иначе
		Запрос.УстановитьПараметр("Счет", БанковскийСчет.СчетУчета);
		Запрос.УстановитьПараметр("БанковскийСчетКасса", БанковскийСчет);	
	КонецЕсли;	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Сумма;
КонецФункции // ПолучитьОстатокПоКассеИлиБанковскомуСчета()

// Процедура, посредствам запроса и обработки, подготавливает данные для создаваемых документов РКО или ППИ.
//
Процедура ПодготовитьДанныеДляДокументовВыплаты() Экспорт 

	Запрос = Новый Запрос();
	Запрос.Текст = 
		// 1. Данные табличной части "Зарплата";
		// 2. Данные ТЧ по карт счетам; 
		// 3. Данные ТЧ по банковским счетам;
		// 4. Данные ТЧ по кассе;
		"ВЫБРАТЬ
		|	ТаблицаЗарплата.ФизЛицо КАК ФизЛицо,
		|	ТаблицаЗарплата.БанковскийСчет КАК БанковскийСчет,
		|	ТаблицаЗарплата.БанкБанковскогоСчета КАК БанкБанковскогоСчета,
		|	ТаблицаЗарплата.СуммаПоБанковскомуСчету КАК СуммаПоБанковскомуСчету,
		|	ТаблицаЗарплата.КартСчет КАК КартСчет,
		|	ТаблицаЗарплата.БанкКартСчета КАК БанкКартСчета,
		|	ТаблицаЗарплата.СуммаПоКартСчету КАК СуммаПоКартСчету,
		|	ТаблицаЗарплата.СуммаПоКассе КАК СуммаПоКассе
		|ПОМЕСТИТЬ ВременнаяТаблицаЗарплата
		|ИЗ
		|	&ТЗЗарплата КАК ТаблицаЗарплата
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗарплата.ФизЛицо,
		|	ТаблицаЗарплата.СуммаПоКартСчету,
		|	ТаблицаЗарплата.КартСчет,
		|	ТаблицаЗарплата.БанкКартСчета
		|ИЗ
		|	ВременнаяТаблицаЗарплата КАК ТаблицаЗарплата
		|ГДЕ
		|	ТаблицаЗарплата.СуммаПоКартСчету <> 0
		|
		|ИТОГИ ПО
		|	ТаблицаЗарплата.БанкКартСчета
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗарплата.ФизЛицо,
		|	ТаблицаЗарплата.СуммаПоБанковскомуСчету,
		|	ТаблицаЗарплата.БанковскийСчет,
		|	ТаблицаЗарплата.БанкБанковскогоСчета
		|ИЗ
		|	ВременнаяТаблицаЗарплата КАК ТаблицаЗарплата
		|ГДЕ
		|    ТаблицаЗарплата.СуммаПоБанковскомуСчету <> 0
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗарплата.ФизЛицо,
		|	ТаблицаЗарплата.СуммаПоКассе
		|ИЗ
		|	ВременнаяТаблицаЗарплата КАК ТаблицаЗарплата
		|ГДЕ
		|    ТаблицаЗарплата.СуммаПоКассе <> 0";
	Запрос.УстановитьПараметр("ТЗЗарплата", Зарплата.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаОбщиеППИ 		 = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИндивидуальныеППИ = МассивРезультатов[2].Выбрать();
	
	// Подготовка данных для общих ППИ
	Пока ВыборкаОбщиеППИ.Следующий() Цикл
		
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ТаблицаЗначений.Колонки.Добавить("ФизЛицо",,"ФизЛицо");
		ТаблицаЗначений.Колонки.Добавить("СуммаПоКартСчету",,"СуммаПоКартСчету");
		ТаблицаЗначений.Колонки.Добавить("КартСчет",,"КартСчет");
		ТаблицаЗначений.Колонки.Добавить("БанкКартСчета",,"БанкКартСчета");
		
		ВыборкаДетальныеЗаписи = ВыборкаОбщиеППИ.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл				
			Стр = ТаблицаЗначений.Добавить();
			Стр.ФизЛицо 		 = ВыборкаДетальныеЗаписи.ФизЛицо;
			Стр.СуммаПоКартСчету = ВыборкаДетальныеЗаписи.СуммаПоКартСчету;
			Стр.КартСчет 		 = ВыборкаДетальныеЗаписи.КартСчет;
			Стр.БанкКартСчета 	 = ВыборкаДетальныеЗаписи.БанкКартСчета;				
		КонецЦикла;
		
		СтруктураДляОбщегоППИ = Новый Структура();
		
		СтруктураДляОбщегоППИ.Вставить("ВедомостьЗП", 					Ссылка);
		СтруктураДляОбщегоППИ.Вставить("Организация", 					Организация);
		СтруктураДляОбщегоППИ.Вставить("БанковскийСчет", 				БанковскийСчет);
		СтруктураДляОбщегоППИ.Вставить("ТаблицаЗначений", 				ТаблицаЗначений);
		СтруктураДляОбщегоППИ.Вставить("СтатьяДвиженияДенежныхСредств", СтатьяДвиженияДенежныхСредств);
																							
		ДокументМенеджер = Документы.ПлатежноеПоручениеИсходящее;
					
		СозданиеДокументовВыплаты(ДокументМенеджер, СтруктураДляОбщегоППИ);	
	КонецЦикла;
	
	// Подготовка данных для индивидуальных ППИ
	Пока ВыборкаИндивидуальныеППИ.Следующий() Цикл
		
		СтруктураДляИндивППИ = Новый Структура();
			
		СтруктураДляИндивППИ.Вставить("ВедомостьЗП", 					Ссылка);
		СтруктураДляИндивППИ.Вставить("Организация", 					Организация);
		СтруктураДляИндивППИ.Вставить("БанковскийСчет", 				БанковскийСчет);
		СтруктураДляИндивППИ.Вставить("Банк", 							ВыборкаИндивидуальныеППИ.БанкБанковскогоСчета);
		СтруктураДляИндивППИ.Вставить("БанковскийСчетПолучателя", 		ВыборкаИндивидуальныеППИ.БанковскийСчет);
		СтруктураДляИндивППИ.Вставить("ФизЛицо", 						ВыборкаИндивидуальныеППИ.ФизЛицо);
		СтруктураДляИндивППИ.Вставить("СуммаКВыплате", 					ВыборкаИндивидуальныеППИ.СуммаПоБанковскомуСчету);
		СтруктураДляИндивППИ.Вставить("СтатьяДвиженияДенежныхСредств", 	СтатьяДвиженияДенежныхСредств);
											
		ДокументМенеджер = Документы.ПлатежноеПоручениеИсходящее;	
		
		СозданиеДокументовВыплаты(ДокументМенеджер, СтруктураДляИндивППИ)
	КонецЦикла;
	
	// Подготовка данных для индивидуальных РКО
	Если ВидДокументаВыплаты = Перечисления.ВидыДокументовВыплаты.Индивидуально Тогда
		ВыборкаРКО = МассивРезультатов[3].Выбрать();	
			
		Пока ВыборкаРКО.Следующий() Цикл				
			СтруктураДляИндивРКО = Новый Структура();
			
			СтруктураДляИндивРКО.Вставить("ВедомостьЗП", 					Ссылка);
			СтруктураДляИндивРКО.Вставить("НомерВыплаты", 					Номер);
			СтруктураДляИндивРКО.Вставить("Дата", 							Дата);
			СтруктураДляИндивРКО.Вставить("Организация", 					Организация);
			СтруктураДляИндивРКО.Вставить("Касса", 							Касса);
			СтруктураДляИндивРКО.Вставить("ФизЛицо", 						ВыборкаРКО.ФизЛицо);
			СтруктураДляИндивРКО.Вставить("СуммаПоКассе", 					ВыборкаРКО.СуммаПоКассе);
			СтруктураДляИндивРКО.Вставить("СтатьяДвиженияДенежныхСредств", 	СтатьяДвиженияДенежныхСредств);
												
			ДокументМенеджер = Документы.РасходныйКассовыйОрдер;
			
			СозданиеДокументовВыплаты(ДокументМенеджер, СтруктураДляИндивРКО);		
		КонецЦикла;
		
	// Подготовка данных для общих РКО	
	ИначеЕсли ВидДокументаВыплаты = Перечисления.ВидыДокументовВыплаты.Общий Тогда
		ТЗ_РКО = МассивРезультатов[3].Выгрузить();
		
		СтруктураДляОбщегоРКО = Новый Структура();
			
		СтруктураДляОбщегоРКО.Вставить("ВедомостьЗП", 					Ссылка);
		СтруктураДляОбщегоРКО.Вставить("НомерВыплаты", 					Номер);
		СтруктураДляОбщегоРКО.Вставить("Дата", 							Дата);
		СтруктураДляОбщегоРКО.Вставить("Организация", 					Организация);
		СтруктураДляОбщегоРКО.Вставить("Касса", 						Касса);
		СтруктураДляОбщегоРКО.Вставить("ТаблицаЗначений", 				ТЗ_РКО);
		СтруктураДляОбщегоРКО.Вставить("СтатьяДвиженияДенежныхСредств", СтатьяДвиженияДенежныхСредств);
											
		ДокументМенеджер = Документы.РасходныйКассовыйОрдер;
		
		СозданиеДокументовВыплаты(ДокументМенеджер, СтруктураДляОбщегоРКО);
	КонецЕсли;

КонецПроцедуры

// Процедура создает документы выплаты.
//
// Параметры:
//  ДокументМенеджер - ДокументМенеджер, документ менеджер.
//
//	СтруктураДанных - Структура, данные для заполнения документов выплаты.
//
Процедура СозданиеДокументовВыплаты(ДокументМенеджер, СтруктураДанных)	
	ДокументОбъект = ДокументМенеджер.СоздатьДокумент();

	ДокументОбъект.Дата = Дата;
	ДокументОбъект.Заполнить(СтруктураДанных);			
	
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);	
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось завершить создание документа.
			|Техническая информация об ошибке: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		#Если Сервер Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить создание документа по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		#КонецЕсли	
	КонецПопытки;				
КонецПроцедуры

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли