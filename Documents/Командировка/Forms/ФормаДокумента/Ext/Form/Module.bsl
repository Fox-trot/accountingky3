#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.КоличествоДней) Тогда
	 	Объект.КоличествоДней 	= КоличествоДней;
	КонецЕсли;
	
	КоличествоДней 	= ПолучитьКоличествоДней();
	Подразделение   = Объект.Подразделение;
	
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	
	ВалютаРегламентированногоУчета 	= ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	УстановитьГородПоОрганизации();	
	
	ДокСсылка = ТаблицаРКО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ДокСсылка.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДокументОснование");
	ДокСсылка.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ДокСсылка.Использование = Истина;
	ДокСсылка.ПравоеЗначение = Объект.Ссылка;
	
	ДокСсылка.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидОперации");
	ДокСсылка.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ДокСсылка.Использование = Истина;
	ДокСсылка.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВыдачаПодотчетнику");
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДанныеФормыСтруктура") Тогда
		СтрокаТабличнойЧасти = Элементы.Сотрудники.ТекущиеДанные;
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
		РасчетТарифов();	
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ТаблицаРКО.Отбор.Элементы.Очистить();
	ДокСсылка = ТаблицаРКО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ДокСсылка.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДокументОснование");
	ДокСсылка.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ДокСсылка.Использование = Истина;
	ДокСсылка.ПравоеЗначение = Объект.Ссылка;
	
	ДокСсылка.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидОперации");
	ДокСсылка.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ДокСсылка.Использование = Истина;
	ДокСсылка.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВыдачаПодотчетнику");
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Объект.Номер = "";
	УстановитьГородПоОрганизации();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ДатаНачала.
//
&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	ПересчитатьТабличнуюЧастьПриИзмененииПериодаКомандировки()	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ДатаОкончания.
//
&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	ПересчитатьТабличнуюЧастьПриИзмененииПериодаКомандировки()
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьТабличнуюЧастьПриИзмененииПериодаКомандировки()
	Объект.КоличествоДней = ПолучитьКоличествоДней();	
	Если Объект.КоличествоДней <> КоличествоДней Тогда
		КоличествоДней = Объект.КоличествоДней;	
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросПересчитатьТабличнуюЧастьПриИзмененииПериодаКомандировки", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Поменялось количество дней командировки! Пересчитать табличную часть?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли;	
КонецПроцедуры 

// Процедура - обработчик события ПриИзменении поля ввода Страна.
//
&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	Объект.Город = "";
	РасчетТарифов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Город.
//
&НаКлиенте
Процедура ГородПриИзменении(Элемент)
	РасчетТарифов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Подразделение.
//
&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	// Значение реквизита не изменилось
	Если Объект.Подразделение = Подразделение Тогда 
		Возврат;
	КонецЕсли;	

	Если Объект.Сотрудники.Количество() > 0 Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросОчиститьТабличнуюЧастьСотрудники", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет очищена. Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	Иначе 
		Подразделение = Объект.Подразделение;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРКО Тогда
		ТаблицаРКО.Параметры.УстановитьЗначениеПараметра("ДокументСсылка", Объект.Ссылка);
		ТаблицаАО.Параметры.УстановитьЗначениеПараметра("ДокументСсылка", Объект.Ссылка);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти 		= Элементы.Сотрудники.ТекущиеДанные;	
		СтрокаТабличнойЧасти.Курс 	= 1;
		СтрокаТабличнойЧасти.Валюта = ВалютаРегламентированногоУчета;
		СтрокаТабличнойЧасти.Город 	= Объект.Город;
		РасчетТарифов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаПодбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	СуммаДокумента = Объект.Сотрудники.Итог("СуммаВсего");	 
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СотрудникиФизЛицо.
//
&НаКлиенте
Процедура СотрудникиФизЛицоПриИзменении(Элемент)
	СтрокаТабличнойЧасти 		= Элементы.Сотрудники.ТекущиеДанные;	
	СтруктураСотрудник = СведенияОСотруднике(ДатаДокумента, Объект.Организация, СтрокаТабличнойЧасти.ФизЛицо);
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтруктураСотрудник);
	РасчетТарифов();
	
	// не удалось получить- возвращаемся
	Если СтрокаТабличнойЧасти = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнитьСтрокиНаСервере(СтрокаТабличнойЧасти.ФизЛицо);

КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СотрудникиГород.
//
&НаКлиенте
Процедура СотрудникиГородПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(Объект.Город) И СтрокаТабличнойЧасти.Город <> Объект.Город Тогда
		ТекстСообщения = НСтр("ru = 'Если верхний реквизит ""Город"" заполнен, то город в строке табличной части должен соответствовать ему.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.Город",,);
		СтрокаТабличнойЧасти.Город = Объект.Город;
	Иначе
		ПересчетСтрокиТабличнойЧасти("Город");	
	КонецЕсли;		
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СотрудникиВалюта.
//
&НаКлиенте
Процедура СотрудникиВалютаПриИзменении(Элемент)
	СтрокаТабличнойЧасти 		= Элементы.Сотрудники.ТекущиеДанные;
	// проверка заполненого значения
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Валюта) Тогда 
		СтрокаТабличнойЧасти.Курс = КурсВалюты(СтрокаТабличнойЧасти.Валюта, ДатаДокумента);
	Иначе
		СтрокаТабличнойЧасти.Курс = 1;
	КонецЕсли;	
	
	ПересчетСтрокиТабличнойЧасти("Валюта");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СотрудникиСуточные.
//
&НаКлиенте
Процедура СотрудникиСуточныеПриИзменении(Элемент)
	ПересчетСтрокиТабличнойЧасти("Сумма");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СотрудникиПроживание.
//
&НаКлиенте
Процедура СотрудникиПроживаниеПриИзменении(Элемент)
	ПересчетСтрокиТабличнойЧасти("Сумма");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СотрудникиПроездные.
//
&НаКлиенте
Процедура СотрудникиПроездныеПриИзменении(Элемент)
	ПересчетСтрокиТабличнойЧасти("Сумма");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьРКО(Команда)
	Если НЕ ЗаписатьДокументОтменивПроведение() Тогда 
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросСоздатьДокументыВыплаты", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Будут сформированы документы РКО! Продолжить выполнение операции?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	
	Элементы.ТаблицаРКО.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПодразделению(Команда)
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено подразделение! Заполнение документа отменено.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Подразделение",,Отказ)		
	КонецЕсли;

	Если Отказ Тогда 
		Возврат
	КонецЕсли;	
	
	Если Объект.Сотрудники.Количество() = 0 Тогда
		ЗаполнитьПоПодразделениюНаСервере();
		РасчетТарифов();
	Иначе	
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросЗаполнитьТабличнуюЧасть", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет перезаполнена! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	Отказ = Ложь;

	Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено подразделение! Заполнение документа отменено.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Подразделение",,Отказ)		
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ТекущаяОрганизация", Объект.Организация);
	СтруктураОтбора.Вставить("ТекущееПодразделение", Объект.Подразделение);
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	ПараметрыФормы.Вставить("АдресСпискаПодобранныхСотрудников", АдресСпискаПодобранныхСотрудников());
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыФормы, Элементы.Сотрудники);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросЗаполнитьТабличнуюЧасть(РезультатВопроса, ДополнительныеПараметры) Экспорт
    Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
        ЗаполнитьПоПодразделениюНаСервере();
		РасчетТарифов();
	КонецЕсли;
КонецПроцедуры	
		
&НаКлиенте
Процедура ОтветНаВопросПересчитатьТабличнуюЧастьПриИзмененииПериодаКомандировки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		РасчетТарифов();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросСоздатьДокументыВыплаты(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СоздатьДокументыВыплатыЗавершение();
	КонецЕсли; 
КонецПроцедуры	

&НаКлиенте
Процедура ОтветНаВопросПересчитатьСтрокуТабличнойЧасти(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		РасчетТарифов();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросПересчитатьСтрокуТабличнойЧастиПоВалюте(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СтрокаТабличнойЧасти = Элементы.Сотрудники.ТекущиеДанные;
		РасчетТарифов(СтрокаТабличнойЧасти.Валюта);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросОчиститьТабличнуюЧастьСотрудники(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Подразделение = Объект.Подразделение;
		Объект.Сотрудники.Очистить();
	Иначе
		// Отмена изменения - возврат прежних значений
		Объект.Подразделение = Подразделение;
	КонецЕсли; 
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, ДатаДокумента, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Процедура - Установить пиктограмму комментария.
//
&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

&НаСервере
Процедура РасчетТарифов(Валюта = Неопределено)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
			
		"ВЫБРАТЬ
		|	Сотрудники.ФизЛицо,
		|	Сотрудники.Подразделение,
		|	Сотрудники.Должность,
		|	&Дни КАК Дни,
		|	&Страна КАК Страна,
		|	Сотрудники.Город,
		|	Сотрудники.Валюта КАК Валюта,
		|	Сотрудники.НомерСтроки
		|ПОМЕСТИТЬ ТаблицаДокумента
		|ИЗ
		|	&Сотрудники КАК Сотрудники
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Тарифы.ВидТарифаКомандировочных = ЗНАЧЕНИЕ(Перечисление.ВидыТарифовКомандировочных.Суточные)
		|			ТОГДА Тарифы.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Суточные,
		|	ВЫБОР
		|		КОГДА Тарифы.ВидТарифаКомандировочных = ЗНАЧЕНИЕ(Перечисление.ВидыТарифовКомандировочных.Проживание)
		|			ТОГДА Тарифы.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Проживание,
		|	ВЫБОР
		|		КОГДА Тарифы.ВидТарифаКомандировочных = ЗНАЧЕНИЕ(Перечисление.ВидыТарифовКомандировочных.Проездные)
		|			ТОГДА Тарифы.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Проездные,
		|	Тарифы.Страна,
		|	Тарифы.Город,
		|	Тарифы.Валюта
		|ПОМЕСТИТЬ Тарифы
		|ИЗ
		|	РегистрСведений.ТарифыКомандировочных.СрезПоследних(
		|			&Дата,
		|			Организация = &Организация
		|				И (Город, Страна) В
		|					(ВЫБРАТЬ
		|						ТаблицаДокумента.Город,
		|						ТаблицаДокумента.Страна
		|					ИЗ
		|						ТаблицаДокумента КАК ТаблицаДокумента)) КАК Тарифы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.Должность,
		|	ТаблицаДокумента.Дни,
		|	ТаблицаДокумента.Страна,
		|	ТаблицаДокумента.Город,
		|	ТаблицаДокумента.НомерСтроки,
		|	ТаблицаДокумента.Валюта КАК Валюта,
		|	СУММА(ЕСТЬNULL(Тарифы.Суточные, 0)) КАК Суточные,
		|	СУММА(ЕСТЬNULL(Тарифы.Проживание, 0)) КАК Проживание,
		|	СУММА(ЕСТЬNULL(Тарифы.Проездные, 0)) КАК Проездные,
		|	СУММА(ТаблицаДокумента.Дни * (ЕСТЬNULL(Тарифы.Суточные, 0) + ЕСТЬNULL(Тарифы.Проживание, 0)) + ЕСТЬNULL(Тарифы.Проездные, 0)) КАК СуммаВсего
		|ИЗ
		|	ТаблицаДокумента КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Тарифы КАК Тарифы
		|		ПО ТаблицаДокумента.Страна = Тарифы.Страна
		|			И ТаблицаДокумента.Город = Тарифы.Город
		|			И ТаблицаДокумента.Валюта = Тарифы.Валюта
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаДокумента.Город,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Дни,
		|	ТаблицаДокумента.Должность,
		|	ТаблицаДокумента.Валюта,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.НомерСтроки,
		|	ТаблицаДокумента.Страна";
	
	Запрос.УстановитьПараметр("Дата", 			ДатаДокумента);
	Запрос.УстановитьПараметр("Организация", 	Объект.Организация);
	Запрос.УстановитьПараметр("Дни",			Объект.КоличествоДней);
	Запрос.УстановитьПараметр("Страна", 		Объект.Страна);
	Запрос.УстановитьПараметр("Сотрудники",		Объект.Сотрудники.Выгрузить());
		
	Если Валюта = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудники.Валюта КАК Валюта","&Валюта");
		Запрос.УстановитьПараметр("Валюта",ВалютаРегламентированногоУчета); 
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.Сотрудники[Выборка.НомерСтроки - 1], Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКоличествоДней()
	Если Объект.ДатаОкончания > Объект.ДатаНачала Тогда
		РезДеления 		= (КонецДня(Объект.ДатаОкончания) - Объект.ДатаНачала) / 86400;
		ЦелаяЧасть 		= Цел(РезДеления);
		ДробнаяЧасть 	= РезДеления - ЦелаяЧасть;
		Возврат ?(ДробнаяЧасть > 0, ЦелаяЧасть + 1, ЦелаяЧасть);
	ИначеЕсли Объект.ДатаОкончания = Объект.ДатаНачала и ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда 
		Возврат 1;
	Иначе 
		Возврат 0;
	КонецЕсли;	

КонецФункции // ПолучитьКоличествоДней()

&НаСервере
Функция СоздатьДокументыРКО(ТЗ_РКО)
	МассивДокументов = Новый Массив();
	
	ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВыдачаПодотчетнику");
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Сотрудники Цикл
		
		СтрокаТаблицыЗначений = ТЗ_РКО.Найти(СтрокаТабличнойЧасти.ФизЛицо, "ФизЛицо");
		Если СтрокаТаблицыЗначений = Неопределено Тогда
			ДокОбъект = Документы.РасходныйКассовыйОрдер.СоздатьДокумент();	
		Иначе
			Если СтрокаТаблицыЗначений.СуммаДокумента = СтрокаТабличнойЧасти.СуммаВсего Тогда
				Возврат МассивДокументов;
			Иначе
				ДокОбъект = СтрокаТаблицыЗначений.Ссылка.ПолучитьОбъект();
			КонецЕсли;			
		КонецЕсли;
		МассивДокументов.Добавить(ДокОбъект.Ссылка);
		
		ДокОбъект.Дата = ДатаДокумента;
		
		СтруктураЗаполнения = Новый Структура();
		СтруктураЗаполнения.Вставить("Командировка",		Объект.Ссылка);
		СтруктураЗаполнения.Вставить("ВидОперации",			ВидОперации);
		СтруктураЗаполнения.Вставить("Организация",			Объект.Организация);
		СтруктураЗаполнения.Вставить("ДокументОснование",	Объект.Ссылка);
		СтруктураЗаполнения.Вставить("ФизЛицо",				СтрокаТабличнойЧасти.ФизЛицо);
		СтруктураЗаполнения.Вставить("СуммаДокумента",		СтрокаТабличнойЧасти.СуммаВсего);
		СтруктураЗаполнения.Вставить("СчетУчета",			ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами);
		
		ДокОбъект.Заполнить(СтруктураЗаполнения);
								
		СтрокаТабличнойЧастиРКО = ДокОбъект.РасшифровкаПлатежа.Добавить();
		СтрокаТабличнойЧастиРКО.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаВсего;	

		// Попытка
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить проведение по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
	КонецЦикла;
		
	Элементы.ТаблицаРКО.Обновить();
	Возврат МассивДокументов;
	
КонецФункции

&НаКлиенте
Процедура СоздатьДокументыВыплатыЗавершение()
    МассивДокументов = СоздатьДокументыВыплатыНаСервереИЗаписать();
    Текст = НСтр("ru = 'Создание:'");
    Для каждого СтрокаДокумент Из МассивДокументов Цикл
        ПоказатьОповещениеПользователя(Текст, ПолучитьНавигационнуюСсылку(СтрокаДокумент), СтрокаДокумент, БиблиотекаКартинок.Информация32);
    КонецЦикла;
КонецПроцедуры // СоздатьДокументыВыплатыЗавершение()

&НаСервере
Функция СоздатьДокументыВыплатыНаСервереИЗаписать()
	МассивДокументов = Новый Массив();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходныйКассовыйОрдер.Ссылка,
		|	РасходныйКассовыйОрдер.ФизЛицо,
		|	РасходныйКассовыйОрдер.СуммаДокумента
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
		|ГДЕ
		|	РасходныйКассовыйОрдер.ДокументОснование = &ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование", Объект.Ссылка);
	
	ТЗ_РКО = Запрос.Выполнить().Выгрузить();	

	МассивДокументов = СоздатьДокументыРКО(ТЗ_РКО);
				
	Возврат МассивДокументов;

КонецФункции //  СоздатьДокументыВыплатыНаСервере()

&НаСервереБезКонтекста
Функция СведенияОСотруднике(Дата, Организация, ФизЛицо)
	
	Возврат ПроведениеРасчетовПоЗарплатеСервер.СведенияОСотруднике(Дата, Организация, ФизЛицо)
	
КонецФункции

&НаКлиенте
Процедура ПересчетСтрокиТабличнойЧасти(Колонка)
	Если Колонка = "Город" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросПересчитатьСтрокуТабличнойЧасти", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'В текущей строке поменялся город! Пересчитать тарифы в этой строке?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	ИначеЕсли Колонка = "Валюта" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросПересчитатьСтрокуТабличнойЧастиПоВалюте", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'В текущей строке поменялась валюта! Пересчитать тарифы в этой строке?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	ИначеЕсли Колонка = "Сумма" Тогда
		СтрокаТабличнойЧасти = Элементы.Сотрудники.ТекущиеДанные;
		СтрокаТабличнойЧасти.СуммаВсего = КоличествоДней * (СтрокаТабличнойЧасти.Суточные + СтрокаТабличнойЧасти.Проживание) + СтрокаТабличнойЧасти.Проездные;
	КонецЕсли;
КонецПроцедуры 

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервереБезКонтекста
Функция КурсВалюты(Валюта, Дата)

	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата).Курс;	

КонецФункции // ()

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"ФизЛицо").ВыгрузитьКолонку("ФизЛицо"), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбработкаПодбораНаСервере(Сотрудники)
	
	Для Каждого ФизЛицо Из Сотрудники Цикл
		СтрокиТабличнойЧасти = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизЛицо", ФизЛицо));
		
		Если СтрокиТабличнойЧасти.Количество() = 0 Тогда
			СтрокаТабличнойЧасти = Объект.Сотрудники.Добавить();
			СтрокаТабличнойЧасти.ФизЛицо = ФизЛицо;
		КонецЕсли;
	КонецЦикла;
	
	ДополнитьСтрокиНаСервере(Сотрудники);
КонецПроцедуры

// Процедура заполняет строки
//
// Параметры:
//  Сотрудники  - Массив - массив физ.лиц, по которым нужно заполнить строки
//
&НаСервере
Процедура ДополнитьСтрокиНаСервере(Сотрудники)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	СотрудникиСрезПоследних.Должность,
		|	&НачалоПериода КАК ДатаНачала,
		|	&КонецПериода КАК ДатаОкончания,
		|	СотрудникиСрезПоследних.Подразделение,
		|	СотрудникиСрезПоследних.ГрафикРаботы
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&ДатаК,
		|			Организация = &Организация
		|				И ФизЛицо В (&Сотрудники)) КАК СотрудникиСрезПоследних
		|";
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", Объект.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ДатаОкончания));
	Запрос.УстановитьПараметр("ДатаК", ДатаДокумента);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтруктураОтбора = ПроведениеРасчетовПоЗарплатеСервер.СтруктураОтбораДанныхСчетовУчетаПоЗП();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокиТабличнойЧасти = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизЛицо", ВыборкаДетальныеЗаписи.ФизЛицо));
		
		Для Каждого СтрокаТабличнойЧасти Из СтрокиТабличнойЧасти Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи);
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТабличнойЧасти);
			СтруктураОтбора.КатегорияДолжности = СтрокаТабличнойЧасти.Должность.Категория;
	
			ДанныеСчетаУчетаЗП = ПроведениеРасчетовПоЗарплатеСервер.ДанныеСчетаУчетаЗП(СтруктураОтбора);
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСчетаУчетаЗП);
			
			СтрокаТабличнойЧасти.Город = Объект.Город;
		КонецЦикла;		
	КонецЦикла;
	
	РасчетТарифов();
КонецПроцедуры // ДополнитьСтрокиНаСервере()

// Функция возвращает ответ пользователя о возможности записи/отмене проведения документа перед рассчетом
//
// Параметры:
//  Действие - действие, при котором выполняется проверка
// Возвращаемое значение:
//   Булево - 
//
&НаКлиенте
Функция ЗаписатьДокументОтменивПроведение()
	Если Объект.Проведен Тогда
		ЗаписатьНаСервере(РежимЗаписиДокумента.ОтменаПроведения);		
	ИначеЕсли Модифицированность 
		Или ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ДатаДокумента;
		ЗаписатьНаСервере(РежимЗаписиДокумента.Запись);		
	КонецЕсли;	
	
	Возврат Истина;
КонецФункции // ЗаписатьДокументОтменивПроведение()

// Процедура - Записать на сервере
//
// Параметры:
//  Режим	 - 	 - 
//
&НаСервере
Процедура ЗаписатьНаСервере(Режим)
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Записать(Режим);
	ЗначениеВРеквизитФормы(Документ, "Объект");
КонецПроцедуры // ЗаписатьНаСервере()

&НаСервере
Процедура ЗаполнитьПоПодразделениюНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	СотрудникиСрезПоследних.Должность,
		|	СотрудникиСрезПоследних.Подразделение,
		|	&Дни
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(&Дата, Организация = &Организация) КАК СотрудникиСрезПоследних
		|ГДЕ
		|	НЕ СотрудникиСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|	И СотрудникиСрезПоследних.Подразделение = &Подразделение";
	Запрос.УстановитьПараметр("Дата", 			ДатаДокумента);
	Запрос.УстановитьПараметр("Организация", 	Объект.Организация);
	Запрос.УстановитьПараметр("Подразделение", 	Объект.Подразделение);
	Запрос.УстановитьПараметр("Дни", 			Объект.КоличествоДней);
	
	РезультатЗапроса = Запрос.Выполнить();
	Объект.Сотрудники.Загрузить(РезультатЗапроса.Выгрузить());	
	
	СтруктураОтбора = ПроведениеРасчетовПоЗарплатеСервер.СтруктураОтбораДанныхСчетовУчетаПоЗП();
	
	// Определение счетов учета
	Для Каждого СтрокаТабличнойЧасти Из Объект.Сотрудники Цикл 
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТабличнойЧасти);
		СтруктураОтбора.КатегорияДолжности = СтрокаТабличнойЧасти.Должность.Категория;
		СтрокаТабличнойЧасти.Город = Объект.Город;

		ДанныеСчетаУчетаЗП = ПроведениеРасчетовПоЗарплатеСервер.ДанныеСчетаУчетаЗП(СтруктураОтбора);
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСчетаУчетаЗП);
	КонецЦикла;	
КонецПроцедуры // ЗаполнитьПоподразделениюНаСервере()

// Процедура установки города по юридическому адресу организации
//
&НаСервере
Процедура УстановитьГородПоОрганизации()
	ЮрАдресОрганизации 	= БухгалтерскийУчетСервер.ПолучитьСведенияОбОрганизации(Объект.Организация);
	ГородОрганизации 	= Справочники.Города.НайтиПоНаименованию(ЮрАдресОрганизации.Город);
	
	Если НЕ ГородОрганизации = Справочники.Города.Бишкек Тогда
		Объект.Город = Справочники.Города.Бишкек;	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти  
