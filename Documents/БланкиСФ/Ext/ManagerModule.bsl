#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыИФункцииОбщегоНазначения

Функция ЧислоВСтроку(Число, ДлинаСтроки)
	
	ПараметрыФорматирования = СтрШаблон("ЧЦ=%1; ЧВН=", ДлинаСтроки);	
	Возврат СтрЗаменить(Формат(Число, ПараметрыФорматирования), Символы.НПП, "");
	
КонецФункции // ЧислоВСтроку()

Функция ПолучитьТаблицуЗначенийБланкиСИндивидуальнымиНомера(ДокументСсылка)

	ТаблицаЗначений = Новый ТаблицаЗначений;
	КЧ = Новый КвалификаторыЧисла(15,2);
	КС = Новый КвалификаторыСтроки(10);
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповС = Новый ОписаниеТипов(Массив, , КС);
	
	Массив.Очистить();
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,КЧ);
	
	Массив.Очистить();
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповСерииБланковСФ = Новый ОписаниеТипов(Массив);	
	
	Массив.Очистить();
	Массив.Добавить(Тип("ПеречислениеСсылка.ФорматыБланковСФ"));
	ОписаниеТиповФорматыБланковСФ = Новый ОписаниеТипов(Массив);	
	
	Массив.Очистить();
	Массив.Добавить(Тип("ДокументСсылка.БланкиСФ"));
	ОписаниеТиповДокументБланкиСФ = Новый ОписаниеТипов(Массив);
	
	Массив.Очистить();
	Массив.Добавить(Тип("Булево"));
	ОписаниеТиповБулево = Новый ОписаниеТипов(Массив);	
	
	ТаблицаЗначений.Колонки.Добавить("Серия",		ОписаниеТиповСерииБланковСФ);
	ТаблицаЗначений.Колонки.Добавить("Номер",		ОписаниеТиповС,"Номер",10);
	ТаблицаЗначений.Колонки.Добавить("Количество",	ОписаниеТиповЧ,"Количество",10);
	ТаблицаЗначений.Колонки.Добавить("Формат",		ОписаниеТиповФорматыБланковСФ);
	ТаблицаЗначений.Колонки.Добавить("Ручные",		ОписаниеТиповБулево);	
	ТаблицаЗначений.Колонки.Добавить("Ссылка",		ОписаниеТиповДокументБланкиСФ);
	
	Для каждого СтрокаТабличнойЧасти Из ДокументСсылка.Спецификация Цикл
		ДлинаСтроки = СтрДлина(СтрокаТабличнойЧасти.ПервыйНомер);

		Для Счетчик = Число(СтрокаТабличнойЧасти.ПервыйНомер) По Число(СтрокаТабличнойЧасти.ПоследнийНомер) Цикл
			СтрокаТаблицыЗначений = ТаблицаЗначений.Добавить();
			СтрокаТаблицыЗначений.Ссылка 		= ДокументСсылка;
			СтрокаТаблицыЗначений.Серия 		= СтрокаТабличнойЧасти.Серия;
			СтрокаТаблицыЗначений.Номер 		= ЧислоВСтроку(Счетчик, ДлинаСтроки);
			СтрокаТаблицыЗначений.Формат 		= СтрокаТабличнойЧасти.Формат;
			СтрокаТаблицыЗначений.Ручные 		= СтрокаТабличнойЧасти.Ручные;
		    СтрокаТаблицыЗначений.Количество	= 1;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаЗначений;

КонецФункции // ()

#КонецОбласти

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаБланкиСФ(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,		
		|	ВременнаяТаблицаЗначенийНомеровБланков.Серия,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Номер,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Формат,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Количество,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Ручные,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
		|ИЗ
		|	ВременнаяТаблицаЗначенийНомеровБланков КАК ВременнаяТаблицаЗначенийНомеровБланков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ПриходБланковСФ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция,				
		|	ВременнаяТаблицаЗначенийНомеровБланков.Серия,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Номер,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Формат,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Количество,
		|	ВременнаяТаблицаЗначенийНомеровБланков.Ручные,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|ИЗ
		|	ВременнаяТаблицаЗначенийНомеровБланков КАК ВременнаяТаблицаЗначенийНомеровБланков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаШапка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСФ.ПриходБланковСФ)";		
		
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаБланкиСФ", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаСотрудники()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	ТаблицаЗначений = ПолучитьТаблицуЗначенийБланкиСИндивидуальнымиНомера(ДокументСсылка);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	БланкиСФ.Организация,
		|	БланкиСФ.Дата КАК Период,
		|	БланкиСФ.Ссылка,
		|	БланкиСФ.Операция
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.БланкиСФ КАК БланкиСФ
		|ГДЕ
		|	БланкиСФ.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БланкиСФСпецификация.Ссылка
		|ПОМЕСТИТЬ ВременнаяТаблицаСпецификация
		|ИЗ
		|	Документ.БланкиСФ.Спецификация КАК БланкиСФСпецификация
		|ГДЕ
		|	БланкиСФСпецификация.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗначений.Ссылка,
		|	ТаблицаЗначений.Серия,
		|	ТаблицаЗначений.Номер,
		|	ТаблицаЗначений.Ручные,
		|	ТаблицаЗначений.Формат,
		|	ТаблицаЗначений.Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаЗначенийНомеровБланков
		|ИЗ
		|	&ТаблицаЗначений КАК ТаблицаЗначений
		|ГДЕ
		|	ТаблицаЗначений.Ссылка = &Ссылка";	
	Запрос.УстановитьПараметр("Ссылка", 			ДокументСсылка);
	Запрос.УстановитьПараметр("ТаблицаЗначений", 	ТаблицаЗначений);
	Запрос.Выполнить();
	
	СформироватьТаблицаБланкиСФ(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#КонецЕсли