#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению) КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_НеПодтвержденный) КАК СчетКт,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК ВалютнаяСуммаКт,
		|	""НДС"" КАК Содержание,
		|	0 КАК КоличествоД,
		|	0 КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.СуммаНДС <> 0
		|	И ВременнаяТаблицаШапка.НДСНеПодтвержден = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицаСчетаФактурыПолученные(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Дата КАК Период,
	|	ВременнаяТаблицаШапка.Организация КАК Организация,
	|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаТовары.ДокументПоступления КАК Документ,
	|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК СерияБланкаСФ,
	|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
	|	ВременнаяТаблицаШапка.Контрагент.ПризнакСтраны КАК ПризнакСтраны,
	|	СУММА(ВременнаяТаблицаТовары.Всего - ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаСебестоимости,
	|	СУММА(ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВременнаяТаблицаТовары.СуммаНСП) КАК СуммаНеоблагаемая,
	|	ВременнаяТаблицаШапка.ДатаСФ КАК ДатаСФ
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Контрагент,
	|	ВременнаяТаблицаШапка.Дата,
	|	ВременнаяТаблицаТовары.ДокументПоступления,
	|	ВременнаяТаблицаШапка.НомерБланкаСФ,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаШапка.Контрагент.ПризнакСтраны,
	|	ВременнаяТаблицаШапка.СерияБланкаСФ,
	|	ВременнаяТаблицаШапка.ДатаСФ";
		
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСчетаФактурыПолученные", РезультатЗапроса.Выгрузить());	
		
КонецПроцедуры

Процедура СформироватьТаблицаРеестрПриобретенных(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Ссылка КАК Документ,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Контрагент.НаименованиеПолное = """"
		|			ТОГДА ВременнаяТаблицаШапка.Контрагент.Наименование
		|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент.НаименованиеПолное
		|	КОНЕЦ КАК КонтрагентНаименование,
		|	ВременнаяТаблицаШапка.Контрагент.ИНН КАК ИННКонтрагента,
		|	ВременнаяТаблицаШапка.Контрагент.ГНС.Код КАК КодГНСКонтрагента,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВременнаяТаблицаШапка.Дата КАК ДатаПоставки,
		|	СУММА(ВременнаяТаблицаТовары.Сумма) КАК Сумма,
		|	СУММА(ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВЫБОР
		|			КОГДА &УказыватьПризнакЗачетаНДС
		|					ИЛИ ВременнаяТаблицаТовары.ЗачетНДС <> ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				ТОГДА ВременнаяТаблицаТовары.СуммаНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаПодлежащаяКЗачетуНДС
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Контрагент.НаименованиеПолное = """"
		|			ТОГДА ВременнаяТаблицаШапка.Контрагент.Наименование
		|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент.НаименованиеПолное
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Ссылка,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.Контрагент.ИНН,
		|	ВременнаяТаблицаШапка.СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ,
		|	ВременнаяТаблицаШапка.Контрагент.ГНС.Код,
		|	ВременнаяТаблицаШапка.Организация";
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
		
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрПриобретенных", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СчетФактураПолученный.Ссылка,
		|	СчетФактураПолученный.Номер,
		|	СчетФактураПолученный.Дата,
		|	СчетФактураПолученный.Организация,
		|	СчетФактураПолученный.ВалютаДокумента,
		|	СчетФактураПолученный.Контрагент,
		|	СчетФактураПолученный.СуммаДокумента,
		|	СчетФактураПолученный.СерияБланкаСФ,
		|	СчетФактураПолученный.НомерБланкаСФ,
		|	СчетФактураПолученный.НДСНеПодтвержден,
		|	СчетФактураПолученный.ЗначениеСтавкиНДС,
		|	СчетФактураПолученный.ЗначениеСтавкиНСП,
		|	СчетФактураПолученный.БезналичныйРасчет,
		|	СчетФактураПолученный.ДатаСФ
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|ГДЕ
		|	СчетФактураПолученный.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетФактураПолученныйТовары.Ссылка,
		|	СчетФактураПолученныйТовары.Номенклатура,
		|	СчетФактураПолученныйТовары.ЕдиницаИзмерения,
		|	СчетФактураПолученныйТовары.Количество,
		|	СчетФактураПолученныйТовары.Цена,
		|	СчетФактураПолученныйТовары.Сумма,
		|	СчетФактураПолученныйТовары.СчетУчета,
		|	СчетФактураПолученныйТовары.СуммаНДС,
		|	СчетФактураПолученныйТовары.СуммаНСП,
		|	СчетФактураПолученныйТовары.Всего,
		|	СчетФактураПолученныйТовары.КоэффициентДопЕдиницы,
		|	СчетФактураПолученныйТовары.КоличествоДопЕдиницы,
		|	СчетФактураПолученныйТовары.ЗачетНДС,
		|	СчетФактураПолученныйТовары.ДокументПоступления
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.СчетФактураПолученный.Товары КАК СчетФактураПолученныйТовары
		|ГДЕ
		|	СчетФактураПолученныйТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();
	
	СформироватьТаблицаСчетаФактурыПолученные(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрПриобретенных(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли