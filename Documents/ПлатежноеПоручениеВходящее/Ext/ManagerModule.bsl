#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьНазначениеПлатежа(Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную, ТолькоСумму = Ложь) Экспорт
	
	ТекстНазначение = Объект.НазначениеПлатежа;
	
	ПозицияСумма = Найти(ТекстНазначение, "Сумма");
	
	Если ПозицияСумма = 0 Тогда // для совместимости, при редактировании старых платежек
		ПозицияСумма = Найти(ТекстНазначение, "В т.ч. НДС");
		Если ПозицияСумма = 0 Тогда
			ПозицияСумма = Найти(ТекстНазначение, "Без налога (НДС)");
		КонецЕсли;
	КонецЕсли;
	
	Если ТолькоСумму Тогда
		
		ТекстНазначение = ?(ПозицияСумма = 0, ТекстНазначение, Лев(ТекстНазначение, ПозицияСумма - 1));
		Если Прав(ТекстНазначение, 1) = Символы.ПС Тогда
			ТекстНазначение = Лев(ТекстНазначение, СтрДлина(ТекстНазначение) - 1);
		КонецЕсли;
		
	Иначе
		
		ТекстНазначенияАвто = ТекстНазначенияПлатежа(Объект.БанковскийСчетПолучателя, Объект.ДоговорКонтрагента, Ложь);
		Если НазначениеПлатежаБылоИзмененоВручную = Ложь ИЛИ ПустаяСтрока(ТекстНазначение) Тогда
			ТекстНазначение = "";
			Если ЗначениеЗаполнено(АвтоЗначенияРеквизитов.ТекстНазначенияПлатежа) Тогда
				// Менять только если значение назначения платежа не было ранее введено вручную
				ТекстНазначение = АвтоЗначенияРеквизитов.ТекстНазначенияПлатежа;
			Иначе
				ТекстНазначение = ТекстНазначенияАвто;
			КонецЕсли;
			
			НазначениеПлатежаБылоИзмененоВручную = Ложь;
		Иначе
			ТекстНазначение = ?(ПозицияСумма = 0, ТекстНазначение, Лев(ТекстНазначение, ПозицияСумма - 1));
			Если Прав(ТекстНазначение, 1) = Символы.ПС Тогда
				ТекстНазначение = Лев(ТекстНазначение, СтрДлина(ТекстНазначение) - 1);
			КонецЕсли;
			
			АвтоЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", ТекстНазначенияАвто);
			НазначениеПлатежаБылоИзмененоВручную = НазначениеПлатежаИзмененоВручную(ТекстНазначение, ТекстНазначенияАвто);
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстСумма = "Сумма " + Формат(Объект.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ=");

	ТекстНДС = ""; 
	
	ТекстСуммаНазначения = ТекстСумма + Символы.ПС + ТекстНДС;
	
	Объект.НазначениеПлатежа = ТекстНазначение
		+ ?(ПустаяСтрока(ТекстСуммаНазначения), "", Символы.ПС + ТекстСуммаНазначения);

КонецПроцедуры // СформироватьНазначениеПлатежа()
	
Функция ТекстНазначенияПлатежа(Знач СчетКонтрагентаНазначения, Знач ДоговорКонтрагентаНазначения, ПеречислениеВБюджет) Экспорт
	
	Если ЗначениеЗаполнено(СчетКонтрагентаНазначения.ТекстНазначения) Тогда
		
		Возврат СчетКонтрагентаНазначения.ТекстНазначения;
		
	ИначеЕсли ПеречислениеВБюджет Тогда
		
		// Как правило, для формирования осмысленного текста недостаточно данных.
		// Кроме того, договор не виден на форме.
		
		Возврат "";
		
	ИначеЕсли ЗначениеЗаполнено(ДоговорКонтрагентаНазначения) Тогда
		
		ПараметрыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагентаНазначения, "Наименование, ВидДоговора");
		
		Если ПараметрыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда
			ТекстНазначение = НСтр("ru = 'Возврат оплаты по договору'");
		Иначе
			ТекстНазначение = НСтр("ru = 'Оплата по договору'");
		КонецЕсли;
		
		Если ПустаяСтрока(ПараметрыДоговора.Наименование) Тогда
			Возврат ТекстНазначение;
		Иначе
			Возврат СтрШаблон(
				НСтр("ru = '%1 %2'"), 
				ТекстНазначение,
				СокрЛП(ПараметрыДоговора.Наименование));
		КонецЕсли;
		
	Иначе
		
		Возврат НСтр("ru = 'Оплата по счету'");
		
	КонецЕсли;
	
КонецФункции

Функция НазначениеПлатежаИзмененоВручную(НазначениеПлатежа, НазначениеПлатежаАвто) Экспорт
	
	Если ПустаяСтрока(НазначениеПлатежа) ИЛИ ПустаяСтрока(НазначениеПлатежаАвто) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПозицияСумма = Найти(НазначениеПлатежа, "Сумма");
	
	Если ПозицияСумма = 0 Тогда // для совместимости, при редактировании старых платежек
		ПозицияСумма = Найти(НазначениеПлатежа, "В т.ч. НДС");
		Если ПозицияСумма = 0 Тогда
			ПозицияСумма = Найти(НазначениеПлатежа, "Без налога (НДС)");
		КонецЕсли;
	КонецЕсли;
	
	ТекстНазначение = ?(ПозицияСумма = 0, НазначениеПлатежа, Лев(НазначениеПлатежа, ПозицияСумма - 1));
	Если Прав(ТекстНазначение, 1) = Символы.ПС Тогда
		ТекстНазначение = Лев(ТекстНазначение, СтрДлина(ТекстНазначение) - 1);
	КонецЕсли;
	
	Возврат СокрЛП(ТекстНазначение) <> СокрЛП(НазначениеПлатежаАвто);
	
КонецФункции

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетДт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоДт1,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоДт2,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.ВалютаДоговора
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета.Валютный
		|			ТОГДА ВЫБОР
		|					КОГДА ВременнаяТаблицаШапка.Курс >= ВременнаяТаблицаШапка.КурсДоговора
		|						ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
		|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа / ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаКт,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция.Наименование + ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1024))) = """"
		|			ТОГДА """"
		|		ИНАЧЕ "" - ""
		|	КОНЕЦ + (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1024))) КАК Содержание,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	(ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ОплатаОтПокупателя)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ВозвратОтПоставщика)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.РасчетыПоЗаймам))
		|	И ВЫБОР
		|			КОГДА ВременнаяТаблицаШапка.ВалютаДоговора = &ВалютаРегламентированногоУчета
		|				ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа
		|			ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.Курс
		|		КОНЕЦ <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета,
		|	ВременнаяТаблицаПрочиеПриходы.СчетУчета,
		|	ВременнаяТаблицаШапка.БанковскийСчет,
		|	ВременнаяТаблицаПрочиеПриходы.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаПрочиеПриходы.Субконто1,
		|	ВременнаяТаблицаПрочиеПриходы.Субконто2,
		|	ВременнаяТаблицаПрочиеПриходы.Субконто3,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаПрочиеПриходы.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаПрочиеПриходы.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция.Наименование + ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1024))) = """"
		|			ТОГДА """"
		|		ИНАЧЕ "" - ""
		|	КОНЕЦ + (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1024))),
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Курс = 0
		|			ТОГДА ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа
		|		ИНАЧЕ ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа * ВременнаяТаблицаШапка.Курс
		|	КОНЕЦ,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаПрочиеПриходы КАК ВременнаяТаблицаПрочиеПриходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаПрочиеПриходы.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ПрочийПриход)
		|	И ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетУчета,
		|	ВременнаяТаблицаШапка.БанковскийСчет,
		|	ВременнаяТаблицаШапка.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств,
		|	ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств,
		|	ВременнаяТаблицаШапка.СуммаДокумента,
		|	ВременнаяТаблицаШапка.СуммаДокумента,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Операция.Наименование + ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1024))) = """"
		|			ТОГДА """"
		|		ИНАЧЕ "" - ""
		|	КОНЕЦ + (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1024))),
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Курс = 0
		|			ТОГДА ВременнаяТаблицаШапка.СуммаДокумента
		|		ИНАЧЕ ВременнаяТаблицаШапка.СуммаДокумента * ВременнаяТаблицаШапка.Курс
		|	КОНЕЦ,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ВозвратОтСорудника)";
			
	//Операционная курсовая разница
	ТекстОКР = "";
	Если НЕ Константы.НеСчитатьОперационныеКурсовыеРазницы.Получить() Тогда	
		Если ДокументСсылка.ВалютаДенежныхСредств <> ВалютаРегламентированногоУчета И ДокументСсылка.ВалютаДоговора <> ВалютаРегламентированногоУчета Тогда
			ТекстОКР =
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс < 0
			|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте)
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|	КОНЕЦ КАК СчетДт,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс < 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте)
			|	КОНЕЦ КАК СчетКт,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс < 0
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиЗатратИДоходов.КурсовыеРазницы)
			|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент
			|	КОНЕЦ КАК СубконтоДт1,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс < 0
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|	КОНЕЦ КАК СубконтоДт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс < 0
			|			ТОГДА ВременнаяТаблицаШапка.Контрагент
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатратИДоходов.КурсовыеРазницыДоходы)
			|	КОНЕЦ КАК СубконтоКт1,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс < 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК СубконтоКт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
			|	&ВалютаРегламентированногоУчета КАК Поле5,
			|	ВременнаяТаблицаШапка.ВалютаДоговора КАК Поле6,
			|	0 КАК ВалютнаяСуммаДт,
			|	0 КАК ВалютнаяСуммаКт,
			|	ВременнаяТаблицаШапка.Дата,
			|	ВременнаяТаблицаШапка.Организация,
			|	ВременнаяТаблицаШапка.СодержаниеОКР КАК Содержание,
			|	ВЫБОР
			|		КОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс > 0
			|			ТОГДА (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс
			|		ИНАЧЕ -((ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс)
			|	КОНЕЦ КАК Сумма,
			|	0 КАК Поле3,
			|	0 КАК Поле9
			|ИЗ
			|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
			|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
			|ГДЕ
			|	НЕ (ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов - ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВЫБОР
			|				КОГДА ВременнаяТаблицаШапка.КурсДоговора > ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов
			|					ТОГДА ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.КурсДоговора
			|				ИНАЧЕ ВременнаяТаблицаШапка.КурсДоговора / ВременнаяТаблицаШапка.Курс
			|			КОНЕЦ) * ВременнаяТаблицаШапка.Курс = 0
			|	И НЕ ВременнаяТаблицаШапка.ВалютаДоговора = ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
			|	И НЕ ВременнаяТаблицаШапка.КурсДоговора = ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов";
		Иначе
			ТекстОКР = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора < 0
			|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте)
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|	КОНЕЦ КАК СчетДт,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора < 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчета
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте)
			|	КОНЕЦ КАК СчетКт,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора < 0
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиЗатратИДоходов.КурсовыеРазницы)
			|		ИНАЧЕ ВременнаяТаблицаШапка.Контрагент
			|	КОНЕЦ КАК СубконтоДт1,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора < 0
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|	КОНЕЦ КАК СубконтоДт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора < 0
			|			ТОГДА ВременнаяТаблицаШапка.Контрагент
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатратИДоходов.КурсовыеРазницыДоходы)
			|	КОНЕЦ КАК СубконтоКт1,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора < 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.ДоговорКонтрагента
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК СубконтоКт2,
			|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
			|	&ВалютаРегламентированногоУчета КАК Поле5,
			|	ВременнаяТаблицаШапка.ВалютаДоговора КАК Поле6,
			|	0 КАК ВалютнаяСуммаДт,
			|	0 КАК ВалютнаяСуммаКт,
			|	ВременнаяТаблицаШапка.Дата,
			|	ВременнаяТаблицаШапка.Организация,
			|	ВременнаяТаблицаШапка.СодержаниеОКР КАК Содержание,
			|	ВЫБОР
			|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора > 0
			|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора
			|		ИНАЧЕ -(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора)
			|	КОНЕЦ КАК Сумма,
			|	0 КАК Поле3,
			|	0 КАК Поле9
			|ИЗ
			|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
			|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
			|ГДЕ
			|	НЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс - ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсДоговора = 0
			|	И НЕ ВременнаяТаблицаШапка.ВалютаДоговора = ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
			|	И НЕ ВременнаяТаблицаШапка.КурсДоговора = ВременнаяТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов";
			
		КонецЕсли;
		ТекстОКР = 
		"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|"
		+ ТекстОКР;	
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ТекстОКР;	
	
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ДвиженияДенежныхСредств.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДДС(СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК Статья,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Дата КАК Период
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРасшифровкаПлатежа.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	(ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ОплатаОтПокупателя)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ВозвратОтПоставщика)
		|			ИЛИ ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.РасчетыПоЗаймам))
		|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПрочиеПриходы.СтатьяДвиженияДенежныхСредств,
		|	ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Дата
		|ИЗ
		|	ВременнаяТаблицаПрочиеПриходы КАК ВременнаяТаблицаПрочиеПриходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаПрочиеПриходы.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ПрочийПриход)
		|	И ВременнаяТаблицаПрочиеПриходы.СуммаПлатежа <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.СтатьяДвиженияДенежныхСредств,
		|	ВременнаяТаблицаШапка.СуммаДокумента * ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Дата
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ВозвратОтСорудника)
		|	И ВременнаяТаблицаШапка.СуммаДокумента <> 0";
			
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДДС", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ВозвратДенежныхСредствПодотчетником.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОтразитьВозвратПодотчетником(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.СуммаДокумента КАК Сумма,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.ФизЛицо КАК ФизЛицо
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППВ.ВозвратОтСорудника)";	
		
		
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОтразитьВозвратПодотчетником", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	
		"ВЫБРАТЬ
		|	ПлатежноеПоручениеВходящее.Ссылка,
		|	ПлатежноеПоручениеВходящее.ВерсияДанных,
		|	ПлатежноеПоручениеВходящее.ПометкаУдаления,
		|	ПлатежноеПоручениеВходящее.Номер,
		|	ПлатежноеПоручениеВходящее.Дата,
		|	ПлатежноеПоручениеВходящее.Проведен,
		|	ПлатежноеПоручениеВходящее.Организация,
		|	ПлатежноеПоручениеВходящее.Операция,
		|	ПлатежноеПоручениеВходящее.БанковскийСчет,
		|	ПлатежноеПоручениеВходящее.СтатьяДвиженияДенежныхСредств,
		|	ПлатежноеПоручениеВходящее.Контрагент,
		|	ПлатежноеПоручениеВходящее.СчетУчета,
		|	ПлатежноеПоручениеВходящее.БанковскийСчетПлательщика,
		|	ПлатежноеПоручениеВходящее.ФизЛицо,
		|	ПлатежноеПоручениеВходящее.ВалютаДенежныхСредств,
		|	ПлатежноеПоручениеВходящее.Курс,
		|	ПлатежноеПоручениеВходящее.ВалютаДоговора,
		|	ПлатежноеПоручениеВходящее.КурсДоговора,
		|	ПлатежноеПоручениеВходящее.НазначениеПлатежа,
		|	ПлатежноеПоручениеВходящее.ДокументОснование,
		|	ПлатежноеПоручениеВходящее.СуммаДокумента,
		|	ПлатежноеПоручениеВходящее.Комментарий,
		|	ПлатежноеПоручениеВходящее.Автор,
		|	ПлатежноеПоручениеВходящее.Субконто1,
		|	ПлатежноеПоручениеВходящее.Субконто2,
		|	ПлатежноеПоручениеВходящее.Субконто3,
		|	ПлатежноеПоручениеВходящее.КартСчет,
		|	&СодержаниеОКР КАК СодержаниеОКР,
		|	&СчетКР КАК СчетКР,
		|	&СубконтоКР КАК СубконтоКР
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ПлатежноеПоручениеВходящее КАК ПлатежноеПоручениеВходящее
		|ГДЕ
		|	ПлатежноеПоручениеВходящее.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.Ссылка,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.НомерСтроки,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.ДоговорКонтрагента,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.СчетУчета,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.СуммаПлатежа,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.КурсВзаиморасчетов,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.Комментарий,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.СуммаВзаиморасчетов,
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.КурсНБКР
		|ПОМЕСТИТЬ ВременнаяТаблицаРасшифровкаПлатежа
		|ИЗ
		|	Документ.ПлатежноеПоручениеВходящее.РасшифровкаПлатежа КАК ПлатежноеПоручениеВходящееРасшифровкаПлатежа
		|ГДЕ
		|	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.Ссылка,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.НомерСтроки,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.СчетУчета,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.СтатьяДвиженияДенежныхСредств,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.СуммаПлатежа,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.Субконто1,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.Субконто2,
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.Субконто3
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочиеПриходы
		|ИЗ
		|	Документ.ПлатежноеПоручениеВходящее.ПрочиеПриходы КАК ПлатежноеПоручениеВходящееПрочиеПриходы
		|ГДЕ
		|	ПлатежноеПоручениеВходящееПрочиеПриходы.Ссылка = &Ссылка";				
		
	Запрос.УстановитьПараметр("Ссылка", 		ДокументСсылка);	
	Запрос.УстановитьПараметр("СчетКР", 		ПланыСчетов.Хозрасчетный.ДоходыОтОперационныхКурсовыхРазниц);
	Запрос.УстановитьПараметр("СубконтоКР", 	Справочники.СтатьиЗатратИДоходов.КурсовыеРазницыДоходы);
	Запрос.УстановитьПараметр("СодержаниеОКР", 	?(ДокументСсылка.РасшифровкаПлатежа.Количество() > 0, "ОКР. Курс НБКР = " + ДокументСсылка.РасшифровкаПлатежа[0].КурсНБКР, ""));	
	
	Запрос.Выполнить();
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДДС(СтруктураДополнительныеСвойства);
	СформироватьТаблицаОтразитьВозвратПодотчетником(СтруктураДополнительныеСвойства);

КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#КонецЕсли
