#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНачисления(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.ПериодРегистрации,
		|	ТаблицаДокумента.ДатаНачала КАК ПериодДействияНачало,
		|	КОНЕЦПЕРИОДА(ТаблицаДокумента.ДатаОкончания, ДЕНЬ) КАК ПериодДействияКонец,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ПроцентомЗаПредМесяц)
		|			ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.ДатаНачала, МЕСЯЦ, -1)
		|		ИНАЧЕ ТаблицаДокумента.ДатаНачала
		|	КОНЕЦ КАК БазовыйПериодНачало,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ПроцентомЗаПредМесяц)
		|			ТОГДА ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ТаблицаДокумента.ДатаОкончания, ДЕНЬ), МЕСЯЦ, -1)
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ТаблицаДокумента.ДатаОкончания, ДЕНЬ)
		|	КОНЕЦ КАК БазовыйПериодКонец,
		|	ТаблицаДокумента.ВидРасчета,
		|	ТаблицаДокумента.СчетУчетаЗатрат,
		|	ТаблицаДокумента.СтатьяЗатрат,
		|	ТаблицаДокумента.НомерСтроки,
		|	ТаблицаДокумента.ФизЛицо,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА ТаблицаДокумента.Подразделение
		|		ИНАЧЕ ВременнаяТаблицаШапка.Подразделение
		|	КОНЕЦ КАК Подразделение,
		|	ТаблицаДокумента.Должность,
		|	ТаблицаДокумента.Размер,
		|	ТаблицаДокумента.Результат,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ВидРасчета.ЗачетОтработанногоВремени
		|			ТОГДА ТаблицаДокумента.НормаЧасов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НормаЧасов,
		|	ТаблицаДокумента.ОтработаноДней,
		|	ТаблицаДокумента.ОтработаноЧасов,
		|	ТаблицаДокумента.ГрафикРаботы,
		|	ТаблицаДокумента.Комментарий
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисления КАК ТаблицаДокумента
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаДокумента.НомерСтроки";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисления", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСотрудники()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВзаиморасчетыССотрудниками(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВременнаяТаблицаШапка.ПериодРегистрации КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Результат КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисления КАК ТаблицаДокумента
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзаиморасчетыССотрудниками", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСотрудники()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.ПериодРегистрации,
		|	ТаблицаДокумента.Подразделение
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.НачислениеЗарплаты КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаНачала,
		|	ТаблицаДокумента.ДатаОкончания,
		|	ТаблицаДокумента.ВидРасчета,
		|	ТаблицаДокумента.ВидРасчета.СпособРасчета КАК СпособРасчета,
		|	ТаблицаДокумента.СчетУчетаЗатрат,
		|	ТаблицаДокумента.СтатьяЗатрат,
		|	ТаблицаДокумента.НомерСтроки,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Должность,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.Размер,
		|	ТаблицаДокумента.Результат,
		|	ТаблицаДокумента.НормаЧасов,		
		|	ТаблицаДокумента.ОтработаноДней,
		|	ТаблицаДокумента.ОтработаноЧасов,
		|	ТаблицаДокумента.ГрафикРаботы,
		|	ТаблицаДокумента.Комментарий
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
		|ИЗ
		|	Документ.НачислениеЗарплаты.Начисления КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();
	
	СформироватьТаблицаНачисления(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаВзаиморасчетыССотрудниками(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти


#КонецЕсли
