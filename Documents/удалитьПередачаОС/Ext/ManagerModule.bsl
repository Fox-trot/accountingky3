#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	НЕОПРЕДЕЛЕНО КАК СчетДт,
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет КАК СчетКт,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеСубконтоДт3,
		|	ТаблицаДокумента.ОсновноеСредство КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаДокумента.АмортизацияЗаМесяц КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ТаблицаДокумента.АмортизацияЗаМесяц КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ТаблицаДокумента.АмортизацияЗаМесяц КАК ВалютнаяСуммаКт,
		|	""Начисление износа за ОС"" КАК Содержание,
		|	1 КАК КоличествоДт,
		|	1 КАК КоличествоКт,
		|	ИСТИНА КАК РаспределятьПоКоэффициентам
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.АмортизацияЗаМесяц = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.СчетРасчетовПокупателя,
		|	ТаблицаДокумента.СчетУчета,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	""Списание стоимости ОС"",
		|	1,
		|	1,
		|	ЛОЖЬ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПередачаСИзносом
		|	И НЕ ТаблицаДокумента.ПервоначальнаяСтоимость = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.СчетРасчетовПокупателя,
		|	ТаблицаДокумента.СчетУчета,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость - ТаблицаДокумента.НакопленнаяАмортизация - ТаблицаДокумента.АмортизацияЗаМесяц,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость - ТаблицаДокумента.НакопленнаяАмортизация - ТаблицаДокумента.АмортизацияЗаМесяц,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость - ТаблицаДокумента.НакопленнаяАмортизация - ТаблицаДокумента.АмортизацияЗаМесяц,
		|	""Списание стоимости ОС"",
		|	1,
		|	1,
		|	ЛОЖЬ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаШапка.ПередачаСИзносом
		|	И НЕ ТаблицаДокумента.ПервоначальнаяСтоимость - ТаблицаДокумента.НакопленнаяАмортизация - ТаблицаДокумента.АмортизацияЗаМесяц = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет,
		|	ВременнаяТаблицаШапка.СчетРасчетовПокупателя,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц,
		|	""Накопленный износ ОС"",
		|	1,
		|	1,
		|	ЛОЖЬ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПередачаСИзносом
		|	И НЕ ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц,
		|	""Накопленный износ ОС"",
		|	1,
		|	1,
		|	ЛОЖЬ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаШапка.ПередачаСИзносом
		|	И НЕ ТаблицаДокумента.НакопленнаяАмортизация + ТаблицаДокумента.АмортизацияЗаМесяц = 0";
		
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	// Добавляем колонки в ТЗ для записи в рег. бухг.
	ТЗ.Колонки.Добавить("СубконтоДт1");
	ТЗ.Колонки.Добавить("СубконтоДт2");
	ТЗ.Колонки.Добавить("СубконтоДт3");
	
	КэшДанныхСпрСпособыОтрРасхПоАморт = Новый Соответствие();
	
	// Создаем результирующую ТЗ с теми же колонками, что и в запросе.
	ТаблицаХозрасчетный = ТЗ.СкопироватьКолонки();
	
	// Данный цикл перебирает строки ТЗ, полученные из запроса.
	//
	// 1. Если накопленная амортизация не равна нуля, тогда по данной строке идет проверка способа отражения 
	// расходов по амортизации. Обращаемся к табличной части данного справочника и в зависимости от количества 
	// строк в ТЧ строка ТЗ дублируется и записывается в результирующую ТЗ. Записывается счет затрат (СчетДт) и в 
	// зависимости от коэффициентов в строках ТЧ распределяется сумма. А также заполняются созданные колонки "СубконтоДт".
	//
	// 2. В остальных случаях распределять сумму по коэффициенам не нужно, поэтому мы просто копируем строку 
	// в исходную ТЗ и заполняем созданные колонки "СубконтоДт" значениями из колонок "ЗначениеСубконтоДт"
	Для Каждого СтрокаТаблицыЗначений Из ТЗ Цикл
		
		Если НЕ СтрокаТаблицыЗначений.РаспределятьПоКоэффициентам Тогда
			СтрокаРезультирующейТЗ = ТаблицаХозрасчетный.Добавить();			
			ЗаполнитьЗначенияСвойств(СтрокаРезультирующейТЗ, СтрокаТаблицыЗначений);
			
			СтрокаРезультирующейТЗ.СубконтоДт1 = СтрокаРезультирующейТЗ.ЗначениеСубконтоДт1;
			СтрокаРезультирующейТЗ.СубконтоДт2 = СтрокаРезультирующейТЗ.ЗначениеСубконтоДт2;
			СтрокаРезультирующейТЗ.СубконтоДт3 = СтрокаРезультирующейТЗ.ЗначениеСубконтоДт3;
			
			Продолжить;
		КонецЕсли;
		
		// В соответствие записывается: 
		// Ключ - ссылка на справочник "СпособыОтраженияРасходовПоАмортизации".
		// Значение - ТЗ из запроса к данному элементу справочника.
		// При каждом шаге основного цикла (перебор ТЗ из данных документа) проверяется наличие строки соответствия 
		// по ссылке на справочник, чтобы не обращаться к запросу каждый раз.
		ТЗСпособаОтражения = КэшДанныхСпрСпособыОтрРасхПоАморт.Получить(СтрокаТаблицыЗначений.СпособОтраженияРасходовПоАмортизации);
		
		Если ТЗСпособаОтражения = Неопределено Тогда
			ТЗСпособаОтражения = УправлениеВнеоборотнымиАктивамиПовтИсп.СпособыОтраженияРасходовПоАмортизации(СтрокаТаблицыЗначений.СпособОтраженияРасходовПоАмортизации); 
			КэшДанныхСпрСпособыОтрРасхПоАморт.Вставить(СтрокаТаблицыЗначений.СпособОтраженияРасходовПоАмортизации, ТЗСпособаОтражения);
		КонецЕсли;
		
		МассивКоэфициентов = ТЗСпособаОтражения.ВыгрузитьКолонку("Коэффициент");
		
		МассивСумм = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаТаблицыЗначений.Сумма, МассивКоэфициентов, 2);
		
		Если МассивСумм = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ИндексМассива = 0;
		Для Каждого СтрокаМассива Из МассивСумм Цикл
			СтрокаРезультирующейТЗ = ТаблицаХозрасчетный.Добавить();			
			ЗаполнитьЗначенияСвойств(СтрокаРезультирующейТЗ, СтрокаТаблицыЗначений);
			
			СтрокаРезультирующейТЗ.Сумма		   = СтрокаМассива;
			СтрокаРезультирующейТЗ.ВалютнаяСуммаДт = СтрокаМассива;
			СтрокаРезультирующейТЗ.ВалютнаяСуммаКт = СтрокаМассива;
			
			СтрокаРезультирующейТЗ.СчетДт		   = ТЗСпособаОтражения[ИндексМассива].СчетЗатрат;
			СтрокаРезультирующейТЗ.СубконтоДт1     = ТЗСпособаОтражения[ИндексМассива].Субконто1;
			СтрокаРезультирующейТЗ.СубконтоДт2     = ТЗСпособаОтражения[ИндексМассива].Субконто2;
			СтрокаРезультирующейТЗ.СубконтоДт3     = ТЗСпособаОтражения[ИндексМассива].Субконто3;
			
			ИндексМассива = ИндексМассива + 1;
		КонецЦикла;
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", ТаблицаХозрасчетный);	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Передано) КАК Состояние
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСостоянияОС", РезультатЗапроса.Выгрузить()); 
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ВременнаяТаблицаШапка.СобытиеОС КАК Событие,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ПередачаСИзносом,
		|	ВременнаяТаблицаШапка.Номер КАК НомерДокумента,
		|	""Передача ОС"" КАК НазваниеДокумента
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСобытияОС", РезультатЗапроса.Выгрузить());
КонецПроцедуры     

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДвижениеОСНУ(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ПараметрыУчетаОССрезПоследних.ГруппаНУ КАК ГруппаНУ,
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость КАК Выбытие,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость * (12 - МЕСЯЦ(ВременнаяТаблицаШапка.Дата) + 0.5) / 12 КАК ТекущееВыбытие,
		|   ТаблицаДокумента.ОстаточнаяСтоимость,
		|   ИСТИНА КАК Реализация
		|ИЗ
		|	ВременнаяТаблицаОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|				&Период,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ТаблицаДокумента.ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаОС КАК ТаблицаДокумента)) КАК ПараметрыУчетаОССрезПоследних
		|		ПО ТаблицаДокумента.ОсновноеСредство = ПараметрыУчетаОССрезПоследних.ОсновноеСредство";
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвижениеОСНУ", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаКорректировкиНУ(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Счет,
		|	Хозрасчетный.Вид
		|ПОМЕСТИТЬ ВременнаяТаблицаСчетов
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Временный = ИСТИНА
		|	И (Хозрасчетный.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ИЛИ Хозрасчетный.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаХозрасчетный.Период,
		|	ТаблицаХозрасчетный.Организация,
		|	ТаблицаХозрасчетный.СчетДт,
		|	ТаблицаХозрасчетный.СчетКт,
		|	ТаблицаХозрасчетный.Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаХозрасчетный
		|ИЗ
		|	&ТаблицаХозрасчетный КАК ТаблицаХозрасчетный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаХозрасчетный.Период КАК Период,
		|	ВременнаяТаблицаХозрасчетный.Организация КАК Организация,
		|	ВременнаяТаблицаХозрасчетный.СчетДт КАК Счет,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакСчета.Расходный)
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакСчета.Доходный)
		|	КОНЕЦ КАК ДоходРасход,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ПР) КАК ВидУчета,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА ВременнаяТаблицаХозрасчетный.Сумма
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА -ВременнаяТаблицаХозрасчетный.Сумма
		|	КОНЕЦ КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаКорректировкиНУ
		|ИЗ
		|	ВременнаяТаблицаХозрасчетный КАК ВременнаяТаблицаХозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов
		|		ПО ВременнаяТаблицаХозрасчетный.СчетДт = ВременнаяТаблицаСчетов.Счет
		|ГДЕ
		|   ВременнаяТаблицаХозрасчетный.СчетДт В (
		|										ВЫБРАТЬ
		|											ВременнаяТаблицаСчетов.Счет
		|										ИЗ
		|											ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаХозрасчетный.Период,
		|	ВременнаяТаблицаХозрасчетный.Организация,
		|	ВременнаяТаблицаХозрасчетный.СчетКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакСчета.Расходный)
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакСчета.Доходный)
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ПР),
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА -ВременнаяТаблицаХозрасчетный.Сумма
		|		КОГДА ВременнаяТаблицаСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА ВременнаяТаблицаХозрасчетный.Сумма
		|	КОНЕЦ		
		|ИЗ
		|	ВременнаяТаблицаХозрасчетный КАК ВременнаяТаблицаХозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов
		|		ПО ВременнаяТаблицаХозрасчетный.СчетКт = ВременнаяТаблицаСчетов.Счет
		|ГДЕ
		|   ВременнаяТаблицаХозрасчетный.СчетКт В (
		|										ВЫБРАТЬ
		|											ВременнаяТаблицаСчетов.Счет
		|										ИЗ
		|											ВременнаяТаблицаСчетов КАК ВременнаяТаблицаСчетов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаКорректировкиНУ.Период,
		|	ВременнаяТаблицаКорректировкиНУ.Организация,
		|	ВременнаяТаблицаКорректировкиНУ.Счет,
		|	ВременнаяТаблицаКорректировкиНУ.ДоходРасход,
		|	ВременнаяТаблицаКорректировкиНУ.ВидУчета,
		|	СУММА(ВременнаяТаблицаКорректировкиНУ.Сумма) КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаКорректировкиНУ КАК ВременнаяТаблицаКорректировкиНУ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаКорректировкиНУ.Счет,
		|	ВременнаяТаблицаКорректировкиНУ.ДоходРасход,
		|	ВременнаяТаблицаКорректировкиНУ.ВидУчета,
		|	ВременнаяТаблицаКорректировкиНУ.Организация,
		|	ВременнаяТаблицаКорректировкиНУ.Период";
	Запрос.УстановитьПараметр("ТаблицаХозрасчетный", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаХозрасчетный);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаКорректировкиНУ", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.СобытиеОС,
		|	ТаблицаДокумента.Контрагент,
		|	ТаблицаДокумента.ДоговорКонтрагента,
		|	ТаблицаДокумента.СтатьяЗатрат,
		|	ТаблицаДокумента.Номер,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.СчетРасчетовПокупателя,
		|	ЛОЖЬ КАК ПередачаСИзносом
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ПередачаОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.ПервоначальнаяСтоимость,
		|	ТаблицаДокумента.ЛиквидационнаяСтоимость,
		|	ТаблицаДокумента.ОстаточнаяСтоимость,
		|	ТаблицаДокумента.НакопленнаяАмортизация,
		|	ТаблицаДокумента.АмортизацияЗаМесяц,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	Документ.ПередачаОС.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();      	
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);			 
	СформироватьТаблицаСостоянияОС(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСобытияОС(ДокументСсылка, СтруктураДополнительныеСвойства); 	
	СформироватьТаблицаДвижениеОСНУ(ДокументСсылка, СтруктураДополнительныеСвойства); 
	СформироватьТаблицаКорректировкиНУ(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует табличный документ с печатной формой Накладной на передачу ОС
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьНакладной(МассивОбъектов, ОбъектыПечати)
	
	//ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПередачаОС_Накладная";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПередачаОС.ПФ_MXL_Накладная");

	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.Номер,
		|	ТаблицаДокумента.Дата,
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.Организация.НаименованиеПолное КАК ОрганизацияПредставление,
		|	ТаблицаДокумента.Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокумента.Подразделение) КАК СкладПредставление,
		|	ТаблицаДокумента.Контрагент,
		|	ТаблицаДокумента.Контрагент.НаименованиеПолное КАК КонтрагентПредставление,
		|	ТаблицаДокумента.ОС.(
		|		НомерСтроки,
		|		ОсновноеСредство,
		|		ОсновноеСредство.НаименованиеПолное КАК ОсновноеСредствоПредставление,
		|		Сумма,
		|		СуммаНДС,
		|		СуммаНСП,
		|		0 КАК СуммаСкидки
		|	)
		|ИЗ
		|	Документ.ПередачаОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В(&СписокДокументов)";		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'Накладная на передачу'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ОрганизацияПредставление", Шапка.ОрганизацияПредставление);
		ДанныеПечати.Вставить("СкладПредставление", Шапка.СкладПредставление);
		ДанныеПечати.Вставить("ПолучательПредставление", Шапка.КонтрагентПредставление);

		ТаблицаОС = Шапка.ОС.Выгрузить();
		
		Всего = ТаблицаОС.Итог("Сумма");
		ВсегоНДС = ТаблицаОС.Итог("СуммаНДС");
		ВсегоНСП = ТаблицаОС.Итог("СуммаНСП");
		КоличествоНаименований = ТаблицаОС.Количество();		
		
		ВсегоСкидка = ТаблицаОС.Итог("СуммаСкидки");
		ЕстьСкидка = ВсегоСкидка <> 0;
		
		ДанныеПечати.Вставить("Всего", Всего);
		ДанныеПечати.Вставить("ВсегоНДС", ВсегоНДС);
		ДанныеПечати.Вставить("ВсегоНСП", ВсегоНСП);
		ДанныеПечати.Вставить("ИтоговаяСтрока", 
			СтрШаблон(НСтр("ru = 'Всего наименований %1, на сумму %2'"), 
			Формат(КоличествоНаименований, "ЧН=0; ЧГ=0"), Формат(ДанныеПечати.Всего, "ЧЦ=15; ЧДЦ=2")));
		ДанныеПечати.Вставить("СуммаПрописью", БухгалтерскийУчетСервер.СформироватьСуммуПрописью(ДанныеПечати.Всего));

		// Области
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Заголовок");
		Если ЕстьСкидка Тогда 
			МассивОбластейМакета.Добавить("ШапкаТаблицыСоСкидкой");
			МассивОбластейМакета.Добавить("СтрокаСоСкидкой");
		Иначе 
			МассивОбластейМакета.Добавить("ШапкаТаблицы");
			МассивОбластейМакета.Добавить("Строка");
		КонецЕсли;	
		МассивОбластейМакета.Добавить("Подвал");
		МассивОбластейМакета.Добавить("Итоги");
		МассивОбластейМакета.Добавить("ИтогиНДС");
		МассивОбластейМакета.Добавить("ИтогиНСП");
		Если ЕстьСкидка Тогда 
			МассивОбластейМакета.Добавить("ИтогиСкидка");
		КонецЕсли;	
		МассивОбластейМакета.Добавить("СуммаПрописью");
		МассивОбластейМакета.Добавить("Подписи");
	
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Строка" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			ИначеЕсли ИмяОбласти = "Строка" Тогда 
				Для Каждого СтрокаТаблицы Из ТаблицаОС Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;	
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует табличный документ с печатной формой Накладной на передачу ОС
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьАктаПриемаПередачи(МассивОбъектов, ОбъектыПечати)
	Перем Ошибки;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати 	= "ПередачаОС_АктПриемаПередачи";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПередачаОС.ПФ_MXL_АктПриемаПередачи");
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияОССрезПоследних.Период КАК ДатаПринятияКУчету,
	|	СостоянияОССрезПоследних.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.СостоянияОС.СрезПоследних(, Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)) КАК СостоянияОССрезПоследних
	|ГДЕ
	|	СостоянияОССрезПоследних.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				ПередачаОСОС.ОсновноеСредство
	|			ИЗ
	|				Документ.ПередачаОС.ОС КАК ПередачаОСОС
	|			ГДЕ
	|				ПередачаОСОС.Ссылка В (&МассивОбъектов))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПередачаОС.Ссылка,
	|	ПередачаОС.Номер,
	|	ПередачаОС.Дата,
	|	ПередачаОС.Организация,
	|	ПередачаОС.ОС.(
	|		Ссылка,
	|		НомерСтроки,
	|		ОсновноеСредство,
	|		ИнвентарныйНомер,
	|		ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|		ОсновноеСредство.ДатаВыпуска КАК ГодВыпуска,
	|		1 КАК КоличествоБУ,
	|		ОсновноеСредство.Наименование,
	|		ПервоначальнаяСтоимость КАК СтоимостьБУ,
	|		СрокСлужбы
	|	),
	|	ПередачаОС.Организация.НаименованиеПолное,
	|	ПередачаОС.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	|	ПередачаОС.ДоговорКонтрагента.Наименование,
	|	ПередачаОС.Подразделение.Наименование,
	|	ПередачаОС.МОЛ.Наименование,
	|	ПередачаОС.Комиссия.(
	|		Ссылка,
	|		НомерСтроки,
	|		ФизЛицо,
	|		Председатель
	|	)
	|ИЗ
	|	Документ.ПередачаОС КАК ПередачаОС
	|ГДЕ
	|	ПередачаОС.Ссылка В(&МассивОбъектов)";		
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);		
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	МассивЗначений = РезультатЗапроса[0].Выгрузить();
	Шапка = РезультатЗапроса[1].Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.Следующий() Цикл		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");		
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ДатаНомер	= "Списание ОС № " + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Шапка.Номер) + " от " + Формат(Шапка.Дата, "ДФ=dd.MM.yyyy");
		ОбластьМакета.Параметры.Дата		= Формат(Шапка.Дата, "ДФ=dd.MM.yyyy");
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ВременнаяТаблицаОС = Шапка.ОС.Выгрузить();
		Для каждого СтрокаТаблицы Из ВременнаяТаблицаОС Цикл		
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
			Структура= МассивЗначений.Найти(СтрокаТаблицы.ОсновноеСредство);
			Если НЕ Структура = Неопределено Тогда
				ОбластьМакета.Параметры.ДатаПринятияКУчету = Структура.ДатаПринятияКУчету;
			КонецЕсли;
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоТаблицы");
		ОбластьМакета.Параметры.КоличествоБУ = ВременнаяТаблицаОС.Итог("КоличествоБУ");
		ОбластьМакета.Параметры.СтоимостьБУ = ВременнаяТаблицаОС.Итог("СтоимостьБУ");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("СредняяОбласть");			
		ТабличныйДокумент.Вывести(ОбластьМакета);			
		
		//Выводим комиссию
		ВыборкаСтрокКомиссия = Шапка.Комиссия.Выбрать();
		ПервыйРядовойЧленКомиссиии = Истина;	
		
		Пока ВыборкаСтрокКомиссия.Следующий() Цикл
			Если ВыборкаСтрокКомиссия.Председатель Тогда
				ЗаголовокРазделаКомиссии = "Председатель комиссии";
			ИначеЕсли ПервыйРядовойЧленКомиссиии Тогда
				ЗаголовокРазделаКомиссии = "Члены комиссии";
				ПервыйРядовойЧленКомиссиии = Ложь;				
			Иначе
				ЗаголовокРазделаКомиссии = "";
			КонецЕсли;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Комиссия");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокКомиссия);
			ФИО = "";
			БухгалтерскийУчетСервер.ФамилияИнициалыПоНаименованию(Фио,ВыборкаСтрокКомиссия.ФизЛицо);
			ОбластьМакета.Параметры.РасшифровкаПодписи = Фио;
			
			ОбластьМакета.Параметры.ЗаголовокРазделаКомиссии = ЗаголовокРазделаКомиссии;
			ОбластьМакета.Параметры.Должность= БухгалтерскийУчетСервер.ДанныеФизЛица(Шапка.Организация, ВыборкаСтрокКомиссия.ФизЛицо, Шапка.Дата).Должность;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			// В табличном документе зададим имя области, в которую был 
			// выведен объект. Нужно для возможности печати покомплектно.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
			
		КонецЦикла;
	КонецЦикла;
	Если НЕ Ошибки = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	КонецЕсли;	
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Добавление дополнительных параметров
	БухгалтерскиеОтчетыКлиентСервер.ЗаполнитьДополнительныеПараметрыПечати(ТабличныйДокумент);
	
	Возврат ТабличныйДокумент;	
	
КонецФункции // ПечатьАктаПриемаПередачи()

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктПриемаПередачи";
	КомандаПечати.Представление = НСтр("ru = 'Акт приема - передачи'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 2;
		
КонецПроцедуры

//Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	//	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"Накладная", НСтр("ru = 'Накладная'"), ПечатьНакладной(МассивОбъектов, ОбъектыПечати),,
		"Документ.ПередачаОС.ПФ_MXL_ПередачаОС");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемаПередачи") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"АктПриемаПередачи", НСтр("ru = 'Акт приема - передачи'"), ПечатьАктаПриемаПередачи(МассивОбъектов, ОбъектыПечати),,
		"Документ.ПередачаОС.ПФ_MXL_АктПриемаПередачи");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Дата          - Дата - дата документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа.
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "")
	
	ДанныеДокумента = Новый Структура("Номер,Дата,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа.
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = СтрНайти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер);
	Возврат СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),
		НазваниеДокумента, НомерНаПечать, Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	
КонецФункции

#КонецОбласти

#КонецЕсли

