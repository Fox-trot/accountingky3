#Область ОбработчикиСлужебные

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервереБезКонтекста
Функция ЭтоУслуга(Номенклатура)

	Возврат Номенклатура.Услуга;	

КонецФункции // ЭтоУслуга()

&НаСервере
Процедура ПолучитьВсеСпособыОценкиЗапасовПоСчетам(СпособыОценки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпособыОценкиЗапасов.СчетУчета,
		|	СпособыОценкиЗапасов.СпособОценки
		|ИЗ
		|	РегистрСведений.СпособыОценкиЗапасов КАК СпособыОценкиЗапасов";
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	ЗначениеВРеквизитФормы(ТаблицаЗначений, "СпособыОценки");
КонецПроцедуры // 

&НаСервереБезКонтекста
Функция ПолучитьСпособОценки(СчетУчета, СпособыОценки)
	Отбор = Новый Структура;
	Отбор.Вставить("СчетУчета", СчетУчета);
	МассивСтрок = СпособыОценки.НайтиСтроки(Отбор);
	Если МассивСтрок.Количество() = 0 Тогда	
		Возврат ПредопределенноеЗначение("Перечисление.СпособыОценки.ПустаяСсылка");		
	Иначе
	    Возврат МассивСтрок[0].СпособОценки;
	КонецЕсли;

КонецФункции // ПолучитьСпособОценки(СчетУчета, СпособыОценки)

&НаСервереБезКонтекста
Функция ПолучитьЕдиницуИзмерения(Номенклатура)

	Возврат Номенклатура.ЕдиницаИзмерения;	

КонецФункции // ()

&НаКлиенте
Процедура ЗаполнитьПараметрыОбъектаИСТЧ(ДанныеСТЧ, ПараметрыОбъекта, ИмяТЧ, СТЧ = Неопределено, НоменклатураИзменена = Ложь)
	
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Дата 				= ДатаДокумента;
	ПараметрыОбъекта.СуммаСкидкиОбщая 	= Объект.СуммаСкидки;	
	ПараметрыОбъекта.ПлательщикНДС 		= УПП.ПлательщикНДС;
	ПараметрыОбъекта.ПлательщикНСП 		= УПП.ПлательщикНСП;
			
	Если НЕ (Элементы[ИмяТЧ].ТекущиеДанные = Неопределено И СТЧ = Неопределено) Тогда
		СтрокаТабличнойЧасти = ?(СТЧ = Неопределено, Элементы[ИмяТЧ].ТекущиеДанные, СТЧ);
		ЗаполнитьЗначенияСвойств(ДанныеСТЧ, СтрокаТабличнойЧасти);

		ПараметрыОбъекта.СтавкаНДС		= СтрокаТабличнойЧасти.СтавкаНДС;

		Если НоменклатураИзменена Тогда
			СтруктураСчетовУчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(Объект.Организация, СтрокаТабличнойЧасти.Номенклатура);
			Если ЗначениеЗаполнено(Объект.СчетДоходов) Тогда 
				СтрокаТабличнойЧасти.СчетДохода = Объект.СчетДоходов;
				ДанныеСТЧ.СчетДохода = Объект.СчетДоходов;
			Иначе 
				СтрокаТабличнойЧасти.СчетДохода = СтруктураСчетовУчета.СчетДохода;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.СубконтоДоходов) Тогда 
				СтрокаТабличнойЧасти.СубконтоДоходов = Объект.СубконтоДоходов;
			Иначе 
				СтрокаТабличнойЧасти.СубконтоДоходов = СтруктураСчетовУчета.СубконтоДоходов1;
			КонецЕсли;
			
			Если ИмяТЧ = "Товары" Тогда
				СтрокаТабличнойЧасти.СчетСебестоимости = СтруктураСчетовУчета.СчетРасхода;
				СтрокаТабличнойЧасти.СубконтоСписанияСебестоимости = СтруктураСчетовУчета.СубконтоРасходов1;
			КонецЕсли;									
			ДанныеСТЧ.СтавкаНСП = ?(ЗначениеЗаполнено(СтруктураСчетовУчета.СтавкаНСП), СтруктураСчетовУчета.СтавкаНСП, УПП.СтавкаНСППоУмолчанию);

			ПараметрыОбъекта.СтавкаНДС 		= ?(СТЧ = Неопределено И НЕ ЗначениеЗаполнено(СТЧ.СтавкаНДС), СтавкаНДС, СТЧ.СтавкаНДС);
			ДанныеСТЧ.СтавкаНДС 			= ПараметрыОбъекта.СтавкаНДС;
			
		Иначе					
			ПараметрыОбъекта.СтавкаНДС = ДанныеСТЧ.СтавкаНДС;	
		КонецЕсли;
		ПараметрыОбъекта.ЗначСтавкаНДС 		= БухгалтерскийУчетСервер.ПолучитьСтавкуНДС(ДатаДокумента, ПараметрыОбъекта.СтавкаНДС).Ставка;
		ЗначСтавкаНСП 		= ПолучитьЗначениеСтавкиНСП(ДатаДокумента, УПП.ПлательщикНДС, ДанныеСТЧ.СтавкаНСП);				
		ДанныеСТЧ.ЗначСтавкаНСП = ЗначСтавкаНСП
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеСтавкиНСП(Дата, ПлательщикНДС, СтавкаНСП)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ПлательщикНДС
	|			ТОГДА СтавкиНСПСрезПоследних.СтавкаПлательщикНДС
	|		ИНАЧЕ СтавкиНСПСрезПоследних.СтавкаНеПлательщикНДС
	|	КОНЕЦ КАК Ставка
	|ИЗ
	|	РегистрСведений.СтавкиНСП.СрезПоследних(&Дата, СтавкаНСП = &СтавкаНСП) КАК СтавкиНСПСрезПоследних";	
	Запрос.УстановитьПараметр("Дата", 			Дата);	
	Запрос.УстановитьПараметр("СтавкаНСП", 		СтавкаНСП);	
	Запрос.УстановитьПараметр("ПлательщикНДС", 	ПлательщикНДС);	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ставка;
	Иначе		
		Возврат 0;
	КонецЕсли;	

КонецФункции // ПолучитьЗначениеСтавкиНСП(СтавкаНСП)

&НаСервереБезКонтекста
Функция ПолучитьТипЦенИзДоговора(Договор)
	
	Возврат Договор.ТипЦен;	

КонецФункции // ПолучитьТипЦенИзДоговора(Договор)

&НаКлиенте
Процедура ПриИзмененииДоговораКонтрагента()
		
	// 1. Выборка счетов	
	// 2. Выборка значений ставок НДС/НСП
	УстановитьСчетаРасчетовСКонтрагентами();
	Объект.ТипЦен = ПолучитьТипЦенИзДоговора(Объект.ДоговорКонтрагента);
	МассивНоменклатуры = ПолучитьМассивНоменклатуры(Объект.Товары);
	ПолучитьЦены(ДатаДокумента, Объект.ТипЦен, МассивНоменклатуры);	
		
	ДанныеДоговора 				= БухгалтерскийУчетВызовСервераПовтИсп.ДанныеДоговора(Объект.ДоговорКонтрагента);
	СтавкаНДС 					= ?(ЗначениеЗаполнено(ДанныеДоговора.СтавкаНДС), ДанныеДоговора.СтавкаНДС, УПП.СтавкаНДСПоУмолчанию);
	ЗначСтавкаНДС 				= БухгалтерскийУчетСервер.ПолучитьСтавкуНДСПоДоговору(Объект.Дата, Объект.ДоговорКонтрагента);
	СтавкаНСПУслуги				= ?(ЗначениеЗаполнено(ДанныеДоговора.СтавкаНСП), ДанныеДоговора.СтавкаНСП, УПП.СтавкаНСППоУмолчанию);
	ЗначСтавкаНСПУслуги			= БухгалтерскийУчетСервер.ПолучитьСтавкуНСППоДоговору(Объект.Дата, Объект.ДоговорКонтрагента);
	
	Объект.ВалютаДокумента 		= ДанныеДоговора.ВалютаРасчетов;
	Объект.ВидПоставкиНДС 		= ДанныеДоговора.ВидПоставкиНДС;
	
	УстановитьКурсВалютыДокумента(ДатаДокумента, Объект.ВалютаДокумента, Объект.Курс);

	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл 
		ПриИзмененииНоменклатуры(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти);
	КонецЦикла;
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл 
		ПриИзмененииУслуги(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти);
	КонецЦикла;
		
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

// Процедура обработки строки ТЧ документов поступления ТМЗ, ОС и услуг
// Выборка цены производится только при установленном парамтре Заполнить цену 
// (при изменении номенклатуры)
&НаКлиенте
Процедура ПриИзмененииНоменклатуры(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти, НоменклатураИзменена = Ложь)
	СтрокаТабличнойЧасти.СпособОценки = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСпособОценкиЗапасов(СтрокаТабличнойЧасти.СчетУчета, ПредопределенноеЗначение("Перечисление.СпособыОценки.ПоСредней"));
	ЗаполнитьПараметрыОбъектаИСТЧ(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти, НоменклатураИзменена);

	ОбработкаТабличныхЧастейВызовСервера.ЗаполнитьСчетУчетаСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта);
	
	КоличествоОстаток = 0;
 	Если ДанныеСТЧ.СчетУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.МБП") Тогда
		Если Объект.МБПЭксплуатация Тогда
			КоличествоОстаток = ДанныеСТЧ.Количество;	
		Иначе
			КоличествоОстаток = БухгалтерскийУчетСервер.ПолучитьОстатокПоМБПНаСкладе(ДанныеСТЧ, ПараметрыОбъекта);
		КонецЕсли;	
	Иначе 
		КоличествоОстаток = БухгалтерскийУчетСервер.ПолучитьОстатокПоНоменклатуреИСчетуУчета(ДанныеСТЧ, ПараметрыОбъекта);
		ОбработкаТабличныхЧастейВызовСервера.ЗаполнитьСебестоимостьСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта);
	КонецЕсли;
	
	ДанныеСТЧ.КоличествоОстаток = КоличествоОстаток;
	ОбработкаТабличныхЧастейВызовСервера.ЗаполнитьЦенуСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта);
	
	Если ДанныеСТЧ.Количество = 0 Тогда
		ДанныеСТЧ.Количество = КоличествоОстаток;
	КонецЕсли;
	ОбработкаТабличныхЧастейВызовСервера.РасчетСтрокиРеализации(ДанныеСТЧ, ПараметрыОбъекта);		
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСТЧ);
	
	Если ЗначениеЗаполнено(Объект.СчетДоходов) Тогда 
		СтрокаТабличнойЧасти.СчетДохода = Объект.СчетДоходов;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Склад) И НЕ Объект.МБПЭксплуатация Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено поле Склад.'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Склад",
			Истина);
			
	ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнена номенклатура.'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Товары[" + (СтрокаТабличнойЧасти.НомерСтроки -1) + "].Номенклатура",
			Истина);
			
	ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетУчета) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнен счет учета номенклатуры.'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Товары[" + (СтрокаТабличнойЧасти.НомерСтроки -1) + "].СчетУчета",
			Истина);
			
	ИначеЕсли СтрокаТабличнойЧасти.Количество = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нулевой остаток для выбранных номенклатуры и счета учета!'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Товары[" + (СтрокаТабличнойЧасти.НомерСтроки -1) + "].Количество",
			Истина);		
		
	ИначеЕсли НЕ ЭтоУслуга(СтрокаТабличнойЧасти.Номенклатура) И СтрокаТабличнойЧасти.Количество > КоличествоОстаток Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Количество для выбранных номенклатуры и счета учета указано больше, чем есть в остатке. Остаток составляет  %1 %2'"), КоличествоОстаток, ПолучитьЕдиницуИзмерения(СтрокаТабличнойЧасти.Номенклатура));
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Товары[" + (СтрокаТабличнойЧасти.НомерСтроки -1) + "].Количество",
			Истина);	
		
	КонецЕсли;
		
	Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦены(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти)
	ЗаполнитьПараметрыОбъектаИСТЧ(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти);
	ОбработкаТабличныхЧастейВызовСервера.РасчетСтрокиРеализации(ДанныеСТЧ, ПараметрыОбъекта);		
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСТЧ);
		
	Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСчетаРасчетовСКонтрагентами()
		
	СтруктураДанные 				= Новый Структура("Организация, Контрагент, ДоговорКонтрагента", Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	СчетаУчета = ПолучитьСчетаРасчетовСКонтрагентами(СтруктураДанные);
	Объект.СчетРасчетов	= СчетаУчета.СчетРасчетовПокупателя;		
	Объект.СчетАвансов	= СчетаУчета.СчетАвансовПокупателя;
	
КонецПроцедуры // УстановитьСчетаРасчетовСКонтрагентами()

&НаСервереБезКонтекста
Функция ПолучитьСчетаРасчетовСКонтрагентами(СтруктураДанные)
	Возврат БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(СтруктураДанные.Организация, СтруктураДанные.Контрагент, СтруктураДанные.ДоговорКонтрагента);
КонецФункции

&НаКлиенте
Процедура ПриИзмененииУслуги(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти, НоменклатураИзменена = Ложь)
	
	ЗаполнитьПараметрыОбъектаИСТЧ(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти, НоменклатураИзменена);
	
	ОбработкаТабличныхЧастейВызовСервера.ЗаполнитьСчетУчетаСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта);
	ОбработкаТабличныхЧастейВызовСервера.ЗаполнитьЦенуСтрокиТабличнойЧасти(ДанныеСТЧ, ПараметрыОбъекта);
	ОбработкаТабличныхЧастейВызовСервера.РасчетСтрокиРеализации(ДанныеСТЧ, ПараметрыОбъекта);		
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСТЧ);
	
	Если ЗначениеЗаполнено(Объект.СчетДоходов) Тогда 
		СтрокаТабличнойЧасти.СчетДохода = Объект.СчетДоходов;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнена номенклатура(услуга).'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Товары[" + (СтрокаТабличнойЧасти.НомерСтроки -1) + "].Номенклатура",
			Истина);
			
	ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетУчета) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнен счет учета номенклатуры(услуги).'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.Товары[" + (СтрокаТабличнойЧасти.НомерСтроки -1) + "].СчетУчета",
			Истина);		
	КонецЕсли;
		
	Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
	
КонецПроцедуры // ПриИзмененииНоменклатурыИСчета()

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	ПриИзмененииДоговораКонтрагента();
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыПодбора(ИмяТаблицы, ОбщийПодбор = Истина)

	ДатаРасчетов		= ДатаДокумента;
	
	Если ИмяТаблицы = "Товары" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Товары'");
	КонецЕсли;
	ЗаголовокПодбора = СтрШаблон(НСтр("ru = 'Подбор номенклатуры в %1 (%2)'"), Объект.Ссылка, ПредставлениеТаблицы);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаРасчетов", ДатаРасчетов);
	ПараметрыФормы.Вставить("Организация" , Объект.Организация);
	ПараметрыФормы.Вставить("Склад"       , Объект.Склад);
	ПараметрыФормы.Вставить("Заголовок"   , ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора"  , "");
	ПараметрыФормы.Вставить("ИмяТаблицы"  , ИмяТаблицы);
	ПараметрыФормы.Вставить("Услуги"      , ИмяТаблицы = "Услуги");
	ПараметрыФормы.Вставить("ПоказыватьОстатки"  , Истина);
	ПараметрыФормы.Вставить("ПоказыватьСчетУчета", Истина);
	ПараметрыФормы.Вставить("ЕстьКоличество", Истина);
	ПараметрыФормы.Вставить("ОбщийПодбор", 	ОбщийПодбор);		
	ПараметрыФормы.Вставить("СпособОценки", ПредопределенноеЗначение("Перечисление.СпособыОценки.ПустаяСсылка"));
	ПараметрыФормы.Вставить("ПодборНоменклатурыПоПартии", Ложь);
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	Если НЕ СтрокаТабличнойЧасти = НЕОПРЕДЕЛЕНО И ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сотрудник) Тогда
		ПараметрыФормы.Вставить("МОЛ"  				, СтрокаТабличнойЧасти.Сотрудник);
		ПараметрыФормы.Вставить("Подразделение"  	, Неопределено); 				
		ПараметрыФормы.Вставить("СтатусМБП"   		, Неопределено);
	КонецЕсли;	
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура ПересчетТабличнойЧасти(ИмяТабличнойЧасти, НоменклатураИзменена = Ложь)
	
	Если ИмяТабличнойЧасти = "Товары" Тогда
		ТабличнаяЧасть = Объект.Товары;			
	ИначеЕсли ИмяТабличнойЧасти = "Услуги" Тогда
		ТабличнаяЧасть = Объект.Услуги;
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
		ПриИзмененииНоменклатуры(ДанныеСТЧ, ПараметрыОбъекта, ИмяТабличнойЧасти, СтрокаТабличнойЧасти, НоменклатураИзменена);
	КонецЦикла;
			
КонецПроцедуры // ПересчетТабличнойЧасти(ИмяТабличнойЧасти)

&НаСервере
Процедура ПолучитьЦены(ДатаДокумента, ТипЦен, МассивНоменклатуры)

	ЗначениеВРеквизитФормы(БухгалтерскийУчетСервер.ПолучитьЦеныНоменклатуры(ДатаДокумента, ТипЦен, МассивНоменклатуры), "ТаблицаЦенНоменклатуры")	
	
КонецПроцедуры // ПолучитьЦены()

&НаСервереБезКонтекста
Функция ПолучитьМассивНоменклатуры(Знач ТабличнаяЧасть)

	Возврат ТабличнаяЧасть.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура")	

КонецФункции // ()

&НаСервереБезКонтекста
Процедура УстановитьКурсВалютыДокумента(ДатаДокумента, ВалютаДокумента, Курс)

	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		Возврат;	
	КонецЕсли;
	КурсСтруктура	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Курс 			= КурсСтруктура.Курс;
	
КонецПроцедуры // УстановитьКурсВалютыДокумента()

&НаКлиенте
Процедура ПриИзмененииСкидки()
	ВидСкидки = Объект.ВидСкидки;
	
	Если НЕ ЗначениеЗаполнено(ВидСкидки) ИЛИ ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.СуммаПоСтроке") Тогда		
		Объект.СкидкаПроцент = 0;
	КонецЕсли;

	ПересчетТабличнойЧасти("Товары");
	ПересчетТабличнойЧасти("Услуги");
	Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
	
	УстановитьВидимостьДоступностьЭлементов();	

КонецПроцедуры // ПриИзмененииСкидки()

&НаКлиенте
Процедура ПолучитьОбщиеИтоги()
	ОбщийИтог 		= Объект.Товары.Итог("Итого") 		+ Объект.Услуги.Итог("Итого")   	+ Объект.ОС.Итог("Сумма"); //+ Объект.Прочее.Итог("Сумма");		
	ОбщийИтогСкидка = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
	ОбщийИтогНДС 	= Объект.Товары.Итог("СуммаНДС") 	+ Объект.Услуги.Итог("СуммаНДС")   	+ Объект.ОС.Итог("СуммаНДС");
	ОбщийИтогНСП 	= Объект.Товары.Итог("СуммаНСП") 	+ Объект.Услуги.Итог("СуммаНСП")   	+ Объект.ОС.Итог("СуммаНСП");
КонецПроцедуры // ПолучитьОбщиеИтоги()

&НаСервереБезКонтекста
Функция ЕстьРеализацияОС(ДокументОснование)

	Возврат ТипЗнч(ДокументОснование) = ТИП("ДокументСсылка.ПередачаОС");		

КонецФункции // ЕстьРеализацияОС(ДокументОснование)()

#КонецОбласти

#Область УправлениеВнешнимВидом
&НаСервере
// Процедура устанавливает видимость и доступность элементов.
//
Процедура УстановитьВидимостьДоступностьЭлементов()	
	
	Элементы.СтраницаСчетФактура.Видимость          = УПП.ПлательщикНДС;
	
	ДоговорВключаетНДС = Объект.ДоговорКонтрагента.СуммаВключаетНалоги;
	ОрганизацияВключаетНДС = РегистрыСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(Объект.Дата)[0].ПлательщикНДС;
	
	Если ЗначениеЗаполнено(Объект.ВалютаДокумента) И Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета Тогда
		Элементы.ВалютаДоговора.Видимость 			= Истина;	
		Элементы.Курс.Видимость 					= Истина;
		Элементы.ПечатьВВалюте.Видимость            = Истина;
	Иначе                                  
		Элементы.ВалютаДоговора.Видимость 			= Ложь;	
		Элементы.Курс.Видимость 					= Ложь;
		Элементы.ПечатьВВалюте.Видимость            = Ложь;
	КонецЕсли;
	
	Элементы.ДокументОснование.Видимость 	= ЗначениеЗаполнено(Объект.ДокументОснование);
	
	Элементы.МБПЭксплуатация.Видимость		= Ложь;
	Элементы.ТоварыПодборМБП.Заголовок		= ?(Объект.МБПЭксплуатация, "Подбор МБП в эксплуатации", "Подбор МБП со склада");
	Элементы.ФормаПодбор.Видимость			= НЕ Объект.МБПЭксплуатация;
	Элементы.Склад.Видимость				= НЕ Объект.МБПЭксплуатация;
	Элементы.СтраницаУслуги.Видимость		= НЕ Объект.МБПЭксплуатация;	
	Элементы.СтраницаОС.Видимость			= НЕ Объект.МБПЭксплуатация;
	Элементы.ТоварыСчетСписанияСебестоимости.Видимость		= НЕ Объект.МБПЭксплуатация;
	Элементы.ТоварыСубконтоСписанияСебестоимости.Видимость	= НЕ Объект.МБПЭксплуатация;
	
	Элементы.СтраницаСчетФактура.Видимость  = ДоговорВключаетНДС ИЛИ ОрганизацияВключаетНДС;
	
	Элементы.СуммаСкидки.Видимость 			= НЕ ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.ПустаяСсылка");
	Элементы.СуммаСкидки.Доступность 		= НЕ ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.ПроцентПоСтроке");	
	Элементы.СкидкаПроцент.Видимость 		= НЕ ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.ПустаяСсылка");
	Элементы.СкидкаПроцент.Доступность 		= НЕ ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.СуммаПоСтроке");
	Элементы.ТоварыСкидкаПроцент.Видимость 	= ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.ПроцентПоСтроке");
	Элементы.ТоварыСкидкаСумма.Видимость 	= Элементы.СуммаСкидки.Видимость;
	Элементы.ТоварыСуммаНДС.Видимость 		= УПП.ПлательщикНДС И НЕ Объект.ДоговорКонтрагента.СтавкаНДС = ПредопределенноеЗначение("Справочник.СтавкиНДС.БезНДС");
	Элементы.ТоварыСуммаНСП.Видимость 		= УПП.ПлательщикНСП;
	Элементы.УслугиСкидкаПроцент.Видимость 	= ВидСкидки = ПредопределенноеЗначение("Перечисление.ВидыСкидок.ПроцентПоСтроке");
	Элементы.УслугиСуммаСкидки.Видимость 	= Элементы.СуммаСкидки.Видимость;
	Элементы.УслугиИтого.Видимость 			= Объект.СуммаСкидки <> 0;
	Элементы.УслугиСуммаНДС.Видимость 		= УПП.ПлательщикНДС И НЕ Объект.ДоговорКонтрагента.СтавкаНДС = ПредопределенноеЗначение("Справочник.СтавкиНДС.БезНДС");
	Элементы.УслугиСуммаНСП.Видимость 		= УПП.ПлательщикНСП;
	
	Элементы.Контрагент.Видимость				= Истина;
	Элементы.ДоговорКонтрагента.Видимость		= Истина;
	
	Элементы.ОС.Видимость						= ЕстьРеализацияОС(Объект.ДокументОснование);

КонецПроцедуры // УстановитьВидимостьДоступностьЭлементов()    

Процедура УстановитьТекущуюСтраницу()	
	
	Если Объект.Товары.Количество() > 0 Тогда 
		Элементы.Страницы.ТекущаяСтраница 			= Элементы.Страницы.ПодчиненныеЭлементы.СтраницаТовары;
	ИначеЕсли Объект.Услуги.Количество() > 0 Тогда 
		Элементы.Страницы.ТекущаяСтраница 			= Элементы.Страницы.ПодчиненныеЭлементы.СтраницаУслуги;
	КонецЕсли;	
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	ВалютаРегламентированногоУчета 	= ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ЗначСтавкаНДС 					= БухгалтерскийУчетСервер.ПолучитьСтавкуНДСПоДоговору(ДатаДокумента, Объект.ДоговорКонтрагента);
	ЗначСтавкаНСП 					= БухгалтерскийУчетСервер.ПолучитьСтавкуНСППоДоговору(ДатаДокумента, Объект.ДоговорКонтрагента);
	
	УПП 							= БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ДанныеДоговора 				= БухгалтерскийУчетВызовСервераПовтИсп.ДанныеДоговора(Объект.ДоговорКонтрагента);
		СтавкаНДС 					= ДанныеДоговора.СтавкаНДС;
		СтавкаНСП 					= ДанныеДоговора.СтавкаНСП;	
	КонецЕсли;
	
	ВидСкидки 						= Объект.ВидСкидки;
	
	ПараметрыОбъекта 	= Новый Структура("Организация, 
											|Дата, 
											|Ссылка, 
											|ВалютаДокумента, 
											|КурсВзаиморасчетов, 
											|Склад,  
											|ДоговорКонтрагента,
											|ТипЦен,
											|СтавкаНДС,  
											|ЗначСтавкаНДС, 
											|ПлательщикНДС,
											|ПлательщикНСП,
											|ВидСкидки,
											|СкидкаПроцент,
											|СуммаСкидкиОбщая");
											
	ДанныеСТЧ 			= Новый Структура("Номенклатура, 
											|Количество,
											|Себестоимость,
											|Цена, 
											|Всего, 
											|Доход, 
											|Итого,
											|СтавкаНДС,											
											|СуммаНДС,
											|СтавкаНСП,
											|ЗначСтавкаНСП,											 
											|СуммаНСП,
											|СкидкаПроцент,
											|СуммаСкидки, 
											|СчетУчета, 
											|СчетДохода,
											|СчетСебестоимости,
											|ЕдиницаИзмерения,
											|КоличествоОстаток,
											|СпособОценки");											
	
	ПолучитьВсеСпособыОценкиЗапасовПоСчетам(СпособыОценки);
		
	//УстановитьДоступностьСубконто();
	
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	БухгалтерскийУчетСервер.ОпределитьТипПоследнегоОткрытогоДокумента(Объект, Истина);
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаВажныеКоманды);
	// Конец СтандартныеПодсистемы.Печать	
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьДоступностьЭлементов(); 
	ПолучитьОбщиеИтоги();
	УстановитьТекущуюСтраницу();
    СформироватьСериюСчетФактуры();
	СформироватьНомераСчетФактур();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбработанаТабличнаяЧастьТовары" И ТипЗнч(Параметр) = Тип("Структура") Тогда		
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметр);
		ПересчетТабличнойЧасти("Товары", Истина);
				
	ИначеЕсли ИмяСобытия = "ПодборВСтрокуТабличнойЧастьТовары" И ТипЗнч(Параметр) = Тип("Структура") Тогда
		ОбработкаОповещенияПодборВСтрокуТабличнойЧастиТоварыНаСервере(Параметр, Элементы.Товары.ТекущиеДанные.НомерСтроки);
		ПересчетТабличнойЧасти("Товары", Истина);
		
	ИначеЕсли ИмяСобытия = "ОбработанаТабличнаяЧастьТоварыМБП" Тогда
		СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
		Если СтрокаТабличнойЧасти = Неопределено Тогда
			НомерСтрокиТовары = Неопределено
		Иначе	
			НомерСтрокиТовары = СтрокаТабличнойЧасти.НомерСтроки;
		КонецЕсли;
		 
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыМБПНаСервере(Параметр, НомерСтрокиТовары);
		ПересчетТабличнойЧасти("Товары", Истина);
		
	ИначеЕсли ИмяСобытия = "МодифицированДоговораКонтрагента" Тогда
		ПриИзмененииДоговораКонтрагента();
	
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	ПолучитьОбщиеИтоги();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если НЕ Объект.СуммаДокумента = ОбщийИтог Тогда
		Объект.СуммаДокумента = ОбщийИтог
	КонецЕсли;
	Если НЕ Объект.СуммаНДС = ОбщийИтогНДС Тогда
		Объект.СуммаНДС = ОбщийИтогНДС
	КонецЕсли;
	Если НЕ Объект.СуммаНСП = ОбщийИтогНСП Тогда
		Объект.СуммаНСП = ОбщийИтогНСП
	КонецЕсли;		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПодборМБП(Команда)
	
	ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары");
	ПараметрыПодбора.Вставить("ВидПодбора" , ?(Объект.МБПЭксплуатация, "ПодборМБПЭксплуатация", "ПодборМБПСклад"));
	Если ПараметрыПодбора <> Неопределено Тогда
		Если Объект.МБПЭксплуатация Тогда
			Если Объект.Товары.Количество() = 0 Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнена табличная часть.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
																ТекстСообщения,
																,
																"Товары",
																"Объект",
																);
				Возврат;			
			ИначеЕсли НЕ ЗначениеЗаполнено(ПараметрыПодбора.МОЛ) Тогда
				ТекстСообщения = НСтр("ru = 'В выбранной строке не заполнено физлицо.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
																ТекстСообщения,
																,
																"Товары",
																"Объект",
																);
				Возврат;																
			КонецЕсли;	
			
		
		КонецЕсли;
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)

	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.АдресПодобраннойНоменклатурыВХранилище);	
	
	Для каждого СтрокаПодбора Из ТаблицаОбработки Цикл
		ОтборСтруктура = Новый Структура;
		ОтборСтруктура.Вставить("Номенклатура", СтрокаПодбора.Номенклатура);
		
		НайденныеСтроки = Объект.Товары.НайтиСтроки(ОтборСтруктура);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество + СтрокаПодбора.Количество;	
		Иначе
			СтрокаТабличнойЧасти 				= Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаПодбора);
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДС;
			СтруктураСчетовУчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(Объект.Организация, СтрокаТабличнойЧасти.Номенклатура);
			СтрокаТабличнойЧасти.СтавкаНСП = СтруктураСчетовУчета.СтавкаНСП;
			СтрокаТабличнойЧасти.СубконтоДоходов = СтруктураСчетовУчета.СубконтоДоходов1;
			СтрокаТабличнойЧасти.СчетСебестоимости = СтруктураСчетовУчета.СчетРасхода;
			СтрокаТабличнойЧасти.СубконтоСписанияСебестоимости = СтруктураСчетовУчета.СубконтоРасходов1;
			СтрокаТабличнойЧасти.Себестоимость = СтрокаПодбора.Цена;
			СтрокаТабличнойЧасти.СпособОценки = ПолучитьСпособОценки(СтрокаТабличнойЧасти.СчетУчета, СпособыОценки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияПодборВСтрокуТабличнойЧастиТоварыНаСервере(Параметры, НомерСтроки)

	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.АдресПодобраннойНоменклатурыВХранилище);
	// Вообще-то, в данном случае в таблице ТаблицаОбработки всегда будет одна строка
	Если ТаблицаОбработки.Количество() > 0 Тогда
		СтрокаПодбора = ТаблицаОбработки[0];
		СтрокаТабличнойЧасти = Объект.Товары[НомерСтроки - 1];
	    ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаПодбора);
		СтрокаТабличнойЧасти.Себестоимость = СтрокаПодбора.Цена;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыМБПНаСервере(Параметр, НомерСтрокиТовары)

	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметр.АдресПодобраннойНоменклатурыВХранилище);
	Если Объект.МБПЭксплуатация Тогда
		СтрокаТабличнойЧасти = Объект.Товары[НомерСтрокиТовары - 1];
		СтрокаОбразец        = Объект.Товары[НомерСтрокиТовары - 1];
		ПерваяСтрока = Истина;
		Для каждого СтрокаПодбора Из ТаблицаОбработки Цикл
			// проверка заполненого значения
			Если ПерваяСтрока И НЕ СтрокаТабличнойЧасти = Неопределено Тогда 			
				ПерваяСтрока = Ложь;
			Иначе
				СтрокаТабличнойЧасти = Объект.Товары.Добавить();
				Если НЕ СтрокаОбразец = Неопределено Тогда
					ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаОбразец); 
				КонецЕсли;
			КонецЕсли;	
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаПодбора);
			СтрокаТабличнойЧасти.СчетУчета      = ПланыСчетов.Хозрасчетный.МБП;
			СтрокаТабличнойЧасти.Себестоимость  = СтрокаПодбора.Цена;
			СтрокаТабличнойЧасти.Всего			= СтрокаПодбора.Цена * СтрокаПодбора.Количество;
			СтрокаТабличнойЧасти.предСтатусМБП 	= СтрокаПодбора.СтатусМБП;							
		КонецЦикла;
		
	Иначе
		Для каждого СтрокаПодбора Из ТаблицаОбработки Цикл
			ОтборСтруктура = Новый Структура;
			ОтборСтруктура.Вставить("Номенклатура", СтрокаПодбора.Номенклатура);
			ОтборСтруктура.Вставить("ИндивидуальныйНомер", СтрокаПодбора.ИндивидуальныйНомер);
			ОтборСтруктура.Вставить("Партия", СтрокаПодбора.Партия);
			
			НайденныеСтроки = Объект.Товары.НайтиСтроки(ОтборСтруктура);
			Если НайденныеСтроки.Количество() > 0 Тогда
				НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество + СтрокаПодбора.Количество;	
				НайденныеСтроки[0].Себестоимость  = СтрокаПодбора.Цена;
				НайденныеСтроки[0].Всего			 = СтрокаПодбора.Цена * НайденныеСтроки[0].Количество;
				НайденныеСтроки[0].предСтатусМБП = СтрокаПодбора.СтатусМБП;
			Иначе
				СтрокаТабличнойЧасти 					= Объект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаПодбора);
				СтрокаТабличнойЧасти.СчетУчета      = ПланыСчетов.Хозрасчетный.МБП;
				СтрокаТабличнойЧасти.Себестоимость  = СтрокаПодбора.Цена;
				СтрокаТабличнойЧасти.Всего			= СтрокаПодбора.Цена * СтрокаПодбора.Количество;
				СтрокаТабличнойЧасти.предСтатусМБП 	= СтрокаПодбора.СтатусМБП;
				
			КонецЕсли;
			
		КонецЦикла;
	
		
	
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитовШапки

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	СформироватьСериюСчетФактуры();
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	// Обработка события изменения организации.
	Объект.Номер = "";
	БухгалтерскийУчетСервер.ПроверитьСуществованиеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
	УПП = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
	
	Объект.БанковскийСчет = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнойБанковскийСчетОрганизации(Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	СтруктураДоговор 			= БухгалтерскийУчетСервер.ПолучитьДоговорКонтрагента(
								Объект.Организация, 
								Объект.Контрагент, 
								ВалютаРегламентированногоУчета, 
								ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
	Объект.ДоговорКонтрагента 	= СтруктураДоговор.ДоговорКонтрагента;	
	
	ПриИзмененииДоговораКонтрагента();
КонецПроцедуры

&НаКлиенте
Процедура ВидСкидкиПриИзменении(Элемент)
	
	ПриИзмененииСкидки();
	
КонецПроцедуры

&НаКлиенте
Процедура СкидкаПроцентПриИзменении(Элемент)
	
	ПриИзмененииСкидки()
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаСкидкиПриИзменении(Элемент)
	
	ПриИзмененииСкидки()
	
КонецПроцедуры

&НаКлиенте
Процедура СерияБланкаСФПриИзменении(Элемент)

	Отказ = Ложь;
	Элементы.НомерБланкаСФ.СписокВыбора.Очистить();
	Элементы.НомерБланкаСФ.СписокВыбора.ЗагрузитьЗначения(БухгалтерскийУчетСервер.СформироватьСписокНомеровБланковСФ(Объект.Организация, Объект.СерияБланкаСФ,Объект.Дата));
	Если Элементы.НомерБланкаСФ.СписокВыбора.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Для пользователя %1 и серии %2 нет доступных номеров бланков счет-фактур'"), Объект.Автор, Объект.СерияБланкаСФ);
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Объект.Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.НомерБланкаСФ",
			Отказ);
		КонецЕсли;
		
	Если НЕ Отказ Тогда
		Объект.ДатаСФ 			= ДатаДокумента;
		Объект.ВидПлатежа		= ПредопределенноеЗначение("Перечисление.ВидыПлатежей.Перечислением");		
	    Объект.ТипПоставкиСФ 	= ПредопределенноеЗначение("Справочник.КодыПоставокСФ.Облагаемые");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СерияБланкаСФНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СформироватьСериюСчетФактуры();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСериюСчетФактуры()
		
	Элементы.СерияБланкаСФ.СписокВыбора.Очистить();
	Элементы.СерияБланкаСФ.СписокВыбора.ЗагрузитьЗначения(БухгалтерскийУчетСервер.СформироватьСписокСерийСФ(Объект.Организация, Объект.Дата));
	
КонецПроцедуры	

&НаКлиенте
Процедура СформироватьНомераСчетФактур()
	Отказ = Ложь;
	Если не ЗначениеЗаполнено(Объект.СерияБланкаСФ) Тогда 
		Возврат;
	КонецЕсли;	
	Элементы.НомерБланкаСФ.СписокВыбора.Очистить();
	Элементы.НомерБланкаСФ.СписокВыбора.ЗагрузитьЗначения(БухгалтерскийУчетСервер.СформироватьСписокНомеровБланковСФ(Объект.Организация, Объект.СерияБланкаСФ,Объект.Дата));
	Если Элементы.НомерБланкаСФ.СписокВыбора.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Для пользователя %1 и серии %2 нет доступных номеров бланков счет-фактур'"), Объект.Автор, Объект.СерияБланкаСФ);
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
		Объект.Ссылка,   
		ТекстСообщения,
		,
		,
		"Объект.НомерБланкаСФ",
		Отказ);
	КонецЕсли;
КонецПроцедуры	
	
&НаКлиенте
Процедура НомерБланкаСФПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.СерияБланкаСФ) И ЗначениеЗаполнено(Объект.НомерБланкаСФ) Тогда
		Объект.ДатаСФ = ДатаДокумента;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НомерБланкаСФНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Элементы.НомерБланкаСФ.РежимВыбораИзСписка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура МБПЭксплуатацияПриИзменении(Элемент)
	Если Объект.МБПЭксплуатация Тогда
		Если Объект.Услуги.Количество() > 0 Тогда
			Объект.Услуги.Очистить();
		КонецЕсли;			
		Если Объект.ОС.Количество() > 0 Тогда
			Объект.ОС.Очистить();
		КонецЕсли;
		Если НЕ Объект.Склад.Пустая() Тогда
			Объект.Склад = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
		КонецЕсли;				
	КонецЕсли;
	УстановитьВидимостьДоступностьЭлементов()
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТабличныхЧастей

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.СтавкаНСП = ПредопределенноеЗначение("Справочник.СтавкиНСП.ПустаяСсылка");
	ПриИзмененииНоменклатуры(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти, Истина);		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ПриИзмененииНоменклатуры(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары", Ложь);
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры
	
&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	ПолучитьОбщиеИтоги()	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПроцентПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ЗаполнитьПараметрыОбъектаИСТЧ(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти);
	ОбработкаТабличныхЧастейВызовСервера.РасчетСкидкиВРеализации(ДанныеСТЧ, ПараметрыОбъекта);
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСТЧ);
	Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	ПолучитьОбщиеИтоги()
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ПриИзмененииУслуги(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ПриИзмененииУслуги(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ПриИзмененииУслуги(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПроцентПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ЗаполнитьПараметрыОбъектаИСТЧ(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти);
	ОбработкаТабличныхЧастейВызовСервера.РасчетСкидкиВРеализации(ДанныеСТЧ, ПараметрыОбъекта);
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСТЧ);
	Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки") + Объект.Услуги.Итог("СуммаСкидки");
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДС;			
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДС;			
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ПриИзмененииЦены(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура ОССуммаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	ДанныеСТЧ_ОС = Новый Структура;
	ДанныеСТЧ_ОС.Вставить("Сумма", 			СтрокаТабличнойЧасти.Сумма);
	ДанныеСТЧ_ОС.Вставить("ЗначСтавкаНСП", 	ПолучитьЗначениеСтавкиНСП(ДатаДокумента, УПП.ПлательщикНДС, УПП.СтавкаНСППоУмолчанию));
	ДанныеСТЧ_ОС.Вставить("СуммаНДС",		СтрокаТабличнойЧасти.СуммаНДС);
	ДанныеСТЧ_ОС.Вставить("СуммаНСП",		СтрокаТабличнойЧасти.СуммаНСП);
	
	ПараметрыОбъекта_ОС = Новый Структура;
	ПараметрыОбъекта_ОС.Вставить("ЗначСтавкаНДС", 		БухгалтерскийУчетСервер.ПолучитьСтавкуНДСПоДоговору(ДатаДокумента, Объект.ДоговорКонтрагента));
	ПараметрыОбъекта_ОС.Вставить("ДоговорКонтрагента", 	Объект.ДоговорКонтрагента);
	
	ОбработкаТабличныхЧастейВызовСервера.РасчетСтрокиРеализации(ДанныеСТЧ_ОС, ПараметрыОбъекта_ОС, "ОС");		
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСТЧ_ОС);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ПриИзмененииЦены(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти);
КонецПроцедуры
	
&НаКлиенте
Процедура ТоварыСтавкаНСППриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ПриИзмененииЦены(ДанныеСТЧ, ПараметрыОбъекта, "Товары", СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ПриИзмененииЦены(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНСППриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ПриИзмененииЦены(ДанныеСТЧ, ПараметрыОбъекта, "Услуги", СтрокаТабличнойЧасти);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если Не ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти


