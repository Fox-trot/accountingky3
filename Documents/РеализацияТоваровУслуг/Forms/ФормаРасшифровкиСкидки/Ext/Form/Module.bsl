
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИтоговыеДанные = Новый Структура();
	ИтоговыеДанные.Вставить("ВсегоБезСкидки", 	0);
	ИтоговыеДанные.Вставить("ДоходБезСкидки", 	0);
	ИтоговыеДанные.Вставить("НДСБезСкидки", 	0);
	ИтоговыеДанные.Вставить("НСПБезСкидки", 	0);
	ИтоговыеДанные.Вставить("ВсегоСкидка", 		0);
	ИтоговыеДанные.Вставить("ДоходСкидка", 		0);
	ИтоговыеДанные.Вставить("НДССкидка", 		0);
	ИтоговыеДанные.Вставить("НСПСкидка", 		0);
	ИтоговыеДанные.Вставить("ВсегоСоСкидкой", 	0);
	ИтоговыеДанные.Вставить("ДоходСоСкидкой", 	0);
	ИтоговыеДанные.Вставить("НДССоСкидкой", 	0);
	ИтоговыеДанные.Вставить("НСПСоСкидкой", 	0);
	
	СтруктураНачальныхСумм = Новый Структура();
	СтруктураНачальныхСумм.Вставить("Всего",  0);
	СтруктураНачальныхСумм.Вставить("Доход",  0);
	СтруктураНачальныхСумм.Вставить("Скидка", 0);
	СтруктураНачальныхСумм.Вставить("НДС", 	  0);
	СтруктураНачальныхСумм.Вставить("НСП", 	  0);
	
	Для Каждого СтрокаТабличнойЧасти Из Параметры.Объект[Параметры.ИмяТаблицы] Цикл
		
		СтруктураНачальныхСумм.Всего 	= СтрокаТабличнойЧасти.Всего - СтрокаТабличнойЧасти.СуммаСкидки;
		СтруктураНачальныхСумм.Доход 	= СтрокаТабличнойЧасти.СуммаДохода;
		СтруктураНачальныхСумм.Скидка 	= СтрокаТабличнойЧасти.СуммаСкидки;
		СтруктураНачальныхСумм.НДС 		= СтрокаТабличнойЧасти.СуммаНДС;
		СтруктураНачальныхСумм.НСП 		= СтрокаТабличнойЧасти.СуммаНСП;
		
		// Обнуление суммы скидки для пересчета остальных без скидки.
		СтрокаТабличнойЧасти.СуммаСкидки = 0;
		
		Если Параметры.Объект.СуммаВключаетНалоги Тогда
		
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧасти(
				СтрокаТабличнойЧасти, 
				Параметры.Дата, 
				Параметры.Объект.Организация, 
				Истина, 
				Параметры.СтавкаНДС,
				Параметры.СтавкаНСП, 
				Параметры.Объект.БезналичныйРасчет);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, Истина);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьДоходСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, Истина);
			
		Иначе
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьДоходСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, Ложь);
			
			СтруктураДопПараметров = Новый Структура();
			СтруктураДопПараметров.Вставить("Период", 			 Параметры.Дата);
			СтруктураДопПараметров.Вставить("Организация", 		 Параметры.Объект.Организация);
			СтруктураДопПараметров.Вставить("БезналичныйРасчет", Параметры.Объект.БезналичныйРасчет);
			СтруктураДопПараметров.Вставить("СтавкаНДС", 		 Параметры.СтавкаНДС);
			СтруктураДопПараметров.Вставить("СтавкаНСП", 		 Параметры.СтавкаНСП);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти,, СтруктураДопПараметров);
			
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти,,,Истина);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧасти(
				СтрокаТабличнойЧасти, 
				Параметры.Дата,
				Параметры.Объект.Организация,  
				Истина,
				Параметры.СтавкаНДС,
				Параметры.СтавкаНСП, 
				Параметры.Объект.БезналичныйРасчет);
				
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, Истина);
		КонецЕсли;
		
		// Строка без скидки с указанием номенклатуры/основного средства.
		СтрокаСписка = Список.Добавить();
		
		Если Параметры.ЭтоНоменклатура Тогда
			СтрокаСписка.ВидСтроки = СтрокаТабличнойЧасти.Номенклатура;
		Иначе
			СтрокаСписка.ВидСтроки = СтрокаТабличнойЧасти.ОсновноеСредство;
		КонецЕсли;	
		
		СтрокаСписка.Всего 	= СтрокаТабличнойЧасти.Всего;
		СтрокаСписка.Доход 	= СтрокаТабличнойЧасти.СуммаДохода;
		СтрокаСписка.НДС 	= СтрокаТабличнойЧасти.СуммаНДС;
		СтрокаСписка.НСП 	= СтрокаТабличнойЧасти.СуммаНСП;
		
		ИтоговыеДанные.ВсегоБезСкидки 	= ИтоговыеДанные.ВсегоБезСкидки + СтрокаСписка.Всего;
		ИтоговыеДанные.ДоходБезСкидки 	= ИтоговыеДанные.ДоходБезСкидки + СтрокаСписка.Доход;
		ИтоговыеДанные.НДСБезСкидки 	= ИтоговыеДанные.НДСБезСкидки + СтрокаСписка.НДС;
		ИтоговыеДанные.НСПБезСкидки 	= ИтоговыеДанные.НСПБезСкидки + СтрокаСписка.НСП;
		
		// Строка сумм скидки.
		СтрокаСписка = Список.Добавить();
		СтрокаСписка.ВидСтроки 	= НСтр("ru = 'Скидка'");
		СтрокаСписка.Всего 		= СтруктураНачальныхСумм.Скидка;
		
		Если Параметры.Объект.СуммаВключаетНалоги Тогда
			СтрокаСписка.Доход 	= СтрокаТабличнойЧасти.СуммаДохода - СтруктураНачальныхСумм.Доход;
		Иначе
			СуммаДоходаСкидки 	= СтруктураНачальныхСумм.Скидка 
									- (СтрокаТабличнойЧасти.СуммаНДС - СтруктураНачальныхСумм.НДС)
									- (СтрокаТабличнойЧасти.СуммаНСП - СтруктураНачальныхСумм.НСП);
			СтрокаСписка.Доход 	= СуммаДоходаСкидки;
		КонецЕсли;	
			
		СтрокаСписка.НДС 	= СтрокаТабличнойЧасти.СуммаНДС - СтруктураНачальныхСумм.НДС;
		СтрокаСписка.НСП 	= СтрокаТабличнойЧасти.СуммаНСП - СтруктураНачальныхСумм.НСП;
		
		ИтоговыеДанные.ВсегоСкидка 	= ИтоговыеДанные.ВсегоСкидка + СтрокаСписка.Всего;
		ИтоговыеДанные.ДоходСкидка 	= ИтоговыеДанные.ДоходСкидка + СтрокаСписка.Доход;
		ИтоговыеДанные.НДССкидка 	= ИтоговыеДанные.НДССкидка + СтрокаСписка.НДС;
		ИтоговыеДанные.НСПСкидка 	= ИтоговыеДанные.НСПСкидка + СтрокаСписка.НСП;
		
		// Строка сумм за вычетом сумм скидки.
		СтрокаСписка = Список.Добавить();
		СтрокаСписка.ВидСтроки 	= НСтр("ru = 'Итого со скидкой'");
		СтрокаСписка.Всего 		= СтрокаТабличнойЧасти.Всего - СтруктураНачальныхСумм.Скидка;
		
		Если Параметры.Объект.СуммаВключаетНалоги Тогда
			СтрокаСписка.Доход 	= СтруктураНачальныхСумм.Доход;	
		Иначе
			СтрокаСписка.Доход 	= СтрокаТабличнойЧасти.СуммаДохода - СуммаДоходаСкидки;
		КонецЕсли;	
			
		СтрокаСписка.НДС 	= СтруктураНачальныхСумм.НДС;
		СтрокаСписка.НСП 	= СтруктураНачальныхСумм.НСП;
		
		ИтоговыеДанные.ВсегоСоСкидкой 	= ИтоговыеДанные.ВсегоСоСкидкой + СтрокаСписка.Всего;
		ИтоговыеДанные.ДоходСоСкидкой 	= ИтоговыеДанные.ДоходСоСкидкой + СтрокаСписка.Доход;
		ИтоговыеДанные.НДССоСкидкой 	= ИтоговыеДанные.НДССоСкидкой + СтрокаСписка.НДС;
		ИтоговыеДанные.НСПСоСкидкой 	= ИтоговыеДанные.НСПСоСкидкой + СтрокаСписка.НСП;
	КонецЦикла;
	
	СтрокаСписка = Список.Добавить();
	СтрокаСписка.ВидСтроки = НСтр("ru = 'Итого Товар'");
	СтрокаСписка.Всего 	= ИтоговыеДанные.ВсегоБезСкидки;
	СтрокаСписка.Доход 	= ИтоговыеДанные.ДоходБезСкидки;
	СтрокаСписка.НДС 	= ИтоговыеДанные.НДСБезСкидки;
	СтрокаСписка.НСП 	= ИтоговыеДанные.НСПБезСкидки;
	
	СтрокаСписка = Список.Добавить();
	СтрокаСписка.ВидСтроки = НСтр("ru = 'Итого скидка'");
	СтрокаСписка.Всего 	= ИтоговыеДанные.ВсегоСкидка;
	СтрокаСписка.Доход 	= ИтоговыеДанные.ДоходСкидка;
	СтрокаСписка.НДС 	= ИтоговыеДанные.НДССкидка;
	СтрокаСписка.НСП 	= ИтоговыеДанные.НСПСкидка;
	
	СтрокаСписка = Список.Добавить();
	СтрокаСписка.ВидСтроки = НСтр("ru = 'Итог общий со скидкой'");
	СтрокаСписка.Всего 	= ИтоговыеДанные.ВсегоСоСкидкой;
	СтрокаСписка.Доход 	= ИтоговыеДанные.ДоходСоСкидкой;
	СтрокаСписка.НДС 	= ИтоговыеДанные.НДССоСкидкой;
	СтрокаСписка.НСП 	= ИтоговыеДанные.НСПСоСкидкой;
	
	УстановитьВидимостьДоступностьЭлементов(Параметры);
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов(Параметры)

	Элементы.Список.ТолькоПросмотр = Истина;

	Если Параметры.Объект.БезналичныйРасчет Тогда
		Элементы.СписокНСП.Видимость = Ложь;
	Иначе
		Элементы.СписокНСП.Видимость = Параметры.ДанныеУчетнойПолитики.ПлательщикНСП;
	КонецЕсли;	

	Если Параметры.СтавкаНДС = Справочники.СтавкиНДС.Нулевая
		ИЛИ Параметры.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная
		ИЛИ Параметры.СтавкаНДС = Справочники.СтавкиНДС.Необлагаемая Тогда
		Элементы.СписокНДС.Видимость = Ложь;
	Иначе
		Элементы.СписокНДС.Видимость = Параметры.ДанныеУчетнойПолитики.ПлательщикНДС;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти