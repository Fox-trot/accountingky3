#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТаблицаОплатаПоставщикам.ОплатаРасчеты
		|			ТОГДА ТаблицаОплатаПоставщикам.СчетРасчетов
		|		ИНАЧЕ ТаблицаОплатаПоставщикам.СчетАвансов
		|	КОНЕЦ КАК СчетДт,
		|	ТаблицаШапка.СчетУчета КАК СчетКт,
		|	ТаблицаОплатаПоставщикам.Контрагент КАК СубконтоДт1,
		|	ТаблицаОплатаПоставщикам.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ТаблицаШапка.ФизЛицо КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаШапка.Период КАК Период,
		|	ТаблицаШапка.Организация,
		|	""Оплата поставщикам(аванс)"" КАК Содержание,
		|	ВЫБОР
		|		КОГДА ТаблицаОплатаПоставщикам.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаОплатаПоставщикам.Сумма
		|		ИНАЧЕ ТаблицаОплатаПоставщикам.Сумма * ТаблицаОплатаПоставщикам.Курс
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ВЫБОР
		|					КОГДА ТаблицаОплатаПоставщикам.ОплатаРасчеты
		|						ТОГДА ТаблицаОплатаПоставщикам.СчетРасчетов
		|					ИНАЧЕ ТаблицаОплатаПоставщикам.СчетАвансов
		|				КОНЕЦ КАК ПланСчетов.Хозрасчетный).Валютный
		|			ТОГДА ТаблицаОплатаПоставщикам.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаДт,
		|	ВЫБОР
		|		КОГДА ТаблицаОплатаПоставщикам.СчетАвансов.Валютный
		|			ТОГДА ТаблицаОплатаПоставщикам.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ВЫБОР
		|					КОГДА ТаблицаОплатаПоставщикам.ОплатаРасчеты
		|						ТОГДА ТаблицаОплатаПоставщикам.СчетРасчетов
		|					ИНАЧЕ ТаблицаОплатаПоставщикам.СчетАвансов
		|				КОНЕЦ КАК ПланСчетов.Хозрасчетный).Валютный
		|			ТОГДА ТаблицаОплатаПоставщикам.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА ТаблицаОплатаПоставщикам.СчетАвансов.Валютный
		|			ТОГДА ТаблицаОплатаПоставщикам.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаКт,
		|	0 КАК Количество,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаОплатаПоставщикам КАК ТаблицаОплатаПоставщикам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаОплатаПоставщикам.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТовары.СчетУчета,
		|	ТаблицаТовары.СчетРасчетов,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Склад,
		|	NULL,
		|	ТаблицаТовары.Контрагент,
		|	ТаблицаТовары.ДоговорКонтрагента,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Оприходованы ТМЗ"",
		|	(ТаблицаТовары.Всего - ТаблицаТовары.СуммаНДС) * ТаблицаТовары.Курс,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчета.Валютный
		|			ТОГДА ТаблицаТовары.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаТовары.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчета.Валютный
		|			ТОГДА ТаблицаТовары.Всего - ТаблицаТовары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаТовары.Всего - ТаблицаТовары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ТаблицаТовары.Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчета.Количественный
		|			ТОГДА ТаблицаТовары.Количество
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(ТаблицаТовары.Всего - ТаблицаТовары.СуммаНДС) * ТаблицаТовары.Курс <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.СчетНДС,
		|	ТаблицаТовары.СчетРасчетов,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Склад,
		|	NULL,
		|	ТаблицаТовары.Контрагент,
		|	ТаблицаТовары.ДоговорКонтрагента,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Выделен НДС"",
		|	ТаблицаТовары.СуммаНДС * ТаблицаТовары.Курс,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчета.Валютный
		|			ТОГДА ТаблицаТовары.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаТовары.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчета.Валютный
		|			ТОГДА ТаблицаТовары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаТовары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаТовары.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТовары.СчетРасчетов,
		|	ТаблицаШапка.СчетУчета,
		|	ТаблицаТовары.Контрагент,
		|	ТаблицаТовары.ДоговорКонтрагента,
		|	NULL,
		|	ТаблицаШапка.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""ТМЗ - прочие поступления."",
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаТовары.Всего
		|		ИНАЧЕ ТаблицаТовары.Всего * ТаблицаТовары.Курс
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетАвансов.Валютный
		|			ТОГДА ТаблицаТовары.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаТовары.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетАвансов.Валютный
		|			ТОГДА ТаблицаТовары.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаТовары.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаТовары.Всего <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаУслуги.СчетУчета,
		|	ТаблицаУслуги.СчетРасчетов,
		|	ТаблицаУслуги.Номенклатура,
		|	NULL,
		|	NULL,
		|	ТаблицаУслуги.Контрагент,
		|	ТаблицаУслуги.ДоговорКонтрагента,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Услуги и прочие поступления"",
		|	(ТаблицаУслуги.Всего - ТаблицаУслуги.СуммаНДС) * ТаблицаУслуги.Курс,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетУчета.Валютный
		|			ТОГДА ТаблицаУслуги.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаУслуги.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетУчета.Валютный
		|			ТОГДА ТаблицаУслуги.Всего - ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаУслуги.Всего - ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ТаблицаУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(ТаблицаУслуги.Всего - ТаблицаУслуги.СуммаНДС) * ТаблицаУслуги.Курс <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.СчетНДС,
		|	ТаблицаУслуги.СчетРасчетов,
		|	ТаблицаУслуги.Номенклатура,
		|	NULL,
		|	NULL,
		|	ТаблицаУслуги.Контрагент,
		|	ТаблицаУслуги.ДоговорКонтрагента,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Выделен НДС"",
		|	ТаблицаУслуги.СуммаНДС * ТаблицаУслуги.Курс,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетУчета.Валютный
		|			ТОГДА ТаблицаУслуги.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаУслуги.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетУчета.Валютный
		|			ТОГДА ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетРасчетов.Валютный
		|			ТОГДА ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ТаблицаУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаУслуги.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаУслуги.СчетРасчетов,
		|	ТаблицаШапка.СчетУчета,
		|	ТаблицаУслуги.Контрагент,
		|	ТаблицаУслуги.ДоговорКонтрагента,
		|	NULL,
		|	ТаблицаШапка.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Услуги - прочие поступления."",
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаУслуги.Всего
		|		ИНАЧЕ ТаблицаУслуги.Всего * ТаблицаУслуги.Курс
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетАвансов.Валютный
		|			ТОГДА ТаблицаУслуги.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаУслуги.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.СчетАвансов.Валютный
		|			ТОГДА ТаблицаУслуги.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаУслуги.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ТаблицаУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаУслуги.Всего <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаПрочее.СчетУчета,
		|	ТаблицаШапка.СчетУчета,
		|	ТаблицаПрочее.СубконтоДт1,
		|	ТаблицаПрочее.СубконтоДт2,
		|	ТаблицаПрочее.СубконтоДт3,
		|	ТаблицаШапка.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Поступления по прочим операциям"",
		|	ВЫБОР
		|		КОГДА ТаблицаПрочее.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаПрочее.Сумма
		|		ИНАЧЕ ТаблицаПрочее.Сумма * ТаблицаПрочее.Курс
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаПрочее.СчетУчета.Валютный
		|			ТОГДА ТаблицаПрочее.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаПрочее.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаПрочее.СчетУчета.Валютный
		|			ТОГДА ТаблицаПрочее.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаПрочее.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ТаблицаПрочее.Количество,
		|	ТаблицаПрочее.Количество,
		|	ТаблицаПрочее.Количество
		|ИЗ
		|	ВременнаяТаблицаПрочее КАК ТаблицаПрочее
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаПрочее.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.СчетУчетаЗП,
		|	ТаблицаШапка.СчетУчета,
		|	ТаблицаЗПСотрудникам.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Выплата ЗП сотрудникам"",
		|	ТаблицаЗПСотрудникам.Сумма,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	ТаблицаЗПСотрудникам.Сумма,
		|	ТаблицаЗПСотрудникам.Сумма,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаЗП КАК ТаблицаЗПСотрудникам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаШапка.ПоВедомости
		|	И ТаблицаЗПСотрудникам.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.СчетУчетаЗП,
		|	ТаблицаШапка.СчетУчета,
		|	ВедомостьНаВыплатуЗарплатыЗарплата.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Выплата ЗП по ведомости"",
		|	ВедомостьНаВыплатуЗарплатыЗарплата.СуммаКВыплате,
		|	&ВалютаРегламентированногоУчета,
		|	&ВалютаРегламентированногоУчета,
		|	ВедомостьНаВыплатуЗарплатыЗарплата.СуммаКВыплате,
		|	ВедомостьНаВыплатуЗарплатыЗарплата.СуммаКВыплате,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаЗП КАК ТаблицаЗПСотрудникам
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВедомостьЗП.Зарплата КАК ВедомостьНаВыплатуЗарплатыЗарплата
		|		ПО ТаблицаЗПСотрудникам.Ведомость = ВедомостьНаВыплатуЗарплатыЗарплата.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаШапка.ПоВедомости
		|	И ВедомостьНаВыплатуЗарплатыЗарплата.СуммаКВыплате <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаШапка.СчетУчета,
		|	ТаблицаШапка.СчетУчета,
		|	ТаблицаШапка.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаШапка.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Обмен валют подотчетником"",
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ВалютаОтчета = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаОбменВалют.СуммаОтчета
		|		КОГДА ТаблицаОбменВалют.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаОбменВалют.Сумма
		|		ИНАЧЕ ТаблицаОбменВалют.СуммаОтчета * ТаблицаОбменВалют.КурсНБКР
		|	КОНЕЦ,
		|	ТаблицаОбменВалют.ВалютаОтчета,
		|	ТаблицаОбменВалют.Валюта,
		|	ТаблицаОбменВалют.СуммаОтчета,
		|	ТаблицаОбменВалют.Сумма,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОбменВалют КАК ТаблицаОбменВалют
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ТаблицаОбменВалют.ВалютаОтчета = &ВалютаРегламентированногоУчета
		|				ТОГДА ТаблицаОбменВалют.СуммаОтчета
		|			КОГДА ТаблицаОбменВалют.Валюта = &ВалютаРегламентированногоУчета
		|				ТОГДА ТаблицаОбменВалют.Сумма
		|			ИНАЧЕ ТаблицаОбменВалют.СуммаОтчета * ТаблицаОбменВалют.КурсНБКР
		|		КОНЕЦ <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА ТаблицаШапка.СчетУчета
		|					ИНАЧЕ ТаблицаОбменВалют.СчетУчетаУбыткаОтКурсовойРазницы
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА ТаблицаОбменВалют.СчетУчетаУбыткаОтКурсовойРазницы
		|				ИНАЧЕ ТаблицаШапка.СчетУчета
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА ТаблицаОбменВалют.СчетУчетаДоходаОтКурсовойРазницы
		|					ИНАЧЕ ТаблицаШапка.СчетУчета
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА ТаблицаШапка.СчетУчета
		|				ИНАЧЕ ТаблицаОбменВалют.СчетУчетаДоходаОтКурсовойРазницы
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА ТаблицаШапка.ФизЛицо
		|					ИНАЧЕ ТаблицаОбменВалют.СубконтоСтатьяЗатратКурсовыеРазницы
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА ТаблицаОбменВалют.СубконтоСтатьяЗатратКурсовыеРазницы
		|				ИНАЧЕ ТаблицаШапка.ФизЛицо
		|			КОНЕЦ
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА ТаблицаОбменВалют.СубконтоСтатьяЗатратКурсовыеРазницы
		|					ИНАЧЕ ТаблицаШапка.ФизЛицо
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА ТаблицаШапка.ФизЛицо
		|				ИНАЧЕ ТаблицаОбменВалют.СубконтоСтатьяЗатратКурсовыеРазницы
		|			КОНЕЦ
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	""Операционная курсовая разница"",
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА -ТаблицаОбменВалют.СуммаОКР
		|					ИНАЧЕ ТаблицаОбменВалют.СуммаОКР
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА -ТаблицаОбменВалют.СуммаОКР
		|				ИНАЧЕ ТаблицаОбменВалют.СуммаОКР
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА ТаблицаОбменВалют.Валюта
		|					ИНАЧЕ NULL
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА NULL
		|				ИНАЧЕ ТаблицаОбменВалют.ВалютаОтчета
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА NULL
		|					ИНАЧЕ ТаблицаОбменВалют.Валюта
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА ТаблицаОбменВалют.ВалютаОтчета
		|				ИНАЧЕ NULL
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА ТаблицаОбменВалют.Сумма
		|					ИНАЧЕ NULL
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА NULL
		|				ИНАЧЕ ТаблицаОбменВалют.СуммаОтчета
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА NULL
		|					ИНАЧЕ ТаблицаОбменВалют.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|					ТОГДА ТаблицаОбменВалют.СуммаОтчета
		|				ИНАЧЕ NULL
		|			КОНЕЦ
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОбменВалют КАК ТаблицаОбменВалют
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ТаблицаОбменВалют.ПризнакДтКт = ""Дт""
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|							ТОГДА -ТаблицаОбменВалют.СуммаОКР
		|						ИНАЧЕ ТаблицаОбменВалют.СуммаОКР
		|					КОНЕЦ
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ТаблицаОбменВалют.СуммаОКР < 0
		|						ТОГДА -ТаблицаОбменВалют.СуммаОКР
		|					ИНАЧЕ ТаблицаОбменВалют.СуммаОКР
		|				КОНЕЦ
		|		КОНЕЦ <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТоварыУслугиПатентника.СчетУчета,
		|	ТаблицаШапка.СчетУчета,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.ЕстьСубконтоНоменклатура
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Номенклатура
		|		ИНАЧЕ ТаблицаТоварыУслугиПатентника.СубконтоДт1
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.ЕстьСубконтоНоменклатура
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Склад
		|		ИНАЧЕ ТаблицаТоварыУслугиПатентника.СубконтоДт2
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.ЕстьСубконтоНоменклатура
		|			ТОГДА NULL
		|		ИНАЧЕ ТаблицаТоварыУслугиПатентника.СубконтоДт3
		|	КОНЕЦ,
		|	ТаблицаШапка.ФизЛицо,
		|	NULL,
		|	NULL,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.ЕстьСубконтоНоменклатура
		|			ТОГДА ""Оприходованы ТМЗ (патенты)""
		|		ИНАЧЕ ""Расходы (патенты)""
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Сумма
		|		ИНАЧЕ ТаблицаТоварыУслугиПатентника.Сумма * ТаблицаТоварыУслугиПатентника.Курс
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.СчетУчета.Валютный
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Валюта
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаТоварыУслугиПатентника.СчетУчета.Валютный
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаШапка.СчетУчета.Валютный
		|			ТОГДА ТаблицаТоварыУслугиПатентника.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТоварыУслугиПатентника КАК ТаблицаТоварыУслугиПатентника
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ТаблицаТоварыУслугиПатентника.Валюта = &ВалютаРегламентированногоУчета
		|				ТОГДА ТаблицаТоварыУслугиПатентника.Сумма
		|			ИНАЧЕ ТаблицаТоварыУслугиПатентника.Сумма * ТаблицаТоварыУслугиПатентника.Курс
		|		КОНЕЦ <> 0";
	
		
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);			
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

Процедура СформироватьТаблицаПоступления(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Ссылка КАК ДокументПоступления,
	|	ТаблицаШапка.Организация,
	|	ТаблицаТовары.Контрагент,
	|	ТаблицаТовары.ДоговорКонтрагента,	
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.СтавкаНДС,	
	|	ТаблицаТовары.ЗачетНДС КАК ЗачетНДС,
	|	ТаблицаТовары.СуммаНДС * ТаблицаТовары.Курс КАК СуммаНДС,
	|	ТаблицаТовары.Сумма * ТаблицаТовары.Курс КАК СуммаСебестоимости,
	|	ТаблицаТовары.СуммаНСП * ТаблицаТовары.Курс КАК СуммаНеоблагаемая,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДСВал,
	|	ТаблицаТовары.Сумма КАК СуммаСебестоимостиВал,
	|	ТаблицаТовары.СуммаНСП КАК СуммаНеоблагаемаяВал,
	|	ТаблицаТовары.Количество
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО ТаблицаТовары.Ссылка = ТаблицаШапка.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Ссылка,
	|	ТаблицаШапка.Организация,
	|	ТаблицаУслуги.Контрагент,
	|	ТаблицаУслуги.ДоговорКонтрагента,	
	|	ТаблицаУслуги.Номенклатура,
	|	ТаблицаУслуги.СтавкаНДС,	
	|	ТаблицаУслуги.ЗачетНДС,
	|	ТаблицаУслуги.СуммаНДС * ТаблицаУслуги.Курс,
	|	ТаблицаУслуги.Сумма * ТаблицаУслуги.Курс,
	|	ТаблицаУслуги.СуммаНСП * ТаблицаУслуги.Курс,
	|	ТаблицаУслуги.СуммаНДС,
	|	ТаблицаУслуги.Сумма,
	|	ТаблицаУслуги.СуммаНСП,
	|	1
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО ТаблицаУслуги.Ссылка = ТаблицаШапка.Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоступления", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

Процедура СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	ОсновнойСпособОценкиЗапасов = СтруктураДополнительныеСвойства.ДляПроведения.ОсновнойСпособОценкиЗапасов;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаТовары.СчетУчета,
	|	ВременнаяТаблицаТовары.Склад,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ ВременнаяТаблицаТовары.СчетУчета.Количественный
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА СпособыОценкиЗапасов.СчетУчета ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА &ОсновнойСпособОценкиЗапасов = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ВременнаяТаблицаШапка.Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СпособыОценкиЗапасов.СпособОценки = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ ВременнаяТаблицаШапка.Ссылка
	|			КОНЕЦ
	|	КОНЕЦ КАК Партия,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаТовары.СчетУчета.Количественный
	|			ТОГДА ВременнаяТаблицаТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ВременнаяТаблицаТовары.Цена * ВременнаяТаблицаТовары.Курс КАК Цена,
	|	ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаТовары.Курс КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОценкиЗапасов КАК СпособыОценкиЗапасов
	|		ПО ВременнаяТаблицаТовары.СчетУчета = СпособыОценкиЗапасов.СчетУчета
	|ГДЕ
	|	НЕ ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)";
	
	Запрос.Параметры.Вставить("ОсновнойСпособОценкиЗапасов", ОсновнойСпособОценкиЗапасов);
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

Процедура СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаШапка.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаШапка.Организация,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаШапка.Ссылка КАК Партия,
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.Сумма * ТаблицаТовары.Курс КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыМБП.Новый) КАК Статус,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.СчетУчета
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМБПСклад", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

Процедура СформироватьТаблицаПоступленияИтоги(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Организация,
	|	ТаблицаШапка.Ссылка КАК ДокументПоступления,
	|	ТаблицаТовары.Контрагент,
	|	ТаблицаТовары.ДоговорКонтрагента КАК Договор,
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.СерияБланкаСФ,
	|	ТаблицаТовары.НомерБланкаСФ,
	|	ВременнаяТаблицаСчетФактуры.ДатаВыписки КАК ДатаСФ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыУчетаНДС.ПустаяСсылка) КАК ВидУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Контрагент.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.Киргизия)
	|				ИЛИ ТаблицаТовары.Контрагент.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|		КОГДА ТаблицаТовары.Контрагент.СтранаРезидентства.ЕАЭС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ПустаяСсылка)
	|	КОНЕЦ КАК ПризнакСтраны
	|ПОМЕСТИТЬ ВременнаяТаблицаСтроки
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО ТаблицаТовары.Ссылка = ТаблицаШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактура КАК ВременнаяТаблицаСчетФактуры
	|		ПО ТаблицаТовары.СерияБланкаСФ = ВременнаяТаблицаСчетФактуры.СерияБланкаСФ
	|			И ТаблицаТовары.НомерБланкаСФ = ВременнаяТаблицаСчетФактуры.НомерБланкаСФ
	|			И ТаблицаТовары.Ссылка = ВременнаяТаблицаСчетФактуры.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Организация,
	|	ТаблицаШапка.Ссылка,
	|	ТаблицаУслуги.Контрагент,
	|	ТаблицаУслуги.ДоговорКонтрагента,
	|	NULL,
	|	ТаблицаУслуги.СерияБланкаСФ,
	|	ТаблицаУслуги.НомерБланкаСФ,
	|	ВременнаяТаблицаСчетФактуры.ДатаВыписки,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыУчетаНДС.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Контрагент.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.Киргизия)
	|				ИЛИ ТаблицаУслуги.Контрагент.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
	|		КОГДА ТаблицаУслуги.Контрагент.СтранаРезидентства.ЕАЭС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ПустаяСсылка)
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО ТаблицаУслуги.Ссылка = ТаблицаШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактура КАК ВременнаяТаблицаСчетФактуры
	|		ПО ТаблицаУслуги.СерияБланкаСФ = ВременнаяТаблицаСчетФактуры.СерияБланкаСФ
	|			И ТаблицаУслуги.НомерБланкаСФ = ВременнаяТаблицаСчетФактуры.НомерБланкаСФ
	|			И ТаблицаУслуги.Ссылка = ВременнаяТаблицаСчетФактуры.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаСтроки.Период,
	|	ВременнаяТаблицаСтроки.Организация,
	|	ВременнаяТаблицаСтроки.ДокументПоступления,
	|	ВременнаяТаблицаСтроки.Контрагент,
	|	ВременнаяТаблицаСтроки.Договор,
	|	МАКСИМУМ(ВременнаяТаблицаСтроки.Склад) КАК Склад,
	|	ВременнаяТаблицаСтроки.СерияБланкаСФ,
	|	ВременнаяТаблицаСтроки.НомерБланкаСФ,
	|	МАКСИМУМ(ВременнаяТаблицаСтроки.ДатаСФ) КАК ДатаСФ,
	|	МАКСИМУМ(ВременнаяТаблицаСтроки.ВидУчета) КАК ВидУчета,
	|	МАКСИМУМ(ВременнаяТаблицаСтроки.ПризнакСтраны) КАК ПризнакСтраны
	|ИЗ
	|	ВременнаяТаблицаСтроки КАК ВременнаяТаблицаСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаСтроки.Период,
	|	ВременнаяТаблицаСтроки.Организация,
	|	ВременнаяТаблицаСтроки.ДокументПоступления,
	|	ВременнаяТаблицаСтроки.Контрагент,
	|	ВременнаяТаблицаСтроки.Договор,
	|	ВременнаяТаблицаСтроки.СерияБланкаСФ,
	|	ВременнаяТаблицаСтроки.НомерБланкаСФ";	
	
	Запрос.УстановитьПараметр("УказанВидЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоступленияИтоги", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	АвансовыйОтчет.Ссылка,
		|	АвансовыйОтчет.ВерсияДанных,
		|	АвансовыйОтчет.ПометкаУдаления,
		|	АвансовыйОтчет.Номер,
		|	АвансовыйОтчет.Дата КАК Период,
		|	АвансовыйОтчет.Проведен,
		|	АвансовыйОтчет.Организация,
		|	АвансовыйОтчет.ФизЛицо,
		|	АвансовыйОтчет.СчетУчета,
		|	АвансовыйОтчет.СуммаДокумента,
		|	АвансовыйОтчет.ДокументОснование,
		|	АвансовыйОтчет.Комментарий,
		|	АвансовыйОтчет.Автор,
		|	АвансовыйОтчет.СчетаФактуры,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению) КАК СчетНДС,
		|	АвансовыйОтчет.ПоВедомости,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата) КАК СчетУчетаЗП
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.АвансовыйОтчет КАК АвансовыйОтчет
		|ГДЕ
		|	АвансовыйОтчет.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетОплатаПоставщикам.Контрагент,
		|	АвансовыйОтчетОплатаПоставщикам.ДоговорКонтрагента,
		|	АвансовыйОтчетОплатаПоставщикам.СчетАвансов,
		|	АвансовыйОтчетОплатаПоставщикам.Ссылка КАК Ссылка,
		|	АвансовыйОтчетОплатаПоставщикам.Сумма,
		|	АвансовыйОтчетОплатаПоставщикам.Валюта,
		|	АвансовыйОтчетОплатаПоставщикам.Курс,
		|	АвансовыйОтчетОплатаПоставщикам.ОплатаРасчеты,
		|	АвансовыйОтчетОплатаПоставщикам.СчетРасчетов
		|ПОМЕСТИТЬ ВременнаяТаблицаОплатаПоставщикам
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчетОплатаПоставщикам
		|ГДЕ
		|	АвансовыйОтчетОплатаПоставщикам.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетТовары.Ссылка КАК Ссылка,
		|	АвансовыйОтчетТовары.Контрагент,
		|	АвансовыйОтчетТовары.ДоговорКонтрагента,
		|	АвансовыйОтчетТовары.Склад,
		|	АвансовыйОтчетТовары.Номенклатура,
		|	АвансовыйОтчетТовары.СчетУчета,
		|	АвансовыйОтчетТовары.СчетРасчетов,
		|	АвансовыйОтчетТовары.СчетАвансов,
		|	АвансовыйОтчетТовары.СтавкаНДС,
		|	АвансовыйОтчетТовары.ЗачетНДС,
		|	ВЫБОР
		|		КОГДА НЕ АвансовыйОтчетТовары.Ссылка.СчетаФактуры
		|			ТОГДА ""ДПБУ""
		|		ИНАЧЕ АвансовыйОтчетТовары.СерияБланкаСФ
		|	КОНЕЦ КАК СерияБланкаСФ,
		|	ВЫБОР
		|		КОГДА НЕ АвансовыйОтчетТовары.Ссылка.СчетаФактуры
		|			ТОГДА АвансовыйОтчетТовары.Ссылка.Номер
		|		ИНАЧЕ АвансовыйОтчетТовары.НомерБланкаСФ
		|	КОНЕЦ КАК НомерБланкаСФ,
		|	АвансовыйОтчетТовары.Валюта,
		|	АвансовыйОтчетТовары.Количество,
		|	АвансовыйОтчетТовары.Курс,
		|	АвансовыйОтчетТовары.СуммаНДС КАК СуммаНДС,
		|	АвансовыйОтчетТовары.СуммаНСП,
		|	АвансовыйОтчетТовары.Цена,
		|	АвансовыйОтчетТовары.Сумма,
		|	АвансовыйОтчетТовары.Всего
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.АвансовыйОтчет.Товары КАК АвансовыйОтчетТовары
		|ГДЕ
		|	АвансовыйОтчетТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетУслуги.Ссылка,
		|	АвансовыйОтчетУслуги.Контрагент,
		|	АвансовыйОтчетУслуги.ДоговорКонтрагента,
		|	АвансовыйОтчетУслуги.Номенклатура,
		|	АвансовыйОтчетУслуги.СчетУчета,
		|	АвансовыйОтчетУслуги.СчетРасчетов,
		|	АвансовыйОтчетУслуги.СчетАвансов,
		|	АвансовыйОтчетУслуги.СубконтоДт1,
		|	АвансовыйОтчетУслуги.СубконтоДт2,
		|	АвансовыйОтчетУслуги.СубконтоДт3,
		|	АвансовыйОтчетУслуги.СтавкаНДС,
		|	АвансовыйОтчетУслуги.ЗачетНДС,
		|	АвансовыйОтчетУслуги.Валюта,
		|	ВЫБОР
		|		КОГДА НЕ АвансовыйОтчетУслуги.Ссылка.СчетаФактуры
		|			ТОГДА ""ДПБУ""
		|		ИНАЧЕ АвансовыйОтчетУслуги.СерияБланкаСФ
		|	КОНЕЦ КАК СерияБланкаСФ,
		|	ВЫБОР
		|		КОГДА НЕ АвансовыйОтчетУслуги.Ссылка.СчетаФактуры
		|			ТОГДА АвансовыйОтчетУслуги.Ссылка.Номер
		|		ИНАЧЕ АвансовыйОтчетУслуги.НомерБланкаСФ
		|	КОНЕЦ КАК НомерБланкаСФ,
		|	АвансовыйОтчетУслуги.Курс,
		|	АвансовыйОтчетУслуги.СуммаНДС,
		|	АвансовыйОтчетУслуги.СуммаНСП,
		|	АвансовыйОтчетУслуги.Сумма,
		|	АвансовыйОтчетУслуги.Всего
		|ПОМЕСТИТЬ ВременнаяТаблицаУслуги
		|ИЗ
		|	Документ.АвансовыйОтчет.Услуги КАК АвансовыйОтчетУслуги
		|ГДЕ
		|	АвансовыйОтчетУслуги.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетПрочее.СчетУчета,
		|	АвансовыйОтчетПрочее.СубконтоДт1,
		|	АвансовыйОтчетПрочее.СубконтоДт2,
		|	АвансовыйОтчетПрочее.СубконтоДт3,
		|	АвансовыйОтчетПрочее.Сумма,
		|	АвансовыйОтчетПрочее.Ссылка,
		|	АвансовыйОтчетПрочее.Валюта,
		|	АвансовыйОтчетПрочее.Курс,
		|	АвансовыйОтчетПрочее.Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочее
		|ИЗ
		|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
		|ГДЕ
		|	АвансовыйОтчетПрочее.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетВыплатаЗаработнойПлаты.Ссылка,
		|	АвансовыйОтчетВыплатаЗаработнойПлаты.Ведомость,
		|	АвансовыйОтчетВыплатаЗаработнойПлаты.Сумма,
		|	АвансовыйОтчетВыплатаЗаработнойПлаты.ФизЛицо
		|ПОМЕСТИТЬ ВременнаяТаблицаЗП
		|ИЗ
		|	Документ.АвансовыйОтчет.ВыплатаЗаработнойПлаты КАК АвансовыйОтчетВыплатаЗаработнойПлаты
		|ГДЕ
		|	АвансовыйОтчетВыплатаЗаработнойПлаты.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетОбменВалют.Ссылка,
		|	АвансовыйОтчетОбменВалют.Сумма,
		|	АвансовыйОтчетОбменВалют.Валюта,
		|	АвансовыйОтчетОбменВалют.СуммаОтчета,
		|	АвансовыйОтчетОбменВалют.ВалютаОтчета,
		|	АвансовыйОтчетОбменВалют.КурсОтчета,
		|	КурсВалюты.Курс КАК КурсНБКР,
		|	ВЫБОР
		|		КОГДА АвансовыйОтчетОбменВалют.Валюта <> &ВалютаРегламентированногоУчета
		|			ТОГДА ""Дт""
		|		КОГДА АвансовыйОтчетОбменВалют.Валюта = &ВалютаРегламентированногоУчета
		|				И АвансовыйОтчетОбменВалют.Валюта <> АвансовыйОтчетОбменВалют.ВалютаОтчета
		|			ТОГДА ""Кт""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ПризнакДтКт,
		|	ВЫБОР
		|		КОГДА АвансовыйОтчетОбменВалют.Валюта <> &ВалютаРегламентированногоУчета
		|			ТОГДА АвансовыйОтчетОбменВалют.Сумма * КурсВалюты.Курс - ВЫБОР
		|					КОГДА АвансовыйОтчетОбменВалют.ВалютаОтчета = &ВалютаРегламентированногоУчета
		|						ТОГДА АвансовыйОтчетОбменВалют.СуммаОтчета
		|					КОГДА АвансовыйОтчетОбменВалют.Валюта = &ВалютаРегламентированногоУчета
		|						ТОГДА АвансовыйОтчетОбменВалют.Сумма
		|					ИНАЧЕ АвансовыйОтчетОбменВалют.СуммаОтчета * КурсВалютыОтчета.Курс
		|				КОНЕЦ
		|		КОГДА АвансовыйОтчетОбменВалют.Валюта = &ВалютаРегламентированногоУчета
		|				И АвансовыйОтчетОбменВалют.Валюта <> АвансовыйОтчетОбменВалют.ВалютаОтчета
		|			ТОГДА АвансовыйОтчетОбменВалют.СуммаОтчета * КурсВалютыОтчета.Курс - ВЫБОР
		|					КОГДА АвансовыйОтчетОбменВалют.ВалютаОтчета = &ВалютаРегламентированногоУчета
		|						ТОГДА АвансовыйОтчетОбменВалют.СуммаОтчета
		|					КОГДА АвансовыйОтчетОбменВалют.Валюта = &ВалютаРегламентированногоУчета
		|						ТОГДА АвансовыйОтчетОбменВалют.Сумма
		|					ИНАЧЕ АвансовыйОтчетОбменВалют.СуммаОтчета * КурсВалютыОтчета.Курс
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаОКР,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыОтОперационныхКурсовыхРазниц) КАК СчетУчетаДоходаОтКурсовойРазницы,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтОперационныхКурсовыхРазниц) КАК СчетУчетаУбыткаОтКурсовойРазницы,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатратИДоходов.КурсовыеРазницыДоходы) КАК СубконтоСтатьяЗатратКурсовыеРазницы
		|ПОМЕСТИТЬ ВременнаяТаблицаОбменВалют
		|ИЗ
		|	Документ.АвансовыйОтчет.ОбменВалют КАК АвансовыйОтчетОбменВалют
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсВалюты
		|		ПО АвансовыйОтчетОбменВалют.Валюта = КурсВалюты.Валюта
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсВалютыОтчета
		|		ПО АвансовыйОтчетОбменВалют.ВалютаОтчета = КурсВалютыОтчета.Валюта
		|ГДЕ
		|	АвансовыйОтчетОбменВалют.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетТоварыУслуги.Ссылка,
		|	АвансовыйОтчетТоварыУслуги.Контрагент,
		|	АвансовыйОтчетТоварыУслуги.Номенклатура,
		|	АвансовыйОтчетТоварыУслуги.Склад,
		|	АвансовыйОтчетТоварыУслуги.Количество,
		|	АвансовыйОтчетТоварыУслуги.Цена,
		|	АвансовыйОтчетТоварыУслуги.Валюта,
		|	АвансовыйОтчетТоварыУслуги.Курс,
		|	АвансовыйОтчетТоварыУслуги.Сумма,
		|	АвансовыйОтчетТоварыУслуги.СчетУчета,
		|	АвансовыйОтчетТоварыУслуги.Всего,
		|	АвансовыйОтчетТоварыУслуги.СубконтоДт1,
		|	АвансовыйОтчетТоварыУслуги.СубконтоДт2,
		|	АвансовыйОтчетТоварыУслуги.СубконтоДт3,
		|	АвансовыйОтчетТоварыУслуги.ИНН,
		|	АвансовыйОтчетТоварыУслуги.ЕстьСубконтоНоменклатура
		|ПОМЕСТИТЬ ВременнаяТаблицаТоварыУслугиПатентника
		|ИЗ
		|	Документ.АвансовыйОтчет.ТоварыУслуги КАК АвансовыйОтчетТоварыУслуги
		|ГДЕ
		|	АвансовыйОтчетТоварыУслуги.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АвансовыйОтчетСчетФактура.Ссылка,
		|	АвансовыйОтчетСчетФактура.Контрагент,
		|	АвансовыйОтчетСчетФактура.ДоговорКонтрагента,
		|	АвансовыйОтчетСчетФактура.СерияБланкаСФ,
		|	АвансовыйОтчетСчетФактура.НомерБланкаСФ,
		|	АвансовыйОтчетСчетФактура.ДатаВыписки
		|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактура
		|ИЗ
		|	Документ.АвансовыйОтчет.СчетФактура КАК АвансовыйОтчетСчетФактура
		|ГДЕ
		|	АвансовыйОтчетСчетФактура.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", 	ДокументСсылка);
	Запрос.УстановитьПараметр("Дата", 		ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);

	Запрос.Выполнить();
	
	СформироватьТаблицаХозрасчетный(СтруктураДополнительныеСвойства);
	СформироватьТаблицаПоступления(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасы(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаМБПСклад(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПоступленияИтоги(ДокументСсылка, СтруктураДополнительныеСвойства);

КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

// Возвращает название месяца по его номеру 
Функция МесяцПрописью(Дата, ВРодительномПадеже = Ложь) Экспорт
	НомерМесяца = Месяц(Дата);
	Если НомерМесяца = 1 Тогда
		Возврат ?(ВРодительномПадеже, "января", "Январь");
	ИначеЕсли НомерМесяца = 2 Тогда
		Возврат ?(ВРодительномПадеже, "февраля", "Февраль");
	ИначеЕсли НомерМесяца = 3 Тогда
		Возврат ?(ВРодительномПадеже, "марта", "Март");
	ИначеЕсли НомерМесяца = 4 Тогда
		Возврат ?(ВРодительномПадеже, "апреля", "Апрель");
	ИначеЕсли НомерМесяца = 5 Тогда
		Возврат ?(ВРодительномПадеже, "мая", "Май");
	ИначеЕсли НомерМесяца = 6 Тогда
		Возврат ?(ВРодительномПадеже, "июня", "Июнь");
	ИначеЕсли НомерМесяца = 7 Тогда
		Возврат ?(ВРодительномПадеже, "июля", "Июль");
	ИначеЕсли НомерМесяца = 8 Тогда
		Возврат ?(ВРодительномПадеже, "августа", "Август");
	ИначеЕсли НомерМесяца = 9 Тогда
		Возврат ?(ВРодительномПадеже, "сентября", "Сентябрь");
	ИначеЕсли НомерМесяца = 10 Тогда
		Возврат ?(ВРодительномПадеже, "октября", "Октябрь");
	ИначеЕсли НомерМесяца = 11 Тогда
		Возврат ?(ВРодительномПадеже, "ноября", "Ноябрь");
	ИначеЕсли НомерМесяца = 12 Тогда
		Возврат ?(ВРодительномПадеже, "декабря", "Декабрь");
	КонецЕсли;		
КонецФункции

#КонецОбласти

#Область ИнтерфейсПечати

Функция ДанныеТабличныхЧастей(ШапкаДокумента) Экспорт 
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("СчетРасчета",	ШапкаДокумента.СчетРасчетовСПодотчетнымЛицом);
	Запрос.УстановитьПараметр("Сотрудник",  	ШапкаДокумента.ПодотчетноеЛицо);
	Запрос.УстановитьПараметр("Организация",	ШапкаДокумента.Организация);
	Запрос.УстановитьПараметр("ДатаИтогов", 	ШапкаДокумента.Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("Ссылка",			ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("ДатаКонОстатков",ШапкаДокумента.Ссылка.Дата+1);  
	Запрос.УстановитьПараметр("ВалРег",      	ВалютаРегламентированногоУчета);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(АвансовыйОтчетОплатаПоставщикам.Сумма * АвансовыйОтчетОплатаПоставщикам.Курс) КАК СуммаВсего
	|ПОМЕСТИТЬ втТабЧасти
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчетОплатаПоставщикам
	|ГДЕ
	|	АвансовыйОтчетОплатаПоставщикам.Ссылка.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(АвансовыйОтчетУслуги.Сумма * АвансовыйОтчетУслуги.Курс)
	|ИЗ
	|	Документ.АвансовыйОтчет.Услуги КАК АвансовыйОтчетУслуги
	|ГДЕ
	|	АвансовыйОтчетУслуги.Ссылка.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(АвансовыйОтчетТовары.Сумма * АвансовыйОтчетТовары.Курс)
	|ИЗ
	|	Документ.АвансовыйОтчет.Товары КАК АвансовыйОтчетТовары
	|ГДЕ
	|	АвансовыйОтчетТовары.Ссылка.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(АвансовыйОтчетТоварыУслуги.Сумма * АвансовыйОтчетТоварыУслуги.Курс)
	|ИЗ
	|	Документ.АвансовыйОтчет.ТоварыУслуги КАК АвансовыйОтчетТоварыУслуги
	|ГДЕ
	|	АвансовыйОтчетТоварыУслуги.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(АвансовыйОтчетПрочее.Сумма * АвансовыйОтчетПрочее.Курс)
	|ИЗ
	|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
	|ГДЕ
	|	АвансовыйОтчетПрочее.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(АвансовыйОтчетВыплатаЗаработнойПлаты.Сумма)
	|ИЗ
	|	Документ.АвансовыйОтчет.ВыплатаЗаработнойПлаты КАК АвансовыйОтчетВыплатаЗаработнойПлаты
	|ГДЕ
	|	АвансовыйОтчетВыплатаЗаработнойПлаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(0) КАК Выдано,
	|	СУММА(0) КАК Расход,
	|	СУММА(0) КАК Приход,
	|	СУММА(0) КАК КонОстаток,
	|	СУММА(0) КАК СуммаВсего
	|ПОМЕСТИТЬ ВТСвод
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаИтогов,
	|			Счет = &СчетРасчета,
	|			,
	|			Организация = &Организация
	|				И Субконто1 = &Сотрудник) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(АвансовыйОтчетОбменВалют.Сумма),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0)
	|ИЗ
	|	Документ.АвансовыйОтчет.ОбменВалют КАК АвансовыйОтчетОбменВалют
	|ГДЕ
	|	АвансовыйОтчетОбменВалют.Ссылка.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(АвансовыйОтчетОбменВалют.СуммаОтчета),
	|	СУММА(0),
	|	СУММА(0)
	|ИЗ
	|	Документ.АвансовыйОтчет.ОбменВалют КАК АвансовыйОтчетОбменВалют
	|ГДЕ
	|	АвансовыйОтчетОбменВалют.Ссылка.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(ХозрасчетныйОстатки.СуммаОстаток),
	|	СУММА(0)
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаКонОстатков,
	|			Счет = &СчетРасчета,
	|			,
	|			Организация = &Организация
	|				И Субконто1 = &Сотрудник) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(втТабЧасти.СуммаВсего)
	|ИЗ
	|	втТабЧасти КАК втТабЧасти
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТСвод.СуммаОстаток) КАК НачОстаток,
	|	СУММА(ВТСвод.Выдано) КАК Выдано,
	|	СУММА(ВТСвод.Расход) КАК Расход,
	|	СУММА(ВТСвод.Приход) КАК Приход,
	|	СУММА(ВТСвод.КонОстаток) КАК КонОстаток,
	|	СУММА(ВТСвод.СуммаВсего) КАК Израсходовано
	|ИЗ
	|	ВТСвод КАК ВТСвод";
	Результат = Запрос.Выполнить();
	Возврат Результат;

КонецФункции // ПолучитьДанныеТабЧастей()

Функция ПечатьАвансовогоОтчета(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);

	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ПерваяСсылкаТовары 		= Истина;
	ПерваяСсылкаПроводки 	= Истина;
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АвансовыйОтчет_АвансовыйОтчет";	
	
	СтрокаПрописи 	= ВалютаРегламентированногоУчета.ПараметрыПрописиНаРусском;
	СтрокаСом = "";
	СтрокаТн  = "";
	Поз1 = Найти(СтрокаПрописи, ",");  
	Если Поз1 <> 0 Тогда 
		СтрокаСом 	= Лев(СтрокаПрописи,Поз1-1);
		СтрокаПрописи = Сред(СтрокаПрописи,Поз1+1);
		Поз2 = Найти(СтрокаПрописи, ",");  
		Если Поз2 <> 0 Тогда 
			СтрокаПрописи = Сред(СтрокаПрописи,Поз2+1);
			Поз3 = Найти(СтрокаПрописи, ",");  
			Если Поз2 <> 0 Тогда 
				СтрокаПрописи = Сред(СтрокаПрописи,Поз3+1);
				Поз4 = Найти(СтрокаПрописи, ",");  
				Если Поз4 <> 0 Тогда 
					СтрокаПрописи = СокрЛП(Сред(СтрокаПрописи,Поз4+1));
					Поз5 = Найти(СтрокаПрописи, ",");  
					Если Поз4 <> 0 Тогда 
						СтрокаТн = Лев(СтрокаПрописи,Поз5-1);					
					КонецЕсли;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	АвансовыйОтчет.Номер,
		|	АвансовыйОтчет.Дата КАК ДатаДокумента,
		|	АвансовыйОтчет.ФизЛицо КАК ПодотчетноеЛицо,
		|	АвансовыйОтчет.ФизЛицо.Представление КАК ПредставлениеПодотчетногоЛица,
		|	АвансовыйОтчет.Организация.Ссылка КАК Организация,
		|	АвансовыйОтчет.СчетУчета КАК СчетРасчетовСПодотчетнымЛицом,
		|	АвансовыйОтчет.СуммаДокумента,
		|	АвансовыйОтчет.Ссылка КАК Ссылка,
		|	АвансовыйОтчет.Организация.НаименованиеПолное
		|ИЗ
		|	Документ.АвансовыйОтчет КАК АвансовыйОтчет
		|ГДЕ
		|	АвансовыйОтчет.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Ссылка,
		|	ХозрасчетныйДвиженияССубконто.СчетДт КАК СчетДт,
		|	ХозрасчетныйДвиженияССубконто.СчетКт КАК СчетКт,
		|	ХозрасчетныйДвиженияССубконто.Сумма КАК Сумма,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК СубконтоДт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК СубконтоКт,
		|	ХозрасчетныйДвиженияССубконто.ВалютаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютаКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор В (&МассивОбъектов), , ) КАК ХозрасчетныйДвиженияССубконто
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки
		|ИТОГИ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК ID,
		|	АвансовыйОтчетТовары.НомерСтроки КАК НомерСтроки,
		|	АвансовыйОтчетТовары.НомерВходящегоДокумента КАК ДокументНомер,
		|	АвансовыйОтчетТовары.ДатаВходящегоДокумента КАК ДокументДата,
		|	АвансовыйОтчетТовары.ВидДокВходящий + "" "" + АвансовыйОтчетТовары.Номенклатура.Наименование + "" от: "" + (ВЫРАЗИТЬ(АвансовыйОтчетТовары.Контрагент.НаименованиеПолное КАК СТРОКА(50))) КАК НаименованиеРасхода,
		|	СУММА(АвансовыйОтчетТовары.Сумма * АвансовыйОтчетТовары.Курс) КАК ПоОтчету,
		|	АвансовыйОтчетТовары.Сумма КАК ПоОтчетуВВалюте,
		|	АвансовыйОтчетТовары.СчетУчета КАК СчетЗатрат,
		|	АвансовыйОтчетТовары.СчетРасчетов КАК СчетРасчетов,
		|	АвансовыйОтчетТовары.Валюта,
		|	АвансовыйОтчетТовары.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет.Товары КАК АвансовыйОтчетТовары
		|ГДЕ
		|	АвансовыйОтчетТовары.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетТовары.Номенклатура.Наименование,
		|	АвансовыйОтчетТовары.НомерВходящегоДокумента,
		|	АвансовыйОтчетТовары.ДатаВходящегоДокумента,
		|	АвансовыйОтчетТовары.СчетУчета,
		|	АвансовыйОтчетТовары.НомерСтроки,
		|	АвансовыйОтчетТовары.ВидДокВходящий,
		|	АвансовыйОтчетТовары.ВидДокВходящий + "" "" + АвансовыйОтчетТовары.Номенклатура.Наименование + "" от: "" + (ВЫРАЗИТЬ(АвансовыйОтчетТовары.Контрагент.НаименованиеПолное КАК СТРОКА(50))),
		|	АвансовыйОтчетТовары.СчетРасчетов,
		|	АвансовыйОтчетТовары.Валюта,
		|	АвансовыйОтчетТовары.Ссылка,
		|	АвансовыйОтчетТовары.Сумма
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	АвансовыйОтчетОплатаПоставщикам.НомерСтроки,
		|	АвансовыйОтчетОплатаПоставщикам.НомерВходящегоДокумента,
		|	АвансовыйОтчетОплатаПоставщикам.ДатаВходящегоДокумента,
		|	"" Оплата поставщику:"" + (ВЫРАЗИТЬ(АвансовыйОтчетОплатаПоставщикам.Контрагент.НаименованиеПолное КАК СТРОКА(50))),
		|	СУММА(АвансовыйОтчетОплатаПоставщикам.Сумма * АвансовыйОтчетОплатаПоставщикам.Курс),
		|	АвансовыйОтчетОплатаПоставщикам.Сумма,
		|	АвансовыйОтчетОплатаПоставщикам.СчетРасчетов,
		|	АвансовыйОтчетОплатаПоставщикам.СчетАвансов,
		|	АвансовыйОтчетОплатаПоставщикам.Валюта,
		|	АвансовыйОтчетОплатаПоставщикам.Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчетОплатаПоставщикам
		|ГДЕ
		|	АвансовыйОтчетОплатаПоставщикам.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетОплатаПоставщикам.НомерВходящегоДокумента,
		|	АвансовыйОтчетОплатаПоставщикам.ДатаВходящегоДокумента,
		|	ВЫРАЗИТЬ(АвансовыйОтчетОплатаПоставщикам.Содержание КАК СТРОКА(1000)),
		|	АвансовыйОтчетОплатаПоставщикам.СчетРасчетов,
		|	АвансовыйОтчетОплатаПоставщикам.НомерСтроки,
		|	АвансовыйОтчетОплатаПоставщикам.СчетАвансов,
		|	АвансовыйОтчетОплатаПоставщикам.Валюта,
		|	"" Оплата поставщику:"" + (ВЫРАЗИТЬ(АвансовыйОтчетОплатаПоставщикам.Контрагент.НаименованиеПолное КАК СТРОКА(50))),
		|	АвансовыйОтчетОплатаПоставщикам.Ссылка,
		|	АвансовыйОтчетОплатаПоставщикам.Сумма
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	АвансовыйОтчетУслуги.НомерСтроки,
		|	АвансовыйОтчетУслуги.НомерВходящегоДокумента,
		|	АвансовыйОтчетУслуги.ДатаВходящегоДокумента,
		|	АвансовыйОтчетУслуги.ВидДокВходящий + "" "" + АвансовыйОтчетУслуги.Номенклатура.Наименование + "" от: "" + (ВЫРАЗИТЬ(АвансовыйОтчетУслуги.Контрагент.НаименованиеПолное КАК СТРОКА(50))),
		|	СУММА(АвансовыйОтчетУслуги.Сумма * АвансовыйОтчетУслуги.Курс),
		|	АвансовыйОтчетУслуги.Сумма,
		|	АвансовыйОтчетУслуги.СчетУчета,
		|	АвансовыйОтчетУслуги.СчетРасчетов,
		|	АвансовыйОтчетУслуги.Валюта,
		|	АвансовыйОтчетУслуги.Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет.Услуги КАК АвансовыйОтчетУслуги
		|ГДЕ
		|	АвансовыйОтчетУслуги.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетУслуги.Номенклатура.Наименование,
		|	АвансовыйОтчетУслуги.ВидДокВходящий,
		|	АвансовыйОтчетУслуги.НомерВходящегоДокумента,
		|	АвансовыйОтчетУслуги.ДатаВходящегоДокумента,
		|	АвансовыйОтчетУслуги.СчетУчета,
		|	АвансовыйОтчетУслуги.НомерСтроки,
		|	АвансовыйОтчетУслуги.ВидДокВходящий + "" "" + АвансовыйОтчетУслуги.Номенклатура.Наименование + "" от: "" + (ВЫРАЗИТЬ(АвансовыйОтчетУслуги.Контрагент.НаименованиеПолное КАК СТРОКА(50))),
		|	АвансовыйОтчетУслуги.СчетРасчетов,
		|	АвансовыйОтчетУслуги.Валюта,
		|	АвансовыйОтчетУслуги.Ссылка,
		|	АвансовыйОтчетУслуги.Сумма
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	АвансовыйОтчетТоварыУслуги.НомерСтроки,
		|	"""",
		|	"""",
		|	АвансовыйОтчетТоварыУслуги.Номенклатура.Наименование + "" от: "" + АвансовыйОтчетТоварыУслуги.Контрагент,
		|	СУММА(АвансовыйОтчетТоварыУслуги.Сумма * АвансовыйОтчетТоварыУслуги.Курс),
		|	АвансовыйОтчетТоварыУслуги.Сумма,
		|	АвансовыйОтчетТоварыУслуги.СчетУчета,
		|	АвансовыйОтчетТоварыУслуги.Ссылка.СчетУчета,
		|	АвансовыйОтчетТоварыУслуги.Валюта,
		|	АвансовыйОтчетТоварыУслуги.Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет.ТоварыУслуги КАК АвансовыйОтчетТоварыУслуги
		|ГДЕ
		|	АвансовыйОтчетТоварыУслуги.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетТоварыУслуги.СчетУчета,
		|	АвансовыйОтчетТоварыУслуги.Номенклатура.Наименование + "" от: "" + АвансовыйОтчетТоварыУслуги.Контрагент,
		|	АвансовыйОтчетТоварыУслуги.Ссылка.СчетУчета,
		|	АвансовыйОтчетТоварыУслуги.Валюта,
		|	АвансовыйОтчетТоварыУслуги.НомерСтроки,
		|	АвансовыйОтчетТоварыУслуги.Ссылка,
		|	АвансовыйОтчетТоварыУслуги.Сумма
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	АвансовыйОтчетПрочее.НомерСтроки,
		|	АвансовыйОтчетПрочее.НомерВходящегоДокумента,
		|	АвансовыйОтчетПрочее.ДатаВходящегоДокумента,
		|	АвансовыйОтчетПрочее.ВидДокВходящий + "" "" + АвансовыйОтчетПрочее.Номенклатура.Наименование,
		|	СУММА(АвансовыйОтчетПрочее.Сумма * АвансовыйОтчетПрочее.Курс),
		|	АвансовыйОтчетПрочее.Сумма,
		|	АвансовыйОтчетПрочее.СчетУчета,
		|	АвансовыйОтчетПрочее.Ссылка.СчетУчета,
		|	АвансовыйОтчетПрочее.Валюта,
		|	АвансовыйОтчетПрочее.Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
		|ГДЕ
		|	АвансовыйОтчетПрочее.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетПрочее.НомерВходящегоДокумента,
		|	АвансовыйОтчетПрочее.ДатаВходящегоДокумента,
		|	АвансовыйОтчетПрочее.ВидДокВходящий + "" "" + АвансовыйОтчетПрочее.Номенклатура.Наименование,
		|	АвансовыйОтчетПрочее.СчетУчета,
		|	АвансовыйОтчетПрочее.Валюта,
		|	АвансовыйОтчетПрочее.НомерСтроки,
		|	АвансовыйОтчетПрочее.Ссылка.СчетУчета,
		|	АвансовыйОтчетПрочее.Ссылка,
		|	АвансовыйОтчетПрочее.Сумма
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	NULL,
		|	NULL,
		|	NULL,
		|	""Выплачено расходными ордерами"",
		|	СУММА(ВыплатаЗарплатыЗарплата.СуммаКВыплате),
		|	СУММА(ВыплатаЗарплатыЗарплата.СуммаКВыплате),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
		|	АвансовыйОтчет.Ссылка.СчетУчета,
		|	&ВалютаРегламентированногоУчета,
		|	АвансовыйОтчет.Ссылка
		|ИЗ
		|	Документ.ВедомостьЗП.Зарплата КАК ВыплатаЗарплатыЗарплата
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет.ВыплатаЗаработнойПлаты КАК АвансовыйОтчет
		|		ПО ВыплатаЗарплатыЗарплата.Ссылка = АвансовыйОтчет.Ведомость
		|ГДЕ
		|	АвансовыйОтчет.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчет.Ссылка.СчетУчета,
		|	АвансовыйОтчет.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|ИТОГИ ПО
		|	Ссылка";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", 					МассивОбъектов);		
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", 	ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("Дата", 								МассивОбъектов[0].Дата);
	Запрос.УстановитьПараметр("Организация", 						МассивОбъектов[0].Организация);
	
	РезультатПакет =  Запрос.ВыполнитьПакет();	
	
	Шапка = РезультатПакет[0].Выбрать();
	Пока Шапка.Следующий() Цикл
	
		Ссылка = Шапка.Ссылка;	
		
		Макет         =  ПолучитьМакет("АвансовыйОтчет");
		
		ОбластьМакета =  Макет.ПолучитьОбласть("Титул1");
		
		//******* ТИТУЛЬНЫЙ ЛИСТ*******
		КурсоваяРазница = 0;
		
		ДанныеПодотчетногоЛица = БухгалтерскийУчетСервер.ДанныеФизЛица(Шапка.Организация, Шапка.ПодотчетноеЛицо, Шапка.ДатаДокумента);
		
		ПодотчетноеЛицо = ДанныеПодотчетногоЛица.Представление;
		ТабельныйНомер =  Шапка.ПодотчетноеЛицо.Код; 
		Подразделение =   ДанныеПодотчетногоЛица.Подразделение;
		Должность =       ДанныеПодотчетногоЛица.Должность;
		
		// Выводим титульный лист авансового отчета
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ПредставлениеПодотчетногоЛица = ПодотчетноеЛицо;
		КопияПодотчетноеЛицо = ПодотчетноеЛицо;
		
		ОбластьМакета.Параметры.ТабельныйНомер = ТабельныйНомер;
		ОбластьМакета.Параметры.ПредставлениеПодразделения = Подразделение;
		ОбластьМакета.Параметры.ДолжностьПодотчетногоЛицаПредставление = Должность;
		ОбластьМакета.Параметры.НомерДокумента  = Шапка.Номер; 
		
		ПредставлениеОрганизации = Шапка.ОрганизацияНаименованиеПолное;
		
		Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Шапка.Организация, Шапка.ДатаДокумента);
		
		Руководитель = Руководители.Руководитель;
		Бухгалтер    = Руководители.ГлавныйБухгалтер;
		
		
		ОбластьМакета.Параметры.ПредставлениеОрганизации           = СокрЛП(ПредставлениеОрганизации);
		ОбластьМакета.Параметры.РуководительПредставление          = Руководитель;
		
		//******Получаем данные таб части авансов******
		
		// функция получает данные документа		
		СтрокаСуммы = "";
		Результат = ДанныеТабличныхЧастей(Шапка);
		ВыборкаТабличныхЧастей = Результат.Выбрать();
		Пока ВыборкаТабличныхЧастей.Следующий() Цикл
			Если ВыборкаТабличныхЧастей.Израсходовано <> 0 тогда 
				СтрокаСуммы   = СтрокаСуммы + Строка(ВыборкаТабличныхЧастей.Израсходовано) + " " + ВалютаРегламентированногоУчета + "; ";
			КонецЕсли;
		КонецЦикла;
		ВыборкаТабличныхЧастей.Сбросить();
		
		ОбластьМакета.Параметры.СуммаОтчетаЧислом = СтрокаСуммы;
		ТабДокумент.Вывести(ОбластьМакета);
		ОбластьСтроки = Макет.ПолучитьОбласть("ШапкаСтроки");
		ТабДокумент.Вывести(ОбластьСтроки);
		ОбластьСтроки = Макет.ПолучитьОбласть("Строки");
		
		Пока ВыборкаТабличныхЧастей.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаТабличныхЧастей.Выдано) 
				ИЛИ ЗначениеЗаполнено(ВыборкаТабличныхЧастей.Приход) 
				ИЛИ ЗначениеЗаполнено(ВыборкаТабличныхЧастей.Израсходовано) Тогда
				ОбластьСтроки.Параметры.НачОстаток             =  ВыборкаТабличныхЧастей.НачОстаток;
				ОбластьСтроки.Параметры.Израсходовано          =  ВыборкаТабличныхЧастей.Израсходовано;
				ОбластьСтроки.Параметры.СуммаОбмена            =  ?(ВыборкаТабличныхЧастей.Расход <> 0,
					"-" + Формат(ВыборкаТабличныхЧастей.Расход,"ЧЦ=15; ЧДЦ=2; ЧН=") + Символы.ПС, "") 
						+ ?(ВыборкаТабличныхЧастей.Приход <> 0, "+" 
						+ Формат(ВыборкаТабличныхЧастей.Приход,"ЧЦ=15; ЧДЦ=2; ЧН="),
					"");
				ОбластьСтроки.Параметры.Итого                  =  ВыборкаТабличныхЧастей.НачОстаток - 
					?(ВыборкаТабличныхЧастей.Расход <> 0, ВыборкаТабличныхЧастей.Расход, 0) + ?(ВыборкаТабличныхЧастей.Приход <> 0, ВыборкаТабличныхЧастей.Приход,0);
				ОбластьСтроки.Параметры.КонечныйОстаток 	   =  ВыборкаТабличныхЧастей.КонОстаток;
				ТабДокумент.Вывести(ОбластьСтроки);
			КонецЕсли;
		КонецЦикла;
		
		// выводим в отчет таблицу документов
		Если Ссылка.ТаблицаДокументов.Количество()<>0 Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТабДокументов");
			ТабДокумент.Вывести(ОбластьМакета);
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокиТабДокументов");
			
			Для каждого СТЧ из Ссылка.ТаблицаДокументов Цикл
				ОбластьМакета.Параметры.Документ  = СТЧ.ДокументОплаты;
				ОбластьМакета.Параметры.Валюта    = СТЧ.Валюта;
				ОбластьМакета.Параметры.СуммаДокумента  = Формат(СТЧ.СуммаДокумента,"ЧЦ=15; ЧДЦ=2");
				ОбластьМакета.Параметры.Остаток   = Формат(СТЧ.Остаток,"ЧЦ=15; ЧДЦ=2") ;
				ТабДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
		КонецЕсли;
		
		//	Выводим таблицу обмена если она заполнена			
		Если Ссылка.ОбменВалют.Количество()<>0 Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаОбмен");
			
			ТабДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокиОбмен");
			
			Для каждого СТЧ из Ссылка.ОбменВалют Цикл
				ОбластьМакета.Параметры.ВалютаПрихода  = Формат(СТЧ.Валюта,"ЧЦ=15; ЧДЦ=2");
				ОбластьМакета.Параметры.СуммаПрихода   = Формат(СТЧ.СуммаОтчета,"ЧЦ=15; ЧДЦ=2");
				ОбластьМакета.Параметры.ВалютаРасхода  = Формат(СТЧ.ВалютаОтчета,"ЧЦ=15; ЧДЦ=2");
				ОбластьМакета.Параметры.СуммаРасхода   = Формат(СТЧ.Сумма,"ЧЦ=15; ЧДЦ=2") ;
				ОбластьМакета.Параметры.Курс   = СТЧ.КурсОтчета ;
				
				ТабДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
		КонецЕсли;
		
		// выводим таблицу бухгалетерских провордок 
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаБухЗапись");
		
		ТабДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокиБухЗапись");
				
		Если ПерваяСсылкаПроводки Тогда
			ВыборкаПоСсылкеПроводки = РезультатПакет[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		    ПерваяСсылкаПроводки 	= Ложь;
		КонецЕсли;

		Если ВыборкаПоСсылкеПроводки.Следующий() Тогда
			
			ВыборкаПроводок = ВыборкаПоСсылкеПроводки.Выбрать();
			
			Счетчик = 0;
			
			Пока ВыборкаПроводок.Следующий() Цикл
				
				ОбластьМакета.Параметры.СубСчетДебет1  =  ВыборкаПроводок.СчетДт;
				ОбластьМакета.Параметры.СубСчетКредит1 =  ВыборкаПроводок.СчетКт;
				Если ТипЗнч(ВыборкаПроводок.СубконтоДт) = ТипЗнч(Справочники.Контрагенты.ПустаяСсылка()) Тогда 
					ОбластьМакета.Параметры.СубконтоДт 	   =  ВыборкаПроводок.СубконтоДт.НаименованиеПолное;
				Иначе
					ОбластьМакета.Параметры.СубконтоДт 	   =  ВыборкаПроводок.СубконтоДт;
				КонецЕсли;
				Если ТипЗнч(ВыборкаПроводок.СубконтоКт) = ТипЗнч(Справочники.Контрагенты.ПустаяСсылка()) Тогда 
					ОбластьМакета.Параметры.СубконтоКт 	   =  ВыборкаПроводок.СубконтоКт.НаименованиеПолное;
				Иначе
					ОбластьМакета.Параметры.СубконтоКт 	   =  ВыборкаПроводок.СубконтоКт;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаПроводок.ВалютаКт) ИЛИ ВыборкаПроводок.ВалютаКт = ВалютаРегламентированногоУчета Тогда  
					ОбластьМакета.Параметры.Валюта         =  ВалютаРегламентированногоУчета;
					ОбластьМакета.Параметры.Сумма 		   =  ВыборкаПроводок.Сумма;
				Иначе
					ОбластьМакета.Параметры.Валюта         =  ВыборкаПроводок.ВалютаКт;
					ОбластьМакета.Параметры.Сумма 		   =  ВыборкаПроводок.ВалютнаяСуммаКт;
				КонецЕсли;
				
				ТабДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Титул2");
			ОбластьМакета.Параметры.ГлавныйБухгалтер    = Бухгалтер;
			ОбластьМакета.Параметры.ДатаДокументаДень   = "" + Строка(День(Шапка.ДатаДокумента)) + "";
			ОбластьМакета.Параметры.ДатаДокументаМесяц  = МесяцПрописью(Шапка.ДатаДокумента, Истина);
			ОбластьМакета.Параметры.ДатаДокументаГод    = Строка(Формат(Год(Шапка.ДатаДокумента), "ЧГ=0")) + "г.";
			ОбластьМакета.Параметры.НомерДокумента    	= Шапка.Номер;
			ОбластьМакета.Параметры.ПредставлениеПодотчетногоЛицаВРодПадеже = ПодотчетноеЛицо;
			ОбластьМакета.Параметры.ПредставлениеГлавногоБухгалтера    = Бухгалтер;
			
			ИзрасходованоЦелая       = 1;
			
			ТабДокумент.Вывести(ОбластьМакета);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Если ПерваяСсылкаТовары Тогда
			ВыборкаПоСсылке 	= РезультатПакет[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		    ПерваяСсылкаТовары 	= Ложь;
		КонецЕсли;
		
		Если ВыборкаПоСсылке.Следующий() Тогда
			
			Товар = ВыборкаПоСсылке.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
				
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка"); 
			ТабДокумент.Вывести(ОбластьМакета);
			
			// Выводим табличные части
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			НомерСтроки   = 0;
			
			ИтогоПоОтчету        = 0;
			ИтогоПоОтчетуВВалюте = 0;
			
			// ТЧ.Товары и услуги
			Пока Товар.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(Товар);
				НомерСтроки = НомерСтроки + 1;
				ОбластьМакета.Параметры.НомерСтроки         = НомерСтроки;
				ОбластьМакета.Параметры.ДокументДата        = Товар.ДокументДата;
				ОбластьМакета.Параметры.ДебетСубСчета 		= Товар.СчетЗатрат.Код;
				ОбластьМакета.Параметры.КредитСубСчета 		= ?(Товар.СчетРасчетов <> Null, Товар.СчетРасчетов.Код, "");
				ОбластьМакета.Параметры.НаименованиеРасхода = СокрЛП(Товар.НаименованиеРасхода) ;
				Сумма = Товар.ПоОтчетуВВалюте; 
				ОбластьМакета.Параметры.ПоОтчету = Сумма ;
				ИтогоПоОтчету = ИтогоПоОтчету + Сумма;
				ТабДокумент.Вывести(ОбластьМакета);   
			КонецЦикла;
			
			// ТЧ.ОПЛАТА: КОНЕЦ ДОКУМЕНТОВ ОПЛАТЫ
			Для Каждого СТЧ Из Ссылка.ОплатаПоставщикам Цикл
				Если ТипЗнч(СТЧ.Сделка) <> Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					Продолжить;
				КонецЕсли;				
				
				// Обход товаров поступления ТМЗ и услуг
				Для Каждого СТЧТ Из СТЧ.Сделка.Товары Цикл
					
					ОбластьМакета.Параметры.Заполнить(Товар);
					НомерСтроки = НомерСтроки + 1;
					ОбластьМакета.Параметры.НомерСтроки         = НомерСтроки;
					ОбластьМакета.Параметры.ДокументДата        = СТЧ.Сделка.Дата;
					ОбластьМакета.Параметры.НаименованиеРасхода = СокрЛП(СТЧТ.Номенклатура.Наименование);
					ОбластьМакета.Параметры.ДебетСубСчета 		= СТЧТ.СчетУчета;
					
					Сумма = СТЧТ.Сумма
					+ ?(СТЧ.Сделка.СуммаВключаетНДС, 0, СТЧТ.СуммаНДС);
					
					ОбластьМакета.Параметры.ПоОтчету = Сумма;
					ОбластьМакета.Параметры.ПоУчету  = Сумма;
					
					ИтогоПоОтчету = ИтогоПоОтчету + Сумма;
					ТабДокумент.Вывести(ОбластьМакета);
				КонецЦикла;			
				
				// Обход услуг поступления ТМЗ и услуг
				Для Каждого СТЧТ Из СТЧ.Сделка.Услуги Цикл
					
					ОбластьМакета.Параметры.Заполнить(Товар);
					НомерСтроки = НомерСтроки + 1;
					ОбластьМакета.Параметры.НомерСтроки         = НомерСтроки;
					ОбластьМакета.Параметры.ДокументДата        = СТЧ.Сделка.Дата;
					ОбластьМакета.Параметры.НаименованиеРасхода = СокрЛП(СТЧТ.Номенклатура.Наименование);
					ОбластьМакета.Параметры.ДебетСубСчета 		= СТЧТ.СчетЗатратБУ;
					
					Сумма = СТЧТ.Сумма
					+ ?(СТЧ.Сделка.СуммаВключаетНДС, 0, СТЧТ.СуммаНДС);
					
					ОбластьМакета.Параметры.ПоОтчету = Сумма;
					ОбластьМакета.Параметры.ПоУчету  = Сумма;
					
					ИтогоПоОтчету = ИтогоПоОтчету + Сумма;
					ТабДокумент.Вывести(ОбластьМакета);
				КонецЦикла;			
				
			КонецЦикла; // КОНЕЦ ДОКУМЕНТОВ ОПЛАТЫ
					
		КонецЕсли;
		
		ТЗ = РезультатПакет[2].Выгрузить();
		
		ТЗ.Свернуть("Валюта");
		Если НЕ ТЗ.Количество() > 1 Тогда
			ОбластьИтоги 					= Макет.ПолучитьОбласть("Итоги");                     		
			ОбластьИтоги.Параметры.Итого 	= ИтогоПоОтчету;	
			ТабДокумент.Вывести(ОбластьИтоги);
		КонецЕсли;  
		
		// Выводим подвал авансовго отчета
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ПредставлениеПодотчетногоЛица 	= ПодотчетноеЛицо;
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
	
	Возврат ТабДокумент;
КонецФункции


// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АвансовыйОтчет") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"АвансовыйОтчет", НСтр("ru = 'Печать Авансового отчета'"), ПечатьАвансовогоОтчета(МассивОбъектов, ОбъектыПечати),,
			"Документ.АвансовыйОтчет.АвансовыйОтчет");
	КонецЕсли;
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АвансовыйОтчет";
	КомандаПечати.Представление = НСтр("ru = 'Печать Авансового отчета'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
