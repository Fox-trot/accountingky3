#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	
		"ВЫБРАТЬ
		|	ВременнаяТаблицаПрочийРасход.СчетУчета КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаПрочийРасход.Субконто1 КАК СубконтоДт1,
		|	ВременнаяТаблицаПрочийРасход.Субконто2 КАК СубконтоДт2,
		|	ВременнаяТаблицаПрочийРасход.Субконто3 КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	ВременнаяТаблицаПрочийРасход.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаПрочийРасход.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаШапка.БанковскийСчет.ВалютаДенежныхСредств
		|		ИНАЧЕ &ВалютаРегламентированногоУчета
		|	КОНЕЦ КАК ВалютаКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаПрочийРасход.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаПрочийРасход.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаДт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета.Валютный
		|			ТОГДА ВременнаяТаблицаПрочийРасход.СуммаПлатежа
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВалютнаяСуммаКт,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	""Списание денежных средств"" + ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1000))) <> """"
		|			ТОГДА "" - "" + (ВЫРАЗИТЬ(ВременнаяТаблицаШапка.Комментарий КАК СТРОКА(1000)))
		|	КОНЕЦ КАК Содержание,
		|	ВременнаяТаблицаПрочийРасход.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаПрочийРасход КАК ВременнаяТаблицаПрочийРасход
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаПрочийРасход.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаПрочийРасход.СуммаПлатежа <> 0";
                   
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);		
	РезультатЗапроса = Запрос.Выполнить();		
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ДвиженияДенежныхСредств.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДДС(СтруктураДополнительныеСвойства)
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	
		"ВЫБРАТЬ
		|	ВременнаяТаблицаПрочийРасход.СтатьяДвиженияДенежныхСредств КАК Статья,
		|	ВременнаяТаблицаПрочийРасход.СуммаПлатежа * ВременнаяТаблицаШапка.Курс КАК Сумма,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Дата КАК Период
		|ИЗ
		|	ВременнаяТаблицаПрочийРасход КАК ВременнаяТаблицаПрочийРасход
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаПрочийРасход.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	НЕ ВременнаяТаблицаПрочийРасход.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
		|	И ВременнаяТаблицаПрочийРасход.СуммаПлатежа <> 0";
		
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДДС", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлатежныйОрдерСписаниеДС.Ссылка,
		|	ПлатежныйОрдерСписаниеДС.ВерсияДанных,
		|	ПлатежныйОрдерСписаниеДС.ПометкаУдаления,
		|	ПлатежныйОрдерСписаниеДС.Номер,
		|	ПлатежныйОрдерСписаниеДС.Дата,
		|	ПлатежныйОрдерСписаниеДС.Проведен,
		|	ПлатежныйОрдерСписаниеДС.Организация,
		|	ПлатежныйОрдерСписаниеДС.БанковскийСчет,
		|	ПлатежныйОрдерСписаниеДС.СуммаДокумента,
		|	ПлатежныйОрдерСписаниеДС.ДокументОснование,
		|	ПлатежныйОрдерСписаниеДС.ВалютаДенежныхСредств,
		|	ПлатежныйОрдерСписаниеДС.Курс,
		|	ПлатежныйОрдерСписаниеДС.Комментарий,
		|	ПлатежныйОрдерСписаниеДС.Автор
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ПлатежныйОрдерСписаниеДС КАК ПлатежныйОрдерСписаниеДС
		|ГДЕ
		|	ПлатежныйОрдерСписаниеДС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.Ссылка,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.НомерСтроки,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.СчетУчета,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.СтатьяДвиженияДенежныхСредств,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.СуммаПлатежа,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.Субконто1,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.Субконто2,
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.Субконто3
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочийРасход
		|ИЗ
		|	Документ.ПлатежныйОрдерСписаниеДС.ПрочиеРасходы КАК ПлатежныйОрдерСписаниеДСПрочиеРасходы
		|ГДЕ
		|	ПлатежныйОрдерСписаниеДСПрочиеРасходы.Ссылка = &Ссылка";
				
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();
	
	СформироватьТаблицаХозрасчетный(СтруктураДополнительныеСвойства);
	СформироватьТаблицаДДС(СтруктураДополнительныеСвойства);

КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли
