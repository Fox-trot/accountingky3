#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// 1. Начисления за месяц
	// 2. Удержания за месяц
	// 3. Удержания этого документа
	// 4. ПФР
	// 5. ГНПФР
	// 6. ПН
	// 7. ПрофВзнос
	// 8. ПФФ
	// 9. МСФ
	// 10. ФОТФ
	// 11. ПФФКорректировка
	// 12. МСФКорректировка
	// 13. ФОТФКорректировка
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ" + // запрос редактируется только в ручную
		// Начисления
		"	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНачисленияЗаМесяц.СчетЗатрат КАК СчетДт,
		|	&СчетУчета КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНачисленияЗаМесяц.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат КАК ВалютнаяСуммаКт,
		|	""Начисление заработной платы"" КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	&СчетУчета,
		|	ВременнаяТаблицаУдержанияЗаМесяц.СчетУчета,
		// Субконто Дт
		|	ВременнаяТаблицаУдержанияЗаМесяц.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаУдержанияЗаМесяц.Результат,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаУдержанияЗаМесяц.Результат,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаУдержанияЗаМесяц.Результат,
		|	""Удержание заработной платы""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУдержанияЗаМесяц КАК ВременнаяТаблицаУдержанияЗаМесяц
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУдержанияЗаМесяц.Результат = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	&СчетУчета,
		|	ВременнаяТаблицаУдержания.СчетУчета,
		// Субконто Дт
		|	ВременнаяТаблицаУдержания.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаУдержания.Результат,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаУдержания.Результат,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаУдержания.Результат,
		|	""Удержание заработной платы""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУдержания.Результат = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	&СчетУчета,
		|	&СчетУчетаРасчетовПФР,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПФР),
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР,
		|	""Начисление взносов по пенсионным отчислениям с сотрудника""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПФР = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	&СчетУчета,
		|	&СчетУчетаРасчетовГНПФР,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ГНПФР),
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР,
		|	""Начисление взносов по отчислениям в гос.накопительный пенсионный фонд с сотрудника""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ГНПФР = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	&СчетУчета,
		|	&СчетУчетаРасчетовПН,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПН),
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН,
		|	""Начисление взносов по подоходному налогу на доходы физических лиц""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПН = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	&СчетУчета,
		|	&СчетУчетаРасчетовПрофВзнос,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПрофВзнос),
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос,
		|	""Профсоюзные отчисления с сотрудника""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизации.СчетЗатрат,
		|	&СчетУчетаРасчетовПФФ,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат1,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат2,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизации.ПФФ,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизации.ПФФ,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизации.ПФФ,
		|	""Начисление взносов по пенсионным отчислениям с фирмы""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизации КАК ВременнаяТаблицаНалогиСОрганизации
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизации.ПФФ = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизации.СчетЗатрат,
		|	&СчетУчетаРасчетовМСФ,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат1,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат2,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизации.МСФ,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизации.МСФ,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизации.МСФ,
		|	""Начисление взносов по медицинскому страхованию с фирмы""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизации КАК ВременнаяТаблицаНалогиСОрганизации
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизации.МСФ = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	10,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизации.СчетЗатрат,
		|	&СчетУчетаРасчетовФОТФ,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат1,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат2,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизации.ФОТФ,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизации.ФОТФ,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизации.ФОТФ,
		|	""Начисление взносов по фонду оздоровления трудящихся с фирмы""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизации КАК ВременнаяТаблицаНалогиСОрганизации
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизации.ФОТФ = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	11,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СчетЗатрат,
		|	&СчетУчетаРасчетовПФФ,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат1,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат2,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка,
		|	""Начисление взносов по пенсионным отчислениям с фирмы - Корректировка""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки КАК ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	12,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СчетЗатрат,
		|	&СчетУчетаРасчетовМСФ,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат1,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат2,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка,
		|	""Начисление взносов по медицинскому страхованию с фирмы - Корректировка""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки КАК ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	13,
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СчетЗатрат,
		|	&СчетУчетаРасчетовФОТФ,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат1,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат2,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка,
		|	&ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка,
		|	""Начисление взносов по фонду оздоровления трудящихся с фирмы - Корректировка""
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки КАК ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка = 0";

	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);	
	Запрос.УстановитьПараметр("СчетУчета", ПланыСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПФР", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовПФР);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовГНПФР", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовГНПФР);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПрофВзнос", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовПрофВзнос);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПФФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовПФФ);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовМСФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовМСФ);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовФОТФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовФОТФ);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПН", ПланыСчетов.Хозрасчетный.ПодоходныйНалогНаДоходыСотрудников);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаХозрасчетный()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУдержания(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.ПериодРегистрации,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаУдержания.ВидРасчета,
		|	ВременнаяТаблицаУдержания.ФизЛицо,
		|	ВременнаяТаблицаУдержания.Размер,
		|	ВременнаяТаблицаУдержания.Результат
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУдержания", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаУдержания()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСоциальныйФонд(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФФ КАК ПФФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСФ КАК МСФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСР КАК МСР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТФ КАК ФОТФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТР КАК ФОТР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ КАК ОблагаемаяСуммаДохода,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФФКорректировка КАК ПФФКорректировка,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСФКорректировка КАК МСФКорректировка,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТФКорректировка КАК ФОТФКорректировка,
		|	ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная КАК ПриведеннаяСЗП
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ > 0";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСоциальныйФонд", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСоциальныйФонд()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПодоходныйНалог(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =		
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.МРД,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.Вычеты * 100 + ВременнаяТаблицаНалогиПоЗарплате.ПФР + ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ПиИВычеты,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФРДляРасчетаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФРДляРасчетаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТР,
		|	ВременнаяТаблицаНалогиПоЗарплате.СуммаНачислено КАК ВсегоНачислено,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДПН > 0";		
		РезультатЗапроса = Запрос.Выполнить();
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПодоходныйНалог", РезультатЗапроса.Выгрузить());
	КонецПроцедуры // СформироватьТаблицаПодоходныйНалог()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНалогиПоЗаработнойПлате(ДокументСсылка,СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВложенныйЗапрос.ФизЛицо,
		|	ВложенныйЗапрос.ВидНалога,
		|	ВложенныйЗапрос.Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПФР) КАК ВидНалога,
		|			ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК Сумма
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.ПФР = 0
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ГНПФР),
		|			ВременнаяТаблицаНалогиПоЗарплате.ГНПФР
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.ГНПФР = 0
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПН),
		|			ВременнаяТаблицаНалогиПоЗарплате.ПН
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.ПН = 0
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.Профвзнос),
		|			ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос = 0
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПФФ),
		|			ВременнаяТаблицаНалогиПоЗарплате.ПФФ
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.ПФФ = 0
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.МСФ),
		|			ВременнаяТаблицаНалогиПоЗарплате.МСФ
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.МСФ = 0
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|			ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ФОТФ),
		|			ВременнаяТаблицаНалогиПоЗарплате.ФОТФ
		|		ИЗ
		|			ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ГДЕ
		|			НЕ ВременнаяТаблицаНалогиПоЗарплате.ФОТФ = 0) КАК ВложенныйЗапрос
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалогиПоЗаработнойПлате", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицуДляОтчетаПоПН(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	СУММА(ВременнаяТаблицаНалогиПоЗарплате.ОДПН) КАК ОДПН,
		|	СУММА(ВременнаяТаблицаНалогиПоЗарплате.ПН) КАК ПН,
		|	СУММА(ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН) КАК ДоплатаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.СуммаНачислено КАК ВсегоНачислено,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо) КАК Количество,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней КАК ОтработаноДней,
		|	ВременнаяТаблицаНалогиПоЗарплате.Статус КАК Статус,
		|	ВременнаяТаблицаСотрудники.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаНалогиПоЗарплате.Статус.НомерСтрокиВОтчетеПН = 0
		|			ТОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.НомерСтрокиВОтчетеПН
		|		КОГДА НЕ ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		|				И НЕ ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиПоЗарплате.ПрименятьВычет, ИСТИНА)
		|						ТОГДА 1
		|					ИНАЧЕ 2
		|				КОНЕЦ
		|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		|			ТОГДА 4
		|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|			ТОГДА 3
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК НомерСтроки,
		|	ВременнаяТаблицаНалогиПоЗарплате.Вычеты * 100 + ВременнаяТаблицаНалогиПоЗарплате.ПФР + ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ПиИВычеты,
		|	СУММА(ВременнаяТаблицаНалогиПоЗарплате.ПФР) КАК ПФР,
		|	СУММА(ВременнаяТаблицаНалогиПоЗарплате.ГНПФР) КАК ГНПФР
		|ПОМЕСТИТЬ ВременнаяТаблицаСборкаПН
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|			ПО ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ПО (ИСТИНА)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаНалогиПоЗарплате.СуммаНачислено,
		|	ВременнаяТаблицаНалогиПоЗарплате.Вычеты * 100 + ВременнаяТаблицаНалогиПоЗарплате.ПФР + ВременнаяТаблицаНалогиПоЗарплате.ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.Статус,
		|	ВременнаяТаблицаСотрудники.Подразделение,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаНалогиПоЗарплате.Статус.НомерСтрокиВОтчетеПН = 0
		|			ТОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.НомерСтрокиВОтчетеПН
		|		КОГДА НЕ ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		|				И НЕ ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиПоЗарплате.ПрименятьВычет, ИСТИНА)
		|						ТОГДА 1
		|					ИНАЧЕ 2
		|				КОНЕЦ
		|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		|			ТОГДА 4
		|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|			ТОГДА 3
		|		ИНАЧЕ 1
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСборкаПН.Период,
		|	ВременнаяТаблицаСборкаПН.Организация,
		|	ВременнаяТаблицаСборкаПН.Подразделение,
		|	СтрокиОтчетаПН.Ссылка КАК СтрокаОтчета,
		|	ВременнаяТаблицаСборкаПН.Количество,
		|	ВременнаяТаблицаСборкаПН.ВсегоНачислено,
		|	ВременнаяТаблицаСборкаПН.ВсегоНачислено - ВременнаяТаблицаСборкаПН.ОДПН КАК Необлагаемая,
		|	ВременнаяТаблицаСборкаПН.ПиИВычеты КАК ПиИВычеты,
		|	ВременнаяТаблицаСборкаПН.ОДПН КАК Облагаемая,
		|	ВременнаяТаблицаСборкаПН.ПН КАК ПНСотрудника,
		|	ВременнаяТаблицаСборкаПН.ДоплатаПН,
		|	ВременнаяТаблицаСборкаПН.ПН + ВременнаяТаблицаСборкаПН.ДоплатаПН КАК ВсегоПН,
		|	ВременнаяТаблицаСборкаПН.ФизЛицо,
		|	ВременнаяТаблицаСборкаПН.ПФР КАК ПФР,
		|	ВременнаяТаблицаСборкаПН.ГНПФР КАК ГНПФР
		|ИЗ
		|	ВременнаяТаблицаСборкаПН КАК ВременнаяТаблицаСборкаПН
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтрокиОтчетаПН КАК СтрокиОтчетаПН
		|		ПО ВременнаяТаблицаСборкаПН.НомерСтроки = СтрокиОтчетаПН.НомерСтроки";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОтчетПоПодоходномуНалогу", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицуДляОтчетаПоЕжемесячномуОтчетуПоСФ(ДокументСсылка, СтруктураДополнительныеСвойства);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаСотрудники.Подразделение КАК Подразделение,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория КАК Категория,
		|	ВременнаяТаблицаНалогиПоЗарплате.Статус КАК Статус,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ КАК ФОТ,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР + ВременнаяТаблицаНалогиПоЗарплате.ПФФ + ВременнаяТаблицаНалогиПоЗарплате.МСФ + ВременнаяТаблицаНалогиПоЗарплате.ФОТФ + ВременнаяТаблицаНалогиПоЗарплате.МСР + ВременнаяТаблицаНалогиПоЗарплате.ПФФКорректировка + ВременнаяТаблицаНалогиПоЗарплате.МСФКорректировка + ВременнаяТаблицаНалогиПоЗарплате.ФОТФКорректировка КАК НачисленыеВзносы,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней КАК ФактДней,
		|	ТаблицаНормыДней.НормаДней,
		//|	ВЫБОР
		//|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		//|			ТОГДА &СреднемесячнаяЗПСФ
		//|		ИНАЧЕ &СреднемесячнаяЗПСФ / 100 * &ПроцентОтСЗПСФ / ТаблицаНормыДней.НормаДней * ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней
		//|	КОНЕЦ КАК ПриведеннаяСЗП,
		//|	ВЫБОР
		//|		КОГДА ВЫБОР
		//|				КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		//|					ТОГДА &СреднемесячнаяЗПСФ
		//|				ИНАЧЕ &СреднемесячнаяЗПСФ / 100 * &ПроцентОтСЗПСФ / ТаблицаНормыДней.НормаДней * ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней
		//|			КОНЕЦ > ВременнаяТаблицаНалогиПоЗарплате.ОДСФ
		//|			ТОГДА ВЫБОР
		//|					КОГДА ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		//|						ТОГДА &СреднемесячнаяЗПСФ
		//|					ИНАЧЕ &СреднемесячнаяЗПСФ / 100 * &ПроцентОтСЗПСФ / ТаблицаНормыДней.НормаДней * ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней
		//|				КОНЕЦ - ВременнаяТаблицаНалогиПоЗарплате.ОДСФ
		//|		ИНАЧЕ 0
		//|	КОНЕЦ КАК ДопФОТ	
		|	ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная КАК ПриведеннаяСЗП,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная > ВременнаяТаблицаНалогиПоЗарплате.ОДСФ
		|			ТОГДА ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная - ВременнаяТаблицаНалогиПоЗарплате.ОДСФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДопФОТ		
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНормыДней КАК ТаблицаНормыДней
		|				ПО ВременнаяТаблицаСотрудники.ГрафикРаботы = ТаблицаНормыДней.ГрафикРаботы
		|			ПО ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ПО (ИСТИНА)	
		|ГДЕ
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ > 0";
		
	Запрос.УстановитьПараметр("СреднемесячнаяЗПСФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СреднемесячнаяЗПСФ);
	Запрос.УстановитьПараметр("ПроцентОтСЗПСФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.ПроцентОтСЗПСФ);

	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДанныеДляОтчетаПоЕжемесячномуСФ", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства, ДокументОбъект) Экспорт
	
	// 1. Получение данных этого документа
	// 2. Получение начислений за месяц
	// 3. Получение удержаний за месяц
	// 4. Формирование таблиц для движения по регистрам	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ПериодРегистрации КАК ПериодРегистрации,
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.Статус КАК Статус,
		|	ТаблицаДокумента.ПрименятьВычет КАК ПрименятьВычет,
		|	ТаблицаДокумента.СуммаНачислено КАК СуммаНачислено,
		|	ТаблицаДокумента.ПФР КАК ПФР,
		|	ТаблицаДокумента.ГНПФР КАК ГНПФР,
		|	ТаблицаДокумента.ПФРДляРасчетаПН КАК ПФРДляРасчетаПН,
		|	ТаблицаДокумента.ГНПФРДляРасчетаПН КАК ГНПФРДляРасчетаПН,
		|	ТаблицаДокумента.ПН КАК ПН,
		|	ТаблицаДокумента.ПрофВзнос КАК ПрофВзнос,
		|	ТаблицаДокумента.ПФФ КАК ПФФ,
		|	ТаблицаДокумента.МСФ КАК МСФ,
		|	ТаблицаДокумента.ФОТФ КАК ФОТФ,
		|	ТаблицаДокумента.МСР КАК МСР,
		|	ТаблицаДокумента.ФОТР КАК ФОТР,
		|	ТаблицаДокумента.Вычеты КАК Вычеты,
		|	ТаблицаДокумента.ОДСФ КАК ОДСФ,
		|	ТаблицаДокумента.ОДПН КАК ОДПН,
		|	ТаблицаДокумента.ОДПрофВзнос КАК ОДПрофВзнос,
		|	ТаблицаДокумента.СуммаДопДоход КАК СуммаДопДоход,
		|	ТаблицаДокумента.ДоплатаПН КАК ДоплатаПН,
		|	ТаблицаДокумента.МРД КАК МРД,
		|	ТаблицаДокумента.ОтработаноДней КАК ОтработаноДней,
		|	ТаблицаДокумента.ПФФКорректировка КАК ПФФКорректировка,
		|	ТаблицаДокумента.МСФКорректировка КАК МСФКорректировка,
		|	ТаблицаДокумента.ФОТФКорректировка КАК ФОТФКорректировка,
		|	ТаблицаДокумента.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная 
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПоЗарплате
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.НалогиПоЗарплате КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаДокумента.СубконтоЗатрат1 КАК СубконтоЗатрат1,
		|	ТаблицаДокумента.СубконтоЗатрат2 КАК СубконтоЗатрат2,
		|	ТаблицаДокумента.СубконтоЗатрат3 КАК СубконтоЗатрат3,
		|	ТаблицаДокумента.ПФФ КАК ПФФ,
		|	ТаблицаДокумента.МСФ КАК МСФ,
		|	ТаблицаДокумента.ФОТФ КАК ФОТФ
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСОрганизации
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.НалогиСОрганизации КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаДокумента.СубконтоЗатрат1 КАК СубконтоЗатрат1,
		|	ТаблицаДокумента.СубконтоЗатрат2 КАК СубконтоЗатрат2,
		|	ТаблицаДокумента.СубконтоЗатрат3 КАК СубконтоЗатрат3,
		|	ТаблицаДокумента.ПФФКорректировка КАК ПФФКорректировка,
		|	ТаблицаДокумента.МСФКорректировка КАК МСФКорректировка,
		|	ТаблицаДокумента.ФОТФКорректировка КАК ФОТФКорректировка
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.НалогиСОрганизацииКорректировки КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.ВидРасчета КАК ВидРасчета,
		|	ТаблицаДокумента.ВидРасчета.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.Размер КАК Размер,
		|	ТаблицаДокумента.Результат КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержания
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.Удержания КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.СпособОтражения.СчетЗатрат КАК СчетЗатрат,
		|	Начисления.СпособОтражения.СубконтоЗатрат1 КАК СубконтоЗатрат1,
		|	Начисления.СпособОтражения.СубконтоЗатрат2 КАК СубконтоЗатрат2,
		|	Начисления.СпособОтражения.СубконтоЗатрат3 КАК СубконтоЗатрат3,
		|	СУММА(Начисления.Результат) КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисленияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|	И НЕ Начисления.ВидРасчета.ДополнительныйДоход
		|	И НЕ Начисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо,
		|	Начисления.СпособОтражения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Удержания.ФизЛицо КАК ФизЛицо,
		|	Удержания.ВидРасчета.СчетУчета КАК СчетУчета,
		|	СУММА(Удержания.Результат) КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержанияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Удержания.Организация = &Организация
		|	И НЕ Удержания.Результат = 0
		|	И НЕ Удержания.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Удержания.ФизЛицо,
		|	Удержания.ВидРасчета.СчетУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение КАК Подразделение,
		|	СотрудникиСрезПоследних.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате)) КАК СотрудникиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
		|	СУММА(ГрафикиРаботы.ЗначениеДней) КАК НормаДней
		|ПОМЕСТИТЬ ТаблицаНормыДней
		|ИЗ
		|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ГрафикиРаботы
		|ГДЕ
		|	ГрафикиРаботы.ГрафикРаботы В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаСотрудники.ГрафикРаботы
		|			ИЗ
		|				ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)
		|	И ГрафикиРаботы.Год = ГОД(&КонецПериода)
		|	И ГрафикиРаботы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикиРаботы.ГрафикРаботы";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(СтруктураДополнительныеСвойства.ДляПроведения.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(СтруктураДополнительныеСвойства.ДляПроведения.Дата));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.Выполнить();
	
	СформироватьТаблицаУдержания(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСоциальныйФонд(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПодоходныйНалог(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалогиПоЗаработнойПлате(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицуДляОтчетаПоПН(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицуДляОтчетаПоЕжемесячномуОтчетуПоСФ(ДокументСсылка, СтруктураДополнительныеСвойства);
		
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли