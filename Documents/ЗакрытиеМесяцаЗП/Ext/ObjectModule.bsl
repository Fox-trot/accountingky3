#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
		
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДанныеУчетнойПолитикиПоПерсоналу", БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация));

	// Инициализация данных документа.
	Документы.ЗакрытиеМесяцаЗП.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, ЭтотОбъект);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьХозрасчетный(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьУдержания(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьПодоходныйНалог(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСоциальныйФонд(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьНалогиПоЗаработнойПлате(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьОтчетПоПодоходному(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьОтчетЕжемесячныйСоцфонд(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	// Проверка повторного закрытия месяца
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗакрытиеМесяцаЗП.Ссылка
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП КАК ЗакрытиеМесяцаЗП
		|ГДЕ
		|	ЗакрытиеМесяцаЗП.Организация = &Организация
		|	И ЗакрытиеМесяцаЗП.Дата = &Дата
		|	И ЗакрытиеМесяцаЗП.Проведен
		|	И ЗакрытиеМесяцаЗП.Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ закрытия месяца по заработной плате за выбранный период уже существует: %1.'"), 
			ВыборкаДетальныеЗаписи.Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	ДанныеУчетнойПолитики = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация);
	
	// Проверка заполнения данных учетной политики.
	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасходовПоДоплатеПН) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расходов по доплате ПН.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасходовПоКорректировкеСФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расходов по корректировке СФ.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовПФР) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ПФР.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовГНПФР) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ГНПФР.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовМСФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов МСФ.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовПФФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ПФФ.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовФОТФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ФОТФ.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовПрофВзнос) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ПрофВзнос.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.ВидРасчета КАК ВидРасчета,
		|	Начисления.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
		|	Начисления.СтатьяЗатрат КАК СтатьяЗатрат,
		|	Начисления.Подразделение КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(Начисления.Регистратор) КАК ДокументСсылка
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаНачисления.ФизЛицо) КАК ФизЛицо,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаНачисления.ВидРасчета) КАК ВидРасчета,
		|	ВременнаяТаблицаНачисления.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
		|	ВременнаяТаблицаНачисления.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ВременнаяТаблицаНачисления.ДокументСсылка КАК ДокументСсылка
		|ИЗ
		|	ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|ГДЕ
		|	(ВременнаяТаблицаНачисления.СчетУчетаЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ИЛИ ВременнаяТаблицаНачисления.СтатьяЗатрат = ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""Виды удержаний"",
		|	ПРЕДСТАВЛЕНИЕ(ВидыУдержаний.Ссылка),
		|	ВидыУдержаний.СчетУчета,
		|	""Без проверки"",
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ПланВидовРасчета.ВидыУдержаний КАК ВидыУдержаний
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаНачисления.Подразделение) КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаНачисления.ВидРасчета) КАК ВидРасчета
		|ИЗ
		|	ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаЗП КАК СчетаУчетаЗП
		|		ПО (ВременнаяТаблицаНачисления.Подразделение = СчетаУчетаЗП.Подразделение
		|				ИЛИ СчетаУчетаЗП.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
		|			И (ВременнаяТаблицаНачисления.ВидРасчета = СчетаУчетаЗП.ВидРасчетаНачисления
		|				ИЛИ СчетаУчетаЗП.ВидРасчетаНачисления = ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНачислений.ПустаяСсылка))
		|ГДЕ
		|	СчетаУчетаЗП.СчетУчетаЗатратСоцОтчисления ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаНачисления.Подразделение,
		|	ВременнаяТаблицаНачисления.ВидРасчета";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Пустой счет или статья затрат. 
	Если НЕ МассивРезультатов[1].Пустой() Тогда
		ВыборкаДетальныеЗаписи = МассивРезультатов[1].Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СчетУчетаЗатрат) Тогда 	
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен счет учета (затраты) Сотрудник: %1, Вид расчета: %2, Документ: %3.'"), 
					ВыборкаДетальныеЗаписи.ФизЛицо, ВыборкаДетальныеЗаписи.ВидРасчета, ВыборкаДетальныеЗаписи.ДокументСсылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Контроль заполнения счетов учета.
	Если НЕ МассивРезультатов[2].Пустой() Тогда
		ВыборкаДетальныеЗаписи = МассивРезультатов[2].Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен счет учета затрат (Соц. отчисления) Подразделение: %1, Вид расчета: %2. Проверьте настройки в регистре сведений ""Счета учета отражения заработной платы"".'"), 
				ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.ВидРасчета);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура выполняет расчет налогов и удержаний
//
Процедура ВыполнитьЗакрытиеМесяца(ЕстьОшибки) Экспорт 
	
	// 1. Расчет налогов по заработной плате
	// 2. Расчет удержаний с учетом рассчитанных налогов
	// 3. Заполнение соответствующих табличных частей- итого удержано	
	// 4. Распределение сумм налогов с организации по счетам учета
	
	РассчитатьНалогиПоЗарплате();	
	РассчитатьПлановыеУдержания();
	ДозаполнитьНалогиПоЗарплате();
	РаспределитьНалогиСОрганизации();
	ДозаполнитьВыплату();
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура - Рассчитать налоги по зарплате
//
Процедура РассчитатьНалогиПоЗарплате()
	
	// 1. Получение начислений за месяц, определение расчетных баз
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.ФизЛицо.Наименование КАК ФизЛицоНаименование,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ЗачетОтработанногоВремени
		|				ТОГДА Начисления.ОтработаноДней
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДней,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ДополнительныйДоход
		|				ТОГДА 0
		|			ИНАЧЕ Начисления.Результат
		|		КОНЕЦ) КАК СуммаНачислено,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ДополнительныйДоход
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаДопДоход,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяСФ
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДСФ,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяПН
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДПН,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяСФ
		|					И Начисления.ВидРасчета.ОблагаетсяПН
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДСФПН,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяПрофВзнос
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДПрофВзнос	
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисленияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо,
		|	Начисления.ФизЛицо.Наименование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	// 2. Получение списка сотрудников, принятых или уволенных в течении месяца- определение больше 15 дней
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период) КАК ДеньПриема,
		|	0 КАК ДеньУвольнения
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо,
		|	СУММА(ВременнаяТаблицаСотрудники.ДеньПриема) КАК ДеньПриема,
		|	СУММА(ВременнаяТаблицаСотрудники.ДеньУвольнения) КАК ДеньУвольнения,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СУММА(ВременнаяТаблицаСотрудники.ДеньУвольнения) = 0
		|					ТОГДА ДЕНЬ(&КонецПериода)
		|				ИНАЧЕ СУММА(ВременнаяТаблицаСотрудники.ДеньУвольнения)
		|			КОНЕЦ - СУММА(ВременнаяТаблицаСотрудники.ДеньПриема) >= 15
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Больше15дней
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиОтработаноДней
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСотрудники.ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	// 3. Получение статусов сотрудников и ставок налогов по этим статусам
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	СтатусыСотрудниковСрезПоследних.Физлицо КАК ФизЛицо,
		|	СтатусыСотрудниковСрезПоследних.Статус КАК Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА) КАК ПрименятьВычет,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА)
		|			ТОГДА ЕСТЬNULL(СтавкиНалоговЗаработнойПлатыСрезПоследних.Вычеты, 0) + ЕСТЬNULL(СтатусыСотрудниковСрезПоследних.ДопВычет, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Вычеты,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаПФР КАК СтавкаПФР,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаГНПФР КАК СтавкаГНПФР,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаПН КАК СтавкаПН,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаПФФ КАК СтавкаПФФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаМСФ КАК СтавкаМСФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаФОТФ КАК СтавкаФОТФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.НеВычитатьПФ КАК НеВычитатьПФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаМСР КАК СтавкаМСР,
		|	ВЫБОР
		|		КОГДА СтатусыСотрудниковСрезПоследних.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатель)
		|			ТОГДА &СреднемесячнаяЗПСФ
		|		ИНАЧЕ &СреднемесячнаяЗПСФ / 100 * &ПроцентОтСЗПСФ 
		|	КОНЕЦ КАК СреднемесячнаяЗПСФ
		|ПОМЕСТИТЬ ВременнаяТаблицаСтатусы
		|ИЗ
		|	РегистрСведений.СтатусыСотрудников.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И Физлицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц)) КАК СтатусыСотрудниковСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалоговЗаработнойПлаты.СрезПоследних(&КонецПериода, ) КАК СтавкиНалоговЗаработнойПлатыСрезПоследних
		|		ПО СтатусыСотрудниковСрезПоследних.Статус = СтавкиНалоговЗаработнойПлатыСрезПоследних.Статус
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудникиОтработаноДней КАК ВременнаяТаблицаСотрудникиОтработаноДней
		|		ПО СтатусыСотрудниковСрезПоследних.Физлицо = ВременнаяТаблицаСотрудникиОтработаноДней.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	// 4. Получение графиков сотрудников и нормы дней по этим графикам, расчет СЗП приведенного
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение КАК Подразделение,
		|	СотрудникиСрезПоследних.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиСрез
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц)) КАК СотрудникиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
		|	СУММА(ГрафикиРаботы.ЗначениеДней) КАК НормаДней
		|ПОМЕСТИТЬ ВременнаяТаблицаНормыДней
		|ИЗ
		|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ГрафикиРаботы
		|ГДЕ
		|	ГрафикиРаботы.ГрафикРаботы В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаСотрудникиСрез.ГрафикРаботы
		|			ИЗ
		|				ВременнаяТаблицаСотрудникиСрез КАК ВременнаяТаблицаСотрудникиСрез)
		|	И ГрафикиРаботы.Год = ГОД(&КонецПериода)
		|	И ГрафикиРаботы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикиРаботы.ГрафикРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней КАК ОтработаноДней,
		|	ВременнаяТаблицаНормыДней.НормаДней КАК НормаДней,
		|	ВременнаяТаблицаСтатусы.СреднемесячнаяЗПСФ КАК СреднемесячнаяЗПСФ,
		|	ВременнаяТаблицаСотрудникиСрез.ГрафикРаботы КАК ГрафикРаботы,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) = 0
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СреднемесячнаяЗПСФ, 0)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней > ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0)
		|					ТОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СреднемесячнаяЗПСФ, 0)
		|				ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ВременнаяТаблицаСтатусы.СреднемесячнаяЗПСФ, 0) * ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней / ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0)  КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК СреднемесячнаяЗПСФПриведенная
		|ПОМЕСТИТЬ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНормыДней КАК ВременнаяТаблицаНормыДней
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудникиСрез КАК ВременнаяТаблицаСотрудникиСрез
		|			ПО (ВременнаяТаблицаСотрудникиСрез.ГрафикРаботы = ВременнаяТаблицаНормыДней.ГрафикРаботы)
		|		ПО (ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСотрудникиСрез.ФизЛицо)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	// 5. Расчет налогов СФ
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПФР, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФР,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаГНПФР, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ГНПФР,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПФР, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФПН * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФРДляВычетаПН,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаГНПФР, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФПН * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ГНПФРДляВычетаПН,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаМСР, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаМСР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК МСР,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПФФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаПФФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФФ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаМСФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаМСФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК МСФ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаФОТФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаФОТФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ФОТФ
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСФ
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	// 6. Расчет корректировок налогов СФ
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ КАК ОДСФ,
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПФФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная, 0) * ВременнаяТаблицаСтатусы.СтавкаПФФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФФотСЗП,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаМСФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная, 0) * ВременнаяТаблицаСтатусы.СтавкаМСФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК МСФотСЗП,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаФОТФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная, 0) * ВременнаяТаблицаСтатусы.СтавкаФОТФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ФОТФотСЗП
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСФКорректировкиПредварит
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная КАК ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаНалогиСФ.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиСФКорректировкиПредварит.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФКорректировкиПредварит.ПФФотСЗП, 0) - ВременнаяТаблицаНалогиСФ.ПФФ > 0
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФКорректировкиПредварит.ПФФотСЗП, 0) - ВременнаяТаблицаНалогиСФ.ПФФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПФФКорректировка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФКорректировкиПредварит.МСФотСЗП, 0) - ВременнаяТаблицаНалогиСФ.МСФ > 0
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФКорректировкиПредварит.МСФотСЗП, 0) - ВременнаяТаблицаНалогиСФ.МСФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК МСФКорректировка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФКорректировкиПредварит.ФОТФотСЗП, 0) - ВременнаяТаблицаНалогиСФ.ФОТФ > 0
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФКорректировкиПредварит.ФОТФотСЗП, 0) - ВременнаяТаблицаНалогиСФ.ФОТФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ФОТФКорректировка
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСФКорректировки
		|ИЗ
		|	ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФКорректировкиПредварит КАК ВременнаяТаблицаНалогиСФКорректировкиПредварит
		|		ПО ВременнаяТаблицаНалогиСФ.ФизЛицо = ВременнаяТаблицаНалогиСФКорректировкиПредварит.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	// 7. Расчет налогов ПН
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПН, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ (ВременнаяТаблицаНачисленияЗаМесяц.ОДПН - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФРДляВычетаПН, 0) - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФРДляВычетаПН, 0) - ВременнаяТаблицаСтатусы.Вычеты * 100) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПН,
		|	ВременнаяТаблицаСтатусы.Вычеты КАК Вычеты,
		|	&МРД КАК МРД,
		|	&ДоплатаПНЗаСчетФирмы КАК ДоплатаПНЗаСчетФирмы
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПН
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПН > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	// 8. Расчет налогов Проф.взнос
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	8 КАК Порядок,
		|	ПрофвзносыСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ВЫРАЗИТЬ((ВременнаяТаблицаНачисленияЗаМесяц.ОДПрофВзнос - ВЫБОР
		|			КОГДА &ПрофВзносЗаВычетомПФРиГНПФР
		|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФР, 0) + ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФР, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ - ВЫБОР
		|			КОГДА &ПрофВзносЗаВычетомПН
		|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиПН.ПН, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ) * &СтавкаПрофВзнос / 100 КАК ЧИСЛО(15, 2)) КАК ПрофВзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПрофВзнос
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Профвзносы.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ФизЛицо В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц)) КАК ПрофвзносыСрезПоследних
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ПрофвзносыСрезПоследних.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПН КАК ВременнаяТаблицаНалогиПН
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиПН.ФизЛицо
		|ГДЕ
		|	ПрофвзносыСрезПоследних.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПоПрофсоюзам.Вступление)
		|	И ВременнаяТаблицаНачисленияЗаМесяц.ОДПрофВзнос > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	// 9. Расчет налогов с организации
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицоНаименование КАК ФизЛицоНаименование,
		|	ВременнаяТаблицаСтатусы.Статус КАК Статус,
		|	ВременнаяТаблицаСтатусы.ПрименятьВычет КАК ПрименятьВычет,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней КАК ОтработаноДней,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СуммаНачислено КАК СуммаНачислено,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СуммаДопДоход КАК СуммаДопДоход,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ КАК ОДСФ,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПН КАК ОДПН,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПрофВзнос КАК ОДПрофВзнос,
		|	ВременнаяТаблицаНалогиСФ.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалогиСФ.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиСФ.МСР КАК МСР,
		|	ВременнаяТаблицаНалогиСФ.ПФФ КАК ПФФ,
		|	ВременнаяТаблицаНалогиСФ.МСФ КАК МСФ,
		|	ВременнаяТаблицаНалогиСФ.ФОТФ КАК ФОТФ,
		|	ВременнаяТаблицаНалогиПН.МРД КАК МРД,
		|	ВременнаяТаблицаНалогиПН.ПН КАК ПН,
		|	ВременнаяТаблицаНалогиПН.Вычеты КАК Вычеты,
		|	ВременнаяТаблицаНалогиПрофВзнос.ПрофВзнос КАК ПрофВзнос,
		|	ВременнаяТаблицаНалогиСФКорректировки.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная,
		|	ВременнаяТаблицаНалогиСФКорректировки.ПФФКорректировка КАК ПФФКорректировка,
		|	ВременнаяТаблицаНалогиСФКорректировки.МСФКорректировка КАК МСФКорректировка,
		|	ВременнаяТаблицаНалогиСФКорректировки.ФОТФКорректировка КАК ФОТФКорректировка
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПН КАК ВременнаяТаблицаНалогиПН
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиПН.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПрофВзнос КАК ВременнаяТаблицаНалогиПрофВзнос
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиПрофВзнос.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФКорректировки КАК ВременнаяТаблицаНалогиСФКорректировки
		|		ПО  ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФКорректировки.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицоНаименование";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
		
	// По данным учетной политики
	ДанныеУчетнойПолитикиПоПерсоналу = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация);
	
	// СФ
	Запрос.УстановитьПараметр("СреднемесячнаяЗПСФ", ДанныеУчетнойПолитикиПоПерсоналу.СреднемесячнаяЗПСФ);
	Запрос.УстановитьПараметр("ПроцентОтСЗПСФ", ДанныеУчетнойПолитикиПоПерсоналу.ПроцентОтСЗПСФ);

	// ПН
	Запрос.УстановитьПараметр("МРД", ДанныеУчетнойПолитикиПоПерсоналу.МинимальныйРасчетныйДоход);	
	Запрос.УстановитьПараметр("ДоплатаПНЗаСчетФирмы", ДанныеУчетнойПолитикиПоПерсоналу.ДоплатаПНЗаСчетФирмы);	
	// Корп.отчисления
	Запрос.УстановитьПараметр("СтавкаПрофВзнос", ДанныеУчетнойПолитикиПоПерсоналу.СтавкаПрофВзнос);
	Запрос.УстановитьПараметр("ПрофВзносЗаВычетомПН", ДанныеУчетнойПолитикиПоПерсоналу.ПрофВзносЗаВычетомПН);
	Запрос.УстановитьПараметр("ПрофВзносЗаВычетомПФРиГНПФР", ДанныеУчетнойПолитикиПоПерсоналу.ПрофВзносЗаВычетомПФРиГНПФР);
	                        
	НалогиПоЗарплате.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры // РассчитатьНалогиПоЗарплате()

// Процедура - Рассчитать удержания
//
Процедура РассчитатьПлановыеУдержания()
	
	// 1. Получение рассчитанных налогов для рассчета размера вычета
	// 2. Получение актуальных плановых удержаний
	// 3. Расчет удержаний
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНалоги.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалоги.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалоги.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалоги.ПН КАК ПН,
		|	ВременнаяТаблицаНалоги.ПрофВзнос КАК ПрофВзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоги
		|ИЗ
		|	&ВременнаяТаблицаНалоги КАК ВременнаяТаблицаНалоги
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеУдержанияНачало.ФизЛицо,
		|	ПлановыеУдержанияНачало.ВидРасчета,
		|	ПлановыеУдержанияНачало.Размер,
		|	ВЫБОР
		|		КОГДА ПлановыеУдержанияНачало.ВидРасчета.РасчетЗаМинусомСФ
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалоги.ПФР, 0) + ЕСТЬNULL(ВременнаяТаблицаНалоги.ГНПФР, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ + ВЫБОР
		|		КОГДА ПлановыеУдержанияНачало.ВидРасчета.РасчетЗаМинусомПН
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалоги.ПН, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ + ВЫБОР
		|		КОГДА ПлановыеУдержанияНачало.ВидРасчета.РасчетЗаМинусомПрофВзнос
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалоги.ПрофВзнос, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РазмерВычета
		|ИЗ
		|	РегистрСведений.ПлановыеУдержанияНачало.СрезПоследних(&Период, Организация = &Организация) КАК ПлановыеУдержанияНачало
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеУдержанияОкончание.СрезПоследних(&Период, Организация = &Организация) КАК ПлановыеУдержанияОкончание
		|		ПО ПлановыеУдержанияНачало.Организация = ПлановыеУдержанияОкончание.Организация
		|			И ПлановыеУдержанияНачало.ФизЛицо = ПлановыеУдержанияОкончание.ФизЛицо
		|			И ПлановыеУдержанияНачало.ВидРасчета = ПлановыеУдержанияОкончание.ВидРасчета
		|			И ПлановыеУдержанияНачало.Регистратор = ПлановыеУдержанияОкончание.ДокументСсылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоги КАК ВременнаяТаблицаНалоги
		|		ПО ПлановыеУдержанияНачало.ФизЛицо = ВременнаяТаблицаНалоги.ФизЛицо
		|ГДЕ
		|	ПлановыеУдержанияОкончание.Организация ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ВременнаяТаблицаНалоги", НалогиПоЗарплате.Выгрузить());
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Нет плановых удержаний для расчета
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;	
	
	Удержания.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// Подготовка таблицы для движения в регистр
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУдержания.ФизЛицо,
		|	ВременнаяТаблицаУдержания.ВидРасчета,
		|	ВременнаяТаблицаУдержания.Размер,
		|	ВременнаяТаблицаУдержания.РазмерВычета,
		|	ВременнаяТаблицаУдержания.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержания
		|ИЗ
		|	&ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) КАК ПериодРегистрации,
		|	НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) КАК БазовыйПериодНачало,
		|	КОНЕЦПЕРИОДА(&Дата, ДЕНЬ) КАК БазовыйПериодКонец,
		|	ТаблицаДокумента.ВидРасчета,
		|	&Организация КАК Организация,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Размер,
		|	ТаблицаДокумента.РазмерВычета,
		|	ТаблицаДокумента.Результат
		|ИЗ
		|	ВременнаяТаблицаУдержания КАК ТаблицаДокумента";
	Запрос.УстановитьПараметр("ВременнаяТаблицаУдержания", Удержания.Выгрузить());	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", Новый Структура);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУдержания", РезультатЗапроса.Выгрузить());
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьУдержания(ДополнительныеСвойства, Движения, Ложь);

	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	Ошибки = Неопределено;
	ПроведениеРасчетовПоЗарплатеСервер.РассчитатьЗаписиРегистраРасчета("Удержания", Движения.Удержания, Удержания, Ошибки);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
КонецПроцедуры // РассчитатьПлановыеУдержания()

// Процедура - Дозаполнить налоги по зарплате
//
Процедура ДозаполнитьНалогиПоЗарплате()
	
	// 1. Получение рассчитанных удержаний
	// 2. Получение разовых удержаний
	// 3. Заполнение суммы удержано
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУдержания.ФизЛицо,
		|	ВременнаяТаблицаУдержания.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержания
		|ИЗ
		|	&ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Удержания.ФизЛицо,
		|	Удержания.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержанияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Удержания.Организация = &Организация
		|	И НЕ Удержания.Результат = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ФизЛицо,
		|	СУММА(ВложенныйЗапрос.Результат) КАК Результат
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаУдержания.ФизЛицо КАК ФизЛицо,
		|		ВременнаяТаблицаУдержания.Результат КАК Результат
		|	ИЗ
		|		ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВременнаяТаблицаУдержанияЗаМесяц.ФизЛицо,
		|		ВременнаяТаблицаУдержанияЗаМесяц.Результат
		|	ИЗ
		|		ВременнаяТаблицаУдержанияЗаМесяц КАК ВременнаяТаблицаУдержанияЗаМесяц) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ФизЛицо";
	Запрос.УстановитьПараметр("ВременнаяТаблицаУдержания", Удержания.Выгрузить());		
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого СтрокаТабличнойЧасти Из НалогиПоЗарплате Цикл 
		СуммаУдержано = 0;
		// Удержания
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Новый Структура("ФизЛицо", СтрокаТабличнойЧасти.ФизЛицо)) Тогда 
			СуммаУдержано = СуммаУдержано + ВыборкаДетальныеЗаписи.Результат;
			ВыборкаДетальныеЗаписи.Сбросить();
		КонецЕсли;	
		
		// Налоги
		СуммаУдержано = СуммаУдержано 
			+ СтрокаТабличнойЧасти.ПФР 
			+ СтрокаТабличнойЧасти.ГНПФР 
			+ СтрокаТабличнойЧасти.ПН 
			+ СтрокаТабличнойЧасти.ПрофВзнос;
			
		СтрокаТабличнойЧасти.СуммаУдержано = СуммаУдержано;	
	КонецЦикла;	
	
КонецПроцедуры // ДозаполнитьНалогиПоЗарплате()

// Процедура - Дозаполнить выплату
//
Процедура ДозаполнитьВыплату()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыплаченнаяЗарплатаОбороты.Физлицо,
		|	ВыплаченнаяЗарплатаОбороты.СуммаОборот КАК Выплачено
		|ИЗ
		|	РегистрНакопления.ВыплаченнаяЗарплата.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК ВыплаченнаяЗарплатаОбороты";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НайденнаяСтрокаТабличнойЧасти = НалогиПоЗарплате.Найти(ВыборкаДетальныеЗаписи.Физлицо, "Физлицо");
		
		Если НайденнаяСтрокаТабличнойЧасти = Неопределено Тогда 
			Продолжить;
		КонецЕсли;	
		
		ЗаполнитьЗначенияСвойств(НайденнаяСтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи, "Выплачено");
	КонецЦикла;	
	
КонецПроцедуры // ДозаполнитьВыплату()

// Процедура - Распределить налоги с организации
//
Процедура РаспределитьНалогиСОрганизации()
	
	// Выборка всех начислений за месяц в разрезе подразделения и вида начисления.
	// Для каждого налога определяется база распределения, для исключения нулевых ставок.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.ВидРасчета КАК ВидРасчета,
		|	СУММА(Начисления.Результат) КАК Результат
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|	И Начисления.ВидРасчета.ОблагаетсяСФ
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ВидРасчета,
		|	Начисления.Подразделение,
		|	Начисления.ФизЛицо
		|ИТОГИ ПО
		|	ФизЛицо";
	Запрос.УстановитьПараметр("НалогиПоЗарплате", НалогиПоЗарплате.Выгрузить());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;	
	КонецЕсли;
	
	// Заполнение начисленной суммы по счетам и статьям затрат.
	СтруктураОтбора = ПроведениеРасчетовПоЗарплатеСервер.СтруктураОтбораДанныхСчетовУчетаПоЗП();
	
	ВыборкаФизЛицо = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФизЛицо.Следующий() Цикл 
		
		// Распределение сумм налогов.
		НайденныеСтроки = НалогиПоЗарплате.НайтиСтроки(Новый Структура("ФизЛицо", ВыборкаФизЛицо.ФизЛицо));
		Если НайденныеСтроки.Количество() = 0 Тогда 
			Продолжить;
		Иначе 
			ПФФ = НайденныеСтроки[0].ПФФ;
			МСФ = НайденныеСтроки[0].МСФ;
			ФОТФ = НайденныеСтроки[0].ФОТФ;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаФизЛицо.Выбрать();
		
		МассивКоэффициентов = Новый Массив;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			МассивКоэффициентов.Добавить(ВыборкаДетальныеЗаписи.Результат);
		КонецЦикла;
		
		МассивСуммПФФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ПФФ, МассивКоэффициентов);
		МассивСуммМСФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(МСФ, МассивКоэффициентов);
		МассивСуммФОТФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ФОТФ, МассивКоэффициентов);
		
		// Определение параметров отражения в учете.
		ВыборкаДетальныеЗаписи.Сбросить();
		
		ИндексВМассиве = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			СтруктураОтбора.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
			СтруктураОтбора.ВидРасчетаНачисления = ВыборкаДетальныеЗаписи.ВидРасчета;
			
			ДанныеСчетаУчетаЗП = ПроведениеРасчетовПоЗарплатеСервер.ДанныеСчетаУчетаЗП(СтруктураОтбора);
			
			НайденныеСтроки = НалогиСОрганизации.НайтиСтроки(Новый Структура("СчетУчетаЗатрат, СтатьяЗатрат", 
				ДанныеСчетаУчетаЗП.СчетУчетаЗатратСоцОтчисления, ДанныеСчетаУчетаЗП.СтатьяЗатратСоцОтчисления));
				
			Если НайденныеСтроки.Количество() = 0 Тогда 
				СтрокаТабличнойЧасти = НалогиСОрганизации.Добавить();
				СтрокаТабличнойЧасти.СчетУчетаЗатрат = ДанныеСчетаУчетаЗП.СчетУчетаЗатратСоцОтчисления;
				СтрокаТабличнойЧасти.СтатьяЗатрат = ДанныеСчетаУчетаЗП.СтатьяЗатратСоцОтчисления;
			Иначе 
				СтрокаТабличнойЧасти = НайденныеСтроки[0];
			КонецЕсли;			
			
			СтрокаТабличнойЧасти.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
			Если НЕ МассивСуммПФФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.ПФФ = СтрокаТабличнойЧасти.ПФФ + МассивСуммПФФ[ИндексВМассиве];
			КонецЕсли;
			Если НЕ МассивСуммМСФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.МСФ = СтрокаТабличнойЧасти.МСФ + МассивСуммМСФ[ИндексВМассиве];
			КонецЕсли;
			Если НЕ МассивСуммФОТФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.ФОТФ = СтрокаТабличнойЧасти.ФОТФ + МассивСуммФОТФ[ИндексВМассиве];
			КонецЕсли;	
			
			ИндексВМассиве = ИндексВМассиве + 1;
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры // РаспределитьНалогиСОрганизации()

#КонецОбласти

#КонецЕсли
