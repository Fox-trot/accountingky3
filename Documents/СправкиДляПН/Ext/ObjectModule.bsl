#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДанные(Отказ, РежимПроведения);
	
	ОтразитьДвижения(Отказ, РежимПроведения);
	
	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если Сотрудники.Количество() = 0 
		И Подразделения.Количество() = 0 Тогда 
		ПроверяемыеРеквизиты.Добавить("Сотрудники");
		ПроверяемыеРеквизиты.Добавить("Подразделения");
	КонецЕсли;	
	
	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииОбработкаПроведения

// Процедура инициализирует данные документа
// и подготавливает таблицы для движения в регистры
//
Процедура ИнициализироватьДанные(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.СправкиДляПН.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
КонецПроцедуры
	
// Процедура заполняет регистры данными
//
Процедура ОтразитьДвижения(Отказ, РежимПроведения)

	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьГНСПоПодразделениям(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСтатусыСотрудников(ДополнительныеСвойства, Движения, Отказ); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьТабличнуюЧасть() Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СотрудникиСрезПоследних
		|ГДЕ
		|	СотрудникиСрезПоследних.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сотрудники.ФизЛицо
		|ИЗ
		|	РегистрСведений.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Период > &НачалоПериода
		|	И Сотрудники.Период <= &КонецПериода
		|	И Сотрудники.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудники.ФизЛицо,
		|	СтатусыСотрудниковСрезПоследних.Статус,
		|	СтатусыСотрудниковСрезПоследних.ДопВычет,
		|	СтатусыСотрудниковСрезПоследних.ГНС
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСотрудников.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ФизЛицо В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСотрудники.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)) КАК СтатусыСотрудниковСрезПоследних
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = СтатусыСотрудниковСрезПоследних.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаСотрудники.ФизЛицо.Наименование";
	Запрос.УстановитьПараметр("Организация", Организация);	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	
	Сотрудники.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудники.НомерСтроки,
		|	ВременнаяТаблицаСотрудники.ФизЛицо
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	&ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПодразделения.НомерСтроки,
		|	ВременнаяТаблицаПодразделения.Подразделение
		|ПОМЕСТИТЬ ВременнаяТаблицаПодразделения
		|ИЗ
		|	&ВременнаяТаблицаПодразделения КАК ВременнаяТаблицаПодразделения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыСотрудниковСрезПоследних.Регистратор,
		|	СтатусыСотрудниковСрезПоследних.ФизЛицо,
		|	МИНИМУМ(ВременнаяТаблицаСотрудники.НомерСтроки) КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСотрудников.СрезПоследних(
		|				&Период,
		|				НЕ Регистратор = &Ссылка
		|					И Организация = &Организация
		|					И ФизЛицо В
		|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|							ВременнаяТаблицаСотрудники.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)) КАК СтатусыСотрудниковСрезПоследних
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = СтатусыСотрудниковСрезПоследних.ФизЛицо
		|ГДЕ
		|	СтатусыСотрудниковСрезПоследних.Период = &Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СтатусыСотрудниковСрезПоследних.Регистратор,
		|	СтатусыСотрудниковСрезПоследних.ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГНСПоПодразделениямСрезПоследних.Регистратор,
		|	ГНСПоПодразделениямСрезПоследних.Подразделение,
		|	МАКСИМУМ(ВременнаяТаблицаПодразделения.НомерСтроки) КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаПодразделения КАК ВременнаяТаблицаПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГНСПоПодразделениям.СрезПоследних(
		|				&Период,
		|				НЕ Регистратор = &Ссылка
		|					И Организация = &Организация
		|					И Подразделение В
		|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|							ВременнаяТаблицаПодразделения.Подразделение
		|						ИЗ
		|							ВременнаяТаблицаПодразделения КАК ВременнаяТаблицаПодразделения)) КАК ГНСПоПодразделениямСрезПоследних
		|		ПО ВременнаяТаблицаПодразделения.Подразделение = ГНСПоПодразделениямСрезПоследних.Подразделение
		|ГДЕ
		|	ГНСПоПодразделениямСрезПоследних.Период = &Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ГНСПоПодразделениямСрезПоследних.Регистратор,
		|	ГНСПоПодразделениямСрезПоследних.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ТаблицаСотрудникиДублиСтрок.НомерСтроки) КАК НомерСтроки,
		|	ТаблицаСотрудникиДублиСтрок.ФизЛицо
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ТаблицаСотрудникиДублиСтрок
		|		ПО ВременнаяТаблицаСотрудники.НомерСтроки <> ТаблицаСотрудникиДублиСтрок.НомерСтроки
		|			И ВременнаяТаблицаСотрудники.ФизЛицо = ТаблицаСотрудникиДублиСтрок.ФизЛицо
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаСотрудникиДублиСтрок.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ТаблицаПодразделенияДублиСтрок.НомерСтроки) КАК НомерСтроки,
		|	ТаблицаПодразделенияДублиСтрок.Подразделение
		|ИЗ
		|	ВременнаяТаблицаПодразделения КАК ВременнаяТаблицаПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПодразделения КАК ТаблицаПодразделенияДублиСтрок
		|		ПО ВременнаяТаблицаПодразделения.НомерСтроки <> ТаблицаПодразделенияДублиСтрок.НомерСтроки
		|			И ВременнаяТаблицаПодразделения.Подразделение = ТаблицаПодразделенияДублиСтрок.Подразделение
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПодразделенияДублиСтрок.Подразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");                          	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Параметры.Вставить("Период", Дата);
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("ВременнаяТаблицаСотрудники", Сотрудники.Выгрузить());
	Запрос.Параметры.Вставить("ВременнаяТаблицаПодразделения", Подразделения.Выгрузить());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Регистр "СтатусыСотрудников"
	Если НЕ МассивРезультатов[2].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Период действия документа противоречит документу ""%1"" по сотруднику ""%2"" в строке %3 списка ""Сотрудники"".'"), 
							ВыборкаИзРезультатаЗапроса.Регистратор, ВыборкаИзРезультатаЗапроса.ФизЛицо, ВыборкаИзРезультатаЗапроса.НомерСтроки);
			БухгалтерскийУчетСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"ФизЛицо",
				Отказ);
		КонецЦикла;
	КонецЕсли;

	// Регистр "ГНСПоПодразделениям".
	Если НЕ МассивРезультатов[3].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Период действия документа противоречит документа ""%1"" по подразделению ""%2"" в строке %3 списка ""Подразделения"".'"), 
							ВыборкаИзРезультатаЗапроса.Регистратор, ВыборкаИзРезультатаЗапроса.Подразделение, ВыборкаИзРезультатаЗапроса.НомерСтроки);
			БухгалтерскийУчетСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Подразделения",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Подразделение",
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
	// Дубли строк Сотрудники
	Если НЕ МассивРезультатов[4].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Сотрудник указывается повторно в строке %1 списка ""Сотрудники"".'"), 
							ВыборкаИзРезультатаЗапроса.НомерСтроки);
			БухгалтерскийУчетСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"ФизЛицо",
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
	// Дубли строк Подразделения
	Если НЕ МассивРезультатов[5].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[5].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Подразделение указывается повторно в строке %1 списка ""Подразделения"".'"), 
							ВыборкаИзРезультатаЗапроса.НомерСтроки);
			БухгалтерскийУчетСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Подразделения",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Подразделение",
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли