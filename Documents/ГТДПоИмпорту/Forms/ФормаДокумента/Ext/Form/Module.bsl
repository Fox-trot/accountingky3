#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ДанныеУчетнойПолитики = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);			
	
	// Одной строкой / Списком.
	Если Объект.Разделы.Количество() = 0 Тогда
		СтрокаТабличнойЧасти = Объект.Разделы.Добавить();
		СтрокаТабличнойЧасти.КлючСвязи = 1;
		СтрокаТабличнойЧасти.СтавкаСбора = ДанныеУчетнойПолитики.СтавкаТаможенногоСбора;
		СтрокаТабличнойЧасти.СтавкаНДС = УчетНДСВызовСервера.ПолучитьСтавкуНДС(ДатаДокумента, Справочники.СтавкиНДС.Стандарт);
		Элементы.Разделы.ТекущаяСтрока = СтрокаТабличнойЧасти.ПолучитьИдентификатор();	
		
	Иначе
		СтрокаТабличнойЧасти = Объект.Разделы[0];
		
		Если СтрокаТабличнойЧасти.КлючСвязи = 0 Тогда
			СтрокаТабличнойЧасти.КлючСвязи = 1;
			СтрокаТабличнойЧасти.СтавкаСбора = ДанныеУчетнойПолитики.СтавкаТаможенногоСбора;
			СтрокаТабличнойЧасти.СтавкаНДС = УчетНДСВызовСервера.ПолучитьСтавкуНДС(ДатаДокумента, Справочники.СтавкиНДС.Стандарт);	
		КонецЕсли;	
		
		Элементы.Разделы.ТекущаяСтрока = Объект.Разделы[0].ПолучитьИдентификатор();
	КонецЕсли;	
	
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.Товары.Количество() > 0 ИЛИ Объект.ОС.Количество() > 0 Тогда 
		ОписатьОтборДокументовПоступленияИКурс(Элементы.Разделы.ТекущиеДанные);
	КонецЕсли;
	
	Если Объект.Разделы.Количество() > 1 Тогда
		РазделыГТД = Истина;
		УстановитьВозможностьРедактированияСпискомФрагмент();
	КонецЕсли;	
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПодборДокументаПоступления"  
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор Тогда
		
		МассивДокументов = Параметр.МассивДокументов;
		СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
		
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			ЗаполнениеПоПоступлению(МассивДокументов, СтрокаТабличнойЧасти.КлючСвязи);
			ОписатьОтборДокументовПоступленияИКурс(СтрокаТабличнойЧасти);
			
			Объект.КонтрагентПоПоступлению = КонтрагентОтбор;
			
			Если ИспользоватьДопЕдиницыОтбор Тогда	
				СтрокаТабличнойЧасти.РаспределениеАкциза = ПредопределенноеЗначение("Перечисление.СпособыРаспределенияДопРасходов.ПоДопЕдиницам");		
			КонецЕсли;	
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПодборДокументовДопрасходов" 
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор Тогда
		
		МассивДокументов = Параметр.МассивДокументов;
		СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
		
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			СуммаДопрасходов = ЗаполнитьДопрасходыИВернутьСумму(МассивДокументов, СтрокаТабличнойЧасти.КлючСвязи);
		КонецЕсли;
		
		СтрокаТабличнойЧасти.ДопРасходы = СтрокаТабличнойЧасти.ДопРасходы + СуммаДопрасходов;
		
		УстановитьВидимостьДоступностьЭлементов();
		
	ИначеЕсли ИмяСобытия = "ПодборНоменклатурыПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор Тогда
		
		АдресЗапасовВХранилище = Параметр;
		ПолучитьТоварыИзХранилища(АдресЗапасовВХранилище, Элементы.Разделы.ТекущиеДанные.КлючСвязи);			
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)	
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);
	ДанныеУчетнойПолитики = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	// Обработка события изменения организации.
	Объект.Номер = "";
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);
	ДанныеУчетнойПолитики = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Контрагент.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация);
	
	Объект.ДоговорКонтрагента = СтруктураДанные.ДоговорКонтрагента;
	
	ПриИзмененииДоговора(ДатаДокумента, Объект.ДоговорКонтрагента);
	
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Договор контрагента.
//
&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	ПриИзмененииДоговора(ДатаДокумента, Объект.ДоговорКонтрагента)
КонецПроцедуры

&НаКлиенте
Процедура ВводСуммГТДВручнуюПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеВручнуюПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура РазделыГТДПриИзменении(Элемент)
	
	КоличествоСтрок = Объект.Разделы.Количество();	
	
	Если НЕ РазделыГТД И КоличествоСтрок > 1 Тогда
		  
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросУстановитьВозможностьРедактированияСписком", ЭтотОбъект, Новый Структура("КоличествоСтрок", КоличествоСтрок));
		ТекстВопроса = НСтр("ru = 'Все строки кроме первой будут удалены! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	Иначе
		УстановитьВозможностьРедактированияСпискомФрагмент();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СопровождениеПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРазделы

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	ИмяТабличнойЧасти = "Разделы";
	БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтотОбъект, "Товары");
	БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтотОбъект, "ОС");
	БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтотОбъект, "ДопРасходы");
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		ОписатьОтборДокументовПоступленияИКурс(СтрокаТабличнойЧасти);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазделыПередУдалением(Элемент, Отказ)
	
	ИмяТабличнойЧасти = "Разделы";
	БухгалтерскийУчетКлиент.УдалитьСтрокиПодчиненнойТабличнойЧасти(ЭтотОбъект, "Товары");
	БухгалтерскийУчетКлиент.УдалитьСтрокиПодчиненнойТабличнойЧасти(ЭтотОбъект, "ОС");
	БухгалтерскийУчетКлиент.УдалитьСтрокиПодчиненнойТабличнойЧасти(ЭтотОбъект, "ДопРасходы");
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ИмяТабличнойЧасти = "Разделы";
	Если Элемент.ТекущиеДанные.КлючСвязи = 0 Тогда
		БухгалтерскийУчетКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект);
	КонецЕсли;
	БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтотОбъект, "Товары");
	БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтотОбъект, "ОС");
	БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтотОбъект, "ДопРасходы");
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.СтавкаСбора = ДанныеУчетнойПолитики.СтавкаТаможенногоСбора;	
		Элемент.ТекущиеДанные.СтавкаНДС = УчетНДСВызовСервера.ПолучитьСтавкуНДС(ДатаДокумента, ПредопределенноеЗначение("Справочник.СтавкиНДС.Стандарт"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазделыРаспределениеАкциза1ПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();	
КонецПроцедуры

&НаКлиенте
Процедура РазделыРаспределениеАкцизаПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

// Процедура - обработчик события ПередНачаломДобавления поля ввода Товары.
//
&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИмяТабличнойЧасти = "Разделы";
	Отказ = БухгалтерскийУчетКлиент.ПередНачаломДобавленияВПодчиненнуюТабличнуюЧасть(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования поля ввода Товары.
//
&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ИмяТабличнойЧасти = "Товары";
		БухгалтерскийУчетКлиент.ДобавитьКлючСвязиВСтрокуПодчиненнойТабличнойЧасти(ЭтотОбъект, Элемент.Имя);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
	
	МассивСтрокТовары = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	МассивСтрокОС = Объект.ОС.НайтиСтроки(СтруктураОтбора);
	
	Если МассивСтрокТовары.Количество() = 0 И МассивСтрокОС.Количество() = 0 Тогда
		
		Если Объект.Товары.Количество() = 0 И Объект.ОС.Количество() = 0 Тогда	
			КонтрагентОтбор = Неопределено;
		КонецЕсли;	
		
		Элементы.ДекорацияОписанияОтборовДокументовПоступления.Заголовок = 
			ПолучитьТекстОписания(КонтрагентОтбор, ДатаОтбор, ВалютаОтбор, ДоговорОтбор, ИспользоватьДопЕдиницыОтбор);
			
		Объект.КонтрагентПоПоступлению = КонтрагентОтбор;	
			
		Если НЕ ВалютаОтбор.Пустая() Тогда
			КурсКратность 	 = ПолучитьКурсИКратность(ВалютаОтбор, ДатаОтбор);
			Объект.Курс 	 = КурсКратность.Курс;
			Объект.Кратность = КурсКратность.Кратность;
			
			СтрокаТабличнойЧасти.Валюта = ВалютаОтбор;
		КонецЕсли;
		
		УстановитьВидимостьДоступностьЭлементов();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОС

// Процедура - обработчик события ПередНачаломДобавления поля ввода ОС.
//
&НаКлиенте
Процедура ОСПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИмяТабличнойЧасти = "Разделы";
	Отказ = БухгалтерскийУчетКлиент.ПередНачаломДобавленияВПодчиненнуюТабличнуюЧасть(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования поля ввода ОС.
//
&НаКлиенте
Процедура ОСПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ИмяТабличнойЧасти = "ОС";
		БухгалтерскийУчетКлиент.ДобавитьКлючСвязиВСтрокуПодчиненнойТабличнойЧасти(ЭтотОбъект, Элемент.Имя);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОСПослеУдаления(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
	
	МассивСтрокТовары = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	МассивСтрокОС = Объект.ОС.НайтиСтроки(СтруктураОтбора);
	
	Если МассивСтрокТовары.Количество() = 0 И МассивСтрокОС.Количество() = 0 Тогда
		
		Если Объект.Товары.Количество() = 0 И Объект.ОС.Количество() = 0 Тогда	
			КонтрагентОтбор = Неопределено;
		КонецЕсли;	
		
		Элементы.ДекорацияОписанияОтборовДокументовПоступления.Заголовок = 
			ПолучитьТекстОписания(КонтрагентОтбор, ДатаОтбор, ВалютаОтбор, ДоговорОтбор, ИспользоватьДопЕдиницыОтбор);
			
		Объект.КонтрагентПоПоступлению = КонтрагентОтбор;	
			
		Если НЕ ВалютаОтбор.Пустая() Тогда
			КурсКратность 	 = ПолучитьКурсИКратность(ВалютаОтбор, ДатаОтбор);
			Объект.Курс 	 = КурсКратность.Курс;
			Объект.Кратность = КурсКратность.Кратность;
			
			СтрокаТабличнойЧасти.Валюта = ВалютаОтбор;
		КонецЕсли;
		
		УстановитьВидимостьДоступностьЭлементов();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДопрасходы

&НаКлиенте
Процедура ДопрасходыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИмяТабличнойЧасти = "Разделы";
	Отказ = БухгалтерскийУчетКлиент.ПередНачаломДобавленияВПодчиненнуюТабличнуюЧасть(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ДопрасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ИмяТабличнойЧасти = "ДопРасходы";
		БухгалтерскийУчетКлиент.ДобавитьКлючСвязиВСтрокуПодчиненнойТабличнойЧасти(ЭтотОбъект, Элемент.Имя);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДопрасходыПередУдалением(Элемент, Отказ)
	
	СтрокаТабличнойЧасти = Элементы.Допрасходы.ТекущиеДанные;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);	
	
	// Поиск по разделам и удаление суммы допрасходов.
	МассивСтрок = Объект.Разделы.НайтиСтроки(СтруктураОтбора);
	
	Если МассивСтрок.Количество() > 0 Тогда
		МассивСтрок[0].ДопРасходы = МассивСтрок[0].ДопРасходы - СтрокаТабличнойЧасти.СуммаДопРасходов;	
	КонецЕсли;	
	
	// Поиск по товарам и удаление суммы допрасходов.
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("ДокументДопРасходов", СтрокаТабличнойЧасти.ДокументДопРасходов);	
	
	МассивСтрок = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		СтрокаМассива.ДопРасходы = 0;	
	КонецЦикла;
	
	// Поиск по основным средствам и удаление суммы допрасходов.
	МассивСтрок = Объект.ОС.НайтиСтроки(СтруктураОтбора);
	
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		СтрокаМассива.ДопРасходы = 0;	
	КонецЦикла;
	
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПоступление(Команда)	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ДатаДокумента", 							ДатаДокумента);
	ПараметрыОтбора.Вставить("Организация", 							Объект.Организация);
	ПараметрыОтбора.Вставить("ИспользоватьДопЕдиницы", 					ИспользоватьДопЕдиницыОтбор);
	ПараметрыОтбора.Вставить("ДатаОтбор", 								ДатаОтбор);
	ПараметрыОтбора.Вставить("Валюта", 									ВалютаОтбор);
	ПараметрыОтбора.Вставить("УникальныйИдентификаторФормыВладельца", 	УникальныйИдентификатор);
	
	ОткрытьФорму("Документ.ГТДПоИмпорту.Форма.ФормаПодбораДокументовПоступления", ПараметрыОтбора);
КонецПроцедуры

&НаКлиенте
Процедура ПодборДокументовДопрасходов(Команда)
	
	КлючСвязи = Неопределено;
	
	Если Объект.Разделы.Количество() = 1 Тогда		
		КлючСвязи = Объект.Разделы[0].КлючСвязи;		
	Иначе		
		СтрокаТабличнойЧастиРазделы = Элементы.Разделы.ТекущиеДанные;
	
		Если СтрокаТабличнойЧастиРазделы = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Для подбора документов подрасходов выделите на закладке ""Раздел"" необходимый раздел.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		Иначе
			КлючСвязи = СтрокаТабличнойЧастиРазделы.КлючСвязи;
		КонецЕсли;		
	КонецЕсли;	
	
	Если КлючСвязи <> Неопределено Тогда
		
		Отбор = Новый Структура();
		Отбор.Вставить("КлючСвязи", КлючСвязи);
		
		МассивСтрокТовары = Объект.Товары.НайтиСтроки(Отбор);
		МассивСтрокОС 	  = Объект.ОС.НайтиСтроки(Отбор);
		МассивПоступлений = Новый Массив();
		
		Для Каждого СтрокаМассива Из МассивСтрокТовары Цикл
			МассивПоступлений.Добавить(СтрокаМассива.ДокументПоступления);	
		КонецЦикла;	
		
		Для Каждого СтрокаМассива Из МассивСтрокОС Цикл
			МассивПоступлений.Добавить(СтрокаМассива.ДокументПоступления);	
		КонецЦикла;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ДатаОтбора", 								ДатаДокумента);
		ПараметрыОтбора.Вставить("МассивПоступлений", 						МассивПоступлений);
		ПараметрыОтбора.Вставить("УникальныйИдентификаторФормыВладельца", 	УникальныйИдентификатор);
		
		ОткрытьФорму("Документ.ГТДПоИмпорту.Форма.ФормаПодбораДокументовДопРасходов", ПараметрыОтбора);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Рассчитать(Команда)
	
	Отказ = ПроверитьЗаполнениеПередРасчетом();
	
	Если Отказ Тогда 
		Возврат
	КонецЕсли;
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	
	ПервыйРассчет = Ложь;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		Если СтрокаТабличнойЧасти.ФактурнаяСтоимость = 0 Тогда
			РассчитатьИРаспределить();
			ПервыйРассчет = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПервыйРассчет Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросРассчитать", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные будут заново распределены и пересчитаны! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли;

	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события действия команды Подбор в табличную часть Товары.
// Открывает форму подбора.
//
&НаКлиенте
Процедура ПодборНоменклатуры(Команда)
	РаботаСПодборомНоменклатурыКлиент.ОткрытьПодбор(ЭтаФорма, "Товары", "Поступление");
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Если Объект.Товары.Количество() > 0 ИЛИ Объект.ОС.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросОчистить", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Закладки ""Товары"" и ""ОС"" будут очищены! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);	
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПоПоступлениям(Команда)
	
	Отказ = ПроверитьЗаполнениеПередРасчетом();
	
	Если Отказ Тогда 
		Возврат
	КонецЕсли;
	
	Если Объект.Товары.Количество() = 0 И Объект.ОС.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Данных в табличных частях ""Товары"" и ""ОС"" нет! Перезаполнение отменено.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);		
	Иначе	
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросПерезаполнить", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные выбранных документов будут перезаполнены и документ ГТД будет рассчитан! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросОчистить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		Объект.ОС.Очистить();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросРассчитать(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		РассчитатьИРаспределить();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросПерезаполнить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПерезаполнитьПоПоступлениямНаСервере();
		ПерезаполнитьСуммыДопрасходов();
		РассчитатьИРаспределить();
		
		Для Каждого СтрокаТабличнойЧасти Из Объект.Разделы Цикл
			ОписатьОтборДокументовПоступленияИКурс(СтрокаТабличнойЧасти);	
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросУстановитьВозможностьРедактированияСписком(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        РазделыГТД = Истина;
        Возврат;
    КонецЕсли;
	
	КоличествоСтрок = ДополнительныеПараметры.КоличествоСтрок;
	
	// Удаление строк ТЧ "Разделы" в обратном порядке.
	Пока КоличествоСтрок > 1 Цикл  
        Объект.Разделы.Удалить(Объект.Разделы[КоличествоСтрок - 1]);
        КоличествоСтрок = КоличествоСтрок - 1;
	КонецЦикла;
	
	СтрокаТабличнойЧастиРазделы = Объект.Разделы[0];
	
	КоличествоТовары = Объект.Товары.Количество();
	// Удаление строк ТЧ "Товары" в обратном порядке.
	Пока КоличествоТовары > 0 Цикл
		СтрокаТабличнойЧастиТовары = Объект.Товары[КоличествоТовары - 1];
		
		Если СтрокаТабличнойЧастиРазделы.КлючСвязи <> СтрокаТабличнойЧастиТовары.КлючСвязи Тогда
			Объект.Товары.Удалить(СтрокаТабличнойЧастиТовары);
		КонецЕсли;
		КоличествоТовары = КоличествоТовары - 1;
	КонецЦикла;
	
	КоличествоОС = Объект.ОС.Количество();
	// Удаление строк ТЧ "ОС" в обратном порядке.
	Пока КоличествоОС > 0 Цикл
		СтрокаТабличнойЧастиОС = Объект.ОС[КоличествоОС - 1];
		
		Если СтрокаТабличнойЧастиРазделы.КлючСвязи <> СтрокаТабличнойЧастиОС.КлючСвязи Тогда
			Объект.ОС.Удалить(СтрокаТабличнойЧастиОС);
		КонецЕсли;
		КоличествоОС = КоличествоОС - 1;
	КонецЦикла;
	
    Элементы.Разделы.ТекущаяСтрока = СтрокаТабличнойЧастиРазделы.ПолучитьИдентификатор();
    
    УстановитьВозможностьРедактированияСпискомФрагмент();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Процедура - Установить пиктограмму комментария.
//
&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

// Процедура устанавливает видимость и доступность элементов.
//
&НаКлиенте
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	
	СопровождениеПоВесу 	= Объект.РаспределениеСопровождения = ПредопределенноеЗначение("Перечисление.СпособРаспределения.ПоВесу");
	АкцизНеНулевой 			= СтрокаТабличнойЧасти.Акциз <> 0;
	СопровождениеНеНулевое 	= Объект.Сопровождение <> 0;
	ДопрасходыНеНулевые 	= СтрокаТабличнойЧасти.ДопРасходы <> 0;
	
	Элементы.ТоварыКоличествоДопЕдиницы.Видимость = СтрокаТабличнойЧасти.РаспределениеАкциза = ПредопределенноеЗначение("Перечисление.СпособыРаспределенияДопРасходов.ПоДопЕдиницам");
		
	Элементы.ТоварыКоэффициентДопЕдиницы.Видимость = СтрокаТабличнойЧасти.РаспределениеАкциза = ПредопределенноеЗначение("Перечисление.СпособыРаспределенияДопРасходов.ПоДопЕдиницам");
														
	Элементы.ТоварыВес.Видимость 						= СопровождениеПоВесу;													
	Элементы.ТоварыАкциз.Видимость 						= АкцизНеНулевой;
	Элементы.ТоварыСопровождение.Видимость 				= СопровождениеНеНулевое;
	Элементы.ТоварыДопРасходы.Видимость 				= ДопрасходыНеНулевые;
	
	Элементы.ОСВес.Видимость 							= СопровождениеПоВесу;
	Элементы.ОСАкциз.Видимость 							= АкцизНеНулевой;	
	Элементы.ОССопровождение.Видимость	 				= СопровождениеНеНулевое;	
	Элементы.ОСДопРасходы.Видимость 					= ДопрасходыНеНулевые;
	
	Элементы.ТоварыКоличество.ТолькоПросмотр 			= НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыКоличествоДопЕдиницы.ТолькоПросмотр 	= НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыКоэффициентДопЕдиницы.ТолькоПросмотр = НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыВес.ТолькоПросмотр 					= НЕ Объект.РаспределениеВручную;	
	Элементы.ТоварыСумма.ТолькоПросмотр 				= НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыПошлина.ТолькоПросмотр 				= НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыТаможенныйСбор.ТолькоПросмотр 		= НЕ Объект.РаспределениеВручную;	
	Элементы.ТоварыАкциз.ТолькоПросмотр 				= НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыСопровождение.ТолькоПросмотр 		= НЕ Объект.РаспределениеВручную;
	Элементы.ТоварыДопРасходы.ТолькоПросмотр 			= НЕ Объект.РаспределениеВручную;
	
	Элементы.ОСВес.ТолькоПросмотр 						= НЕ Объект.РаспределениеВручную;	
	Элементы.ОССумма.ТолькоПросмотр 					= НЕ Объект.РаспределениеВручную;
	Элементы.ОСПошлина.ТолькоПросмотр	 				= НЕ Объект.РаспределениеВручную;
	Элементы.ОСТаможенныйСбор.ТолькоПросмотр 			= НЕ Объект.РаспределениеВручную;	
	Элементы.ОСАкциз.ТолькоПросмотр 					= НЕ Объект.РаспределениеВручную;
	Элементы.ОССопровождение.ТолькоПросмотр 			= НЕ Объект.РаспределениеВручную;
	Элементы.ОСДопРасходы.ТолькоПросмотр 				= НЕ Объект.РаспределениеВручную;
	
	Элементы.РазделыФактурнаяСтоимость.ТолькоПросмотр	= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыФактурнаяСтоимость1.ТолькоПросмотр	= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыДопРасходы.ТолькоПросмотр			= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыДопРасходы1.ТолькоПросмотр			= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыТаможеннаяСтоимость.ТолькоПросмотр 	= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыТаможеннаяСтоимость1.ТолькоПросмотр = НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыБазаНДС.ТолькоПросмотр 				= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыБазаНДС1.ТолькоПросмотр 			= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыНДС.ТолькоПросмотр 					= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыНДС1.ТолькоПросмотр 				= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыТаможенныйСбор.ТолькоПросмотр 		= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыТаможенныйСбор1.ТолькоПросмотр 		= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыПошлина.ТолькоПросмотр 				= НЕ Объект.ВводСуммГТДВручную;
	Элементы.РазделыПошлина1.ТолькоПросмотр 			= НЕ Объект.ВводСуммГТДВручную;
		
	Элементы.РазделыЗачетНДС.Видимость					= ДанныеУчетнойПолитики.УказыватьПризнакЗачетаНДСПриПоступлении; 
	Элементы.РазделыЗачетНДС1.Видимость					= ДанныеУчетнойПолитики.УказыватьПризнакЗачетаНДСПриПоступлении;
	
	Элементы.ТоварыСуммаБазыНДС.Видимость				= Объект.ВводСуммГТДВручную И Объект.РаспределениеВручную;
	Элементы.ОССуммаБазыНДС.Видимость					= Объект.ВводСуммГТДВручную И Объект.РаспределениеВручную;
	
	Если НЕ ВалютаОтбор.Пустая() Тогда
		Элементы.Курс.Видимость = Истина;
		
		Если Объект.Кратность > 1 Тогда
			Элементы.Кратность.Видимость = Истина;
		Иначе 
			Элементы.Кратность.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.Курс.Видимость = Ложь;
		Элементы.Кратность.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры 

// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ДоговорКонтрагента",
		ДоговорПоУмолчанию);
		
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

// Получает договор по умолчанию
//
&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ВидыДоговораДляДокумента(Документ);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура ЗаполнениеПоПоступлению(МассивДокументов, КлючСвязи)
	Для каждого ДокументПоступления Из МассивДокументов Цикл	
		
		Для каждого СтрокаПоступления Из ДокументПоступления.Товары Цикл
			СтрокаТабличнойЧасти = Объект.Товары.Добавить();                     	
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаПоступления);
			СтрокаТабличнойЧасти.СуммаНДС			 = 0;
			СтрокаТабличнойЧасти.Сумма 				 = СтрокаПоступления.Всего;
			СтрокаТабличнойЧасти.ФактурнаяСтоимость  = СтрокаПоступления.Всего * ДокументПоступления.Курс / ?(ЗначениеЗаполнено(ДокументПоступления.Кратность), ДокументПоступления.Кратность, 1);
			СтрокаТабличнойЧасти.Вес 				 = 0;
			СтрокаТабличнойЧасти.ДокументПоступления = ДокументПоступления;
			СтрокаТабличнойЧасти.КлючСвязи 			 = КлючСвязи;	
		КонецЦикла;
		
		Для каждого СтрокаПоступления Из ДокументПоступления.ОС Цикл
			СтрокаТабличнойЧасти = Объект.ОС.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаПоступления);
			СтрокаТабличнойЧасти.СуммаНДС			 = 0;
			СтрокаТабличнойЧасти.Сумма 				 = СтрокаПоступления.Всего;
			СтрокаТабличнойЧасти.ФактурнаяСтоимость  = СтрокаПоступления.Всего * ДокументПоступления.Курс / ?(ЗначениеЗаполнено(ДокументПоступления.Кратность), ДокументПоступления.Кратность, 1);			
			СтрокаТабличнойЧасти.ДокументПоступления = ДокументПоступления;
			СтрокаТабличнойЧасти.КлючСвязи 			 = КлючСвязи;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры //

&НаСервере
Функция ЗаполнитьДопрасходыИВернутьСумму(МассивДокументов, КлючСвязи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеРасходыТовары.Ссылка КАК Ссылка,
		|	ДополнительныеРасходыТовары.ДокументПоступления КАК ДокументПоступления,
		|	ДополнительныеРасходыТовары.Номенклатура КАК Номенклатура,
		|	ДополнительныеРасходыТовары.СуммаРасходов КАК СуммаРасходов,
		|	ДополнительныеРасходыТовары.Количество КАК Количество,
		|	ДополнительныеРасходыТовары.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаДанные
		|ИЗ
		|	Документ.ДополнительныеРасходы.Товары КАК ДополнительныеРасходыТовары
		|ГДЕ
		|	ДополнительныеРасходыТовары.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДополнительныеРасходыОС.Ссылка,
		|	ДополнительныеРасходыОС.ДокументПоступления,
		|	ДополнительныеРасходыОС.ОсновноеСредство,
		|	ДополнительныеРасходыОС.СуммаРасходов,
		|	ДополнительныеРасходыОС.Количество,
		|	ДополнительныеРасходыОС.Сумма
		|ИЗ
		|	Документ.ДополнительныеРасходы.ОС КАК ДополнительныеРасходыОС
		|ГДЕ
		|	ДополнительныеРасходыОС.Ссылка В(&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДанные.Ссылка КАК Ссылка,
		|	ВременнаяТаблицаДанные.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
		|	ВременнаяТаблицаДанные.ДокументПоступления КАК ДокументПоступления,
		|	ВременнаяТаблицаДанные.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаДанные.СуммаРасходов КАК СуммаРасходов,
		|	ВременнаяТаблицаДанные.Количество КАК Количество,
		|	ВременнаяТаблицаДанные.Сумма КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаДанные КАК ВременнаяТаблицаДанные
		|ИТОГИ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументПоступления),
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Номенклатура),
		|	СУММА(СуммаРасходов)
		|ПО
		|	Ссылка";
	Запрос.УстановитьПараметр("МассивСсылок", МассивДокументов);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Сумма = 0;
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Объект.ДопРасходы.Добавить();
		СтрокаТабличнойЧасти.ДокументДопРасходов = Выборка.Ссылка;
		СтрокаТабличнойЧасти.КлючСвязи 			 = КлючСвязи;
		
		КурсКратность = ПолучитьКурсИКратность(Выборка.ВалютаДокумента, ДатаДокумента);
		
		ВыборкаДетальныеЗаписи = Выборка.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Отбор = Новый Структура();
			Отбор.Вставить("Номенклатура", ВыборкаДетальныеЗаписи.Номенклатура);
			Отбор.Вставить("ДокументПоступления", ВыборкаДетальныеЗаписи.ДокументПоступления);
			// Могут быть дубли номенклатуры, пожтому добавляем поиск по количеству и сумме.
			Отбор.Вставить("Количество", ВыборкаДетальныеЗаписи.Количество);
			Отбор.Вставить("Сумма", ВыборкаДетальныеЗаписи.Сумма);
			
			МассивСтрок = Объект.Товары.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				СтрокаМассива.ДокументДопРасходов = Выборка.Ссылка;
				СтрокаМассива.ДопРасходы = Окр(ВыборкаДетальныеЗаписи.СуммаРасходов * КурсКратность.Курс / КурсКратность.Кратность, 2);	
				СтрокаТабличнойЧасти.СуммаДопРасходов = СтрокаТабличнойЧасти.СуммаДопРасходов + СтрокаМассива.ДопРасходы;
			КонецЦикла;	
			
			Отбор = Новый Структура();
			Отбор.Вставить("ОсновноеСредство", ВыборкаДетальныеЗаписи.Номенклатура);
			Отбор.Вставить("ДокументПоступления", ВыборкаДетальныеЗаписи.ДокументПоступления);
			
			МассивСтрок = Объект.ОС.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				СтрокаМассива.ДокументДопРасходов = Выборка.Ссылка;
				СтрокаМассива.ДопРасходы = Окр(ВыборкаДетальныеЗаписи.СуммаРасходов * КурсКратность.Курс / КурсКратность.Кратность, 2);	
				СтрокаТабличнойЧасти.СуммаДопРасходов = СтрокаТабличнойЧасти.СуммаДопРасходов + СтрокаМассива.ДопРасходы;		
			КонецЦикла;
		КонецЦикла;	
		
		Сумма = Сумма + СтрокаТабличнойЧасти.СуммаДопРасходов;
	КонецЦикла;

	Возврат Сумма;
КонецФункции //

&НаКлиенте
Процедура ПриИзмененииДоговора(ДатаДокумента, ДоговорКонтрагента)
	УстановитьСчетаРасчетовСКонтрагентами();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСчетаРасчетовСКонтрагентами()
	СтруктураДанные = Новый Структура("Организация, Контрагент, ДоговорКонтрагента", Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	СчетаУчета = ПолучитьСчетаРасчетовСКонтрагентами(СтруктураДанные);
	Объект.СчетРасчетовСКонтрагентом = СчетаУчета.СчетРасчетовПоставщика;		
	
КонецПроцедуры // УстановитьСчетаРасчетовСКонтрагентами()

&НаСервереБезКонтекста
Функция ПолучитьСчетаРасчетовСКонтрагентами(СтруктураДанные)
	Возврат БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(СтруктураДанные.Организация, СтруктураДанные.Контрагент, СтруктураДанные.ДоговорКонтрагента);
КонецФункции

&НаСервере
Процедура РассчитатьИРаспределить()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ПолноеРаспределениеПоДокументу();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
КонецПроцедуры // РаспределитьТабЧастьРасходыПоКоличеству()

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
        УстановитьВидимостьДоступностьЭлементов();
    КонецЕсли;
КонецПроцедуры // ЗаполнитьПоОснованию()

// Процедура - обработчик подбора товаров.
//
&НаСервере
Процедура ПолучитьТоварыИзХранилища(АдресЗапасовВХранилище, КлючСвязи)
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Для Каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		НайденныеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрокаЗагрузки.Номенклатура));
		
		Если НайденныеСтроки.Количество() > 0 Тогда 
			Продолжить;
		КонецЕсли;	
		
		СтрокаТабличнойЧасти = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаЗагрузки);
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
	КонецЦикла;
		
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

// Процедура - устанавливает возможность редактирования списком (продолжение).
//
&НаКлиенте
Процедура УстановитьВозможностьРедактированияСпискомФрагмент()
    Если РазделыГТД Тогда
        Элементы.СтраницыРазделыСтрокойСписком.ТекущаяСтраница = Элементы.Списком;
    Иначе
        Элементы.СтраницыРазделыСтрокойСписком.ТекущаяСтраница = Элементы.ОднаСтрока;
    КонецЕсли;
КонецПроцедуры // УстановитьВозможностьРедактированияСписком()

&НаКлиенте
Процедура ОписатьОтборДокументовПоступленияИКурс(СтрокаТабличнойЧасти)

	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
	
	МассивТовары = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	МассивОС 	 = Объект.ОС.НайтиСтроки(СтруктураОтбора);
	
	Если МассивТовары.Количество() > 0 Тогда
		Текст = ПолучитьТекстОписания(КонтрагентОтбор, ДатаОтбор, ВалютаОтбор, ДоговорОтбор, ИспользоватьДопЕдиницыОтбор, МассивТовары[0].ДокументПоступления);	
	ИначеЕсли МассивОС.Количество() > 0 Тогда
		Текст = ПолучитьТекстОписания(КонтрагентОтбор, ДатаОтбор, ВалютаОтбор, ДоговорОтбор, ИспользоватьДопЕдиницыОтбор, МассивОС[0].ДокументПоступления);
	Иначе
		Текст = ПолучитьТекстОписания(КонтрагентОтбор, ДатаОтбор, ВалютаОтбор, ДоговорОтбор, ИспользоватьДопЕдиницыОтбор);
	КонецЕсли;	
	
	Если НЕ ВалютаОтбор.Пустая() Тогда
		КурсКратность 	 = ПолучитьКурсИКратность(ВалютаОтбор, ДатаОтбор);
		Объект.Курс 	 = КурсКратность.Курс;
		Объект.Кратность = КурсКратность.Кратность;
		
		СтрокаТабличнойЧасти.Валюта = ВалютаОтбор;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЭлементов();
	
	Элементы.ДекорацияОписанияОтборовДокументовПоступления.Заголовок = Текст;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекстОписания(КонтрагентОтбор, ДатаОтбор, ВалютаОтбор, ДоговорОтбор, ИспользоватьДопЕдиницыОтбор, Документ = Неопределено)
	
	Если Документ = Неопределено И КонтрагентОтбор = Справочники.Контрагенты.ПустаяСсылка() Тогда
		ДоговорОтбор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ВалютаОтбор = Справочники.Валюты.ПустаяСсылка();
		Возврат "";
	Иначе	
		Если Документ = Неопределено Тогда
			ДоговорОтбор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		Иначе	
			КонтрагентОтбор = Документ.Контрагент;
			ДоговорОтбор = Документ.ДоговорКонтрагента;
			ИспользоватьДопЕдиницыОтбор = Документ.ИспользоватьДопЕдиницы;
			ВалютаОтбор = Документ.ВалютаДокумента;
			ДатаОтбор = Документ.Дата; 
		КонецЕсли;
		
		Если ДоговорОтбор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка() Тогда
			Возврат СтрШаблон(НСтр("ru = 'Контрагент: %1; Дата: %2; Валюта: %3.'"), 
						КонтрагентОтбор.НаименованиеПолное,
						Формат(ДатаОтбор, "ДЛФ=DD"),
						ВалютаОтбор);
			
		Иначе
			Возврат СтрШаблон(НСтр("ru = 'Контрагент: %1; Дата: %2; Валюта: %3; Договор: %4; %5.'"), 
						КонтрагентОтбор.НаименованиеПолное,
						Формат(ДатаОтбор, "ДЛФ=DD"),
						ВалютаОтбор,
						ДоговорОтбор.Наименование,
						?(ИспользоватьДопЕдиницыОтбор, НСтр("ru = 'С использованием допединиц'"), НСтр("ru = 'Без использования допединиц'")));
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКурсИКратность(Валюта, Дата)

	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);

КонецФункции // ПолучитьКурсИКратность()

&НаСервере
Функция ПроверитьЗаполнениеПередРасчетом()

	Отказ = Ложь;
	
	Если Объект.Сопровождение <> 0 И Объект.РаспределениеСопровождения = Перечисления.СпособРаспределения.ПустаяСсылка() Тогда
		ТекстСообщения = НСтр("ru = 'Сумма сопровождения заполнена, но не указан способ распределения! Рассчет отменен.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.РаспределениеСопровождения",,Отказ);
	КонецЕсли;
	
	Если Объект.Разделы.Количество() = 1 Тогда
		СтрокаТабличнойЧасти = Объект.Разделы[0];
		
		Если СтрокаТабличнойЧасти.Акциз <> 0  
			И СтрокаТабличнойЧасти.РаспределениеАкциза = Перечисления.СпособыРаспределенияДопРасходов.ПустаяСсылка() Тогда
				
			ТекстСообщения = НСтр("ru = 'В разделе заполнена сумма акциза, но не указан способ распределения! Рассчет отменен.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
		
		Если СтрокаТабличнойЧасти.СтавкаПошлины <> 0  
			И Объект.РаспределениеПошлины = Перечисления.СпособыРаспределенияПошлины.ПустаяСсылка() Тогда
				
			ТекстСообщения = НСтр("ru = 'В разделе заполнена ставка пошлины, но не указан способ распределения! Рассчет отменен.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.РаспределениеПошлины",,Отказ);
		КонецЕсли;
		
	Иначе	
		Для Каждого СтрокаТабличнойЧасти Из Объект.Разделы Цикл	
			Если СтрокаТабличнойЧасти.Акциз <> 0  
				И СтрокаТабличнойЧасти.РаспределениеАкциза = Перечисления.СпособыРаспределенияДопРасходов.ПустаяСсылка() Тогда
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В разделе №%1 заполнена сумма акциза, но не указан способ распределения! Рассчет отменен.'"),
											СтрокаТабличнойЧасти.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
			
			Если СтрокаТабличнойЧасти.СтавкаПошлины <> 0  
				И Объект.РаспределениеПошлины = Перечисления.СпособыРаспределенияПошлины.ПустаяСсылка() Тогда
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В разделе №%1 заполнена ставка пошлины, но не указан способ распределения! Рассчет отменен.'"),
											СтрокаТабличнойЧасти.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.РаспределениеПошлины",,Отказ);
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат Отказ;
КонецФункции // ПроверитьЗаполнениеПередРасчетом()

&НаСервере
Процедура ПерезаполнитьПоПоступлениямНаСервере()

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.ДокументПоступления КАК Документ,
		|   ТаблицаТовары.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|  	&ТаблицаТовары КАК ТаблицаТовары
		|;
		|
		|//////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОС.ДокументПоступления КАК Документ,
		|   ТаблицаОС.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|  	&ТаблицаОС КАК ТаблицаОС
		|;
		|
		|//////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.Документ КАК Документ,
		|   ВременнаяТаблицаТовары.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаДокументы
		|ИЗ
		|  	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОС.Документ КАК Документ,
		|   ВременнаяТаблицаОС.КлючСвязи КАК КлючСвязи
		|ИЗ
		|  	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|;
		|
		|//////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаПоступлений.ДоговорКонтрагента КАК Договор,
		|	ТаблицаПоступлений.ИспользоватьДопЕдиницы КАК ИспользоватьДопЕдиницы,
		|	ДЕНЬ(ТаблицаПоступлений.Дата) КАК Дата,
		|	ТаблицаПоступлений.ВалютаДокумента КАК ВалютаДокумента,
		|   ВременнаяТаблицаДокументы.КлючСвязи КАК КлючСвязи
		|ИЗ
		|  	ВременнаяТаблицаДокументы КАК ВременнаяТаблицаДокументы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ТаблицаПоступлений
		|		ПО ВременнаяТаблицаДокументы.Документ = ТаблицаПоступлений.Ссылка
		|;
		|
		|//////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаДокументы.Документ КАК Документ,
		|   ВременнаяТаблицаДокументы.КлючСвязи КАК КлючСвязи,
		|	ТаблицаПоступлений.ИспользоватьДопЕдиницы КАК ИспользоватьДопЕдиницы
		|ИЗ
		|  	ВременнаяТаблицаДокументы КАК ВременнаяТаблицаДокументы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ТаблицаПоступлений
		|		ПО ВременнаяТаблицаДокументы.Документ = ТаблицаПоступлений.Ссылка";
	Запрос.УстановитьПараметр("ТаблицаТовары", Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаОС", Объект.ОС.Выгрузить());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаОтборов = МассивРезультатов[3].Выгрузить();
	
	// Контроль
	Если ТаблицаОтборов.Количество() <> Объект.Разделы.Количество() Тогда
		
		ТекстСообщения = НСтр("ru = 'В одном из разделов есть документы поступления с разными значениями реквизита(реквизитов) учавствующего в отборе.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;	
	
	ТаблицаДокументов = МассивРезультатов[4].Выгрузить();
	
	Объект.Товары.Очистить();
	Объект.ОС.Очистить();
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючСвязи", 0);
	
	МассивДокументов = Новый Массив();
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Разделы Цикл
		
		СтруктураОтбора.КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
		
		МассивСтрок = ТаблицаДокументов.НайтиСтроки(СтруктураОтбора);
		
		МассивДокументов.Очистить();
		
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			МассивДокументов.Добавить(СтрокаМассива.Документ);
			ИспользоватьДопЕдиницы = СтрокаМассива.ИспользоватьДопЕдиницы; 
		КонецЦикла;	
		
		ЗаполнениеПоПоступлению(МассивДокументов, СтрокаТабличнойЧасти.КлючСвязи);
		
		Если ИспользоватьДопЕдиницы Тогда
			СтрокаТабличнойЧасти.РаспределениеАкциза = Перечисления.СпособыРаспределенияДопРасходов.ПоДопЕдиницам;
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьСуммыДопрасходов()

	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючСвязи", 0);
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ДопРасходы Цикл
		
		КурсКратность = ПолучитьКурсИКратность(СтрокаТабличнойЧасти.ДокументДопРасходов.ВалютаДокумента, ДатаДокумента);
		
		СтрокаТабличнойЧасти.СуммаДопРасходов = Окр(СтрокаТабличнойЧасти.ДокументДопРасходов.СуммаДопРасходов * КурсКратность.Курс / КурсКратность.Кратность, 2); 	
		
	КонецЦикла;	
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Разделы Цикл
		
		СтруктураОтбора.КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
		
		ТаблицаДокументов = Объект.ДопРасходы.Выгрузить(СтруктураОтбора, "СуммаДопРасходов"); 
		
		СтрокаТабличнойЧасти.ДопРасходы = ТаблицаДокументов.Итог("СуммаДопРасходов");
		
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
