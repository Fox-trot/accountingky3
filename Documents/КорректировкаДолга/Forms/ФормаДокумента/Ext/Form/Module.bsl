&НаКлиенте
Перем СтарыйВидОперации;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;


#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконтоДт(ЭтотОбъект));
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконтоКт(ЭтотОбъект));

	ЗаполнитьДобавленныеКолонкиТаблиц();

	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	УстановитьУсловноеОформление();

	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПодвалФормы();

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	УстановитьПараметрыВыбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)

	УстановитьВидимостьДоступностьЭлементов();
	
	Если НЕ Объект.ВидОперации = СтарыйВидОперации Тогда
		
		Объект.ДебиторскаяЗадолженность.Очистить();
		Объект.КредиторскаяЗадолженность.Очистить();
		
		Объект.СчетДт = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
		Объект.СчетКт = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
		
		Объект.СубконтоДт1 = Неопределено;
		Объект.СубконтоДт2 = Неопределено;
		Объект.СубконтоДт3 = Неопределено;
		Объект.СубконтоКт1 = Неопределено;
		Объект.СубконтоКт2 = Неопределено;
		Объект.СубконтоКт3 = Неопределено;
		
	КонецЕсли;
	
	УстановитьФлагОтметкиНезаполненногоДебитора(ЭтотОбъект);
	УстановитьФлагОтметкиНезаполненногоКредитора(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТипаАванса()

	Если Объект.ТипАвансаЗадолженности = ПредопределенноеЗначение("Перечисление.ТипыЗадолженности.СамогоКонтрагента") Тогда
		
		Если Объект.ВидАвансаЗадолженности = ПредопределенноеЗначение("Перечисление.ВидыАвансаЗадолженности.АвансыПокупателя")
			или Объект.ВидАвансаЗадолженности = ПредопределенноеЗначение("Перечисление.ВидыАвансаЗадолженности.ЗадолженностьПоставщику") Тогда
			
			Объект.КонтрагентДебитор = Объект.КонтрагентКредитор;
			
			Объект.КредиторскаяЗадолженность.Очистить();
			
		Иначе
			
			Объект.КонтрагентКредитор = Объект.КонтрагентДебитор;
			
			Объект.ДебиторскаяЗадолженность.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтарыйвидОперации = Объект.ВидОперации;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
		
		ПриИзмененииДатыНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.Номер = "";
	
	Объект.ДебиторскаяЗадолженность.Очистить();
	Объект.КредиторскаяЗадолженность.Очистить();

	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииОрганизации(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконтоДт(ЭтотОбъект));
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииОрганизации(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконтоКт(ЭтотОбъект));
		
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДокументаПриИзменении(Элемент)

	ВалютаДокументаПриИзмененииСервер();

КонецПроцедуры

&НаКлиенте
Процедура КурсДокументаПриИзменении(Элемент)

	КурсДокументаПриИзмененииСервер();

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентДебиторПриИзменении(Элемент)
	
	Если Элемент = Элементы.Дебитор Тогда

		Объект.ДебиторскаяЗадолженность.Очистить();
		Объект.КредиторскаяЗадолженность.Очистить();
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.ПереносЗадолженности")
			или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности") Тогда
			
			Объект.КредиторскаяЗадолженность.Очистить();
			Объект.ДебиторскаяЗадолженность.Очистить();
			
		Иначе
			Объект.ДебиторскаяЗадолженность.Очистить();
		КонецЕсли;
		
	Иначе
		Объект.ДебиторскаяЗадолженность.Очистить();
	КонецЕсли;
	
	УстановитьДоступностьДоговоров(ЭтотОбъект);
	
	УстановитьВалютуКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентКредиторПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.ПереносЗадолженности")
		или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности") Тогда
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.ПереносЗадолженности") Тогда
			КонтрагентКредиторПриИзмененииСервер();
		КонецЕсли;
		
	Иначе
		Объект.КредиторскаяЗадолженность.Очистить();
	КонецЕсли;
	
	УстановитьДоступностьДоговоров(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗадолженностиДляПереносаПриИзменении(Элемент)
		
	УстановитьВидимостьДоступностьЭлементов();
	
	Объект.ДебиторскаяЗадолженность.Очистить();
	Объект.КредиторскаяЗадолженность.Очистить();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

// Закладка "СчетаУчета"

&НаКлиенте
Процедура СчетДтПриИзменении(Элемент)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект,
		Объект,
		ПараметрыУстановкиСвойствСубконтоДт(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКтПриИзменении(Элемент)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект,
		Объект,
		ПараметрыУстановкиСвойствСубконтоКт(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоДт(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоДт(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоДт(2);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоДт(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоДт(3);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоДт(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоКт(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоКт(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоКт(2);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоКт(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоКт(3);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоКт(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДебиторскаяЗадолженность

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьПриИзменении(Элемент)
	
	Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
		Объект.СуммаДтЗадолженности = Объект.ДебиторскаяЗадолженность.Итог("Сумма");
	Иначе
		Объект.СуммаДтЗадолженности = Объект.ДебиторскаяЗадолженность.Итог("СуммаВзаиморасчетов");
	КонецЕсли;
	Разница = Объект.СуммаДтЗадолженности - Объект.СуммаКтЗадолженности;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьДоговорКонтрагентаПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента, КонтрагентДебитор, КонтрагентКредитор");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;
	
	ПриИзмененииДоговораВСтроке(ДанныеСтрокаТаблицы, ПараметрыОбъекта, "ДебиторскаяЗадолженность", ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагентаПриИзменении(Элемент)
	
	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента, КонтрагентДебитор, КонтрагентКредитор");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;
	
	ПриИзмененииКорДоговораВСтроке(ДанныеСтрокаТаблицы, ПараметрыОбъекта, "ДебиторскаяЗадолженность");
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьКурсВзаиморасчетовПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;

	ПересчитатьСуммуПоСтрокеНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта, ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьСуммаПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;

	ПересчитатьСуммуПоСтрокеНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта, ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьСчетУчетаРасчетовПриИзменении(Элемент)
	
	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;

	ПриИзмененииСчетаРасчетовВСтроке(ДанныеСтрокаТаблицы, ПараметрыОбъекта, "ДебиторскаяЗадолженность", ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьПослеУдаления(Элемент)
	
	УстановитьФлагОтметкиНезаполненногоСчетов(ЭтотОбъект);
	
	УстановитьФлагОтметкиНезаполненногоДебитора(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДебиторскаяЗадолженностьПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		
		УстановитьФлагОтметкиНезаполненногоСчетов(ЭтотОбъект);
		
		УстановитьФлагОтметкиНезаполненногоДебитора(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКредиторскаяЗадолженность

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьПриИзменении(Элемент)
	
	Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
		Объект.СуммаКтЗадолженности = Объект.КредиторскаяЗадолженность.Итог("Сумма");
	Иначе
		Объект.СуммаКтЗадолженности = Объект.КредиторскаяЗадолженность.Итог("СуммаВзаиморасчетов");
	КонецЕсли;
	Разница = Объект.СуммаДтЗадолженности - Объект.СуммаКтЗадолженности;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьДоговорКонтрагентаПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента, КонтрагентДебитор, КонтрагентКредитор");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;
	
	ПриИзмененииДоговораВСтроке(ДанныеСтрокаТаблицы, ПараметрыОбъекта, "КредиторскаяЗадолженность", ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьКурсВзаиморасчетовПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;

	ПересчитатьСуммуПоСтрокеНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта, ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагентаПриИзменении(Элемент)
	
	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента, КонтрагентДебитор, КонтрагентКредитор");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;
	
	ПриИзмененииКорДоговораВСтроке(ДанныеСтрокаТаблицы, ПараметрыОбъекта, "КредиторскаяЗадолженность");
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьСуммаПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;

	ПересчитатьСуммуПоСтрокеНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта, ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьСчетУчетаРасчетовПриИзменении(Элемент)
	
	ДанныеСтрокаТаблицы = ПолучитьСтруктуруРеквизитовСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элемент.Родитель.Родитель.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка,  
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Ссылка = Объект.Ссылка;

	ПриИзмененииСчетаРасчетовВСтроке(ДанныеСтрокаТаблицы, ПараметрыОбъекта, "КредиторскаяЗадолженность", ВалютаРегламентированногоУчета);
	
	ЗаполнитьЗначенияСвойств(Элемент.Родитель.Родитель.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		
		УстановитьФлагОтметкиНезаполненногоСчетов(ЭтотОбъект);
		
		УстановитьФлагОтметкиНезаполненногоКредитора(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКредиторскаяЗадолженностьПослеУдаления(Элемент)
	
	УстановитьФлагОтметкиНезаполненногоСчетов(ЭтотОбъект);
	
	УстановитьФлагОтметкиНезаполненногоКредитора(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьВзаиморасчеты(Команда)

	ВыполнитьКомандуЗаполнения(Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДебиторку(Команда)

	ВыполнитьКомандуЗаполнения(Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКредиторку(Команда)

	ВыполнитьКомандуЗаполнения(Команда.Имя);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности
		или Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности Тогда
		
		Если Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ПустаяСсылка() Тогда
			Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПокупателя;
		КонецЕсли;
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности Тогда
			Элементы.СчетаУчета.Видимость = Ложь;
		Иначе
			
			Элементы.СчетаУчета.Заголовок = "Счет списания";
			Элементы.СчетаУчета.Видимость = Истина;
			
		КонецЕсли;
		
		Элементы.ВидЗадолженностиДляПереноса.Заголовок = ?(Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности, "Перенести", "Списать");
		
		СписокВыбораПереключателя = Элементы.ВидЗадолженностиДляПереноса.СписокВыбора;
		
		Если СписокВыбораПереключателя.Количество() < 4 Тогда
			
			СписокВыбораПереключателя.Очистить();
			
			СписокВыбораПереключателя.Добавить(Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПокупателя, "Задолженность покупателя");
			СписокВыбораПереключателя.Добавить(Перечисления.ВидыАвансаЗадолженности.АвансыПокупателя,        "Авансы покупателя");
			СписокВыбораПереключателя.Добавить(Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПоставщику, "Задолженность поставщику");
			СписокВыбораПереключателя.Добавить(Перечисления.ВидыАвансаЗадолженности.АвансыПоставщику,        "Авансы поставщику");
			
			Если Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ПустаяСсылка() Тогда
				Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПокупателя;
			КонецЕсли;
			
		КонецЕсли;
		
		Элементы.ДебиторскаяЗадолженностьВспомогательная.Видимость = Ложь;
		
		Элементы.ДебиторВспомогательный.Видимость = Ложь;
		
		Если Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПокупателя 
			или Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПоставщику Тогда
			
			Элементы.Дебитор.Заголовок  = "Контрагент";
			Элементы.Кредитор.Заголовок = "Новый контрагент";
			
			Элементы.ДебиторскаяЗадолженность.Заголовок  = "Дебиторская задолженность";
			
			Если Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности Тогда
				
				Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.Видимость = Ложь;
				Элементы.ТаблицаДебиторскаяЗадолженностьКорСчетУчетаРасчетов.Видимость  = Ложь;
				
				Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.АвтоОтметкаНезаполненного = Ложь;
				Элементы.ТаблицаДебиторскаяЗадолженностьКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного  = Ложь;
				
			Иначе
				
				Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.Видимость = Истина;
				Элементы.ТаблицаДебиторскаяЗадолженностьКорСчетУчетаРасчетов.Видимость  = Истина;
				
				Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.АвтоОтметкаНезаполненного = Истина;
				Элементы.ТаблицаДебиторскаяЗадолженностьКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного  = Истина;
				
			КонецЕсли;
			
		ИначеЕсли Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПокупателя 
			или Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПоставщику Тогда
			
			Элементы.Дебитор.Заголовок  = "Контрагент";
			Элементы.Кредитор.Заголовок = "Новый контрагент";
			
			Элементы.КредиторскаяЗадолженность.Заголовок = "Кредиторская задолженность";
			
			Если Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности Тогда
				
				Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.Видимость = Ложь;
				Элементы.ТаблицаКредиторскаяЗадолженностьКорСчетУчетаРасчетов.Видимость  = Ложь;
				
				Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.АвтоОтметкаНезаполненного = Ложь;
				Элементы.ТаблицаКредиторскаяЗадолженностьКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного  = Ложь;
				
			Иначе
				
				Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.Видимость = Истина;
				Элементы.ТаблицаКредиторскаяЗадолженностьКорСчетУчетаРасчетов.Видимость  = Истина;
				
				Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.АвтоОтметкаНезаполненного = Истина;
				Элементы.ТаблицаКредиторскаяЗадолженностьКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного  = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Элементы.Дебитор.Видимость                = Истина;
		Элементы.Кредитор.Видимость               = ?(Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности, Истина, Ложь);
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		
		Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.Видимость               = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.Видимость                = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяКорДоговорКонтрагента.Видимость = Ложь;
		
		Элементы.ТаблицаКредиторскаяЗадолженностьКорСчетУчетаРасчетов.Видимость               = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьКорСчетУчетаРасчетов.Видимость                = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяКорСчетУчетаРасчетов.Видимость = Ложь;
		
		Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.АвтоОтметкаНезаполненного               = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.АвтоОтметкаНезаполненного                = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяКорДоговорКонтрагента.АвтоОтметкаНезаполненного = Ложь;
		
		Элементы.ТаблицаКредиторскаяЗадолженностьКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного               = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного                = Ложь;
		Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяКорСчетУчетаРасчетов.АвтоОтметкаНезаполненного = Ложь;
		
		Элементы.ВидЗадолженностиДляПереноса.Видимость = Ложь;
		
		Элементы.Дебитор.Заголовок  = "Дебитор";
		Элементы.Кредитор.Заголовок = "Кредитор";
		
		Элементы.Дебитор.Видимость                = Истина;
		Элементы.Кредитор.Видимость               = Истина;
		Элементы.ДебиторВспомогательный.Видимость = Ложь;
		
		Элементы.ДебиторскаяЗадолженность.Заголовок  = "Дебиторская задолженность";
		Элементы.КредиторскаяЗадолженность.Заголовок = "Кредиторская задолженность";
		
		Элементы.ДебиторскаяЗадолженность.Видимость  = Истина;
		Элементы.КредиторскаяЗадолженность.Видимость = Истина;
		Элементы.ДебиторскаяЗадолженностьВспомогательная.Видимость = Ложь;
		
		Элементы.СчетаУчета.Заголовок = "Счета учета";
		Элементы.СчетаУчета.Видимость = Истина;
		
		Элементы.ГруппаСчетДт.Видимость = Истина;
		Элементы.ГруппаСчетКт.Видимость = Истина;
		
	КонецЕсли;
	
	УстановитьФлагОтметкиНезаполненногоСчетов(ЭтотОбъект);
	УстановитьФлагОтметкиНезаполненногоДебитора(ЭтотОбъект);
	УстановитьФлагОтметкиНезаполненногоКредитора(ЭтотОбъект);
	
	УстановитьПараметрыВыбора();
	УстановитьДоступностьДоговоров(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// СчетДт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетДт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтметкаНезаполненногоСчетДт", ВидСравненияКомпоновкиДанных.НеРавно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// СчетКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетКт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтметкаНезаполненногоСчетКт", ВидСравненияКомпоновкиДанных.НеРавно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// Только для валютной дебиторской задолженности

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаДебиторскаяЗадолженностьКурсВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаДебиторскаяЗадолженностьКратностьВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаДебиторскаяЗадолженностьВспомогательнаяКурсВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаДебиторскаяЗадолженностьВспомогательнаяКратностьВзаиморасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ДебиторскаяЗадолженность.КурсВзаиморасчетовДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветРамки);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// Только для валютной кредиторской задолженности

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаКредиторскаяЗадолженностьКурсВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаКредиторскаяЗадолженностьКратностьВзаиморасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КредиторскаяЗадолженность.КурсВзаиморасчетовДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветРамки);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	// Разница

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Разница");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Разница", ВидСравненияКомпоновкиДанных.Заполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ВидОперации", ВидСравненияКомпоновкиДанных.ВСписке);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);


	// Дебитор, ДебиторВспомогательный

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Дебитор");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДебиторВспомогательный");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтметкаНезаполненногоДебитор", ВидСравненияКомпоновкиДанных.НеРавно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// Кредитор

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Кредитор");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтметкаНезаполненногоКредитор", ВидСравненияКомпоновкиДанных.НеРавно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбора()
	
	МассивПараметровДебиторскойЗадолженности  = Новый Массив;
	МассивПараметровКредиторскойЗадолженности = Новый Массив;
	
	Если Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета Тогда
		МассивПараметровДебиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ВалютаРасчетов", Объект.ВалютаДокумента));
		МассивПараметровКредиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ВалютаРасчетов", Объект.ВалютаДокумента));
	Иначе
		МассивПараметровДебиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ОплатаВВалюте", Ложь));
		МассивПараметровКредиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ОплатаВВалюте", Ложь));
	КонецЕсли;
	
	МассивВидовДоговоровДебиторскойЗадолженности  = Новый Массив;
	МассивВидовДоговоровКредиторскойЗадолженности = Новый Массив;
	
	Если Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПокупателя Тогда
		
		МассивВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		
		МассивПараметровКредиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", Новый ФиксированныйМассив(МассивВидовДоговоровКредиторскойЗадолженности)));
		
	ИначеЕсли Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПокупателя Тогда
		
		МассивВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		
		МассивПараметровДебиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", Новый ФиксированныйМассив(МассивВидовДоговоровДебиторскойЗадолженности)));
		
	ИначеЕсли Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПоставщику Тогда
		
		МассивВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		
		МассивПараметровДебиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", Новый ФиксированныйМассив(МассивВидовДоговоровДебиторскойЗадолженности)));
		
	ИначеЕсли Объект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПоставщику Тогда
		
		МассивВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		
		МассивПараметровКредиторскойЗадолженности.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", Новый ФиксированныйМассив(МассивВидовДоговоровКредиторскойЗадолженности)));
		
	КонецЕсли;
	
	НовыеПараметрыВыбораДебиторскойЗадолженности  = Новый ФиксированныйМассив(МассивПараметровДебиторскойЗадолженности);
	НовыеПараметрыВыбораКредиторскойЗадолженности = Новый ФиксированныйМассив(МассивПараметровКредиторскойЗадолженности);
	
	Элементы.ТаблицаДебиторскаяЗадолженностьДоговорКонтрагента.ПараметрыВыбора                = НовыеПараметрыВыбораДебиторскойЗадолженности;
	Элементы.ТаблицаКредиторскаяЗадолженностьДоговорКонтрагента.ПараметрыВыбора               = НовыеПараметрыВыбораКредиторскойЗадолженности;
	Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяДоговорКонтрагента.ПараметрыВыбора = НовыеПараметрыВыбораДебиторскойЗадолженности;
	
	Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.ПараметрыВыбора                = НовыеПараметрыВыбораДебиторскойЗадолженности;
	Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.ПараметрыВыбора               = НовыеПараметрыВыбораКредиторскойЗадолженности;
	Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяКорДоговорКонтрагента.ПараметрыВыбора = НовыеПараметрыВыбораКредиторскойЗадолженности;
	
	СвязиДоговораКредиторки = Новый Массив;
	СвязиДоговораКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	
	СвязиКорДоговораКредиторки = Новый Массив;
	СвязиКорДоговораКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	СвязиКорДоговораКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.ВалютаРасчетов", "Элементы.ТаблицаКредиторскаяЗадолженность.ТекущиеДанные.ВалютаДоговораКонтрагента"));

	СвязиДоговораДебиторки = Новый Массив;
	СвязиДоговораДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));

	СвязиКорДоговораДебиторки = Новый Массив;
	СвязиКорДоговораДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	СвязиКорДоговораДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.ВалютаРасчетов", "Элементы.ТаблицаДебиторскаяЗадолженность.ТекущиеДанные.ВалютаДоговораКонтрагента"));

	СвязиДокументаКредиторки = Новый Массив;
	СвязиДокументаКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	
	СвязиДокументаДебиторки = Новый Массив;
	СвязиДокументаДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности Тогда

		СвязиДоговораКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.КонтрагентКредитор"));
		
		СвязиДоговораДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.КонтрагентДебитор"));
		СвязиДокументаДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.КонтрагентДебитор"));
		
		СвязиКорДоговораКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.КонтрагентКредитор"));
		СвязиКорДоговораДебиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.КонтрагентКредитор"));
		
		Элементы.ТаблицаКредиторскаяЗадолженностьДоговорКонтрагента.СвязиПараметровВыбора    = Новый ФиксированныйМассив(СвязиДоговораДебиторки);
		Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиКорДоговораКредиторки);
		Элементы.ТаблицаДебиторскаяЗадолженностьДоговорКонтрагента.СвязиПараметровВыбора    = Новый ФиксированныйМассив(СвязиДоговораДебиторки);
		Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиКорДоговораДебиторки);

	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности Тогда

		СвязиДоговораКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.КонтрагентДебитор"));
		СвязиДокументаКредиторки.Добавить(Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.КонтрагентДебитор"));
		
		Элементы.ТаблицаКредиторскаяЗадолженностьДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиДоговораКредиторки);
		Элементы.ТаблицаДебиторскаяЗадолженностьДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиДоговораКредиторки);

	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьДоговоров(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	// Доступность по умолчанию
	Элементы.ТаблицаКредиторскаяЗадолженностьДоговорКонтрагента.Доступность               = Истина;
	Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.Доступность            = Истина;
	Элементы.ТаблицаДебиторскаяЗадолженностьДоговорКонтрагента.Доступность                = Истина;
	Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.Доступность             = Истина;
	Элементы.ТаблицаДебиторскаяЗадолженностьВспомогательнаяДоговорКонтрагента.Доступность = Истина;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.ПереносЗадолженности") Тогда

		Элементы.ТаблицаКредиторскаяЗадолженностьДоговорКонтрагента.Доступность    = ЗначениеЗаполнено(Объект.КонтрагентДебитор);
		Элементы.ТаблицаКредиторскаяЗадолженностьКорДоговорКонтрагента.Доступность = ЗначениеЗаполнено(Объект.КонтрагентКредитор);
		Элементы.ТаблицаДебиторскаяЗадолженностьДоговорКонтрагента.Доступность     = ЗначениеЗаполнено(Объект.КонтрагентДебитор);
		Элементы.ТаблицаДебиторскаяЗадолженностьКорДоговорКонтрагента.Доступность  = ЗначениеЗаполнено(Объект.КонтрагентКредитор);

	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности") Тогда

		Элементы.ТаблицаКредиторскаяЗадолженностьДоговорКонтрагента.Доступность = ЗначениеЗаполнено(Объект.КонтрагентДебитор);
		Элементы.ТаблицаДебиторскаяЗадолженностьДоговорКонтрагента.Доступность  = ЗначениеЗаполнено(Объект.КонтрагентДебитор);
		
	КонецЕсли;
	
	

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФлагОтметкиНезаполненногоСчетов(Форма)
	
	// Проверяем реквизиты шапки:
	Если Форма.Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности") Тогда
		
		Если Форма.Объект.ВидАвансаЗадолженности = ПредопределенноеЗначение("Перечисление.ВидыАвансаЗадолженности.АвансыПокупателя")
			или Форма.Объект.ВидАвансаЗадолженности = ПредопределенноеЗначение("Перечисление.ВидыАвансаЗадолженности.ЗадолженностьПоставщику") Тогда
			Форма.ОтметкаНезаполненногоСчетДт = Ложь;
			Форма.ОтметкаНезаполненногоСчетКт = Истина;
		Иначе
			Форма.ОтметкаНезаполненногоСчетДт = Истина;
			Форма.ОтметкаНезаполненногоСчетКт = Ложь;
		КонецЕсли;
		
	Иначе
		
		Форма.ОтметкаНезаполненногоСчетДт = Ложь;
		Форма.ОтметкаНезаполненногоСчетКт = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВалютуКонтрагента()

	Объект.ВалютаДокумента = Объект.КонтрагентДебитор.ОсновнойДоговорКонтрагента.ВалютаРасчетов;

КонецПроцедуры // ПолучитьВалютуКонтрагента()

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФлагОтметкиНезаполненногоДебитора(Форма)
	
	Форма.ОтметкаНезаполненногоДебитор = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФлагОтметкиНезаполненногоКредитора(Форма)
	
	Форма.ОтметкаНезаполненногоКредитор = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ОбщегоНазначенияБПВызовСервера.ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			РезультатВыполнения = ЗагрузитьПодготовленныеДанные();
			ПоказатьРезультатВыполненияЗадания(РезультатВыполнения);
			
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруРеквизитовСтроки()

	Возврат Новый Структура("ДоговорКонтрагента, КорДоговорКонтрагента, Сумма, СуммаВзаиморасчетов, 
		|КурсВзаиморасчетов, КратностьВзаиморасчетов, СчетУчетаРасчетов, КорСчетУчетаРасчетов,
		|ЭтоАванс, КурсВзаиморасчетовДоступность, ВалютаДоговораКонтрагента"); 

КонецФункции

// Заполнение табличных частей

&НаКлиенте
Процедура ВыполнитьКомандуЗаполнения(ИмяКнопки)

	Если Объект.Проведен Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Заполнение возможно только в непроведенном документе'"));
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	СтрокаСообщения = НСтр("ru = 'Заполнение невозможно!'") ;
	Продолжать = Истина;
	
	Если НЕ ЗначениеЗаполнено(Объект.КонтрагентДебитор) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
		НСтр("ru = 'Контрагент'"),,, СтрокаСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка,
		"КонтрагентДебитор", "Объект");
		Продолжать = Ложь;
		
	КонецЕсли;
	
	Если НЕ Продолжать Тогда
		Возврат;
	КонецЕсли;

	ТекстВопроса = "";

	Если ИмяКнопки = "ЗаполнитьВзаиморасчеты" И (Объект.ДебиторскаяЗадолженность.Количество() > 0 ИЛИ Объект.КредиторскаяЗадолженность.Количество() > 0) Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличные части будут очищены. Заполнить?'");
	ИначеЕсли ИмяКнопки = "ЗаполнитьДебиторку" И Объект.ДебиторскаяЗадолженность.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением дебиторская задолженность будет очищена. Заполнить?'");
	ИначеЕсли ИмяКнопки = "ЗаполнитьКредиторку" И Объект.КредиторскаяЗадолженность.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением кредиторская задолженность будет очищена. Заполнить?'");
	КонецЕсли;

	Если ТекстВопроса <> "" Тогда
		ДополнительныеПараметры = Новый Структура("ИмяКнопки", ИмяКнопки);
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗаполнениемТабличнойЧастиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да );
	Иначе
		ЗаполнитьТаблицыРасчетовНаКлиенте(ИмяКнопки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаполнениемТабличнойЧастиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьТаблицыРасчетовНаКлиенте(ДополнительныеПараметры.ИмяКнопки)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицыРасчетовНаКлиенте(ИмяКомандыЗаполнения)
	
	Результат = ЗаполнитьТаблицыРасчетовНаСервере(ИмяКомандыЗаполнения);
	
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
		
	Иначе
		
		ПоказатьРезультатВыполненияЗадания(Результат);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьТаблицыРасчетовНаСервере(ИмяКомандыЗаполнения)

	Если ЭтоАдресВременногоХранилища(АдресХранилищаСОшибками) Тогда
		УдалитьИзВременногоХранилища(АдресХранилищаСОшибками);
		АдресХранилищаСОшибками = "";
	КонецЕсли;
		
	ПараметрыЗаполнения = СформироватьПараметрыЗаполнения(ИмяКомандыЗаполнения);
	
	АдресХранилищаСОшибками = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ПараметрыЗаполнения.Вставить("АдресХранилищаСОшибками", АдресХранилищаСОшибками);
	
	Для каждого ЗаполняемаяТаблица Из ПараметрыЗаполнения.ЗаполняемыеТаблицы Цикл
		Если ЗаполняемаяТаблица.ВидЗадолженности = Перечисления.ВидыЗадолженности.Дебиторская Тогда
			Объект.ДебиторскаяЗадолженность.Очистить();
			Объект.СуммаДтЗадолженности = 0;
		Иначе
			Объект.КредиторскаяЗадолженность.Очистить();
			Объект.СуммаКтЗадолженности = 0;
		КонецЕсли;
	КонецЦикла;
	Разница = Объект.СуммаДтЗадолженности - Объект.СуммаКтЗадолженности;
		
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор, 
		"Документы.КорректировкаДолга.ЗаполнитьОстаткамиВзаиморасчетов", 
		ПараметрыЗаполнения, 
		НСтр("ru = 'Заполнить корректировку долга остатками по взаиморасчетам'"));
		
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	АдресХранилища       = Результат.АдресХранилища;
	
	Если Результат.ЗаданиеВыполнено Тогда
		Результат = ЗагрузитьПодготовленныеДанные();
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
Функция СформироватьПараметрыЗаполнения(ИмяКомандыЗаполнения)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Дата",               Объект.Дата);
	ПараметрыЗаполнения.Вставить("Ссылка",             Объект.Ссылка);
	ПараметрыЗаполнения.Вставить("ЭтоНовый",           НЕ ЗначениеЗаполнено(Объект.Ссылка));
	ПараметрыЗаполнения.Вставить("Организация",        Объект.Организация);
	ПараметрыЗаполнения.Вставить("ВалютаДокумента",    Объект.ВалютаДокумента);
	ПараметрыЗаполнения.Вставить("КурсДокумента",      Объект.КурсДокумента);
	ПараметрыЗаполнения.Вставить("КратностьДокумента", Объект.КратностьДокумента);
	ПараметрыЗаполнения.Вставить("ВидОперации",        Объект.ВидОперации);
	
	СписокВидовДоговоровДебиторскойЗадолженности = Новый Массив;
	
	СписокВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	СписокВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	
	СписокВидовДоговоровКредиторскойЗадолженности = Новый Массив;
	
	СписокВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	СписокВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		
	ЗаполняемыеТаблицы = Новый ТаблицаЗначений;
	ЗаполняемыеТаблицы.Колонки.Добавить("ПоКонтрагенту",        Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ЗаполняемыеТаблицы.Колонки.Добавить("ВидЗадолженности",     Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЗадолженности"));
	ЗаполняемыеТаблицы.Колонки.Добавить("МассивВидовДоговоров", Новый ОписаниеТипов("Массив"));
	
	Если ИмяКомандыЗаполнения = "ЗаполнитьВзаиморасчеты"
		Или ИмяКомандыЗаполнения = "ЗаполнитьКредиторку" Тогда
		
		ЗаполняемаяТаблица = ЗаполняемыеТаблицы.Добавить();
		ЗаполняемаяТаблица.ПоКонтрагенту = Объект.КонтрагентДебитор;
		
		ЗаполняемаяТаблица.ВидЗадолженности     = Перечисления.ВидыЗадолженности.Кредиторская;
		ЗаполняемаяТаблица.МассивВидовДоговоров = СписокВидовДоговоровКредиторскойЗадолженности;
		
	КонецЕсли;
	
	Если ИмяКомандыЗаполнения = "ЗаполнитьВзаиморасчеты"
		Или ИмяКомандыЗаполнения = "ЗаполнитьДебиторку" Тогда
		
		ЗаполняемаяТаблица = ЗаполняемыеТаблицы.Добавить();
		ЗаполняемаяТаблица.ПоКонтрагенту        = Объект.КонтрагентДебитор;
		ЗаполняемаяТаблица.ВидЗадолженности     = Перечисления.ВидыЗадолженности.Дебиторская;
		ЗаполняемаяТаблица.МассивВидовДоговоров = СписокВидовДоговоровДебиторскойЗадолженности;
		
	КонецЕсли;
		
	
	ПараметрыЗаполнения.Вставить("ЗаполняемыеТаблицы", ЗаполняемыеТаблицы);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

&НаСервере
Функция ЗагрузитьПодготовленныеДанные()

	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("ЗаданиеВыполнено",   Истина);
	РезультатВыполнения.Вставить("Успешно",            Ложь);
	РезультатВыполнения.Вставить("ЗаполненныеТаблицы", Новый Массив);
	
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура")
		Или Не СтруктураДанных.Успешно Тогда
		Возврат РезультатВыполнения;
	КонецЕсли;
	
	РезультатВыполнения.Успешно = Истина;
	
	Для каждого ЗаполняемаяТаблица Из СтруктураДанных.ЗаполненныеТаблицы Цикл
		
		Если ЗаполняемаяТаблица.Ключ = Перечисления.ВидыЗадолженности.Дебиторская Тогда
			Объект.ДебиторскаяЗадолженность.Загрузить(ЗаполняемаяТаблица.Значение);
		Иначе
			Объект.КредиторскаяЗадолженность.Загрузить(ЗаполняемаяТаблица.Значение);
		КонецЕсли;
		РезультатВыполнения.ЗаполненныеТаблицы.Добавить(ЗаполняемаяТаблица.Ключ);
		
	КонецЦикла;
		
	Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
		Объект.СуммаДтЗадолженности = Объект.ДебиторскаяЗадолженность.Итог("Сумма");
		Объект.СуммаКтЗадолженности = Объект.КредиторскаяЗадолженность.Итог("Сумма");
	Иначе
		Объект.СуммаДтЗадолженности = Объект.ДебиторскаяЗадолженность.Итог("СуммаВзаиморасчетов");
		Объект.СуммаКтЗадолженности = Объект.КредиторскаяЗадолженность.Итог("СуммаВзаиморасчетов");
	КонецЕсли;
	Разница = Объект.СуммаДтЗадолженности - Объект.СуммаКтЗадолженности;
		
	ЗаполнитьДобавленныеКолонкиТаблиц();

	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьРезультатВыполненияЗадания(РезультатВыполнения)

	//Если НЕ РезультатВыполнения.Успешно Тогда
	//	ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(ЭтотОбъект, АдресХранилищаСОшибками);
	//КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()

	ТаблицаДоговоров = Объект.ДебиторскаяЗадолженность.Выгрузить(, "ДоговорКонтрагента");
	Для Каждого СтрокаКредиторки Из Объект.КредиторскаяЗадолженность Цикл
		СтрокаДоговора = ТаблицаДоговоров.Добавить();
		СтрокаДоговора.ДоговорКонтрагента = СтрокаКредиторки.ДоговорКонтрагента;
	КонецЦикла; 
	ТаблицаДоговоров.Свернуть("ДоговорКонтрагента");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДоговоров", ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорКонтрагента"));
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента,
		|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
		|	ДоговорыКонтрагентов.ВалютаРасчетов КАК ВалютаРасчетов
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка В(&СписокДоговоров)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДоговорКонтрагента";
	РеквизитыДоговоров = Запрос.Выполнить().Выгрузить();
	РеквизитыДоговоров.Индексы.Добавить("ДоговорКонтрагента");
	
	Для Каждого ДанныеСтроки Из Объект.ДебиторскаяЗадолженность Цикл
		СтрокаДоговора = РеквизитыДоговоров.Найти(ДанныеСтроки.ДоговорКонтрагента, "ДоговорКонтрагента");
		Если СтрокаДоговора <> Неопределено Тогда
			УстановитьПризнакАвансаВСтроке("ДебиторскаяЗадолженность", ДанныеСтроки, СтрокаДоговора);
			Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
				
				Если НЕ ДанныеСтроки.ЭтоАванс Тогда
					ДанныеСтроки.КурсВзаиморасчетовДоступность = СтрокаДоговора.ВалютаРасчетов <> Объект.ВалютаДокумента;
				Иначе
					ДанныеСтроки.КурсВзаиморасчетовДоступность = Ложь;
				КонецЕсли;
				
			Иначе
				ДанныеСтроки.КурсВзаиморасчетовДоступность = НЕ ДанныеСтроки.ЭтоАванс;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	Для Каждого ДанныеСтроки Из Объект.КредиторскаяЗадолженность Цикл
		СтрокаДоговора = РеквизитыДоговоров.Найти(ДанныеСтроки.ДоговорКонтрагента, "ДоговорКонтрагента");
		Если СтрокаДоговора <> Неопределено Тогда
			УстановитьПризнакАвансаВСтроке("КредиторскаяЗадолженность", ДанныеСтроки, СтрокаДоговора);
			Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
				
				Если НЕ ДанныеСтроки.ЭтоАванс Тогда
					ДанныеСтроки.КурсВзаиморасчетовДоступность = СтрокаДоговора.ВалютаРасчетов <> Объект.ВалютаДокумента;
				Иначе
					ДанныеСтроки.КурсВзаиморасчетовДоступность = Ложь;
				КонецЕсли;
				
			Иначе
				ДанныеСтроки.КурсВзаиморасчетовДоступность = НЕ ДанныеСтроки.ЭтоАванс;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПризнакАвансаВСтроке(Знач ИмяТабЧасти, ДанныеСтроки, Знач РеквизитыДоговора)

	Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
		ДанныеСтроки.ЭтоАванс = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком
				И (БухгалтерскийУчетПовтИсп.СчетВИерархии(ДанныеСтроки.СчетУчетаРасчетов, ПланыСчетов.Хозрасчетный.АвансыПолученные));
	ИначеЕсли ИмяТабЧасти = "КредиторскаяЗадолженность" Тогда
		ДанныеСтроки.ЭтоАванс = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем
				И (БухгалтерскийУчетПовтИсп.СчетВИерархии(ДанныеСтроки.СчетУчетаРасчетов, ПланыСчетов.Хозрасчетный.АвансыВыданные));
	КонецЕсли; 

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьФлагиУсловногоОформления(Форма)

	Форма.КурсВалютыВШапке = Форма.Объект.ВалютаДокумента <> Форма.ВалютаРегламентированногоУчета 
		И ЗначениеЗаполнено(Форма.Объект.ВалютаДокумента);

КонецПроцедуры

// Изменение связанных реквизитов

&НаСервере 
Процедура ПриИзмененииДатыНаСервере()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	// Изменим курс в шапке документа
	Если Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета Тогда
		СтруктураКурсаДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
		Объект.КурсДокумента      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьДокумента = СтруктураКурсаДокумента.Кратность;
	КонецЕсли;
	
	// Изменим курс в табличных частях
	Если Объект.ДебиторскаяЗадолженность.Количество() > 0 ИЛИ Объект.КредиторскаяЗадолженность.Количество() > 0 Тогда
		
		НовыеКурсы = ПолучитьНовыеКурсыВалют();
		ИзменитьКурсВТабличнойЧасти(Объект.ДебиторскаяЗадолженность, НовыеКурсы);
		ИзменитьКурсВТабличнойЧасти(Объект.КредиторскаяЗадолженность, НовыеКурсы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Функция ПолучитьНовыеКурсыВалют()

	Если Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета Тогда
		Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРегламентированногоУчета);
	ТаблицаДоговоров = Объект.ДебиторскаяЗадолженность.Выгрузить(, "ДоговорКонтрагента");
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(Объект.КредиторскаяЗадолженность.Выгрузить(, "ДоговорКонтрагента"), ТаблицаДоговоров);
	Запрос.УстановитьПараметр("СписокДоговоров", ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорКонтрагента"));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Договоры.Ссылка КАК ДоговорКонтрагента,
		|	Договоры.ВалютаРасчетов КАК ВалютаРасчетов
		|ПОМЕСТИТЬ ДоговорыИВалюты
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК Договоры
		|ГДЕ
		|	Договоры.ВалютаРасчетов <> &ВалютаРеглУчета
		|	И Договоры.Ссылка В(&СписокДоговоров)
		|	И Договоры.Организация = &Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Договоры.ВалютаРасчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
		|	КурсыВалютСрезПоследних.Курс КАК Курс,
		|	КурсыВалютСрезПоследних.Кратность КАК Кратность
		|ПОМЕСТИТЬ НовыйКурсы
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			&Дата,
		|			Валюта В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ДоговорыИВалюты.ВалютаРасчетов
		|				ИЗ
		|					ДоговорыИВалюты КАК ДоговорыИВалюты)) КАК КурсыВалютСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыИВалюты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ДоговорыИВалюты.ВалютаРасчетов КАК ВалютаРасчетов,
		|	НовыйКурсы.Курс КАК Курс,
		|	НовыйКурсы.Кратность КАК Кратность
		|ИЗ
		|	ДоговорыИВалюты КАК ДоговорыИВалюты
		|		ЛЕВОЕ СОЕДИНЕНИЕ НовыйКурсы КАК НовыйКурсы
		|		ПО ДоговорыИВалюты.ВалютаРасчетов = НовыйКурсы.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДоговорКонтрагента";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

&НаСервере
Процедура ВалютаДокументаПриИзмененииСервер(ПараметрыОбработки = Неопределено)

	Если ПараметрыОбработки = Неопределено Тогда
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("ВалютаРасчетовПередИзменением", Неопределено);
		ПараметрыОбработки.Вставить("КурсДоИзменения", Объект.КурсДокумента);
	КонецЕсли; 
	
	Если ПараметрыОбработки.ВалютаРасчетовПередИзменением <> Объект.ВалютаДокумента Тогда
		
		Объект.ДебиторскаяЗадолженность.Очистить();
		Объект.КредиторскаяЗадолженность.Очистить();
		
		СтруктураКурсаДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
		Объект.КурсДокумента      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьДокумента = СтруктураКурсаДокумента.Кратность;
		
	КонецЕсли; 
	
	ЕстьСтроки = Объект.ДебиторскаяЗадолженность.Количество() + Объект.КредиторскаяЗадолженность.Количество() > 0;
	Если ПараметрыОбработки.КурсДоИзменения <> Объект.КурсДокумента И ЕстьСтроки Тогда
		КурсДокументаПриИзмененииСервер();
	КонецЕсли;
	
	ОбновитьФлагиУсловногоОформления(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	УстановитьВидимостьДоступностьЭлементов();

КонецПроцедуры

&НаСервере
Процедура КурсДокументаПриИзмененииСервер()

	КурсИКратность = Новый Структура("Курс, Кратность", Объект.КурсДокумента, Объект.КратностьДокумента);
	ИзменитьКурсВТабличнойЧасти(Объект.ДебиторскаяЗадолженность,  КурсИКратность);
	ИзменитьКурсВТабличнойЧасти(Объект.КредиторскаяЗадолженность, КурсИКратность);

КонецПроцедуры

&НаСервере
Процедура КонтрагентКредиторПриИзмененииСервер()
	
	КоличествоСтрокКоллекции = Объект.ДебиторскаяЗадолженность.Количество();
	
	Для Сч = 0 по (КоличествоСтрокКоллекции - 1) Цикл
		Объект.ДебиторскаяЗадолженность.Получить(Сч).КорДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЦикла;
	
	КоличествоСтрокКоллекции = Объект.КредиторскаяЗадолженность.Количество();
	
	Для Сч = 0 по (КоличествоСтрокКоллекции - 1) Цикл
		Объект.КредиторскаяЗадолженность.Получить(Сч).КорДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКурсВТабличнойЧасти(ТабличнаяЧасть, НовыеКурсы)
	
	Если НовыеКурсы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоТаблица = (ТипЗнч(НовыеКурсы) = Тип("ТаблицаЗначений"));
	
	Для Каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		
		Если СтрокаТаблицы.ДоговорКонтрагента.ВалютаРасчетов = ВалютаРегламентированногоУчета 
			ИЛИ СтрокаТаблицы.ЭтоАванс Тогда
			Продолжить
		КонецЕсли;
		
		Если ЭтоТаблица Тогда
			НовыйКурс = НовыеКурсы.Найти(СтрокаТаблицы.ДоговорКонтрагента, "ДоговорКонтрагента");
		Иначе
			НовыйКурс = НовыеКурсы;
		КонецЕсли;
		
		Если НовыйКурс = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы.КурсВзаиморасчетов      = НовыйКурс.Курс;
		СтрокаТаблицы.КратностьВзаиморасчетов = НовыйКурс.Кратность;
		
		ПересчитатьСуммуПоСтрокеНаСервере(СтрокаТаблицы, Объект, ВалютаРегламентированногоУчета, Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста 
Процедура ПересчитатьСуммуПоСтрокеНаСервере(СтрокаДанных, ДанныеОбъекта, ВалютаРегламентированногоУчета, ПересчетВалютнойСуммы = Истина)

	Если СтрокаДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВалютаДоговора = СтрокаДанных.ДоговорКонтрагента.ВалютаРасчетов;
	Если ПересчетВалютнойСуммы Тогда
		СтрокаДанных.СуммаВзаиморасчетов = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаДанных.Сумма,
				ДанныеОбъекта.ВалютаДокумента, ВалютаДоговора,
				ДанныеОбъекта.КурсДокумента, СтрокаДанных.КурсВзаиморасчетов,
				ДанныеОбъекта.КратностьДокумента,СтрокаДанных.КратностьВзаиморасчетов);
	Иначе
		СтрокаДанных.Сумма = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаДанных.СуммаВзаиморасчетов,
				ВалютаДоговора, ДанныеОбъекта.ВалютаДокумента,
				СтрокаДанных.КурсВзаиморасчетов, ДанныеОбъекта.КурсДокумента,
				СтрокаДанных.КратностьВзаиморасчетов, ДанныеОбъекта.КратностьДокумента);
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПодвалФормы()
	Разница = Объект.СуммаДтЗадолженности - Объект.СуммаКтЗадолженности;
КонецПроцедуры 

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Процедура - Установить пиктограмму комментария.
//
&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриИзмененииДоговораВСтроке(ДанныеСтроки, ДанныеОбъекта, ИмяТабЧасти, ВалютаРегламентированногоУчета)

	Если ЗначениеЗаполнено(ДанныеСтроки.ДоговорКонтрагента) Тогда
		
		ДанныеСтроки.ВалютаДоговораКонтрагента = ДанныеСтроки.ДоговорКонтрагента.ВалютаРАсчетов;
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеСтроки.ДоговорКонтрагента, "ВидДоговора, ВалютаРасчетов");
	
		Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
			Контрагент = ДанныеОбъекта.КонтрагентДебитор;
		Иначе
			Контрагент = ДанныеОбъекта.КонтрагентКредитор;
		КонецЕсли;
		
		СчетаУчета = БухгалтерскийУчетВызовСервера.ПолучитьСчетаРасчетовСКонтрагентом(ДанныеОбъекта.Организация, Контрагент, ДанныеСтроки.ДоговорКонтрагента);
		
		Если ДанныеСтроки.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком Тогда
			
			Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
				ДанныеСтроки.СчетУчетаРасчетов = СчетаУчета.СчетАвансовПоставщика;
			Иначе
				ДанныеСтроки.СчетУчетаРасчетов = СчетаУчета.СчетРасчетовПоставщика;
			КонецЕсли;
					
		ИначеЕсли ДанныеСтроки.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда
			
			Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
				ДанныеСтроки.СчетУчетаРасчетов = СчетаУчета.СчетРасчетовПокупателя;
			Иначе
				ДанныеСтроки.СчетУчетаРасчетов = СчетаУчета.СчетАвансовПокупателя;
			КонецЕсли;
			
		КонецЕсли;

		СчетУчетаРасчетов = ДанныеСтроки.СчетУчетаРасчетов;
	
		Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
			УстановитьПризнакАвансаВСтроке("ДебиторскаяЗадолженность", ДанныеСтроки, РеквизитыДоговора);
		Иначе
			УстановитьПризнакАвансаВСтроке("КредиторскаяЗадолженность", ДанныеСтроки, РеквизитыДоговора);
		КонецЕсли;
		
		Если ДанныеОбъекта.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
			ДанныеСтроки.КурсВзаиморасчетовДоступность = РеквизитыДоговора.ВалютаРасчетов <> ДанныеОбъекта.ВалютаДокумента;
		Иначе
			ДанныеСтроки.КурсВзаиморасчетовДоступность = ДанныеСтроки.ЭтоАванс;
		КонецЕсли; 
			
		Если ДанныеСтроки.ЭтоАванс Тогда
			
			ДанныеСтроки.КурсВзаиморасчетов = Документы.КорректировкаДолга.ПолучитьКурсВалютыАванса(ДанныеСтроки.ДоговорКонтрагента, ДанныеОбъекта.Организация, ДанныеОбъекта.Дата, ДанныеОбъекта.КурсДокумента);
			ДанныеСтроки.КратностьВзаиморасчетов = ДанныеОбъекта.КратностьДокумента;
			
			ДанныеСтроки.КурсВзаиморасчетовДоступность = НЕ ДанныеСтроки.ЭтоАванс;
			
		Иначе
			Если ДанныеСтроки.КурсВзаиморасчетовДоступность Тогда
				
				КурсДоговора = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДанныеСтроки.ДоговорКонтрагента.ВалютаРасчетов, ДанныеОбъекта.Дата);
				
				ДанныеСтроки.КурсВзаиморасчетов      = КурсДоговора.Курс;
				ДанныеСтроки.КратностьВзаиморасчетов = КурсДоговора.Кратность;
				
			Иначе
				
				ДанныеСтроки.КурсВзаиморасчетов = ДанныеОбъекта.КурсДокумента;
				ДанныеСтроки.КратностьВзаиморасчетов = ДанныеОбъекта.КратностьДокумента;
				
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриИзмененииКорДоговораВСтроке(ДанныеСтроки, ДанныеОбъекта, ИмяТабЧасти)
	
	Если ЗначениеЗаполнено(ДанныеСтроки.ДоговорКонтрагента) Тогда
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеСтроки.ДоговорКонтрагента, "ВидДоговора, ВалютаРасчетов");
	
		Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
			Контрагент = ДанныеОбъекта.КонтрагентДебитор;
		Иначе
			Контрагент = ДанныеОбъекта.КонтрагентКредитор;
		КонецЕсли;
		
		СчетаУчета = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(ДанныеОбъекта.Организация, Контрагент, ДанныеСтроки.КорДоговорКонтрагента);

		Если ДанныеСтроки.КорДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком Тогда
			
			Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
				ДанныеСтроки.КорСчетУчетаРасчетов = СчетаУчета.СчетАвансов;
			Иначе
				ДанныеСтроки.КорСчетУчетаРасчетов = СчетаУчета.СчетРасчетов;
			КонецЕсли;
					
		ИначеЕсли ДанныеСтроки.КорДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда
			
			Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
				ДанныеСтроки.КорСчетУчетаРасчетов = СчетаУчета.СчетРасчетовПокупателя;
			Иначе
				ДанныеСтроки.КорСчетУчетаРасчетов = СчетаУчета.СчетАвансовПокупателя;
			КонецЕсли;
			
		КонецЕсли;

		КорСчетУчетаРасчетов = ДанныеСтроки.КорСчетУчетаРасчетов;
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура ПриИзмененииСчетаРасчетовВСтроке(ДанныеСтроки, ДанныеОбъекта, ИмяТабЧасти, ВалютаРегламентированногоУчета)
	
	Если ЗначениеЗаполнено(ДанныеСтроки.ДоговорКонтрагента) Тогда
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеСтроки.ДоговорКонтрагента, "ВидДоговора, ВалютаРасчетов");
	
		Если ИмяТабЧасти = "ДебиторскаяЗадолженность" Тогда
			УстановитьПризнакАвансаВСтроке("ДебиторскаяЗадолженность", ДанныеСтроки, РеквизитыДоговора);
		Иначе
			УстановитьПризнакАвансаВСтроке("КредиторскаяЗадолженность", ДанныеСтроки, РеквизитыДоговора);
		КонецЕсли;
		
		Если ДанныеСтроки.ЭтоАванс Тогда
			
			ДанныеСтроки.КурсВзаиморасчетов = Документы.КорректировкаДолга.ПолучитьКурсВалютыАванса(ДанныеСтроки.ДоговорКонтрагента, ДанныеОбъекта.Организация, ДанныеОбъекта.Дата, ДанныеОбъекта.КурсДокумента);
			ДанныеСтроки.КратностьВзаиморасчетов = ДанныеОбъекта.КратностьДокумента;
			
			ДанныеСтроки.КурсВзаиморасчетовДоступность = НЕ ДанныеСтроки.ЭтоАванс;
			
		Иначе
			ДанныеСтроки.КурсВзаиморасчетов = ДанныеОбъекта.КурсДокумента;
			ДанныеСтроки.КратностьВзаиморасчетов = ДанныеОбъекта.КратностьДокумента;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииРаботаССубконто

&НаКлиенте
Процедура ПриИзмененииСубконтоДт(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		ЭтотОбъект,
		Объект,
		НомерСубконто,
		ПараметрыУстановкиСвойствСубконтоДт(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСубконтоКт(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		ЭтотОбъект,
		Объект,
		НомерСубконто,
		ПараметрыУстановкиСвойствСубконтоКт(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконтоДт(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Объект,
		ПараметрыУстановкиСвойствСубконтоДт(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконтоКт(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Объект,
		ПараметрыУстановкиСвойствСубконтоКт(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконтоДт(Форма)
	
	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"СубконтоДт", "СубконтоДт", "СчетДт");
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконтоКт(Форма)
	
	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"СубконтоКт", "СубконтоКт", "СчетКт");
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
