#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ЗаполненияДокумента

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПо_ДемоЧек(ДанныеЗаполнения) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Чек.ТипОперации,
		|	Чек.Кассир,
		|	Чек.Организация,
		|	Чек.Сдача,
		|	Чек.СуммаБезСкидки,
		|	Чек.СуммаДокумента,
		|	Чек.СуммаНДС,
		|	Чек.СуммаОплаты,
		|	Чек.СуммаСкидки,
		|	Чек.Товары,
		|	Чек.Оплата
		|ИЗ
		|	Документ._ДемоЧек КАК Чек
		|ГДЕ
		|	Чек.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТипОперации = Выборка.ТипОперации;
		Кассир = Выборка.Кассир;
		Организация = Выборка.Организация;
		Сдача = Выборка.Сдача;
		СуммаБезСкидки = Выборка.СуммаБезСкидки;
		СуммаДокумента = Выборка.СуммаДокумента;
		СуммаНДС = Выборка.СуммаНДС;
		СуммаОплаты = Выборка.СуммаОплаты;
		СуммаСкидки = Выборка.СуммаСкидки;
		Товары.Загрузить(Выборка.Товары.Выгрузить());
		Оплата.Загрузить(Выборка.Оплата.Выгрузить());
	КонецЕсли;
	
	Дата = ТекущаяДатаСеанса();
	
КонецПроцедуры

Процедура ЗаполнитьПоТаблицаЗначений(ДанныеЗаполнения) Экспорт

	Товары.Загрузить(ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка._ДемоЧек")] = "ЗаполнитьПо_ДемоЧек";
	СтратегияЗаполнения[Тип("ТаблицаЗначений")] = "ЗаполнитьПоТаблицаЗначений";
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);

	//СпособФорматноЛогическогоКонтроля = Перечисления.СпособыФорматноЛогическогоКонтроля.НеКонтролировать;
	//ДопустимоеРасхождениеФорматноЛогическогоКонтроля = 0.01;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли