#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭДПрисоединенныеФайлы.Ссылка
		|ИЗ
		|	Справочник.ПакетОбменСБанкамиПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		|ГДЕ
		|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла";
	Запрос.УстановитьПараметр("ВладелецФайла", Ссылка);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Объект = Результат.Ссылка.ПолучитьОбъект();
		ОбъектУспешноЗаблокирован = Истина;
		
		Попытка
			Объект.Заблокировать();
		Исключение
			
			ОбъектУспешноЗаблокирован = Ложь;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось заблокировать %1: %2, для установки/снятия пометки на удаление, по причине:
					|%3'", ОбщегоНазначения.КодОсновногоЯзыка()), 
					Результат.Ссылка.Метаданные().ПредставлениеОбъекта, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Предупреждение,, Объект, ОписаниеОшибки());
			
		КонецПопытки;
		
		// Если удалось заблокировать, изменим банковский счет по умолчанию у контрагента/организации.
		Если ОбъектУспешноЗаблокирован Тогда
			Объект.ПометкаУдаления = ПометкаУдаления;
			Объект.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	
#Область Инициализация
	
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
	
#КонецОбласти
	
#КонецЕсли