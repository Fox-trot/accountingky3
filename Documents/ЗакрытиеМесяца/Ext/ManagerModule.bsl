#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =		
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ТаблицаШапка.Период КАК Период,
		|	ТаблицаШапка.Организация КАК Организация,
		|	ТаблицаДокумента.СчетЗатрат КАК СчетДт,
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет КАК СчетКт,
		|	ТаблицаДокумента.Субконто1 КАК СубконтоДт1,
		|	ТаблицаДокумента.Субконто2 КАК СубконтоДт2,
		|	ТаблицаДокумента.Субконто3 КАК СубконтоДт3,
		|	ТаблицаДокумента.ОсновноеСредство КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	""Начисление амортизации"" КАК Содержание,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК ВалютнаяСуммаКт
		|ИЗ
		|	ВременнаяТаблицаАмортизацияОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте),
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаДоход,
		|	""Доход от курсовых разниц"",
		|	0,
		|	0,
		|	ТаблицаДокумента.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаПереоценкаВалюты КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаДоход = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте),
		|	ТаблицаДокумента.СчетУчета,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.СуммаУбыток,
		|	""Убытки от курсовых разниц"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Валюта,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаПереоценкаВалюты КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаУбыток = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНаАвансы),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
		|	ТаблицаАвансы.Контрагент,
		|	ТаблицаАвансы.Договор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаАвансы.СуммаНДС,
		|	""Авансы полученные"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаАвансы КАК ТаблицаАвансы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаАвансы.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНаАвансы),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
		|	ТаблицаДоотгрузка.Контрагент,
		|	ТаблицаДоотгрузка.Договор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	-ТаблицаДоотгрузка.СуммаНДС,
		|	""Отгрузка в счет аванса"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаДоотгрузкаРасшифровка КАК ТаблицаДоотгрузка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДоотгрузка.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ТаблицаДокумента.СчетКт,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.Содержание,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаКорректировкаНДС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	ТаблицаДокумента.СчетУчета,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаДт,
		|	""Закрытие временных счетов"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременныеСчетаУчета КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаДт = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаКт,
		|	""Закрытие временных счетов"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременныеСчетаУчета КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаКт = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	-ТаблицаДокумента.Сумма5300,
		|	""Закрытие 5300"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременные5300 КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.Сумма5300 < 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	10,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма5300,
		|	""Закрытие 5300"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременные5300 КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.Сумма5300 > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	11,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ТаблицаДокумента.СчетКт,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.Сумма,
		|	""Закрытие парных счетов"",
		|	0,
		|	0,
		|	ТаблицаДокумента.Валюта,
		|	ТаблицаДокумента.Валюта,
		|	ТаблицаДокумента.ВалютнаяСумма,
		|	ТаблицаДокумента.ВалютнаяСумма
		|ИЗ
		|	ВременнаяТаблицаПарныеСчетаУчета КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	12,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыБудущихПериодов),
		|	ТаблицаДокумента.СубконтоДт1,
		|	ТаблицаДокумента.СубконтоДт2,
		|	ТаблицаДокумента.СубконтоДт3,
		|	ТаблицаДокумента.СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	""Расходы БП за "" + ТаблицаШапка.ПредставлениеПериода,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаРасходыБудущихПериодов КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	13,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВспомогательноеПроизводство),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Заказ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаВспомогательногоПроизводства,
		|	""Вспомогательное производство"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаВспомогательногоПроизводства = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	14,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НакладныеРасходы),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Заказ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаОбщехозяйственныхРасходов,
		|	""Накладные расходы"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаОбщехозяйственныхРасходов = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	15,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	ТаблицаДокумента.Заказ,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Заказ,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаНЗП,
		|	""Дозакрытие прямых затрат"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаНЗП = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	16,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДтРеализации,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	ТаблицаДокумента.Заказ,
		|	ТаблицаДокумента.СуммаКорректировкиРеализации,
		|	""Дозакрытие суммы реализованной ГП"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаКорректировкиРеализации = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	17,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаПрибыль),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаНаличнаяЕдиныйНалог,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ВидДеятельности = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиЕН.ПереработкаПроизводствоТорговля)
		|			ТОГДА ""1. Наличные""
		|		ИНАЧЕ ""2. Наличные""
		|	КОНЕЦ,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаЕдиныйНалог КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаНаличнаяЕдиныйНалог = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	18,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаПрибыль),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаБезналичнаяЕдиныйНалог,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ВидДеятельности = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиЕН.Прочее)
		|			ТОГДА ""1. Безналичные""
		|		ИНАЧЕ ""2. Безналичные""
		|	КОНЕЦ,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаЕдиныйНалог КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаБезналичнаяЕдиныйНалог = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	19,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаНаличнаяЕдиныйНалог + ТаблицаДокумента.СуммаБезналичнаяЕдиныйНалог КАК ЧИСЛО(15, 2))),
		|	"""",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаЕдиныйНалог КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация
		|
		|ИМЕЮЩИЕ
		|	НЕ СУММА(ТаблицаДокумента.СуммаНаличнаяЕдиныйНалог) + СУММА(ТаблицаДокумента.СуммаБезналичнаяЕдиныйНалог) = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	20,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетЗатрат,
		|	ТаблицаДокумента.СчетРасходов,
		|	ТаблицаДокумента.Подразделение,
		|	ТаблицаДокумента.НоменклатурнаяГруппа,
		|	ТаблицаДокумента.СтатьяЗатрат,
		|	ТаблицаДокумента.ПодразделениеЗатрат,
		|	ТаблицаДокумента.СтатьяЗатрат,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	""Накладные расходы"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаНакладныеРасходы КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0";	
	ТаблицаХозрасчетный = Запрос.Выполнить().Выгрузить();
	
	// Подготовка таблиц для проведения.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Регистратор,
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	&БазаРаспределения КАК БазаРаспределения
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка";
	Запрос.УстановитьПараметр("БазаРаспределения", СтруктураДополнительныеСвойства.УчетнаяПолитика.БазаРаспределенияКорректировкиСебестоимости);                 

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = МассивРезультатов[0].Выгрузить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);
	
	// Если рассчитывается налог на прибыль, то делаются дополнительные проводки.
	Если ДокументСсылка.РасчетНалогаНаПрибыль Тогда
		СформироватьНалогНаПрибыльВТаблицеХозрасчетный(СтруктураДополнительныеСвойства.ДляПроведения.Дата, 
			СтруктураДополнительныеСвойства.ДляПроведения.Организация, ТаблицаХозрасчетный, ДокументСсылка);		
	КонецЕсли;
		
	// Проводки по налогу на имущество формируются в конце года.
	Если СтруктураДополнительныеСвойства.ДляПроведения.Дата = КонецГода(СтруктураДополнительныеСвойства.ДляПроведения.Дата) Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СформироватьТаблицаХозрасчетныйНалогНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства), ТаблицаХозрасчетный);
	КонецЕсли;	
			
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", ТаблицаХозрасчетный);
КонецПроцедуры      

Функция СформироватьТаблицаХозрасчетныйНалогНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоНалогам) КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаИмущество) КАК СчетКт,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	СУММА(ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога) КАК Сумма,
		|	""НИ: Недвижимость"" КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоНалогам),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаТранспортныеСредства),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	СУММА(ЗакрытиеМесяцаТранспорт.СуммаНалога),
		|	""НИ: Транспорт""
		|ИЗ
		|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДоотгрузка.Контрагент КАК Контрагент,
		|	ТаблицаДоотгрузка.Договор КАК Договор,
		|	ТаблицаДоотгрузка.ДокументАванса КАК ДокументА,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаДоотгрузка.Сумма КАК Сумма,
		|	ТаблицаДоотгрузка.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДоотгрузка.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДоотгрузка.ДокументОперации КАК ДокументОтгрузки,
		|	ВЫРАЗИТЬ(ТаблицаДоотгрузка.Договор КАК Справочник.ДоговорыКонтрагентов).СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаАвасыДоотгрузка
		|ИЗ
		|	ВременнаяТаблицаДоотгрузкаРасшифровка КАК ТаблицаДоотгрузка
		|ГДЕ
		|	ТаблицаДоотгрузка.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаАвансы.Контрагент,
		|	ТаблицаАвансы.Договор,
		|	ТаблицаАвансы.Документ,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход),
		|	ТаблицаАвансы.Сумма,
		|	ТаблицаАвансы.СуммаНДС,
		|	ТаблицаАвансы.СуммаНСП,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫРАЗИТЬ(ТаблицаАвансы.Договор КАК Справочник.ДоговорыКонтрагентов).СтавкаНДС
		|ИЗ
		|	ВременнаяТаблицаАвансы КАК ТаблицаАвансы
		|ГДЕ
		|	ТаблицаАвансы.Документ <> НЕОПРЕДЕЛЕНО
		|	И ТаблицаАвансы.СуммаДокумента > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаАвасыДоотгрузка.Контрагент,
		|	ТаблицаАвасыДоотгрузка.Договор,
		|	ТаблицаАвасыДоотгрузка.ДокументА,
		|	ТаблицаАвасыДоотгрузка.ВидДвижения,
		|	ТаблицаАвасыДоотгрузка.Сумма,
		|	ТаблицаАвасыДоотгрузка.СуммаНДС,
		|	ТаблицаАвасыДоотгрузка.СуммаНСП,
		|	ТаблицаАвасыДоотгрузка.ДокументОтгрузки,
		|	ТаблицаАвасыДоотгрузка.СтавкаНДС
		|ИЗ
		|	ВременнаяТаблицаАвасыДоотгрузка КАК ТаблицаАвасыДоотгрузка
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыДоотгрузка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицаСведенияПоНалогуНаимущество(ДокументСсылка, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД) КАК Год,
		|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНедвижимость.СуммаНалога КАК СуммаНалога
		|ИЗ
		|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
		|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога
		|ИЗ
		|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияПоНалогуНаимущество", РезультатЗапроса.Выгрузить());
КонецПроцедуры

Процедура СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Период КАК Год,
		|	ВременнаяТаблицаНалоговаяАмортизация.ГруппаНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ОсновноеСредство,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьНаНГ КАК БалансоваяСтоимостьНаНачалоГода,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимости КАК СуммаПоступления,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ПоступлениеПГ КАК СуммаПоступленияОстаток,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияОбщая КАК СуммаВыбытия,		
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ВыбытиеПГ КАК СуммаВыбытияОстаток,
		|	ВременнаяТаблицаНалоговаяАмортизация.ЗатратыНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.НормаНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.Превышение КАК ПревышениеЗатратНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимость КАК БалансоваяСтоимостьНаКонецГода,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьИтого,
		|	ВременнаяТаблицаНалоговаяАмортизация.НормаАО КАК НАО,
		|	ВременнаяТаблицаНалоговаяАмортизация.Амортизация КАК АмортизацияЗаГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСОД,
		|	ВременнаяТаблицаНалоговаяАмортизация.ВычетМеньше100РП,
		|	ВременнаяТаблицаНалоговаяАмортизация.ДатаПринятияКУчету,
		|	ВременнаяТаблицаНалоговаяАмортизация.КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаНалоговаяАмортизация.БазаПрямолинейнойАмортизации
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоговаяАмортизация КАК ВременнаяТаблицаНалоговаяАмортизация
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяАмортизация", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаНалоговаяВыверка(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Период КАК Год,
		|	ВременнаяТаблицаНалоговаяВыверка.НомерСтроки КАК Строка,
		|	ВременнаяТаблицаНалоговаяВыверка.Сумма,
		|	ВременнаяТаблицаНалоговаяВыверка.НаименованиеСтроки
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоговаяВыверка КАК ВременнаяТаблицаНалоговаяВыверка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяВыверка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Процедура для формирования дополнительных проводок в РБ по налогу на прибыль
//
// Параметры:
//  ДатаДокумента  - Дата - дата документа.
//  Организация  - СправочникСсылка.Организация - организация документа.
//  ТЗХозрасчетный  - ТаблицаЗначений - данные основного запроса из данного документа для записи в РБ.
//  СтруктураДополнительныеСвойства  - Структура - структура дополнительных свойств.
//
Процедура СформироватьНалогНаПрибыльВТаблицеХозрасчетный(ДатаДокумента, Организация, ТаблицаХозрасчетный, ДокументСсылка)

	// Проводки в РБ Хозрасчетный по налогу на прибыль.
	Если ДокументСсылка.СуммаНалогНаПрибыль <> 0 Тогда
		// Простая проводка 9910 - 3410
		Если ДокументСсылка.НеФормироватьОтсроченныеПроводки Тогда 
			
			// Налог на прибыль 9910 - 3410
			СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
			СтрокаТЗ.Период 		= ДатаДокумента;
			СтрокаТЗ.Организация 	= Организация;
			СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
			СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
			СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыль;
			СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
			
			// Закрытие 5300 - 9910
			СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
			СтрокаТЗ.Период 		= ДатаДокумента;
			СтрокаТЗ.Организация 	= Организация;
			СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль;
			СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
			СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыль;
			СтрокаТЗ.Содержание 	= "НалогНаПрибыль";			
			
			// Учитываем отсроченные проводки
		Иначе			
			
			// Отсроченная проводки 4310
			// Если бухгатерская прибыль больше налоговой: 
			Если ДокументСсылка.СуммаНалогНаПрибыльБУ > ДокументСсылка.СуммаНалогНаПрибыль Тогда 
				// Основная проводка налог на прибыль НУ 9910 - 3410
				СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
				СтрокаТЗ.Период 		= ДатаДокумента;
				СтрокаТЗ.Организация 	= Организация;
				СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
				СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
				СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыль;
				СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
				
				// Отсроченный налог НА 9910 - 4310
				СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
				СтрокаТЗ.Период 		= ДатаДокумента;
				СтрокаТЗ.Организация 	= Организация;
				СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
				СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеОбязательстваПоНалогуНаПрибыль;
				СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыльБУ - ДокументСсылка.СуммаНалогНаПрибыль;
				СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
				
				// Отсроченная проводка 2410
				// Если налоговая прибыль больше бухгалтерской: 
			ИначеЕсли ДокументСсылка.СуммаНалогНаПрибыльБУ < ДокументСсылка.СуммаНалогНаПрибыль Тогда  
				// Основная проводка налог на прибыль НУ 9910 - 3410				
				Если ДокументСсылка.СуммаНалогНаПрибыльБУ <> 0 Тогда
					СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
					СтрокаТЗ.Период 		= ДатаДокумента;
					СтрокаТЗ.Организация 	= Организация;
					СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
					СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
					СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыль;
					СтрокаТЗ.Содержание 	= "НалогНаПрибыль";
					
					// Отсроченный налог НА 9910 - 2410
					СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
					СтрокаТЗ.Период 		= ДатаДокумента;
					СтрокаТЗ.Организация 	= Организация;
					СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
					СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеТребованияНП;
					СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыльБУ - ДокументСсылка.СуммаНалогНаПрибыль;
					СтрокаТЗ.Содержание 	= "НалогНаПрибыль";
				КонецЕсли;					
				
			ИначеЕсли ДокументСсылка.СуммаНалогНаПрибыльБУ = ДокументСсылка.СуммаНалогНаПрибыль Тогда  
				// Основная проводка налог на прибыль НУ 9910 - 3410				
				Если ДокументСсылка.СуммаНалогНаПрибыльБУ <> 0 Тогда
					СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
					СтрокаТЗ.Период 		= ДатаДокумента;
					СтрокаТЗ.Организация 	= Организация;
					СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
					СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
					СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыльБУ;
					СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
				КонецЕсли;
			КонецЕсли;
			
			// Закрытие 5300 - 9910 
			Если ДокументСсылка.СуммаНалогНаПрибыльБУ <> 0 Тогда
				СтрокаТЗ				= ТаблицаХозрасчетный.Добавить();
				СтрокаТЗ.Период 		= ДатаДокумента;
				СтрокаТЗ.Организация 	= Организация;
				СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль;
				СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
				СтрокаТЗ.Сумма 			= ДокументСсылка.СуммаНалогНаПрибыльБУ;
				СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
			КонецЕсли;				
		КонецЕсли; 
	КонецЕсли;	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт 		
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Дата КАК Период,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.РасчетНалогаНаПрибыль КАК РасчетНалогаНаПрибыль,
		|	&ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СчетАмортизации КАК СчетАмортизации,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаДокумента.Субконто1 КАК Субконто1,
		|	ТаблицаДокумента.Субконто2 КАК Субконто2,
		|	ТаблицаДокумента.Субконто3 КАК Субконто3,
		|	ТаблицаДокумента.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаАмортизацияОС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АмортизацияОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СуммаДт КАК СуммаДт,
		|	ТаблицаДокумента.СуммаКт КАК СуммаКт
		|ПОМЕСТИТЬ ВременнаяТаблицаВременныеСчетаУчета
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ВременныеСчетаУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ТаблицаДокумента.СуммаКт <> 0
		|				ТОГДА ТаблицаДокумента.СуммаКт
		|			КОГДА ТаблицаДокумента.СуммаДт <> 0
		|				ТОГДА -ТаблицаДокумента.СуммаДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма5300
		|ПОМЕСТИТЬ ВременнаяТаблицаВременные5300
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ВременныеСчетаУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Ссылка.РасчетНераспределеннойПрибыли
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.Валюта КАК Валюта,
		|	ТаблицаДокумента.Субконто1 КАК Субконто1,
		|	ТаблицаДокумента.Субконто2 КАК Субконто2,
		|	ТаблицаДокумента.Субконто3 КАК Субконто3,
		|	ТаблицаДокумента.СуммаДоход КАК СуммаДоход,
		|	ТаблицаДокумента.СуммаУбыток КАК СуммаУбыток
		|ПОМЕСТИТЬ ВременнаяТаблицаПереоценкаВалюты
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ПереоценкаВалюты КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Договор КАК Договор,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.ДокументАванса КАК ДокументАванса,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.СуммаНСП КАК СуммаНСП,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.ДокументОперации КАК ДокументОперации
		|ПОМЕСТИТЬ ВременнаяТаблицаДоотгрузкаРасшифровка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ДоотгрузкаРасшифровка КАК ЗакрытиеМесяцаДоотгрузкаРасшифровка
		|ГДЕ
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаАвансы.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаАвансы.Договор КАК Договор,
		|	ЗакрытиеМесяцаАвансы.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаАвансы.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаАвансы.СуммаНСП КАК СуммаНСП,
		|	ЗакрытиеМесяцаАвансы.Документ КАК Документ,
		|	ЗакрытиеМесяцаАвансы.СуммаДокумента КАК СуммаДокумента
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансы
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АвансыРасшифровка КАК ЗакрытиеМесяцаАвансы
		|ГДЕ
		|	ЗакрытиеМесяцаАвансы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаКорректировкаНДС.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаКорректировкаНДС.СчетКт КАК СчетКт,
		|	ЗакрытиеМесяцаКорректировкаНДС.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаКорректировкаНДС.Содержание КАК Содержание
		|ПОМЕСТИТЬ ВременнаяТаблицаКорректировкаНДС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.КорректировкаНДС КАК ЗакрытиеМесяцаКорректировкаНДС
		|ГДЕ
		|	ЗакрытиеМесяцаКорректировкаНДС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаПарныеСчетаУчета.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.СчетКт КАК СчетКт,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто1 КАК Субконто1,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто2 КАК Субконто2,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто3 КАК Субконто3,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Валюта КАК Валюта,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.ВалютнаяСумма КАК ВалютнаяСумма
		|ПОМЕСТИТЬ ВременнаяТаблицаПарныеСчетаУчета
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ПарныеСчетаУчета КАК ЗакрытиеМесяцаПарныеСчетаУчета
		|ГДЕ
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНакладныеРасходы.СчетЗатрат КАК СчетЗатрат,
		|	ЗакрытиеМесяцаНакладныеРасходы.СчетРасходов КАК СчетРасходов,
		|	ЗакрытиеМесяцаНакладныеРасходы.Подразделение КАК Подразделение,
		|	ЗакрытиеМесяцаНакладныеРасходы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ЗакрытиеМесяцаНакладныеРасходы.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
		|	ЗакрытиеМесяцаНакладныеРасходы.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ЗакрытиеМесяцаНакладныеРасходы.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаНакладныеРасходы
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НакладныеРасходы КАК ЗакрытиеМесяцаНакладныеРасходы
		|ГДЕ
		|	ЗакрытиеМесяцаНакладныеРасходы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества КАК ГруппаИмущества,
		|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцев,
		|	ЗакрытиеМесяцаНедвижимость.Ставка КАК Ставка,
		|	ЗакрытиеМесяцаНедвижимость.СуммаНалога КАК СуммаНалога,
		|	ЗакрытиеМесяцаНедвижимость.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ЗакрытиеМесяцаНедвижимость.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ЗакрытиеМесяцаНедвижимость.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.Кр КАК Кр,
		|	ЗакрытиеМесяцаНедвижимость.Кз КАК Кз,
		|	ЗакрытиеМесяцаНедвижимость.Ко КАК Ко,
		|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ КАК СтоимостьНУ,
		|	ЗакрытиеМесяцаНедвижимость.МатериалСтен КАК МатериалСтен,
		|	ЗакрытиеМесяцаНедвижимость.ЗемНалог КАК ЗемНалог,
		|	ЗакрытиеМесяцаНедвижимость.ОтЛет КАК ОтЛет,
		|	ЗакрытиеМесяцаНедвижимость.ДоЛет КАК ДоЛет,
		|	ЗакрытиеМесяцаНедвижимость.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НежилаяПлощадь КАК НежилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога КАК НалогСУчетомЗемельногоНалога,
		|	ЗакрытиеМесяцаНедвижимость.Объект КАК Объект
		|ПОМЕСТИТЬ ВременнаяТаблицаНедвижимость
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Недвижимость КАК ЗакрытиеМесяцаНедвижимость
		|ГДЕ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаТранспорт.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества КАК ГруппаИмущества,
		|	ЗакрытиеМесяцаТранспорт.ВидТранспортаКатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцев,
		|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ЗакрытиеМесяцаТранспорт.Ставка КАК Ставка,
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога КАК СуммаНалога,
		|	ЗакрытиеМесяцаТранспорт.ОтЛет КАК ОтЛет,
		|	ЗакрытиеМесяцаТранспорт.ДоЛет КАК ДоЛет
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспорт
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Транспорт КАК ЗакрытиеМесяцаТранспорт
		|ГДЕ
		|	ЗакрытиеМесяцаТранспорт.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаРасходыБП.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаРасходыБП.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаРасходыБП.РБП.Субконто1 КАК СубконтоДт1,
		|	ЗакрытиеМесяцаРасходыБП.РБП.Субконто2 КАК СубконтоДт2,
		|	ЗакрытиеМесяцаРасходыБП.РБП.Субконто3 КАК СубконтоДт3,
		|	ЗакрытиеМесяцаРасходыБП.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаРасходыБП.РБП КАК СубконтоКт1
		|ПОМЕСТИТЬ ВременнаяТаблицаРасходыБудущихПериодов
		|ИЗ
		|	Документ.ЗакрытиеМесяца.РасходыБП КАК ЗакрытиеМесяцаРасходыБП
		|ГДЕ
		|	ЗакрытиеМесяцаРасходыБП.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ГруппаНУ КАК ГруппаНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьНаНГ КАК БалансоваяСтоимостьНаНГ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСтоимости КАК УвеличениеСтоимости,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК УвеличениеСтоимостиНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ПоступлениеПГ КАК ПоступлениеПГ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.СуммаВыбытияОбщая КАК СуммаВыбытияОбщая,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ВыбытиеПГ КАК ВыбытиеПГ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ЗатратыНаРемонт КАК ЗатратыНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.НормаНаРемонт КАК НормаНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Превышение КАК Превышение,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимость КАК БалансоваяСтоимость,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьИтого КАК БалансоваяСтоимостьИтого,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.НормаАО КАК НормаАО,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Амортизация КАК Амортизация,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСОД КАК УвеличениеСОД,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ВычетМеньше100РП КАК ВычетМеньше100РП,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БазаПрямолинейнойАмортизации КАК БазаПрямолинейнойАмортизации
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяАмортизация
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НалоговаяАмортизация КАК ЗакрытиеМесяцаНалоговаяАмортизация
		|ГДЕ
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНалоговаяВыверка.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаНалоговаяВыверка.НомерСтроки КАК НомерСтроки,
		|	ЗакрытиеМесяцаНалоговаяВыверка.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаНалоговаяВыверка.НаименованиеСтроки КАК НаименованиеСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяВыверка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НалоговаяВыверка КАК ЗакрытиеМесяцаНалоговаяВыверка
		|ГДЕ
		|	ЗакрытиеМесяцаНалоговаяВыверка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаОХРиВП.Номенклатура КАК Номенклатура,
		|	ЗакрытиеМесяцаОХРиВП.Заказ КАК Заказ,
		|	ЗакрытиеМесяцаОХРиВП.Склад КАК Склад,
		|	ЗакрытиеМесяцаОХРиВП.СуммаОбщехозяйственныхРасходов КАК СуммаОбщехозяйственныхРасходов,
		|	ЗакрытиеМесяцаОХРиВП.СуммаВспомогательногоПроизводства КАК СуммаВспомогательногоПроизводства,
		|	ЗакрытиеМесяцаОХРиВП.СуммаНЗП КАК СуммаНЗП,
		|	ЗакрытиеМесяцаОХРиВП.СуммаКорректировкиРеализации КАК СуммаКорректировкиРеализации,
		|	ЗакрытиеМесяцаОХРиВП.СчетДтРеализации КАК СчетДтРеализации
		|ПОМЕСТИТЬ ВременнаяТаблицаОХРиВП
		|ИЗ
		|	Документ.ЗакрытиеМесяца.РасходыНаПроизводство КАК ЗакрытиеМесяцаОХРиВП
		|ГДЕ
		|	ЗакрытиеМесяцаОХРиВП.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаЕдиныйНалог.ВидДеятельности КАК ВидДеятельности,
		|	ЗакрытиеМесяцаЕдиныйНалог.СуммаНаличнаяЕдиныйНалог КАК СуммаНаличнаяЕдиныйНалог,
		|	ЗакрытиеМесяцаЕдиныйНалог.СуммаБезналичнаяЕдиныйНалог КАК СуммаБезналичнаяЕдиныйНалог
		|ПОМЕСТИТЬ ВременнаяТаблицаЕдиныйНалог
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ЕдиныйНалог КАК ЗакрытиеМесяцаЕдиныйНалог
		|ГДЕ
		|	ЗакрытиеМесяцаЕдиныйНалог.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПредставлениеПериода", Формат(ДокументСсылка.Дата, "ДФ=ММММ") + " " + Формат(ДокументСсылка.Дата, "ДФ=гггг"));
	Запрос.Выполнить();    	
	                                  
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияПоНалогуНаимущество(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяВыверка(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Выполнить закрытие месяца
//
Процедура ВыполнитьЗакрытиеМесяца(СтруктураПараметров, АдресХранилища) Экспорт
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") 
		И СтруктураПараметров.Свойство("Ссылка") Тогда 
		ДокументОбъект = СтруктураПараметров.Ссылка.ПолучитьОбъект();
	Иначе 
		Возврат;
	КонецЕсли;	
	
	// Предварительная очистка.
	ДокументОбъект.АмортизацияОС.Очистить();
	ДокументОбъект.Недвижимость.Очистить();
	ДокументОбъект.Транспорт.Очистить();
	ДокументОбъект.ПереоценкаВалюты.Очистить();
	ДокументОбъект.АвансыИДоотгрузка.Очистить();
	ДокументОбъект.АвансыРасшифровка.Очистить();
	ДокументОбъект.ДоотгрузкаРасшифровка.Очистить();
	ДокументОбъект.НДС.Очистить();
	ДокументОбъект.ПарныеСчетаУчета.Очистить();
	ДокументОбъект.ВременныеСчетаУчета.Очистить();
	ДокументОбъект.РасходыНаПроизводство.Очистить();
	ДокументОбъект.НакладныеРасходы.Очистить();
	ДокументОбъект.НалоговаяАмортизация.Очистить();
	ДокументОбъект.НалоговаяВыверка.Очистить();
	ДокументОбъект.РасходыБП.Очистить();
	ДокументОбъект.КорректировкаНДС.Очистить();
	ДокументОбъект.ЕдиныйНалог.Очистить();
	
	ДокументОбъект.СуммаГотоваяПродукцияПлановая = 0;
	ДокументОбъект.СуммаГотоваяПродукцияКорректировка = 0;
	ДокументОбъект.СуммаОбщехозяйственныхРасходов = 0;
	ДокументОбъект.СуммаВспомогательногоПроизводства = 0;
	ДокументОбъект.СуммаНалогНаПрибыль = 0;
	ДокументОбъект.СуммаНалогНаПрибыльБУ = 0;
	
	Если ДокументОбъект.Проведен Тогда
		ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);		
	КонецЕсли;	

	ДокументОбъект.ВыполнитьЗакрытиеМесяца(СтруктураПараметров);

	АдресХранилища = ПоместитьВоВременноеХранилище(ДокументОбъект.Ссылка, АдресХранилища);
КонецПроцедуры 

Функция ПолучитьДанныеПоНалогуНаИмущество(Организация, Дата) Экспорт
	
	// Начальные данные.
	
	// 1. Все основные средства принятые к учету на начало расчетного года
	//    +
	// 	  все основные средства принятые к в течение расчетного года.
	// 2. Все основные средства снятые с учета в течение расчетного года.
	// 3. Все основные средства для расчета налога.
	
	// Недвижимость.
	
	// 4. Сведения об имуществе для групп 1,2,3.
	// 5. Шкала оценки по виду материалов стен.
	// 6. Коэффициенты региональный, зональный и отраслевой.
	// 7. Ставки налога на имущество.
	// 8. Результат для расчета (временная таблица).	
	// 9. Результат для расчета в док. "Закрытие месяца".
	// 10. Данные для док. "Отчет по налогу на имущество".	
	
	// Транспорт.
	
	// 11. Сведения о транспорте для групп 4.
	// 12. Шкала оценки по виду материалов стен.
	// 13. Накопленная амортизация.
	// 14. Результат для расчета (временная таблица).	
	// 15. Результат для расчета в док. "Закрытие месяца".
	// 16. Данные для док. "Отчет по налогу на имущество".
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	СостоянияОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СостоянияОССрезПоследних.Период КАК ПериодПринятоКУчету
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОСПринятоКУчету
		|ИЗ
		|	РегистрСведений.СостоянияОС.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СостоянияОССрезПоследних
		|ГДЕ
		|	СостоянияОССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1,
		|	СостоянияОС.ОсновноеСредство,
		|	СостоянияОС.Период
		|ИЗ
		|	РегистрСведений.СостоянияОС КАК СостоянияОС
		|ГДЕ
		|	СостоянияОС.Период > &НачалоПериода
		|	И СостоянияОС.Период <= &КонецПериода
		|	И СостоянияОС.Организация = &Организация
		|	И СостоянияОС.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	2 КАК Порядок,
		|	СостоянияОССрезПервых.ОсновноеСредство КАК ОсновноеСредство,
		|	СостоянияОССрезПервых.Период КАК ПериодСнятоСУчета,
		|	МЕСЯЦ(СостоянияОССрезПервых.Период) КАК МесяцСнятоСУчета
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОССнятоСУчета
		|ИЗ
		|	РегистрСведений.СостоянияОС.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство КАК ОсновноеСредство
		|					ИЗ
		|						ВременнаяТаблицаСостоянияОСПринятоКУчету КАК ВременнаяТаблицаСостоянияОСПринятоКУчету)) КАК СостоянияОССрезПервых
		|ГДЕ
		|	СостоянияОССрезПервых.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	3 КАК Порядок,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету КАК ПериодПринятоКУчету,
		// Расчет количетва месяцев эксплкатации.		
		|	ВЫБОР
		// Если принято к учету и снято с учета в текущем году в одном и том же месяце.
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету) = ГОД(&НачалоПериода)
		|				И ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|				И МЕСЯЦ(ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету) = МЕСЯЦ(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1)))
		|			ТОГДА 1
		// Если принято к учету и снято с учета в текущем году.
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету) = ГОД(&НачалоПериода)
		|				И ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|			ТОГДА ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцСнятоСУчета - МЕСЯЦ(ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету)
		// Если принято к учету в текущем году.
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету) = ГОД(&НачалоПериода)
		|			ТОГДА 13 - МЕСЯЦ(ВременнаяТаблицаСостоянияОСПринятоКУчету.ПериодПринятоКУчету)
		// Если снято с учета в текущем году.
		|		КОГДА НЕ ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцСнятоСУчета - 1
		// Если принято ранее текущего года и не снято с учета. 
		|		ИНАЧЕ 12
		|	КОНЕЦ КАК КоличествоМесяцевЭксплуатации,
		|	ПараметрыУчетаОССрезПоследних.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПараметрыУчетаОССрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОС
		|ИЗ
		|	ВременнаяТаблицаСостоянияОСПринятоКУчету КАК ВременнаяТаблицаСостоянияОСПринятоКУчету
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСостоянияОССнятоСУчета КАК ВременнаяТаблицаСостоянияОССнятоСУчета
		|		ПО ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство = ВременнаяТаблицаСостоянияОССнятоСУчета.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаСостоянияОСПринятоКУчету КАК ВременнаяТаблицаСостоянияОСПринятоКУчету)) КАК ПараметрыУчетаОССрезПоследних
		|		ПО ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство = ПараметрыУчетаОССрезПоследних.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
	 	|;
		|
		|////////////////////////////////////////////////////////////////////////////////
	 	|ВЫБРАТЬ
		|	4 КАК Порядок,
		|	СведенияОбИмуществеСрезПоследних.ГруппаИмущества КАК ГруппаИмущества,
		|	СведенияОбИмуществеСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияОбИмуществеСрезПоследних.МатериалСтен КАК МатериалСтен,
		|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВвода,
		|	СведенияОбИмуществеСрезПоследних.РегиональныйКоэффициент КАК РегиональныйКоэффициент,
		|	СведенияОбИмуществеСрезПоследних.ЗональныйКоэффициент КАК ЗональныйКоэффициент,
		|	СведенияОбИмуществеСрезПоследних.ОтраслевойКоэффициент КАК ОтраслевойКоэффициент,
		|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества КАК КодНазначенияИмущества,
		|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества КАК КодПользователяИмущества,
		|	СведенияОбИмуществеСрезПоследних.КодИмущества КАК КодИмущества,
		|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества КАК КатегорияОбъектаИмущества,
		|	СведенияОбИмуществеСрезПоследних.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	СведенияОбИмуществеСрезПоследних.НежилаяПлощадь КАК НежилаяПлощадь,
		|	СведенияОбИмуществеСрезПоследних.ЖилаяПлощадь + СведенияОбИмуществеСрезПоследних.НежилаяПлощадь КАК ОбщаяПлощадь,
		|	ГОД(&КонецПериода) - ГОД(ВременнаяТаблицаСостоянияОС.ПериодПринятоКУчету) КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.ИнвентарныйНомер КАК ИнвентарныйНомер
		|ПОМЕСТИТЬ ВременнаяТаблицаИмущество
		|ИЗ
		|	ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИмуществе.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСостоянияОС.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС)) КАК СведенияОбИмуществеСрезПоследних
		|		ПО ВременнаяТаблицаСостоянияОС.ОсновноеСредство = СведенияОбИмуществеСрезПоследних.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ШкалаОценкиСтоимостиМакс.ОсновноеСредство КАК ОсновноеСредство,
		|	ШкалаОценкиСтоимости.Ставка КАК ОценочнаяСтоимость
		|ПОМЕСТИТЬ ВременнаяТаблицаОценкиСтоимостиНедвижимость
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|		ВременнаяТаблицаИмущество.МатериалСтен КАК МатериалСтен,
		|		МАКСИМУМ(ШкалаОценкиСтоимости.НижняяГраница) КАК НижняяГраница
		|	ИЗ
		|		ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыМатериаловСтенНалогНаИмущество.ШкалаОценкиСтоимости КАК ШкалаОценкиСтоимости
		|			ПО ВременнаяТаблицаИмущество.МатериалСтен = ШкалаОценкиСтоимости.Ссылка
		|				И ВременнаяТаблицаИмущество.КоличествоЛетЭксплуатации >= ШкалаОценкиСтоимости.НижняяГраница
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВременнаяТаблицаИмущество.ОсновноеСредство,
		|		ВременнаяТаблицаИмущество.МатериалСтен) КАК ШкалаОценкиСтоимостиМакс
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыМатериаловСтенНалогНаИмущество.ШкалаОценкиСтоимости КАК ШкалаОценкиСтоимости
		|		ПО ШкалаОценкиСтоимостиМакс.МатериалСтен = ШкалаОценкиСтоимости.Ссылка
		|			И ШкалаОценкиСтоимостиМакс.НижняяГраница = ШкалаОценкиСтоимости.НижняяГраница
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(КоэффициентыНалогаНаИмуществоРегиональные.Коэффициент, 0) КАК РегиональныйКоэффициент,
		|	ЕСТЬNULL(КоэффициентыНалогаНаИмуществоЗональные.Коэффициент, 0) КАК ЗональныйКоэффициент,
		|	ЕСТЬNULL(КоэффициентыНалогаНаИмуществоОтраслевые.Коэффициент, 0) КАК ОтраслевойКоэффициент
		|ПОМЕСТИТЬ ВременнаяТаблицаКоэффициенты
		|ИЗ
		|	ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыНалогаНаИмущество.СрезПоследних(
		|				&КонецПериода,
		|				Классификация В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаИмущество.РегиональныйКоэффициент КАК РегиональныйКоэффициент
		|					ИЗ
		|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК КоэффициентыНалогаНаИмуществоРегиональные
		|		ПО ВременнаяТаблицаИмущество.РегиональныйКоэффициент = КоэффициентыНалогаНаИмуществоРегиональные.Классификация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыНалогаНаИмущество.СрезПоследних(
		|				&КонецПериода,
		|				Классификация В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаИмущество.ЗональныйКоэффициент КАК ЗональныйКоэффициент
		|					ИЗ
		|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК КоэффициентыНалогаНаИмуществоЗональные
		|		ПО ВременнаяТаблицаИмущество.ЗональныйКоэффициент = КоэффициентыНалогаНаИмуществоЗональные.Классификация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыНалогаНаИмущество.СрезПоследних(
		|				&КонецПериода,
		|				Классификация В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаИмущество.ОтраслевойКоэффициент КАК ОтраслевойКоэффициент
		|					ИЗ
		|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК КоэффициентыНалогаНаИмуществоОтраслевые
		|		ПО ВременнаяТаблицаИмущество.ОтраслевойКоэффициент = КоэффициентыНалогаНаИмуществоОтраслевые.Классификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.Ставка, 0) КАК Ставка
		|ПОМЕСТИТЬ ВременнаяТаблицаСтавки
		|ИЗ
		|	ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(
		|				&КонецПериода,
		|				ГруппаНалогаНаИмущество В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаИмущество.ГруппаИмущества КАК ГруппаИмущества
		|					ИЗ
		|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК СтавкиНалогаНаИмуществоСрезПоследних
		|		ПО ВременнаяТаблицаИмущество.ГруппаИмущества = СтавкиНалогаНаИмуществоСрезПоследних.ГруппаНалогаНаИмущество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	8 КАК Порядок,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаИмущество.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаИмущество.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаИмущество.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ВременнаяТаблицаИмущество.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаИмущество.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаИмущество.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаИмущество.МатериалСтен КАК МатериалСтен,
		|	ЕСТЬNULL(ВременнаяТаблицаОценкиСтоимости.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимость,
		|	ЕСТЬNULL(ВременнаяТаблицаКоэффициенты.РегиональныйКоэффициент, 0) КАК РегиональныйКоэффициент,
		|	ЕСТЬNULL(ВременнаяТаблицаКоэффициенты.ЗональныйКоэффициент, 0) КАК ЗональныйКоэффициент,
		|	ЕСТЬNULL(ВременнаяТаблицаКоэффициенты.ОтраслевойКоэффициент, 0) КАК ОтраслевойКоэффициент,
		|	ЕСТЬNULL(ВременнаяТаблицаСтавки.Ставка, 0) КАК Ставка
		|ПОМЕСТИТЬ ВременнаяТаблицаИмуществоГотовая
		|ИЗ
		|	ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОценкиСтоимостиНедвижимость КАК ВременнаяТаблицаОценкиСтоимости
		|		ПО ВременнаяТаблицаИмущество.ОсновноеСредство = ВременнаяТаблицаОценкиСтоимости.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКоэффициенты КАК ВременнаяТаблицаКоэффициенты
		|		ПО ВременнаяТаблицаИмущество.ОсновноеСредство = ВременнаяТаблицаКоэффициенты.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтавки КАК ВременнаяТаблицаСтавки
		|		ПО ВременнаяТаблицаИмущество.ОсновноеСредство = ВременнаяТаблицаСтавки.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	9 КАК Порядок,
		|	ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаИмуществоГотовая.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаИмуществоГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаИмуществоГотовая.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ВременнаяТаблицаИмуществоГотовая.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаИмуществоГотовая.МатериалСтен КАК МатериалСтен,
		|	ВременнаяТаблицаИмуществоГотовая.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаИмуществоГотовая.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаИмуществоГотовая.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаИмуществоГотовая.РегиональныйКоэффициент КАК РегиональныйКоэффициент,
		|	ВременнаяТаблицаИмуществоГотовая.ЗональныйКоэффициент КАК ЗональныйКоэффициент,
		|	ВременнаяТаблицаИмуществоГотовая.ОтраслевойКоэффициент КАК ОтраслевойКоэффициент,
		|	ВременнаяТаблицаИмуществоГотовая.Ставка КАК Ставка
		|ИЗ
		|	ВременнаяТаблицаИмуществоГотовая КАК ВременнаяТаблицаИмуществоГотовая
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИнвентарныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ		
		|	10 КАК Порядок,
		|	СведенияПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияПоНалогуНаИмущество.СуммаНалога КАК СуммаНалога,
		|	ВременнаяТаблицаИмуществоГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаИмуществоГотовая.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаИмуществоГотовая.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцев,
		|	ВременнаяТаблицаИмуществоГотовая.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаИмуществоГотовая.РегиональныйКоэффициент КАК Кр,
		|	ВременнаяТаблицаИмуществоГотовая.ЗональныйКоэффициент КАК Кз,
		|	ВременнаяТаблицаИмуществоГотовая.ОтраслевойКоэффициент КАК Ко,
		|	ВременнаяТаблицаИмуществоГотовая.Ставка КАК Ставка,
		|	ТаблицаИмущество.ДатаВвода КАК ДатаВводаВЭксплуатацию,
		|	ТаблицаИмущество.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ТаблицаИмущество.НежилаяПлощадь КАК НежилаяПлощадь,
		|	ТаблицаИмущество.МатериалСтен КАК МатериалСтен,
		|	ТаблицаИмущество.КодНазначенияИмущества КАК КодНазначенияИмущества,
		|	ТаблицаИмущество.КодПользователяИмущества КАК КодПользователяИмущества,
		|	ТаблицаИмущество.КодИмущества КАК КодИмущества,
		|	ТаблицаИмущество.КатегорияОбъектаИмущества КАК КатегорияОбъектаИмущества
		|ИЗ
		|	РегистрСведений.СведенияПоНалогуНаИмущество КАК СведенияПоНалогуНаИмущество
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаИмуществоГотовая КАК ВременнаяТаблицаИмуществоГотовая
		|		ПО СведенияПоНалогуНаИмущество.ОсновноеСредство = ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаИмущество КАК ТаблицаИмущество
		|		ПО СведенияПоНалогуНаИмущество.ОсновноеСредство = ТаблицаИмущество.ОсновноеСредство
		|ГДЕ
		|	СведенияПоНалогуНаИмущество.Год = &КонецПериода
		|	И СведенияПоНалогуНаИмущество.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	11 КАК Порядок,
		|	СведенияОТранспортеСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияОТранспортеСрезПоследних.ГруппаИмущества КАК ГруппаИмущества,
		|	СведенияОТранспортеСрезПоследних.ВидТранспорта КАК ВидТранспорта,
		|	СведенияОТранспортеСрезПоследних.ВидТранспорта.КатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	СведенияОТранспортеСрезПоследних.ОбъемДвигателя КАК ОбъемДвигателя,
		|	СведенияОТранспортеСрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	СведенияОТранспортеСрезПоследних.МаркаТранспортногоСредства КАК МаркаТранспортногоСредства,
		|	ГОД(&КонецПериода) - ГОД(ВременнаяТаблицаСостоянияОС.ПериодПринятоКУчету) КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаСостоянияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспорт
		|ИЗ
		|	ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОТранспорте.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСостоянияОС.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС)) КАК СведенияОТранспортеСрезПоследних
		|		ПО ВременнаяТаблицаСостоянияОС.ОсновноеСредство = СведенияОТранспортеСрезПоследних.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	12 КАК Порядок,
		|	ШкалаОценкиСтоимостиМакс.ОсновноеСредство КАК ОсновноеСредство,
		|	ШкалаОценкиСтоимости.Ставка КАК ОценочнаяСтоимость
		|ПОМЕСТИТЬ ВременнаяТаблицаОценкиСтоимостиТранспорт
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|		ВременнаяТаблицаТранспорт.ВидТранспорта КАК ВидТранспорта,
		|		МАКСИМУМ(ШкалаОценкиСтоимости.НижняяГраница) КАК НижняяГраница
		|	ИЗ
		|		ВременнаяТаблицаТранспорт КАК ВременнаяТаблицаТранспорт
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыТранспортаНалогНаИмущество.ШкалаОценкиСтоимости КАК ШкалаОценкиСтоимости
		|			ПО ВременнаяТаблицаТранспорт.ВидТранспорта = ШкалаОценкиСтоимости.Ссылка
		|				И ВременнаяТаблицаТранспорт.КоличествоЛетЭксплуатации >= ШкалаОценкиСтоимости.НижняяГраница
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВременнаяТаблицаТранспорт.ОсновноеСредство,
		|		ВременнаяТаблицаТранспорт.ВидТранспорта) КАК ШкалаОценкиСтоимостиМакс
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыТранспортаНалогНаИмущество.ШкалаОценкиСтоимости КАК ШкалаОценкиСтоимости
		|		ПО ШкалаОценкиСтоимостиМакс.ВидТранспорта = ШкалаОценкиСтоимости.Ссылка
		|			И ШкалаОценкиСтоимостиМакс.НижняяГраница = ШкалаОценкиСтоимости.НижняяГраница
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	13 КАК Порядок,
		|	ХозрасчетныйОстатки.Счет КАК Счет,
		|	ХозрасчетныйОстатки.Субконто1 КАК ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстатки.СуммаОстаток < 0
		|			ТОГДА ХозрасчетныйОстатки.СуммаОстаток * -1
		|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
		|	КОНЕЦ КАК НакопленныйИзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНакопленнаяАмортизация
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&КонецПериода,
		|			Счет В (&СписокСчетовНакопленнойАмортизации),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСостоянияОС.ОсновноеСредство
		|					ИЗ
		|						ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС)) КАК ХозрасчетныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	14 КАК Порядок,
		|	ВременнаяТаблицаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаТранспорт.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаТранспорт.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаТранспорт.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаТранспорт.ВидТранспорта КАК ВидТранспорта,
		|	ВременнаяТаблицаТранспорт.ВидТранспортаКатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	ВременнаяТаблицаТранспорт.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ВременнаяТаблицаТранспорт.МаркаТранспортногоСредства КАК МаркаТранспортногоСредства,
		|	ЕСТЬNULL(ВременнаяТаблицаОценкиСтоимости.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаТранспорт.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаТранспорт.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ЕСТЬNULL(ВременнаяТаблицаНакопленнаяАмортизация.НакопленныйИзнос, 0) КАК НакопленныйИзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспортГотовая
		|ИЗ
		|	ВременнаяТаблицаТранспорт КАК ВременнаяТаблицаТранспорт
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОценкиСтоимостиТранспорт КАК ВременнаяТаблицаОценкиСтоимости
		|		ПО ВременнаяТаблицаТранспорт.ОсновноеСредство = ВременнаяТаблицаОценкиСтоимости.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНакопленнаяАмортизация КАК ВременнаяТаблицаНакопленнаяАмортизация
		|		ПО ВременнаяТаблицаТранспорт.ОсновноеСредство = ВременнаяТаблицаНакопленнаяАмортизация.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	15 КАК Порядок,
		|	ВременнаяТаблицаТранспортГотовая.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаТранспортГотовая.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаТранспортГотовая.ОсновноеСредствоНаименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаТранспортГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаТранспортГотовая.ВидТранспорта КАК ВидТранспорта,
		|	ВременнаяТаблицаТранспортГотовая.ВидТранспортаКатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	ВременнаяТаблицаТранспортГотовая.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ВременнаяТаблицаТранспортГотовая.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаТранспортГотовая.ОценочнаяСтоимость КАК Ставка,
		|	ВременнаяТаблицаТранспортГотовая.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаТранспортГотовая.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаТранспортГотовая.НакопленныйИзнос КАК НакопленныйИзнос
		|ИЗ
		|	ВременнаяТаблицаТранспортГотовая КАК ВременнаяТаблицаТранспортГотовая
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИнвентарныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	16 КАК Порядок,
		|	СведенияПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияПоНалогуНаИмущество.СуммаНалога КАК СуммаНалога,
		|	ВременнаяТаблицаТранспортГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаТранспортГотовая.ВидТранспорта КАК ВидТранспорта,
		|	ВременнаяТаблицаТранспортГотовая.ВидТранспортаКатегорияАБВ КАК АБВ,
		|	ВременнаяТаблицаТранспортГотовая.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ВременнаяТаблицаТранспортГотовая.ОценочнаяСтоимость КАК Ставка,
		|	ВременнаяТаблицаТранспортГотовая.МаркаТранспортногоСредства КАК МаркаТранспортногоСредства
		|ИЗ
		|	РегистрСведений.СведенияПоНалогуНаИмущество КАК СведенияПоНалогуНаИмущество
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТранспортГотовая КАК ВременнаяТаблицаТранспортГотовая
		|		ПО СведенияПоНалогуНаИмущество.ОсновноеСредство = ВременнаяТаблицаТранспортГотовая.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаТранспорт КАК ТаблицаТранспорт
		|		ПО СведенияПоНалогуНаИмущество.ОсновноеСредство = ТаблицаТранспорт.ОсновноеСредство
		|ГДЕ
		|	СведенияПоНалогуНаИмущество.Год = &КонецПериода
		|	И СведенияПоНалогуНаИмущество.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновноеСредство";
	Запрос.УстановитьПараметр("Организация", Организация);			   	   
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Дата));			   
	Запрос.УстановитьПараметр("КонецПериода", Дата);
	
	СписокСчетовНакопленнойАмортизации = Новый СписокЗначений;
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияЗданияСооружения);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияОборудование);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияКонторскоеОборудование);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияМебельИПринадлежности);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияТранспортныеСредства);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияБлагоустройствоАрендованнойСобственности);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияБлагоустройствоЗемльныхУчастков);
	Запрос.УстановитьПараметр("СписокСчетовНакопленнойАмортизации",СписокСчетовНакопленнойАмортизации);
	
	Возврат Запрос.ВыполнитьПакет();
КонецФункции
	
#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли
