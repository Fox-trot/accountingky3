#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Выполнить закрытие месяца
//
Процедура ВыполнитьЗакрытиеМесяца(СтруктураПараметров, АдресХранилища) Экспорт
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") 
		И СтруктураПараметров.Свойство("Ссылка") Тогда 
		ДокументОбъект = СтруктураПараметров.Ссылка.ПолучитьОбъект();
	Иначе 
		Возврат;
	КонецЕсли;	
	
	ДокументОбъект.АмортизацияОС.Очистить();
	ДокументОбъект.Недвижимость.Очистить();
	ДокументОбъект.Транспорт.Очистить();
	ДокументОбъект.ПереоценкаВалюты.Очистить();
	ДокументОбъект.АвансыДоотгрузкаРасшифровка.Очистить();
	ДокументОбъект.АвансыДоотгрузка.Очистить();
	ДокументОбъект.ПарныеСчетаУчета.Очистить();
	ДокументОбъект.ВременныеСчетаУчета.Очистить();
	
	Если ДокументОбъект.Проведен Тогда
		ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);		
	КонецЕсли;	

	ДокументОбъект.ВыполнитьЗакрытиеМесяца();

	АдресХранилища = ПоместитьВоВременноеХранилище(ДокументОбъект.Ссылка, АдресХранилища);
КонецПроцедуры 

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения	

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Дата          - Дата - дата документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа.
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "") Экспорт
	
	ДанныеДокумента = Новый Структура("Номер,Дата,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа.
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = СтрНайти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокумента.Номер);
	Возврат СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),
		НазваниеДокумента, НомерНаПечать, Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	
	КонецФункции
	
#КонецОбласти

#Область ПроцедурыПроведенияДокумента

Функция СформироватьТаблицаХозрасчетныйНалогНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период КАК Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоНалогам) КАК СчетДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаИмущество) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога) КАК Сумма,
	|	""НИ: Недвижимость"" КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоНалогам),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаТранспортныеСредства),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ЗакрытиеМесяцаТранспорт.СуммаНалога),
	|	""НИ: Транспорт""
	|ИЗ
	|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗакрытиеМесяцаТранспорт.СуммаНалога <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Дата", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);   		
	Возврат Запрос.Выполнить().Выгрузить();    
	
КонецФункции

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаШапка.Период КАК Период,
	|	ТаблицаШапка.Организация КАК Организация,
	|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации.СчетЗатрат КАК СчетДт,
	|	ТаблицаДокумента.СчетУчета.ПарныйСчет КАК СчетКт,
	|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации.Субконто1 КАК СубконтоДт1,
	|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации.Субконто2 КАК СубконтоДт2,
	|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации.Субконто3 КАК СубконтоДт3,
	|	ТаблицаДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	""Начисление амортизации"" КАК Содержание,
	|	0 КАК КоличествоДт,
	|	0 КАК КоличествоКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК ВалютнаяСуммаКт
	|ИЗ
	|	ВременнаяТаблицаАмортизацияОС КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДокумента.Сумма = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Организация,
	|	ТаблицаДокумента.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте),
	|	ТаблицаДокумента.Субконто1,
	|	ТаблицаДокумента.Субконто2,
	|	ТаблицаДокумента.Субконто3,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.СуммаДоход,
	|	""Доход от курсовых разниц"",
	|	0,
	|	0,
	|	ТаблицаДокумента.Валюта,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаПереоценкаВалюты КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДокумента.СуммаДоход = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте),
	|	ТаблицаДокумента.СчетУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.Субконто1,
	|	ТаблицаДокумента.Субконто2,
	|	ТаблицаДокумента.Субконто3,
	|	ТаблицаДокумента.СуммаУбыток,
	|	""Убытки от курсовых разниц"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.Валюта,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаПереоценкаВалюты КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДокумента.СуммаУбыток = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНаАвансы),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
	|	ТаблицаАвансы.Контрагент,
	|	ТаблицаАвансы.Договор,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаАвансы.НДС,
	|	""Авансы полученные"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаАвансыДоотгрузка КАК ТаблицаАвансы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаАвансы.Аванс = ЗНАЧЕНИЕ(Перечисление.ВидыАвансовОтгрузки.Аванс)
	|	И ТаблицаАвансы.НДС <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНаАвансы),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
	|	ТаблицаДоотгрузка.Контрагент,
	|	ТаблицаДоотгрузка.Договор,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	-ТаблицаДоотгрузка.НДС,
	|	""Отгрузка в счет аванса"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаАвансыДоотгрузка КАК ТаблицаДоотгрузка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДоотгрузка.Аванс = ЗНАЧЕНИЕ(Перечисление.ВидыАвансовОтгрузки.Аванс)
	|	И ТаблицаДоотгрузка.НДС <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНДС.Сумма059 < ВременнаяТаблицаНДС.Сумма064
	|			ТОГДА ВременнаяТаблицаНДС.Сумма059
	|		ИНАЧЕ ВременнаяТаблицаНДС.Сумма064
	|	КОНЕЦ,
	|	""НДС в зачет"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаНДС КАК ВременнаяТаблицаНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВременнаяТаблицаНДС.Сумма059 <> 0
	|	И ВременнаяТаблицаНДС.Сумма064 <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
	|	ТаблицаДокумента.СчетУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.СуммаДт,
	|	""Закрытие временных счетов"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаВременныеСчетаУчета КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДокумента.СуммаДт = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ТаблицаДокумента.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.СуммаКт,
	|	""Закрытие временных счетов"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаВременныеСчетаУчета КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДокумента.СуммаКт = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	-ТаблицаДокумента.Сумма5300,
	|	""Закрытие 5300"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаВременные5300 КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаДокумента.Сумма5300 < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.Сумма5300,
	|	""Закрытие 5300"",
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	ВременнаяТаблицаВременные5300 КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаДокумента.Сумма5300 > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	11,
	|	ТаблицаШапка.Период,
	|	ТаблицаШапка.Организация,
	|	ТаблицаДокумента.СчетДт,
	|	ТаблицаДокумента.СчетКт,
	|	ТаблицаДокумента.Субконто1,
	|	ТаблицаДокумента.Субконто2,
	|	ТаблицаДокумента.Субконто3,
	|	ТаблицаДокумента.Субконто1,
	|	ТаблицаДокумента.Субконто2,
	|	ТаблицаДокумента.Субконто3,
	|	ТаблицаДокумента.Сумма,
	|	""Закрытие парных счетов"",
	|	0,
	|	0,
	|	ТаблицаДокумента.Валюта,
	|	ТаблицаДокумента.Валюта,
	|	ТаблицаДокумента.ВалютнаяСумма,
	|	ТаблицаДокумента.ВалютнаяСумма
	|ИЗ
	|	ВременнаяТаблицаПарныеСчетаУчета КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаДокумента.Сумма = 0";
	
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СформироватьТаблицаХозрасчетныйНалогНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства), 
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаХозрасчетный);
	
КонецПроцедуры      

Процедура СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ВременнаяТаблицаАвансы.Контрагент,
	|	ВременнаяТаблицаАвансы.Договор,
	|	ВременнаяТаблицаАвансы.ДокументАванса КАК ДокументА,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаАвансы.Аванс = ЗНАЧЕНИЕ(Перечисление.ВидыАвансовОтгрузки.Аванс)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ВременнаяТаблицаАвансы.Сумма КАК Сумма,
	|	ВременнаяТаблицаАвансы.НДС КАК СуммаНДС,
	|	ВременнаяТаблицаАвансы.НСП КАК СуммаНСП,
	|	ВременнаяТаблицаАвансы.ДокументОплаты КАК ДокументОтгр,
	|	ВЫРАЗИТЬ(ВременнаяТаблицаАвансы.Договор КАК Справочник.ДоговорыКонтрагентов).СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	ВременнаяТаблицаАвансыДоотгрузка КАК ВременнаяТаблицаАвансы,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыДоотгрузка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицаСведенияПоНалогуНаимущество(ДокументСсылка, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД) КАК Год,
	|	ВременнаяТаблицаШапка.Организация КАК Организация,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство,
	|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества,
	|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцев КАК КоличествоМесяцев,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.СуммаНалога) КАК СуммаНалога,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ОценочнаяСтоимость) КАК ОценочнаяСтоимость,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ЖилаяПлощадь) КАК ЖилаяПлощадь,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.НежилаяПлощадь) КАК НежилаяПлощадь,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ОбщаяПлощадь) КАК ОбщаяПлощадь,
	|	ЗакрытиеМесяцаНедвижимость.Кр,
	|	ЗакрытиеМесяцаНедвижимость.Кз,
	|	ЗакрытиеМесяцаНедвижимость.Ко,
	|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ,
	|	ЗакрытиеМесяцаНедвижимость.МатериалСтен,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ЗемНалог) КАК СуммаЗемельногоНалога,
	|	ЗакрытиеМесяцаНедвижимость.Ставка КАК СтавкаНалога,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.МестонахождениеИмущества КАК МестоНахожденияИмущества,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.ИдентификационныйНомерИмущества КАК ИдентификационныйКодИмущества,
	|	ЗакрытиеМесяцаНедвижимость.ОтЛет,
	|	ЗакрытиеМесяцаНедвижимость.ДоЛет,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	NULL КАК АБВ,
	|	NULL КАК ОбъемДвигателя
	|ИЗ
	|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИмуществе.СрезПоследних(&Дата, ) КАК СведенияОбИмуществеСрезПоследних
	|		ПО ЗакрытиеМесяцаНедвижимость.ОсновноеСредство = СведенияОбИмуществеСрезПоследних.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества,
	|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцев,
	|	ЗакрытиеМесяцаНедвижимость.Кр,
	|	ЗакрытиеМесяцаНедвижимость.Кз,
	|	ЗакрытиеМесяцаНедвижимость.Ко,
	|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ,
	|	ЗакрытиеМесяцаНедвижимость.Ставка,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.МестонахождениеИмущества,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.ИдентификационныйНомерИмущества,
	|	ЗакрытиеМесяцаНедвижимость.МатериалСтен,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
	|	ЗакрытиеМесяцаНедвижимость.ОтЛет,
	|	ЗакрытиеМесяцаНедвижимость.ДоЛет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
	|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества,
	|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцев,
	|	СУММА(ЗакрытиеМесяцаТранспорт.СуммаНалога),
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ЗакрытиеМесяцаТранспорт.Ставка,
	|	NULL,
	|	NULL,
	|	ЗакрытиеМесяцаТранспорт.ОтЛет,
	|	ЗакрытиеМесяцаТранспорт.ДоЛет,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	ЗакрытиеМесяцаТранспорт.АБВ,
	|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя
	|ИЗ
	|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИмуществе.СрезПоследних(&Дата, ) КАК СведенияОбИмуществеСрезПоследних
	|		ПО ЗакрытиеМесяцаТранспорт.ОсновноеСредство = СведенияОбИмуществеСрезПоследних.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Организация,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя,
	|	ЗакрытиеМесяцаТранспорт.Ставка,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	ЗакрытиеМесяцаТранспорт.АБВ,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцев,
	|	ВременнаяТаблицаШапка.Период,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества,
	|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
	|	ЗакрытиеМесяцаТранспорт.ОтЛет,
	|	ЗакрытиеМесяцаТранспорт.ДоЛет";

	Запрос.УстановитьПараметр("Дата", ДокументСсылка.Дата);
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияПоНалогуНаимущество", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт 		
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.Дата КАК Период,
		|	ТаблицаДокумента.Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство,
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.СчетАмортизации,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.СчетЗатрат,
		|	ТаблицаДокумента.Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаАмортизацияОС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АмортизацияОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.СуммаДт,
		|	ТаблицаДокумента.СуммаКт
		|ПОМЕСТИТЬ ВременнаяТаблицаВременныеСчетаУчета
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ВременныеСчетаУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ТаблицаДокумента.СуммаКт <> 0
		|				ТОГДА ТаблицаДокумента.СуммаКт
		|			КОГДА ТаблицаДокумента.СуммаДт <> 0
		|				ТОГДА -ТаблицаДокумента.СуммаДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма5300
		|ПОМЕСТИТЬ ВременнаяТаблицаВременные5300
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ВременныеСчетаУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Ссылка.РасчетНераспределеннойПрибыли
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетУчета,
		|	ТаблицаДокумента.Валюта,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.СуммаДоход,
		|	ТаблицаДокумента.СуммаУбыток
		|ПОМЕСТИТЬ ВременнаяТаблицаПереоценкаВалюты
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ПереоценкаВалюты КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаАвансы.Контрагент,
		|	ЗакрытиеМесяцаАвансы.Договор,
		|	ЗакрытиеМесяцаАвансы.ДокументАванса,
		|	ЗакрытиеМесяцаАвансы.Сумма,
		|	ЗакрытиеМесяцаАвансы.НДС,
		|	ЗакрытиеМесяцаАвансы.НСП,
		|	ЗакрытиеМесяцаАвансы.Аванс,
		|	ЗакрытиеМесяцаАвансы.ДокументОплаты,
		|	ЗакрытиеМесяцаАвансы.СуммаПогашения
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансыДоотгрузка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АвансыДоотгрузкаРасшифровка КАК ЗакрытиеМесяцаАвансы
		|ГДЕ
		|	ЗакрытиеМесяцаАвансы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЗакрытиеМесяцаНДС.Строка = ""059""
		|				ТОГДА ЗакрытиеМесяцаНДС.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма059,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЗакрытиеМесяцаНДС.Строка = ""064""
		|				ТОГДА ЗакрытиеМесяцаНДС.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма064
		|ПОМЕСТИТЬ ВременнаяТаблицаНДС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НДС КАК ЗакрытиеМесяцаНДС
		|ГДЕ
		|	ЗакрытиеМесяцаНДС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаПарныеСчетаУчета.СчетДт,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.СчетКт,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто1,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто2,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто3,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Валюта,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Сумма,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.ВалютнаяСумма
		|ПОМЕСТИТЬ ВременнаяТаблицаПарныеСчетаУчета
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ПарныеСчетаУчета КАК ЗакрытиеМесяцаПарныеСчетаУчета
		|ГДЕ
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка,
		|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство,
		|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества,
		|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцев,
		|	ЗакрытиеМесяцаНедвижимость.Ставка,
		|	ЗакрытиеМесяцаНедвижимость.СуммаНалога,
		|	ЗакрытиеМесяцаНедвижимость.ОценочнаяСтоимость,
		|	ЗакрытиеМесяцаНедвижимость.СрокЭксплуатации,
		|	ЗакрытиеМесяцаНедвижимость.ОбщаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.Кр,
		|	ЗакрытиеМесяцаНедвижимость.Кз,
		|	ЗакрытиеМесяцаНедвижимость.Ко,
		|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ,
		|	ЗакрытиеМесяцаНедвижимость.МатериалСтен,
		|	ЗакрытиеМесяцаНедвижимость.ЗемНалог,
		|	ЗакрытиеМесяцаНедвижимость.ОтЛет,
		|	ЗакрытиеМесяцаНедвижимость.ДоЛет,
		|	ЗакрытиеМесяцаНедвижимость.ЖилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НежилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога,
		|	ЗакрытиеМесяцаНедвижимость.Объект
		|ПОМЕСТИТЬ ВременнаяТаблицаНедвижимость
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Недвижимость КАК ЗакрытиеМесяцаНедвижимость
		|ГДЕ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаТранспорт.Ссылка,
		|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
		|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества,
		|	ЗакрытиеМесяцаТранспорт.АБВ,
		|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцев,
		|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя,
		|	ЗакрытиеМесяцаТранспорт.Ставка,
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога,
		|	ЗакрытиеМесяцаТранспорт.ОтЛет,
		|	ЗакрытиеМесяцаТранспорт.ДоЛет
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспорт
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Транспорт КАК ЗакрытиеМесяцаТранспорт
		|ГДЕ
		|	ЗакрытиеМесяцаТранспорт.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();    	
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияПоНалогуНаимущество(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#КонецЕсли

