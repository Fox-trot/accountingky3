#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Функция СформироватьТаблицаХозрасчетныйНалогНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период КАК Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоНалогам) КАК СчетДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаИмущество) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога) КАК Сумма,
	|	""НИ: Недвижимость"" КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыПоНалогам),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаТранспортныеСредства),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ЗакрытиеМесяцаТранспорт.СуммаНалога),
	|	""НИ: Транспорт""
	|ИЗ
	|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗакрытиеМесяцаТранспорт.СуммаНалога <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Период,
	|	ВременнаяТаблицаШапка.Организация";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Дата", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);   		
	Возврат Запрос.Выполнить().Выгрузить();    
	
КонецФункции

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ТаблицаШапка.Период КАК Период,
		|	ТаблицаШапка.Организация КАК Организация,
		|	ТаблицаДокумента.СчетЗатрат КАК СчетДт,
		|	ТаблицаДокумента.СчетУчета.ПарныйСчет КАК СчетКт,
		|	ТаблицаДокумента.Субконто1 КАК СубконтоДт1,
		|	ТаблицаДокумента.Субконто2 КАК СубконтоДт2,
		|	ТаблицаДокумента.Субконто3 КАК СубконтоДт3,
		|	ТаблицаДокумента.ОсновноеСредство КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	""Начисление амортизации"" КАК Содержание,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК ВалютнаяСуммаКт
		|ИЗ
		|	ВременнаяТаблицаАмортизацияОС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте),
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаДоход,
		|	""Доход от курсовых разниц"",
		|	0,
		|	0,
		|	ТаблицаДокумента.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаПереоценкаВалюты КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаДоход = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте),
		|	ТаблицаДокумента.СчетУчета,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.СуммаУбыток,
		|	""Убытки от курсовых разниц"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Валюта,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаПереоценкаВалюты КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаУбыток = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНаАвансы),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
		|	ТаблицаАвансы.Контрагент,
		|	ТаблицаАвансы.Договор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаАвансы.СуммаНДС,
		|	""Авансы полученные"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаАвансы КАК ТаблицаАвансы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаАвансы.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНаАвансы),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
		|	ТаблицаДоотгрузка.Контрагент,
		|	ТаблицаДоотгрузка.Договор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	-ТаблицаДоотгрузка.СуммаНДС,
		|	""Отгрузка в счет аванса"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаДоотгрузкаРасшифровка КАК ТаблицаДоотгрузка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДоотгрузка.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНДС.Сумма059 < ВременнаяТаблицаНДС.Сумма064
		|			ТОГДА ВременнаяТаблицаНДС.Сумма059
		|		ИНАЧЕ ВременнаяТаблицаНДС.Сумма064
		|	КОНЕЦ,
		|	""НДС в зачет"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаНДС КАК ВременнаяТаблицаНДС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНДС.Сумма059 <> 0
		|	И ВременнаяТаблицаНДС.Сумма064 <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ТаблицаДокумента.СчетКт,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.Содержание,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаКорректировкаНДС КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0		
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	ТаблицаДокумента.СчетУчета,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаДт,
		|	""Закрытие временных счетов"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременныеСчетаУчета КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаДт = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаКт,
		|	""Закрытие временных счетов"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременныеСчетаУчета КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаКт = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	-ТаблицаДокумента.Сумма5300,
		|	""Закрытие 5300"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременные5300 КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.Сумма5300 < 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	10,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма5300,
		|	""Закрытие 5300"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаВременные5300 КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаДокумента.Сумма5300 > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	11,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ТаблицаДокумента.СчетКт,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.Субконто1,
		|	ТаблицаДокумента.Субконто2,
		|	ТаблицаДокумента.Субконто3,
		|	ТаблицаДокумента.Сумма,
		|	""Закрытие парных счетов"",
		|	0,
		|	0,
		|	ТаблицаДокумента.Валюта,
		|	ТаблицаДокумента.Валюта,
		|	ТаблицаДокумента.ВалютнаяСумма,
		|	ТаблицаДокумента.ВалютнаяСумма
		|ИЗ
		|	ВременнаяТаблицаПарныеСчетаУчета КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	12,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДт,
		|	ТаблицаДокумента.СчетКт,
		|	ТаблицаДокумента.СубконтоДт1,
		|	ТаблицаДокумента.СубконтоДт2,
		|	ТаблицаДокумента.СубконтоДт3,
		|	ТаблицаДокумента.СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Сумма,
		|	""Расходы БП за "" + ТаблицаШапка.ПредставлениеПериода,
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаРасходыБудущихПериодов КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.Сумма = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	13,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВспомогательноеПроизводство),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Заказ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаВспомогательногоПроизводства,
		|	""Вспомогательное производство"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаВспомогательногоПроизводства = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	14,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НакладныеРасходы),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Заказ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаОбщехозяйственныхРасходов,
		|	""Накладные расходы"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаОбщехозяйственныхРасходов = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	15,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция),
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство),
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	ТаблицаДокумента.Заказ,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Заказ,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.СуммаНЗП,
		|	""Дозакрытие прямых затрат"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаНЗП = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	16,
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаДокумента.СчетДтРеализации,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Склад,
		|	ТаблицаДокумента.Заказ,
		|	ТаблицаДокумента.СуммаКорректировкиРеализации,
		|	""Дозакрытие суммы реализованной ГП"",
		|	0,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОХРиВП КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаДокумента.СуммаКорректировкиРеализации = 0";	
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	// Если рассчитывается налог на прибыль, то делаются дополнительные проводки.
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаШапка.РасчетНалогаНаПрибыль
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ТаблицаШапка";
	ДанныеШапки = Запрос.Выполнить().Выбрать();
	ДанныеШапки.Следующий();
	
	Если ДанныеШапки.РасчетНалогаНаПрибыль Тогда
		
		ДатаДокумента = ДанныеШапки.Период;
		Организация = ДанныеШапки.Организация;
		                                        
		СформироватьНалогНаПрибыльВТаблицеХозрасчетный(ДатаДокумента, Организация, РезультатЗапроса, СтруктураДополнительныеСвойства);		
	КонецЕсли;
		
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СформироватьТаблицаХозрасчетныйНалогНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства), 
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаХозрасчетный);
	
КонецПроцедуры      

Процедура СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДоотгрузка.Контрагент КАК Контрагент,
		|	ТаблицаДоотгрузка.Договор КАК Договор,
		|	ТаблицаДоотгрузка.ДокументАванса КАК ДокументА,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаДоотгрузка.Сумма КАК Сумма,
		|	ТаблицаДоотгрузка.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДоотгрузка.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДоотгрузка.ДокументОплаты КАК ДокументОтгр,
		|	ВЫРАЗИТЬ(ТаблицаДоотгрузка.Договор КАК Справочник.ДоговорыКонтрагентов).СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаАвасыДоотгрузка
		|ИЗ
		|	ВременнаяТаблицаДоотгрузкаРасшифровка КАК ТаблицаДоотгрузка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаАвансы.Контрагент,
		|	ТаблицаАвансы.Договор,
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход),
		|	ТаблицаАвансы.Сумма,
		|	ТаблицаАвансы.СуммаНДС,
		|	ТаблицаАвансы.СуммаНСП,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫРАЗИТЬ(ТаблицаАвансы.Договор КАК Справочник.ДоговорыКонтрагентов).СтавкаНДС
		|ИЗ
		|	ВременнаяТаблицаАвансы КАК ТаблицаАвансы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаШапка.Период,
		|	ТаблицаШапка.Организация,
		|	ТаблицаАвасыДоотгрузка.Контрагент,
		|	ТаблицаАвасыДоотгрузка.Договор,
		|	ТаблицаАвасыДоотгрузка.ДокументА,
		|	ТаблицаАвасыДоотгрузка.ВидДвижения,
		|	ТаблицаАвасыДоотгрузка.Сумма,
		|	ТаблицаАвасыДоотгрузка.СуммаНДС,
		|	ТаблицаАвасыДоотгрузка.СуммаНСП,
		|	ТаблицаАвасыДоотгрузка.ДокументОтгр,
		|	ТаблицаАвасыДоотгрузка.СтавкаНДС
		|ИЗ
		|	ВременнаяТаблицаАвасыДоотгрузка КАК ТаблицаАвасыДоотгрузка
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыДоотгрузка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицаСведенияПоНалогуНаимущество(ДокументСсылка, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД) КАК Год,
	|	ВременнаяТаблицаШапка.Организация КАК Организация,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство,
	|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества,
	|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцев КАК КоличествоМесяцев,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.СуммаНалога) КАК СуммаНалога,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ОценочнаяСтоимость) КАК ОценочнаяСтоимость,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ЖилаяПлощадь) КАК ЖилаяПлощадь,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.НежилаяПлощадь) КАК НежилаяПлощадь,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ОбщаяПлощадь) КАК ОбщаяПлощадь,
	|	ЗакрытиеМесяцаНедвижимость.Кр,
	|	ЗакрытиеМесяцаНедвижимость.Кз,
	|	ЗакрытиеМесяцаНедвижимость.Ко,
	|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ,
	|	ЗакрытиеМесяцаНедвижимость.МатериалСтен,
	|	СУММА(ЗакрытиеМесяцаНедвижимость.ЗемНалог) КАК СуммаЗемельногоНалога,
	|	ЗакрытиеМесяцаНедвижимость.Ставка КАК СтавкаНалога,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.МестонахождениеИмущества КАК МестоНахожденияИмущества,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.ИдентификационныйНомерИмущества КАК ИдентификационныйКодИмущества,
	|	ЗакрытиеМесяцаНедвижимость.ОтЛет,
	|	ЗакрытиеМесяцаНедвижимость.ДоЛет,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	NULL КАК АБВ,
	|	NULL КАК ОбъемДвигателя
	|ИЗ
	|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИмуществе.СрезПоследних(&Дата, ) КАК СведенияОбИмуществеСрезПоследних
	|		ПО ЗакрытиеМесяцаНедвижимость.ОсновноеСредство = СведенияОбИмуществеСрезПоследних.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство,
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества,
	|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцев,
	|	ЗакрытиеМесяцаНедвижимость.Кр,
	|	ЗакрытиеМесяцаНедвижимость.Кз,
	|	ЗакрытиеМесяцаНедвижимость.Ко,
	|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ,
	|	ЗакрытиеМесяцаНедвижимость.Ставка,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.МестонахождениеИмущества,
	|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство.ИдентификационныйНомерИмущества,
	|	ЗакрытиеМесяцаНедвижимость.МатериалСтен,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
	|	ЗакрытиеМесяцаНедвижимость.ОтЛет,
	|	ЗакрытиеМесяцаНедвижимость.ДоЛет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
	|	ВременнаяТаблицаШапка.Организация,
	|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
	|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества,
	|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцев,
	|	СУММА(ЗакрытиеМесяцаТранспорт.СуммаНалога),
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ЗакрытиеМесяцаТранспорт.Ставка,
	|	NULL,
	|	NULL,
	|	ЗакрытиеМесяцаТранспорт.ОтЛет,
	|	ЗакрытиеМесяцаТранспорт.ДоЛет,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	ЗакрытиеМесяцаТранспорт.АБВ,
	|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя
	|ИЗ
	|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИмуществе.СрезПоследних(&Дата, ) КАК СведенияОбИмуществеСрезПоследних
	|		ПО ЗакрытиеМесяцаТранспорт.ОсновноеСредство = СведенияОбИмуществеСрезПоследних.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаШапка.Организация,
	|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества,
	|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию,
	|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя,
	|	ЗакрытиеМесяцаТранспорт.Ставка,
	|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества,
	|	ЗакрытиеМесяцаТранспорт.АБВ,
	|	СведенияОбИмуществеСрезПоследних.КодИмущества,
	|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцев,
	|	ВременнаяТаблицаШапка.Период,
	|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества,
	|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества,
	|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
	|	ЗакрытиеМесяцаТранспорт.ОтЛет,
	|	ЗакрытиеМесяцаТранспорт.ДоЛет";

	Запрос.УстановитьПараметр("Дата", ДокументСсылка.Дата);
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияПоНалогуНаимущество", РезультатЗапроса.Выгрузить());
КонецПроцедуры

Процедура СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Период КАК Год,
		|	ВременнаяТаблицаНалоговаяАмортизация.ГруппаНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ОсновноеСредство,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьНаНГ КАК БалансоваяСтоимостьНаНачалоГода,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимости КАК СуммаПоступления,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ПоступлениеПГ КАК СуммаПоступленияОстаток,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияОбщая КАК СуммаВыбытия,		
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ВыбытиеПГ КАК СуммаВыбытияОстаток,
		|	ВременнаяТаблицаНалоговаяАмортизация.ЗатратыНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.НормаНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.Превышение КАК ПревышениеЗатратНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимость КАК БалансоваяСтоимостьНаКонецГода,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьИтого,
		|	ВременнаяТаблицаНалоговаяАмортизация.НормаАО КАК НАО,
		|	ВременнаяТаблицаНалоговаяАмортизация.Амортизация КАК АмортизацияЗаГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСОД,
		|	ВременнаяТаблицаНалоговаяАмортизация.ВычетМеньше100РП,
		|	ВременнаяТаблицаНалоговаяАмортизация.ДатаПринятияКУчету,
		|	ВременнаяТаблицаНалоговаяАмортизация.КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаНалоговаяАмортизация.БазаПрямолинейнойАмортизации
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоговаяАмортизация КАК ВременнаяТаблицаНалоговаяАмортизация
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяАмортизация", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаНалоговаяВыверка(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Период КАК Год,
		|	ВременнаяТаблицаНалоговаяВыверка.НомерСтроки КАК Строка,
		|	ВременнаяТаблицаНалоговаяВыверка.Сумма,
		|	ВременнаяТаблицаНалоговаяВыверка.НаименованиеСтроки
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоговаяВыверка КАК ВременнаяТаблицаНалоговаяВыверка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяВыверка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Процедура для формирования дополнительных проводок в РБ по налогу на прибыль
//
// Параметры:
//  ДатаДокумента  - Дата - дата документа.
//  Организация  - СправочникСсылка.Организация - организация документа.
//  ТЗХозрасчетный  - ТаблицаЗначений - данные основного запроса из данного документа для записи в РБ.
//  СтруктураДополнительныеСвойства  - Структура - структура дополнительных свойств.
//
Процедура СформироватьНалогНаПрибыльВТаблицеХозрасчетный(ДатаДокумента, Организация, ТЗХозрасчетный, СтруктураДополнительныеСвойства)

	// Из ТЧ "Налоговая выверка" выбираются определенные данные для дальнейших расчетов.
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Сумма,
		|	ТаблицаДокумента.НаименованиеСтроки,
		|	ТаблицаДокумента.НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаНалоговаяВыверка КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.НомерСтроки = 7
		|	ИЛИ ТаблицаДокумента.НомерСтроки = 17
		|	ИЛИ ТаблицаДокумента.НомерСтроки = 18";
	ДанныеНалоговойВыверки = Запрос.Выполнить().Выбрать();
	
	Пока ДанныеНалоговойВыверки.Следующий() Цикл
		Если ДанныеНалоговойВыверки.НомерСтроки = 7 Тогда
			ПрибыльСУчетомПР = ДанныеНалоговойВыверки.Сумма;
			
		ИначеЕсли ДанныеНалоговойВыверки.НомерСтроки = 17 Тогда
			СтавкаНП = ДанныеНалоговойВыверки.Сумма;
			
		ИначеЕсли ДанныеНалоговойВыверки.НомерСтроки = 18 Тогда
			НалогНаПрибыль = ДанныеНалоговойВыверки.Сумма;
		КонецЕсли;
	КонецЦикла;                                  
	
	УПП = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Организация);
	
	// Расчет налога на прибыль БУ в соответствии с данными учетной политики организации.
	Если УПП.ПрибыльБУБезУчетаНераспределеннойПрибыли Тогда
		НалогНаПрибыльБУ = Окр(ПрибыльСУчетомПР * СтавкаНП / 100, 0);
		
		Если НалогНаПрибыльБУ < 0 Тогда
			НалогНаПрибыльБУ = 0;	
		КонецЕсли;		
	Иначе			
		Запрос = Новый Запрос();
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ХозрасчетныйОстатки.СуммаОстаток
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет = &Счет, , Организация = &Организация) КАК ХозрасчетныйОстатки";
		Запрос.УстановитьПараметр("Дата", КонецГода(ДатаДокумента) + 1);
		Запрос.УстановитьПараметр("Счет", ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль);
		Запрос.УстановитьПараметр("Организация", Организация);
		РезультатНераспрПрибыли = Запрос.Выполнить().Выбрать();
		
		Если РезультатНераспрПрибыли.Следующий() Тогда
			ПрибыльБУ = -РезультатНераспрПрибыли.СуммаОстаток; 	
		Иначе
			ПрибыльБУ = 0;
		КонецЕсли;
		
		НалогНаПрибыльБУ = Окр(ПрибыльБУ * СтавкаНП / 100, 0);
		
		Если НалогНаПрибыльБУ < 0 Тогда 
			НалогНаПрибыльБУ = 0;
		КонецЕсли;		
	КонецЕсли;
	
	// Проводки в РБ Хозрасчетный по налогу на прибыль.
	Если НалогНаПрибыль <> 0 Тогда
		// Простая проводка 9910 - 3410
		Если УПП.НеФормироватьОтсроченныеПроводки Тогда 
			
			// Налог на прибыль 9910 - 3410
			СтрокаТЗ				= ТЗХозрасчетный.Добавить();
			СтрокаТЗ.Период 		= ДатаДокумента;
			СтрокаТЗ.Организация 	= Организация;
			СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
			СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
			СтрокаТЗ.Сумма 			= НалогНаПрибыль;
			СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
			
			// Закрытие 5300 - 9910
			СтрокаТЗ				= ТЗХозрасчетный.Добавить();
			СтрокаТЗ.Период 		= ДатаДокумента;
			СтрокаТЗ.Организация 	= Организация;
			СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль;
			СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
			СтрокаТЗ.Сумма 			= НалогНаПрибыль;
			СтрокаТЗ.Содержание 	= "НалогНаПрибыль";			
			
			// Учитываем отсроченные проводки
		Иначе			
			
			// Отсроченная проводки 4310
			// Если бухгатерская прибыль больше налоговой: 
			Если НалогНаПрибыльБУ > НалогНаПрибыль Тогда 
				// Основная проводка налог на прибыль НУ 9910 - 3410
				СтрокаТЗ				= ТЗХозрасчетный.Добавить();
				СтрокаТЗ.Период 		= ДатаДокумента;
				СтрокаТЗ.Организация 	= Организация;
				СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
				СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
				СтрокаТЗ.Сумма 			= НалогНаПрибыль;
				СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
				
				// Отсроченный налог НА 9910 - 4310
				СтрокаТЗ				= ТЗХозрасчетный.Добавить();
				СтрокаТЗ.Период 		= ДатаДокумента;
				СтрокаТЗ.Организация 	= Организация;
				СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
				СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеОбязательстваПоНалогуНаПрибыль;
				СтрокаТЗ.Сумма 			= НалогНаПрибыльБУ - НалогНаПрибыль;
				СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
				
				// Отсроченная проводка 2410
				// Если налоговая прибыль больше бухгалтерской: 
			ИначеЕсли НалогНаПрибыльБУ < НалогНаПрибыль Тогда  
				// Основная проводка налог на прибыль НУ 9910 - 3410				
				Если НалогНаПрибыльБУ <> 0 Тогда
					СтрокаТЗ				= ТЗХозрасчетный.Добавить();
					СтрокаТЗ.Период 		= ДатаДокумента;
					СтрокаТЗ.Организация 	= Организация;
					СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
					СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
					СтрокаТЗ.Сумма 			= НалогНаПрибыль;
					СтрокаТЗ.Содержание 	= "НалогНаПрибыль";
					
					// Отсроченный налог НА 9910 - 2410
					СтрокаТЗ				= ТЗХозрасчетный.Добавить();
					СтрокаТЗ.Период 		= ДатаДокумента;
					СтрокаТЗ.Организация 	= Организация;
					СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
					СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеТребованияНП;
					СтрокаТЗ.Сумма 			= НалогНаПрибыльБУ - НалогНаПрибыль;
					СтрокаТЗ.Содержание 	= "НалогНаПрибыль";
				КонецЕсли;					
				
			ИначеЕсли НалогНаПрибыльБУ = НалогНаПрибыль Тогда  
				// Основная проводка налог на прибыль НУ 9910 - 3410				
				Если НалогНаПрибыльБУ <> 0 Тогда
					СтрокаТЗ				= ТЗХозрасчетный.Добавить();
					СтрокаТЗ.Период 		= ДатаДокумента;
					СтрокаТЗ.Организация 	= Организация;
					СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
					СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.НалогиНаПрибыль;
					СтрокаТЗ.Сумма 			= НалогНаПрибыльБУ;
					СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
				КонецЕсли;
			КонецЕсли;
			
			// Закрытие 5300 - 9910 
			Если НалогНаПрибыльБУ <> 0 Тогда
				СтрокаТЗ				= ТЗХозрасчетный.Добавить();
				СтрокаТЗ.Период 		= ДатаДокумента;
				СтрокаТЗ.Организация 	= Организация;
				СтрокаТЗ.СчетДт 		= ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль;
				СтрокаТЗ.СчетКт 		= ПланыСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль;
				СтрокаТЗ.Сумма 			= НалогНаПрибыльБУ;
				СтрокаТЗ.Содержание 	= "НалогНаПрибыль";	
			КонецЕсли;				
		КонецЕсли; 
	КонецЕсли;	

КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт 		
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Дата КАК Период,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.РасчетНалогаНаПрибыль КАК РасчетНалогаНаПрибыль,
		|	&ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СчетАмортизации КАК СчетАмортизации,
		|	ТаблицаДокумента.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ТаблицаДокумента.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаДокумента.Субконто1 КАК Субконто1,
		|	ТаблицаДокумента.Субконто2 КАК Субконто2,
		|	ТаблицаДокумента.Субконто3 КАК Субконто3,
		|	ТаблицаДокумента.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаАмортизацияОС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АмортизацияОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СуммаДт КАК СуммаДт,
		|	ТаблицаДокумента.СуммаКт КАК СуммаКт
		|ПОМЕСТИТЬ ВременнаяТаблицаВременныеСчетаУчета
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ВременныеСчетаУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ТаблицаДокумента.СуммаКт <> 0
		|				ТОГДА ТаблицаДокумента.СуммаКт
		|			КОГДА ТаблицаДокумента.СуммаДт <> 0
		|				ТОГДА -ТаблицаДокумента.СуммаДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма5300
		|ПОМЕСТИТЬ ВременнаяТаблицаВременные5300
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ВременныеСчетаУчета КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Ссылка.РасчетНераспределеннойПрибыли
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.Валюта КАК Валюта,
		|	ТаблицаДокумента.Субконто1 КАК Субконто1,
		|	ТаблицаДокумента.Субконто2 КАК Субконто2,
		|	ТаблицаДокумента.Субконто3 КАК Субконто3,
		|	ТаблицаДокумента.СуммаДоход КАК СуммаДоход,
		|	ТаблицаДокумента.СуммаУбыток КАК СуммаУбыток
		|ПОМЕСТИТЬ ВременнаяТаблицаПереоценкаВалюты
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ПереоценкаВалюты КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Договор КАК Договор,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.ДокументАванса КАК ДокументАванса,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.СуммаНСП КАК СуммаНСП,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.ДокументОплаты КАК ДокументОплаты
		|ПОМЕСТИТЬ ВременнаяТаблицаДоотгрузкаРасшифровка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ДоотгрузкаРасшифровка КАК ЗакрытиеМесяцаДоотгрузкаРасшифровка
		|ГДЕ
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаАвансы.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаАвансы.Договор КАК Договор,
		|	ЗакрытиеМесяцаАвансы.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаАвансы.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаАвансы.СуммаНСП КАК СуммаНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансы
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Авансы КАК ЗакрытиеМесяцаАвансы
		|ГДЕ
		|	ЗакрытиеМесяцаАвансы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЗакрытиеМесяцаНДС.Строка = ""059""
		|				ТОГДА ЗакрытиеМесяцаНДС.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма059,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЗакрытиеМесяцаНДС.Строка = ""064""
		|				ТОГДА ЗакрытиеМесяцаНДС.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма064
		|ПОМЕСТИТЬ ВременнаяТаблицаНДС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НДС КАК ЗакрытиеМесяцаНДС
		|ГДЕ
		|	ЗакрытиеМесяцаНДС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаКорректировкаНДС.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаКорректировкаНДС.СчетКт КАК СчетКт,
		|	ЗакрытиеМесяцаКорректировкаНДС.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаКорректировкаНДС.Содержание КАК Содержание
		|ПОМЕСТИТЬ ВременнаяТаблицаКорректировкаНДС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.КорректировкаНДС КАК ЗакрытиеМесяцаКорректировкаНДС
		|ГДЕ
		|	ЗакрытиеМесяцаКорректировкаНДС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаПарныеСчетаУчета.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.СчетКт КАК СчетКт,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто1 КАК Субконто1,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто2 КАК Субконто2,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Субконто3 КАК Субконто3,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Валюта КАК Валюта,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаПарныеСчетаУчета.ВалютнаяСумма КАК ВалютнаяСумма
		|ПОМЕСТИТЬ ВременнаяТаблицаПарныеСчетаУчета
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ПарныеСчетаУчета КАК ЗакрытиеМесяцаПарныеСчетаУчета
		|ГДЕ
		|	ЗакрытиеМесяцаПарныеСчетаУчета.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества КАК ГруппаИмущества,
		|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ЗакрытиеМесяцаНедвижимость.Ставка КАК Ставка,
		|	ЗакрытиеМесяцаНедвижимость.СуммаНалога КАК СуммаНалога,
		|	ЗакрытиеМесяцаНедвижимость.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ЗакрытиеМесяцаНедвижимость.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ЗакрытиеМесяцаНедвижимость.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.Кр КАК Кр,
		|	ЗакрытиеМесяцаНедвижимость.Кз КАК Кз,
		|	ЗакрытиеМесяцаНедвижимость.Ко КАК Ко,
		|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ КАК СтоимостьНУ,
		|	ЗакрытиеМесяцаНедвижимость.МатериалСтен КАК МатериалСтен,
		|	ЗакрытиеМесяцаНедвижимость.ЗемНалог КАК ЗемНалог,
		|	ЗакрытиеМесяцаНедвижимость.ОтЛет КАК ОтЛет,
		|	ЗакрытиеМесяцаНедвижимость.ДоЛет КАК ДоЛет,
		|	ЗакрытиеМесяцаНедвижимость.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НежилаяПлощадь КАК НежилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога КАК НалогСУчетомЗемельногоНалога,
		|	ЗакрытиеМесяцаНедвижимость.Объект КАК Объект
		|ПОМЕСТИТЬ ВременнаяТаблицаНедвижимость
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Недвижимость КАК ЗакрытиеМесяцаНедвижимость
		|ГДЕ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаТранспорт.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества КАК ГруппаИмущества,
		|	ЗакрытиеМесяцаТранспорт.АБВ КАК АБВ,
		|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ЗакрытиеМесяцаТранспорт.Ставка КАК Ставка,
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога КАК СуммаНалога,
		|	ЗакрытиеМесяцаТранспорт.ОтЛет КАК ОтЛет,
		|	ЗакрытиеМесяцаТранспорт.ДоЛет КАК ДоЛет
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспорт
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Транспорт КАК ЗакрытиеМесяцаТранспорт
		|ГДЕ
		|	ЗакрытиеМесяцаТранспорт.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаРасходыБП.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаРасходыБП.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаРасходыБП.СчетКт КАК СчетКт,
		|	ЗакрытиеМесяцаРасходыБП.СубконтоДт1 КАК СубконтоДт1,
		|	ЗакрытиеМесяцаРасходыБП.СубконтоДт2 КАК СубконтоДт2,
		|	ЗакрытиеМесяцаРасходыБП.СубконтоДт3 КАК СубконтоДт3,
		|	ЗакрытиеМесяцаРасходыБП.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаРасходыБП.РБП КАК СубконтоКт1,
		|	ЗакрытиеМесяцаРасходыБП.СуммаНДС КАК СуммаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаРасходыБудущихПериодов
		|ИЗ
		|	Документ.ЗакрытиеМесяца.РасходыБП КАК ЗакрытиеМесяцаРасходыБП
		|ГДЕ
		|	ЗакрытиеМесяцаРасходыБП.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ГруппаНУ КАК ГруппаНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьНаНГ КАК БалансоваяСтоимостьНаНГ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСтоимости КАК УвеличениеСтоимости,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК УвеличениеСтоимостиНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ПоступлениеПГ КАК ПоступлениеПГ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.СуммаВыбытияОбщая КАК СуммаВыбытияОбщая,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ВыбытиеПГ КАК ВыбытиеПГ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ЗатратыНаРемонт КАК ЗатратыНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.НормаНаРемонт КАК НормаНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Превышение КАК Превышение,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимость КАК БалансоваяСтоимость,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьИтого КАК БалансоваяСтоимостьИтого,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.НормаАО КАК НормаАО,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Амортизация КАК Амортизация,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСОД КАК УвеличениеСОД,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ВычетМеньше100РП КАК ВычетМеньше100РП,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БазаПрямолинейнойАмортизации КАК БазаПрямолинейнойАмортизации
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяАмортизация
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НалоговаяАмортизация КАК ЗакрытиеМесяцаНалоговаяАмортизация
		|ГДЕ
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНалоговаяВыверка.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаНалоговаяВыверка.НомерСтроки КАК НомерСтроки,
		|	ЗакрытиеМесяцаНалоговаяВыверка.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаНалоговаяВыверка.НаименованиеСтроки КАК НаименованиеСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяВыверка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НалоговаяВыверка КАК ЗакрытиеМесяцаНалоговаяВыверка
		|ГДЕ
		|	ЗакрытиеМесяцаНалоговаяВыверка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаОХРиВП.Номенклатура КАК Номенклатура,
		|	ЗакрытиеМесяцаОХРиВП.Заказ КАК Заказ,
		|	ЗакрытиеМесяцаОХРиВП.Склад КАК Склад,
		|	ЗакрытиеМесяцаОХРиВП.СуммаОбщехозяйственныхРасходов КАК СуммаОбщехозяйственныхРасходов,
		|	ЗакрытиеМесяцаОХРиВП.СуммаВспомогательногоПроизводства КАК СуммаВспомогательногоПроизводства,
		|	ЗакрытиеМесяцаОХРиВП.СуммаНЗП КАК СуммаНЗП,
		|	ЗакрытиеМесяцаОХРиВП.СуммаКорректировкиРеализации КАК СуммаКорректировкиРеализации,
		|	ЗакрытиеМесяцаОХРиВП.СчетДтРеализации КАК СчетДтРеализации
		|ПОМЕСТИТЬ ВременнаяТаблицаОХРиВП
		|ИЗ
		|	Документ.ЗакрытиеМесяца.РасходыНаПроизводство КАК ЗакрытиеМесяцаОХРиВП
		|ГДЕ
		|	ЗакрытиеМесяцаОХРиВП.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПредставлениеПериода", Формат(ДокументСсылка.Дата, "ДФ=ММММ") + " " + Формат(ДокументСсылка.Дата, "ДФ=гггг"));
	Запрос.Выполнить();    	
	                                  
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияПоНалогуНаимущество(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяВыверка(ДокументСсылка, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Выполнить закрытие месяца
//
Процедура ВыполнитьЗакрытиеМесяца(СтруктураПараметров, АдресХранилища) Экспорт
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") 
		И СтруктураПараметров.Свойство("Ссылка") Тогда 
		ДокументОбъект = СтруктураПараметров.Ссылка.ПолучитьОбъект();
	Иначе 
		Возврат;
	КонецЕсли;	
	
	ДокументОбъект.АмортизацияОС.Очистить();
	ДокументОбъект.Недвижимость.Очистить();
	ДокументОбъект.Транспорт.Очистить();
	ДокументОбъект.ПереоценкаВалюты.Очистить();
	
	ДокументОбъект.Авансы.Очистить();
	ДокументОбъект.Доотгрузка.Очистить();
	ДокументОбъект.ДоотгрузкаРасшифровка.Очистить();
	ДокументОбъект.НДС.Очистить();
	ДокументОбъект.ПарныеСчетаУчета.Очистить();
	ДокументОбъект.ВременныеСчетаУчета.Очистить();
	ДокументОбъект.РасходыНаПроизводство.Очистить();
	
	Если ДокументОбъект.Проведен Тогда
		ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);		
	КонецЕсли;	

	ДокументОбъект.ВыполнитьЗакрытиеМесяца();

	АдресХранилища = ПоместитьВоВременноеХранилище(ДокументОбъект.Ссылка, АдресХранилища);
КонецПроцедуры 
	
#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли
