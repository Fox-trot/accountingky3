#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидПодбора = Параметры.ВидПодбора;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявлениеОВвозеИВывозеТовары.ДокументПоступления
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях
		|ИЗ
		|	Документ.ЗаявлениеОВвозеИВывозе.Товары КАК ЗаявлениеОВвозеИВывозеТовары
		|ГДЕ
		|	ЗаявлениеОВвозеИВывозеТовары.Ссылка.Дата <= &ДатаОтбора
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаявлениеОВвозеИВывозеТовары.ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК ДокументПоступления
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокУжеВыбранных
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Ссылка В(&СписокУжеВыбранных)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК ДокументПоступления,
		|	ПоступлениеТоваровУслуг.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокУжеВыбранных.ДокументПоступления ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Доступно
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях КАК ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях
		|		ПО ПоступлениеТоваровУслуг.Ссылка = ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях.ДокументПоступления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСписокУжеВыбранных КАК ВременнаяТаблицаСписокУжеВыбранных
		|		ПО ПоступлениеТоваровУслуг.Ссылка = ВременнаяТаблицаСписокУжеВыбранных.ДокументПоступления
		|ГДЕ
		|	ПоступлениеТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Справочник.ОперацииПоступлениеТоваровУслуг.ЕАЭС)
		|	И ПоступлениеТоваровУслуг.Дата <= &ДатаОтбора
		|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
		|	И ПоступлениеТоваровУслуг.Организация = &Организация
		|	И ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях.ДокументПоступления ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПоступлениеТоваровУслуг.Дата";
	
	Запрос.УстановитьПараметр("ДатаОтбора", 	Параметры.ДатаОтбора);
	Запрос.УстановитьПараметр("Контрагент", 	Параметры.Контрагент);
	Запрос.УстановитьПараметр("Организация", 	Параметры.Организация);
	Запрос.УстановитьПараметр("СписокУжеВыбранных", 	Параметры.СписокУжеВыбранных);	
	
	ТаблицаДокументов.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТабличныхЧастей 

&НаКлиенте
Процедура ТаблицаДокументовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	МассивДокументов = Новый Массив;	
	Для каждого ИндексСтроки Из Значение Цикл
		Если ТаблицаДокументов[ИндексСтроки].Доступно Тогда
			МассивДокументов.Добавить(ТаблицаДокументов[ИндексСтроки].ДокументПоступления);
		КонецЕсли;
	КонецЦикла;
	
	ЗакрыватьПриВыборе = Истина;
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ВидПодбора", 		ВидПодбора);
	СтруктураВозврата.Вставить("МассивДокументов", 	МассивДокументов);
	
	ОповеститьОВыборе(СтруктураВозврата);

КонецПроцедуры

#КонецОбласти
