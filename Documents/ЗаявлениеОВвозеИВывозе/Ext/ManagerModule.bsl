#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьЗаявленияОВвозе(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ЗаявлениеОВвозеИВывозе_ЗаявлениеОВвозеИВывозе";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗаявлениеОВвозеИВывозе.Ссылка,
	|	ЗаявлениеОВвозеИВывозе.Дата,
	|	ЗаявлениеОВвозеИВывозе.Организация,
	|	ЗаявлениеОВвозеИВывозе.Организация.ИНН КАК ОрганизацияИНН,
	|	ЗаявлениеОВвозеИВывозе.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	ЗаявлениеОВвозеИВывозе.Контрагент.ИНН КАК КонтрагентИНН,
	|	ЗаявлениеОВвозеИВывозе.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	|	ЗаявлениеОВвозеИВывозе.Контрагент.СтранаРезидентства.Код КАК КонтрагентСтрана,
	|	ЗаявлениеОВвозеИВывозе.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура.Наименование КАК НоменклатураНаименование,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		КодТНВЭД,
	|		Количество,
	|		Стоимость,
	|		Валюта.Код КАК ВалютаКод,
	|		СерияНомерСопроводительной,
	|		ДатаСопроводительной,
	|		СерияНомерСФ,
	|		ДатаСФ,
	|		ДатаПринятияНаУчет,
	|		НалоговаяБазаАкциз,
	|		НалоговаяБазаНДС,
	|		СтавкаАкцизТверд,
	|		СтавкаАкцизАдвалор,
	|		СтавкаНДС,
	|		СуммаАкциза,
	|		СуммаНДС,
	|		Курс,
	|		ЗачетНДС,
	|		ДокументПоступления
	|	)
	|ИЗ
	|	Документ.ЗаявлениеОВвозеИВывозе КАК ЗаявлениеОВвозеИВывозе
	|ГДЕ
	|	ЗаявлениеОВвозеИВывозе.Ссылка В(&СписокДокументов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	РезультатЗапроса 	= Запрос.Выполнить();
	ВыборкаДок 			= РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДок.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(ВыборкаДок.Организация,ВыборкаДок.Дата);
		ДанныеПечати.Вставить("ФИОРуководителя", 		Руководители.Руководитель);
		ДанныеПечати.Вставить("ФИОГлавногоБухгалтера", 	Руководители.ГлавныйБухгалтер);
		
		ДанныеПечати.Вставить("ОрганизацияСтрана", 		"417");
		ДанныеПечати.Вставить("Д1Д2",					Сред(ВыборкаДок.Дата, 1, 2));
		ДанныеПечати.Вставить("Д3Д4",					Сред(ВыборкаДок.Дата, 4, 2));
		ДанныеПечати.Вставить("Д5Д6",					Сред(ВыборкаДок.Дата, 7, 4));
		
		Товары = ВыборкаДок.Товары.Выгрузить();
		ДанныеПечати.Вставить("Количество", 		Товары.Итог("Количество"));
		ДанныеПечати.Вставить("Стоимость",  		Товары.Итог("Стоимость"));
		ДанныеПечати.Вставить("НалоговаяБазаАкциз", Товары.Итог("НалоговаяБазаАкциз"));
		ДанныеПечати.Вставить("НалоговаяБазаНДС",   Товары.Итог("НалоговаяБазаНДС"));
		ДанныеПечати.Вставить("СтавкаАкцизТверд",  	Товары.Итог("СтавкаАкцизТверд"));
		ДанныеПечати.Вставить("СтавкаАкцизАдвалор", Товары.Итог("СтавкаАкцизАдвалор"));
		ДанныеПечати.Вставить("СтавкаНДС",          Товары.Итог("СтавкаНДС"));
		ДанныеПечати.Вставить("СуммаАкциза",         Товары.Итог("СуммаАкциза"));
		ДанныеПечати.Вставить("СуммаНДС",        	Товары.Итог("СуммаНДС"));		
			
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявлениеОВвозеИВывозе.ПФ_MXL_ЗаявлениеОВвозеИВывозе");
		
		МассивОбластейМакета = Новый Массив;		
		МассивОбластейМакета.Добавить("Шапка");
		МассивОбластейМакета.Добавить("СтрокаТаблицы");		
		МассивОбластейМакета.Добавить("Итоги");
		МассивОбластейМакета.Добавить("Подписи");
		МассивОбластейМакета.Добавить("Страница2");
		МассивОбластейМакета.Добавить("Страница3");
		МассивОбластейМакета.Добавить("ТретийРаздел");

		ВыборкаСтрокТовары 	= ВыборкаДок.Товары.Выбрать();

		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);			
			Если ИмяОбласти <> "СтрокаТаблицы" Тогда
				Если ИмяОбласти = "Страница2" ИЛИ ИмяОбласти = "Страница3" ИЛИ ИмяОбласти = "ТретийРаздел" Тогда
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ВыборкаДок);
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);								
			ИначеЕсли ИмяОбласти = "СтрокаТаблицы" Тогда 
				Пока ВыборкаСтрокТовары.Следующий() Цикл
					ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
					ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;

			КонецЕсли;
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДок.Ссылка);		
	КонецЦикла;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ПолеСлева = 10;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеОВвозе") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"ЗаявлениеОВвозе", НСтр("ru = 'Заявление о ввозе товаров и уплате косвенных налогов'"), ПечатьЗаявленияОВвозе(МассивОбъектов, ОбъектыПечати),,
		"Документ.Доверенность.ПФ_MXL_Доверенность_М2");
	КонецЕсли;	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЗаявлениеОВвозе";
	КомандаПечати.Представление = НСтр("ru = 'Заявление о ввозе товаров и уплате косвенных налогов'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли