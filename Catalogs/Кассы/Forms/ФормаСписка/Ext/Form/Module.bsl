#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокКассПоУмолчанию = ПолучитьСписокКассПоУмолчанию();
	Если СписокКассПоУмолчанию.Количество() > 0 Тогда
		УстановитьОформлениеКассаПоУмолчанию(СписокКассПоУмолчанию);
	КонецЕсли;

	// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Кассы);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Устанавливает текущий элемент кассой по умолчанию для владельца
//
Процедура УстановитьКакКассаПоУмолчанию(Команда)
	
	СписокКассПоУмолчанию = Новый СписокЗначений;
	
	ТекущаяСтрокаСписка = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрокаСписка = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрана касса, которую необходимо установить как кассу по умолчанию'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	НоваяКассаПоУмолчанию	= ТекущаяСтрокаСписка.Ссылка;
	ВладелецСсылка			= ТекущаяСтрокаСписка.Владелец;
	
	Для каждого ЭлементУсловногоОформления Из Список.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" И
			ЭлементУсловногоОформления.Представление = "Касса по умолчанию" Тогда
			
			ЭлементОтбора			= ЭлементУсловногоОформления.Отбор.Элементы[0];
			СписокКассПоУмолчанию	= ЭлементОтбора.ПравоеЗначение;
			
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СписокКассПоУмолчанию.НайтиПоЗначению(НоваяКассаПоУмолчанию) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьКарточкуОбъектаВладелецаИПоменятьОформлениеСписка(ВладелецСсылка, НоваяКассаПоУмолчанию, СписокКассПоУмолчанию);
	
	Оповестить("ИзмененаОсновнаяКасса");
	
КонецПроцедуры // УстановитьКакКассаПоУмолчанию()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Функция получает и возвращает выборку касс по умолчанию 
// 
// МассивКасс - массив касс по умолчанию.
//
Функция ПолучитьСписокКассПоУмолчанию(МассивВладельцев = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.ОсновнаяКасса КАК Касса
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.ОсновнаяКасса = ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяССылка)
		|	И &УсловиеОтбораПоОрганизациям";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, 
		"&УсловиеОтбораПоОрганизациям",
		?(ТипЗнч(МассивВладельцев) = Тип("Массив"), "Организации.Ссылка В (&МассивВладельцев)", "Истина"));
		
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	СписокКассПоУмолчанию = Новый СписокЗначений;
	СписокКассПоУмолчанию.ЗагрузитьЗначения(РезультатЗапроса.ВыгрузитьКолонку("Касса"));
	
	Возврат СписокКассПоУмолчанию;
	
КонецФункции //ПолучитьСписокКассПоУмолчанию()

&НаСервере
// Процедура выделяет в списке кассу по умолчанию
//
Процедура УстановитьОформлениеКассаПоУмолчанию(СписокКассПоУмолчанию)
	
	Для каждого ЭлементУсловногоОформления Из Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" Тогда
			Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Удалить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыУсловногоОформленияСписка	= Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы;
	ЭлементУсловногоОформления			= ЭлементыУсловногоОформленияСписка.Добавить();
	
	ЭлементОтбора 						= ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение 		= Новый ПолеКомпоновкиДанных("Ссылка");
	ЭлементОтбора.ВидСравнения 			= ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.ПравоеЗначение 		= СписокКассПоУмолчанию;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина,));
	
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
	ЭлементУсловногоОформления.Представление = "Касса по умолчанию";  
	
КонецПроцедуры //УстановитьОформлениеКассаУмолчанию()

&НаСервере
// Процедура - записывает в карточку владельца новое значение
// и обновляет визуальное представление касс по умолчанию в форме списке
//
Процедура ИзменитьКарточкуОбъектаВладелецаИПоменятьОформлениеСписка(ВладелецСсылка, НоваяКассаПоУмолчанию, СписокКассПоУмолчанию)
	
	ОбъектВладелец 					= ВладелецСсылка.ПолучитьОбъект();
	СтараяКассаПоУмолчанию			= ОбъектВладелец.ОсновнаяКасса;
	ОбъектВладелец.ОсновнаяКасса 	= НоваяКассаПоУмолчанию;
	
	БухгалтерскийУчетКлиентСервер.ЗаписатьСправочникОбъект(ОбъектВладелец);
	
	ЭлементСпискаЗначений = СписокКассПоУмолчанию.НайтиПоЗначению(СтараяКассаПоУмолчанию);
	
	Если НЕ ЭлементСпискаЗначений = Неопределено Тогда
		СписокКассПоУмолчанию.Удалить(ЭлементСпискаЗначений);
	КонецЕсли;
	
	СписокКассПоУмолчанию.Добавить(НоваяКассаПоУмолчанию);
	
	УстановитьОформлениеКассаПоУмолчанию(СписокКассПоУмолчанию);
	
КонецПроцедуры //ИзменитьКарточкуВладельцаИПоменятьОформлениеСписка()

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

#КонецОбласти
