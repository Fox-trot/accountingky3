///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("СпособУстановкиКурса");
	Результат.Добавить("Наценка");
	Результат.Добавить("ОсновнаяВалюта");
	Результат.Добавить("ФормулаРасчетаКурса");
	Возврат Результат;
	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

#КонецОбласти

#Область ПрограммныйИнтерфейсБПКР

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	СправочникМенеджер = Справочники.Валюты;
	
	КлассификаторXML = Справочники.Валюты.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица  = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл 
		Код					  	= СокрЛП(СтрокаТаблицыЗначений.Code);
		Наименование 		  	= СокрЛП(СтрокаТаблицыЗначений.CodeSymbol);
		НаименованиеПолное    	= СокрЛП(СтрокаТаблицыЗначений.Name);
		ЗагружаетсяИзИнтернета	= СокрЛП(СтрокаТаблицыЗначений.NBKRLoading);
		ПараметрыПрописи		= СокрЛП(СтрокаТаблицыЗначений.NumerationItemOptions);
		
		СправочникСсылка = СправочникМенеджер.НайтиПоКоду(Код);
		
		Если СправочникСсылка.Пустая() Тогда
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;	
		
		СправочникОбъект.Код				  	= Код;
		СправочникОбъект.Наименование 	       	= Наименование;
		СправочникОбъект.НаименованиеПолное    	= НаименованиеПолное;
		СправочникОбъект.ЗагружаетсяИзИнтернета	= ?(ЗагружаетсяИзИнтернета = "Да", Истина, Ложь);
		СправочникОбъект.СпособУстановкиКурса 	= ?(СправочникОбъект.ЗагружаетсяИзИнтернета, Перечисления.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета, Перечисления.СпособыУстановкиКурсаВалюты.РучнойВвод);
		СправочникОбъект.ПараметрыПрописи 		= ПараметрыПрописи;
		
		БухгалтерскийУчетКлиентСервер.ЗаписатьСправочникОбъект(СправочникОбъект,,,,Истина);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти

#КонецОбласти

#КонецЕсли