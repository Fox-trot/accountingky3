

#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиенте
Процедура РасчитатьОбщийИтог()

	Объект.СуммаОбщийИтог = Объект.СуммаИтогоМатериалы + Объект.СуммаИтогоУслуги + Объект.СуммаИтогоЭлектроэнергия;	

КонецПроцедуры // РасчитатьОбщийИтог()

&НаКлиенте
Процедура ПриИзмененииСтрокиМатериалы(СтрокаТабличнойЧасти = Неопределено)
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Материалы.ТекущиеДанные;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	Объект.СуммаИтогоМатериалы = Объект.Материалы.Итог("Сумма");
КонецПроцедуры // ПриИзмененииСтрокиМатериалы()

&НаКлиенте
Процедура ПриИзмененииСтрокиУслуги(СтрокаТабличнойЧасти = Неопределено)
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	КонецЕсли;	
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	СтрокаТабличнойЧасти.Всего = Окр(СтрокаТабличнойЧасти.ЧасоваяСтавка 
									* СтрокаТабличнойЧасти.НормаЧасов 
									* СтрокаТабличнойЧасти.Количество, 2);
									
	СтрокаТабличнойЧасти.СуммаВредность = Окр(СтрокаТабличнойЧасти.Всего * СтрокаТабличнойЧасти.ПроцентВредность / 100, 2);
	СтрокаТабличнойЧасти.Сумма 			= СтрокаТабличнойЧасти.Всего + СтрокаТабличнойЧасти.СуммаВредность;
КонецПроцедуры 

&НаКлиенте
Процедура ПриИзмененииСтрокиЭлектроэнергия(СтрокаТабличнойЧасти = Неопределено)
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Электроэнергия.ТекущиеДанные;
	КонецЕсли;
	СтрокаТабличнойЧасти.СтоимостьЕдиницы = СтрокаТабличнойЧасти.ЧасоваяСтавка * СтрокаТабличнойЧасти.НормаЧасов;
КонецПроцедуры 

&НаКлиенте
Процедура РассчитатьИтогУслуги()
	Объект.СуммаУслугиИВредность 	= Объект.Услуги.Итог("Сумма");
	Объект.СуммаПремия 				= Объект.СуммаУслугиИВредность * Объект.ПроцентПремия / 100;
	Объект.СуммаВыслуга 			= Объект.СуммаУслугиИВредность * Объект.ПроцентВыслуга / 100;
	Объект.ВсегоУслуги 				= Объект.СуммаУслугиИВредность + Объект.СуммаПремия + Объект.СуммаВыслуга;
	Объект.СоцСтрах 				= Объект.ВсегоУслуги * Объект.ПроцентСоцСтрах / 100; 
	Объект.ПрочиеРасходы 			= Объект.ВсегоУслуги * Объект.ПроцентПрочиеРасходы / 100;
	Объект.СуммаИтогоУслуги 		= Объект.ВсегоУслуги + Объект.СоцСтрах + Объект.ПрочиеРасходы;
КонецПроцедуры // РассчитатьИтогУслуги()

&НаКлиенте
Процедура РассчитатьИтогЭлектроэнергия()
	Объект.СуммаИтогоЭлектроэнергия = Объект.Электроэнергия.Итог("СтоимостьЕдиницы") * Объект.КоличествоЕдиниц;
КонецПроцедуры // РассчитатьИтогУслуги()

&НаКлиенте
Процедура РассчитатьСпецификациюНаКлиенте()
	Для каждого СтрокаТабличнойЧасти Из Объект.Материалы Цикл
		ПриИзмененииСтрокиМатериалы(СтрокаТабличнойЧасти)	
	КонецЦикла;
	Для каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
		ПриИзмененииСтрокиУслуги(СтрокаТабличнойЧасти)	
	КонецЦикла;
	Для каждого СтрокаТабличнойЧасти Из Объект.Электроэнергия Цикл
		ПриИзмененииСтрокиЭлектроэнергия(СтрокаТабличнойЧасти)	
	КонецЦикла;
	РассчитатьИтогУслуги();
	РассчитатьИтогЭлектроэнергия();
	РасчитатьОбщийИтог();
	
КонецПроцедуры // ЗаполнитьТабличнуюЧастьНаСервере()

#КонецОбласти

#Область УправлениеВнешнимВидом

#КонецОбласти

#Область ОбработчикиСобытийФормы


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьСпецификацию(Команда)		
	
	Если Объект.Материалы.Количество() + Объект.Услуги.Количество() + Объект.Электроэнергия.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросРассчитатьСпецификацию", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Табличные части и итоговые суммы будут пересчитаны! Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);	
	Иначе
		РассчитатьСпецификациюНаКлиенте();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросРассчитатьСпецификацию(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
        РассчитатьСпецификациюНаКлиенте();
    КонецЕсли;
КонецПроцедуры // ЗаполнитьПоОснованию()

#КонецОбласти

#Область ОбработчикиРеквизитовШапки

&НаКлиенте
Процедура КоличествоЕдиницПриИзменении(Элемент)
	РассчитатьИтогЭлектроэнергия();
	РасчитатьОбщийИтог();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТабличныхЧастей

&НаКлиенте
Процедура ИсходныеКомплектующиеКоличествоПриИзменении(Элемент)
	ПриИзмененииСтрокиМатериалы();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЦенаПриИзменении(Элемент)
	ПриИзмененииСтрокиМатериалы();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура УслугиЧасоваяСтавкаПриИзменении(Элемент)
	ПриИзмененииСтрокиУслуги();
	РассчитатьИтогУслуги();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура УслугиНормаЧасовПриИзменении(Элемент)
	ПриИзмененииСтрокиУслуги();
	РассчитатьИтогУслуги();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	ПриИзмененииСтрокиУслуги();
	РассчитатьИтогУслуги();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура УслугиВредностьПриИзменении(Элемент)
	ПриИзмененииСтрокиУслуги();
	РассчитатьИтогУслуги();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура ЭлектроэнергияЧасоваяСтавкаПриИзменении(Элемент)
	ПриИзмененииСтрокиЭлектроэнергия();
	РассчитатьИтогЭлектроэнергия();
	РасчитатьОбщийИтог();
КонецПроцедуры

&НаКлиенте
Процедура ЭлектроэнергияНормаЧасовПриИзменении(Элемент)
	ПриИзмененииСтрокиЭлектроэнергия();
	РассчитатьИтогЭлектроэнергия();
	РасчитатьОбщийИтог();
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если Не ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсисте.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаВажныеКоманды);
	// Конец СтандартныеПодсистемы.Печать
КонецПроцедуры

#КонецОбласти


