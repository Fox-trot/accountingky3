#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ИнтерфейсПечати

Функция ПечатьСпецификации(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СпецификацииНоменклатуры.Ссылка,
		|	СпецификацииНоменклатуры.Владелец.Наименование КАК НоменклатураНаименование,
		|	СпецификацииНоменклатуры.Наименование,
		|	СпецификацииНоменклатуры.СуммаИтогоМатериалы,
		|	СпецификацииНоменклатуры.ПроцентВредность,
		|	СпецификацииНоменклатуры.СуммаУслугиИВредность,
		|	СпецификацииНоменклатуры.ПроцентПремия,
		|	СпецификацииНоменклатуры.СуммаПремия,
		|	СпецификацииНоменклатуры.ПроцентВыслуга,
		|	СпецификацииНоменклатуры.СуммаВыслуга,
		|	СпецификацииНоменклатуры.ВсегоУслуги,
		|	СпецификацииНоменклатуры.ПроцентСоцСтрах,
		|	СпецификацииНоменклатуры.СоцСтрах,
		|	СпецификацииНоменклатуры.ПроцентПрочиеРасходы,
		|	СпецификацииНоменклатуры.ПрочиеРасходы,
		|	СпецификацииНоменклатуры.СуммаИтогоУслуги,
		|	СпецификацииНоменклатуры.КоличествоЕдиниц,
		|	СпецификацииНоменклатуры.СуммаИтогоЭлектроэнергия,
		|	СпецификацииНоменклатуры.СуммаОбщийИтог,
		|	СпецификацииНоменклатуры.Материалы.(
		|		Ссылка,
		|		Номенклатура,
		|		Номенклатура.Наименование КАК Наименование,
		|		Количество,
		|		Цена,
		|		Сумма,
		|		Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕИ
		|	),
		|	СпецификацииНоменклатуры.Услуги.(
		|		Ссылка,
		|		Должность,
		|		Должность.Наименование КАК Наименование,
		|		СтатьяЗатрат,
		|		ЧасоваяСтавка,
		|		НормаЧасов,
		|		Количество,
		|		Всего,
		|		ПроцентВредность,
		|		СуммаВредность,
		|		Сумма,
		|		ЕдиницаИзмерения.Наименование КАК ЕИ
		|	),
		|	СпецификацииНоменклатуры.Электроэнергия.(
		|		Ссылка,
		|		ОсновноеСредство,
		|		ЧасоваяСтавка,
		|		НормаЧасов,
		|		СтоимостьЕдиницы,
		|		ОсновноеСредство.Наименование КАК Наименование
		|	)
		|ИЗ
		|	Справочник.СпецификацииНоменклатуры КАК СпецификацииНоменклатуры
		|ГДЕ
		|	СпецификацииНоменклатуры.Ссылка В(&МассивОбъектов)";	
	Запрос = Новый Запрос(ТекстЗапроса);	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СпецификацияНоменклатуры";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.СпецификацииНоменклатуры.ПФ_MXL_Спецификация");
	
	Организация = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяОрганизация");
	Дата = ТекущаяДата();
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизаций(Организация, Дата);
		ДанныеПечати.Вставить("ФИОРуководителя", 			Руководители.Руководитель);		
		ДанныеПечати.Вставить("ДолжностьРуководителя", 		Руководители.РуководительДолжность);
		
		ДанныеПечати.Вставить("Дата", 						Дата);		
		ДанныеПечати.Вставить("НоменклатураНаименование", 	Шапка.НоменклатураНаименование);
		ДанныеПечати.Вставить("ОрганизацияНаименование", 	Организация.НаименованиеПолное);
		ДанныеПечати.Вставить("ПроцентВредность", 			Шапка.ПроцентВредность);
		
		// Инициализация массива областей макета
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("Строка");
		МассивОбластейМакета.Добавить("Подвал");
		ОбластьМакетаГруппировки = Макет.ПолучитьОбласть("Группировка");
		ОбластьМакетаСтрокаЖирная = Макет.ПолучитьОбласть("СтрокаЖирная");
		
		ВременнаяТаблицаУслуги = Шапка.Услуги.Выгрузить();
		ВременнаяТаблицаМатериалы = Шапка.Материалы.Выгрузить();
		ВременнаяТаблицаЭлектроэнергия = Шапка.Электроэнергия.Выгрузить();
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Строка" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Шапка);
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			ИначеЕсли ИмяОбласти = "Строка" Тогда
				// Работа
				ОбластьМакетаГруппировки.Параметры.Номер = 1;
				ОбластьМакетаГруппировки.Параметры.НаименованиеГруппировки = "Затраты на оплату труда";
				ТабличныйДокумент.Вывести(ОбластьМакетаГруппировки);				
				Для Каждого СтрокаТаблицы Из ВременнаяТаблицаУслуги Цикл					
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
				ОбластьМакета.Параметры.Наименование = "Итого:";
				ОбластьМакета.Параметры.ЕИ = "сом";
				ОбластьМакета.Параметры.Сумма = Шапка.СуммаУслугиИВредность;
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
				ОбластьМакета.Параметры.Наименование = "Премия " + Шапка.ПроцентПремия;
				ОбластьМакета.Параметры.ЕИ = "сом";
				ОбластьМакета.Параметры.Сумма = Шапка.СуммаПремия;
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
				ОбластьМакета.Параметры.Наименование = "Выслуга лет " + Шапка.ПроцентВыслуга;
				ОбластьМакета.Параметры.ЕИ = "сом";
				ОбластьМакета.Параметры.Сумма = Шапка.СуммаВыслуга;
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
				ОбластьМакета.Параметры.Наименование = "Всего по п.1";
				ОбластьМакета.Параметры.ЕИ = "сом";
				ОбластьМакета.Параметры.Сумма = Шапка.ВсегоУслуги;
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбластьМакетаСтрокаЖирная.Параметры.Наименование = "Соц.страх " + Шапка.ПроцентСоцСтрах;
				ОбластьМакетаСтрокаЖирная.Параметры.ЕИ = "сом";
				ОбластьМакетаСтрокаЖирная.Параметры.Сумма = Шапка.СоцСтрах;
				ТабличныйДокумент.Вывести(ОбластьМакетаСтрокаЖирная);
				
				// Материалы
				ОбластьМакетаГруппировки.Параметры.Номер = 3;
				ОбластьМакетаГруппировки.Параметры.НаименованиеГруппировки = "Материальные затраты";
				ТабличныйДокумент.Вывести(ОбластьМакетаГруппировки);				
				Для Каждого СтрокаТаблицы Из ВременнаяТаблицаМатериалы Цикл					
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
				ОбластьМакетаСтрокаЖирная.Параметры.Наименование = "Итого:";
				ОбластьМакетаСтрокаЖирная.Параметры.ЕИ = "сом";
				ОбластьМакетаСтрокаЖирная.Параметры.Сумма = Шапка.СуммаИтогоМатериалы;
				ТабличныйДокумент.Вывести(ОбластьМакетаСтрокаЖирная);
				
				// Электроэнергия
				ОбластьМакетаГруппировки.Параметры.Номер = 4;
				ОбластьМакетаГруппировки.Параметры.НаименованиеГруппировки = "Затраты на электроэнергию";
				ТабличныйДокумент.Вывести(ОбластьМакетаГруппировки);				
				Для Каждого СтрокаТаблицы Из ВременнаяТаблицаЭлектроэнергия Цикл					
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
				ОбластьМакетаСтрокаЖирная.Параметры.Наименование = "Итого:";
				ОбластьМакетаСтрокаЖирная.Параметры.ЕИ = "сом";
				ОбластьМакетаСтрокаЖирная.Параметры.Сумма = Шапка.СуммаИтогоЭлектроэнергия;
				ТабличныйДокумент.Вывести(ОбластьМакетаСтрокаЖирная);
				
				// Итого
				ОбластьМакетаСтрокаЖирная.Параметры.Номер = 5;
				ОбластьМакетаСтрокаЖирная.Параметры.Наименование = "Прочие расходы";
				ОбластьМакетаСтрокаЖирная.Параметры.ЕИ = "сом";
				ОбластьМакетаСтрокаЖирная.Параметры.Сумма = Шапка.ПрочиеРасходы;
				ТабличныйДокумент.Вывести(ОбластьМакетаСтрокаЖирная);
				
				ОбластьМакетаСтрокаЖирная.Параметры.Номер = 6;
				ОбластьМакетаСтрокаЖирная.Параметры.Наименование = "Всего затрат";
				ОбластьМакетаСтрокаЖирная.Параметры.ЕИ = "сом";
				ОбластьМакетаСтрокаЖирная.Параметры.Сумма = Шапка.СуммаОбщийИтог;
				ТабличныйДокумент.Вывести(ОбластьМакетаСтрокаЖирная);				
				
			КонецЕсли;	
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Спецификация") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"ПФ_MXL_Спецификация", 
		"Спецификация номенклатуры", 
		ПечатьСпецификации(МассивОбъектов, ОбъектыПечати),
		,
		"Справочник.СпецификацииНоменклатуры.ПФ_MXL_Спецификация");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_Спецификация";
	КомандаПечати.Представление = НСтр("ru = 'Печать спецификации'");
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли