#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли; 
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьОтраслевыеКоэффициенты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРегиональныеКоэффициенты

&НаКлиенте
Процедура ОтраслевыеКоэффициентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборВСпискеОтраслевыхКоэффициентов(СтандартнаяОбработка);
КонецПроцедуры         

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОтраслевыеКоэффициенты()
	
	// Заполняет региональные коэффицента из Макета
	
	КлассификаторXML = Справочники.КлассификаторОтраслевойПринадлежностиНИ.ПолучитьМакет("ОтраслевыеКоэффициенты").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого ЗаписьОКВ Из КлассификаторТаблица Цикл
		НоваяСтрока = ОтраслевыеКоэффициенты.Добавить();
		НоваяСтрока.Отрасль      = ЗаписьОКВ.Наименование;
		НоваяСтрока.Коэффициент  = ЗаписьОКВ.Коэффициент; 
	КонецЦикла;
	
КонецПроцедуры       

&НаСервере
Функция СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки)
	
	ТекущаяСсылка = Неопределено;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ОтраслевыеКоэффициенты[НомерСтроки];
		
		СтрокаВБазе = Справочники.КлассификаторОтраслевойПринадлежностиНИ.НайтиПоНаименованию(ТекущиеДанные.Отрасль);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.ОтраслевыеКоэффициенты.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Справочники.КлассификаторОтраслевойПринадлежностиНИ.СоздатьЭлемент(); 		
		НоваяСтрока.Наименование              = ТекущиеДанные.Отрасль;
		НоваяСтрока.Коэффициент				  = ТекущиеДанные.Коэффициент;   			
		НоваяСтрока.Записать();
		
		Если НомерСтроки = Элементы.ОтраслевыеКоэффициенты.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;               		
				
	КонецЦикла;
	
	Возврат ТекущаяСсылка;

КонецФункции

&НаКлиенте
Процедура ОбработатьВыборВСпискеОтраслевыхКоэффициентов(СтандартнаяОбработка = Неопределено)
		
	// Добавление элемента справочника и вывод результата пользователю
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСсылка = СохранитьВыбранныеСтроки(Элементы.ОтраслевыеКоэффициенты.ВыделенныеСтроки);
	
	ОповеститьОВыборе(ТекущаяСсылка);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Отраслевые принадлежности добавлены.'"), , "",БиблиотекаКартинок.Информация32);
	Закрыть();
	
КонецПроцедуры


#КонецОбласти           
