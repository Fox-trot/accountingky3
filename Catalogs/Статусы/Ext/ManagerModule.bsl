#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	СправочникМенеджер = Справочники.Статусы;
	
	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;     
	
	НаборЗаписей = РегистрыСведений.СтавкиНалоговЗаработнойПлаты.СоздатьНаборЗаписей();
	НаборЗаписей.Очистить();                 
	Период = Дата(2018,01,01);
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Наименование = СокрЛП(СтрокаТаблицыЗначений.Наименование);
		Категория = СокрЛП(СтрокаТаблицыЗначений.Категория);
		ВидЗанятости = СокрЛП(СтрокаТаблицыЗначений.ВидЗанятости);
		МетодРасчетаПН = СокрЛП(СтрокаТаблицыЗначений.МетодРасчетаПН);
		МетодРасчетаСФ = СокрЛП(СтрокаТаблицыЗначений.МетодРасчетаСФ);
		
		СправочникСсылка = СправочникМенеджер.НайтиПоНаименованию(Наименование, Истина);
		
		Если СправочникСсылка.Пустая() Тогда
			СправочникСсылка = СправочникМенеджер.ПолучитьСсылку(Новый УникальныйИдентификатор);
			
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
			СправочникОбъект.УстановитьСсылкуНового(СправочникСсылка);
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;	
		
		СправочникОбъект.Наименование = Наименование;
		СправочникОбъект.Категория = Справочники.КатегорииСотрудников.НайтиПоКоду(Категория);
		СправочникОбъект.ВидЗанятости = Справочники.ВидыЗанятости[ВидЗанятости];
		СправочникОбъект.МетодРасчетаПН = Перечисления.МетодыРасчетаПН[МетодРасчетаПН];
		СправочникОбъект.МетодРасчетаСФ = Перечисления.МетодыРасчетаСФ[МетодРасчетаСФ];
		
		БухгалтерскийУчетКлиентСервер.ЗаписатьСправочникОбъект(СправочникОбъект);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицыЗначений); 
		НоваяЗапись.Период = Период;
		НоваяЗапись.Статус = СправочникСсылка;
	КонецЦикла;
			
	Попытка
		НаборЗаписей.Записать();
	Исключение
	КонецПопытки;
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти
	
#КонецЕсли

