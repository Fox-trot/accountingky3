
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Заполнение списка валют из ОКВ
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьРегиональныеКоэффициенты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРегиональныеКоэффициенты

&НаКлиенте
Процедура РегиональныеКоэффициентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборВСпискеРегиональныхКоэффицентов(СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВыполнить()
	
	ОбработатьВыборВСпискеРегиональныхКоэффицентов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРегиональныеКоэффициенты()
	
	// Заполняет региональные коэффицента из Макета
	
	КлассификаторXML = Справочники.РегиональныеКоэффициентыНИ.ПолучитьМакет("РегиональныеКоэффициенты").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого ЗаписьОКВ Из КлассификаторТаблица Цикл
		НоваяСтрока = РегиональныеКоэффициенты.Добавить();
		НоваяСтрока.Регион       = ЗаписьОКВ.Наименование;
		НоваяСтрока.Коэффициент  = ЗаписьОКВ.Коэффициент; 
	КонецЦикла;
	
КонецПроцедуры       

&НаСервере
Функция СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки)
	
	ТекущаяСсылка = Неопределено;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = РегиональныеКоэффициенты[НомерСтроки];
		
		СтрокаВБазе = Справочники.РегиональныеКоэффициентыНИ.НайтиПоНаименованию(ТекущиеДанные.Регион);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.РегиональныеКоэффициенты.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Справочники.РегиональныеКоэффициентыНИ.СоздатьЭлемент(); 		
		НоваяСтрока.Наименование              = ТекущиеДанные.Регион;
		НоваяСтрока.Коэффициент				  = ТекущиеДанные.Коэффициент;   			
		НоваяСтрока.Записать();
		
		Если НомерСтроки = Элементы.РегиональныеКоэффициенты.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;               		
				
	КонецЦикла;
	
	Возврат ТекущаяСсылка;

КонецФункции

&НаКлиенте
Процедура ОбработатьВыборВСпискеРегиональныхКоэффицентов(СтандартнаяОбработка = Неопределено)
		
	// Добавление элемента справочника и вывод результата пользователю
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСсылка = СохранитьВыбранныеСтроки(Элементы.РегиональныеКоэффициенты.ВыделенныеСтроки);
	
	ОповеститьОВыборе(ТекущаяСсылка);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Региональные коэффициенты добавлены.'"), , "",БиблиотекаКартинок.Информация32);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти      
