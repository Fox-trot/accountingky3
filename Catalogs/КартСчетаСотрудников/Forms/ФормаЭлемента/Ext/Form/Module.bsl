#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		ПроверкаНаличияСчетовФизЛица();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Владелец.
//
&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		ПроверкаНаличияСчетовФизЛица();
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Банк.
//
&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	УстановитьПредставлениеСчета();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода НомерСчета.
//
&НаКлиенте
Процедура НомерСчетаПриИзменении(Элемент)
	УстановитьПредставлениеСчета();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПредставлениеСчета()

	Если ЗначениеЗаполнено(Объект.НомерСчета) И ЗначениеЗаполнено(Объект.НомерСчета) Тогда
		Объект.Наименование = СтрШаблон(НСтр("ru = '%1, в %2'"), Строка(Объект.НомерСчета), Строка(Объект.Банк));
	КонецЕсли;

КонецПроцедуры


&НаСервере
// Процедура проверяет наличие у физ лица банковского счета или карт. счета 
// и если таковые имеются, то запрещает создавать банковский счет.
Процедура ПроверкаНаличияСчетовФизЛица()

	ДанныеПоСчету = СотрудникиФормы.ПроверитьНаличиеБанковскогоИлиКартСчета(Объект.Владелец);
		
	Если ТипЗнч(ДанныеПоСчету) = Тип("Структура") Тогда 		
		Если ДанныеПоСчету.Свойство("КартСчет") Тогда
			ТекстСообщения = НСтр("ru = 'Для данного физического лица уже создан карт. счет!'");
			Если НЕ ДанныеПоСчету.КартСчет = Объект.Ссылка Тогда
				Объект.Владелец = Справочники.ФизическиеЛица.ПустаяСсылка();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
			КонецЕсли;
			
		Иначе
			ТекстСообщения = НСтр("ru = 'Для данного физического лица уже создан банковский счет!'");
			Объект.Владелец = Справочники.ФизическиеЛица.ПустаяСсылка();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти