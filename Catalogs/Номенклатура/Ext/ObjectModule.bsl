#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		// Установка флага "Услуга" по значению родителя.
		Если ДанныеЗаполнения.Свойство("Родитель")
			И Не ДанныеЗаполнения.Свойство("Услуга") Тогда 
			
			Услуга = ДанныеЗаполнения.Родитель.Услуга;
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа 
		И НЕ ЗначениеЗаполнено(НаименованиеПолное) Тогда 
		НаименованиеПолное = Наименование;
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Предварительный контроль.
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	// Контроль дублей наименования.
	// Не может быть номенклатуры с одинаковым Наименованием.
	Если НЕ Константы.РазрешитьДублированиеНоменклатуры.Получить() Тогда 
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Номенклатура.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	НЕ Номенклатура.ПометкаУдаления
			|	И НЕ Номенклатура.Ссылка = &Ссылка
			|	И Номенклатура.Наименование = &Наименование"); 
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Параметры.Вставить("Наименование", Наименование);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда  
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "КОРРЕКТНОСТЬ", НСтр("ru = 'Наименование'"),,,
				НСтр("ru = 'Номенклатура с таким наименованием уже существует. Введите другое Наименование.'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.Наименование",,Отказ);		
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

