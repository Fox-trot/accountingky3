#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	СправочникМенеджер = Справочники.СпособыОтраженияЗаработнойПлаты;
	
	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;     
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Наименование = СокрЛП(СтрокаТаблицыЗначений.Наименование);
		СчетЗатрат = ПланыСчетов.Хозрасчетный.НайтиПоКоду(СокрЛП(СтрокаТаблицыЗначений.СчетЗатрат));
		СчетЗатратСФ = ПланыСчетов.Хозрасчетный.НайтиПоКоду(СокрЛП(СтрокаТаблицыЗначений.СчетЗатратСФ));
		
		СправочникСсылка = СправочникМенеджер.НайтиПоНаименованию(Наименование, Истина);
		
		Если СправочникСсылка.Пустая() Тогда
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;	
		
		СправочникОбъект.Наименование = Наименование;
		СправочникОбъект.СчетЗатрат = СчетЗатрат;
		СправочникОбъект.СчетЗатратСФ = СчетЗатратСФ;
		
		БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникОбъект);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти
	
#КонецЕсли

