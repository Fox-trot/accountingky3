#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	СправочникМенеджер = Справочники.ВидыТранспортаНалогНаИмущество;

	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;     
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Код = СокрЛП(СтрокаТаблицыЗначений.Код);
		Наименование = СокрЛП(СтрокаТаблицыЗначений.Наименование);
		НаименованиеПолное = СокрЛП(СтрокаТаблицыЗначений.НаименованиеПолное); 
		КатегорияАБВ = СокрЛП(СтрокаТаблицыЗначений.КатегорияАБВ); 
		
		СправочникСсылка = СправочникМенеджер.НайтиПоНаименованию(Наименование, Истина);
		
		Если СправочникСсылка.Пустая() Тогда
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;	
		
		СправочникОбъект.Код = Код;
		СправочникОбъект.Наименование = Наименование;
		СправочникОбъект.НаименованиеПолное	= НаименованиеПолное;
		СправочникОбъект.КатегорияАБВ	= КатегорияАБВ;
		
		БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникОбъект);
	КонецЦикла;
	
	// Заполнение шкалы оценки стоимости.
	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполненияШкалаОценки").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	КлассификаторТаблица.Индексы.Добавить("Наименование");
	
	Выборка = СправочникМенеджер.Выбрать();
	Пока Выборка.Следующий() Цикл
		НайденныеСтроки = КлассификаторТаблица.НайтиСтроки(Новый Структура("Наименование", Выборка.Наименование));
		Если НайденныеСтроки.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.ШкалаОценкиСтоимости.Очистить();

		// Заполнение табличной части.
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
			СтрокаТабличнойЧасти = СправочникОбъект.ШкалаОценкиСтоимости.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, НайденнаяСтрока);			
		КонецЦикла;			
		
		БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникОбъект);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти	

#КонецЕсли

