
&НаСервере
Процедура ЗаполнитьНаСервере()
	
	КлассификаторXML = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	ТЗ_ГруппыСправочника = КлассификаторТаблица.СкопироватьКолонки("Наименование, НомерГруппы");
	Для каждого СТЗ Из КлассификаторТаблица Цикл
		Если ЗначениеЗаполнено(СТЗ.НомерГруппы) Тогда
			НоваяСтрока = ТЗ_ГруппыСправочника.Добавить(); 	
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СТЗ); 
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЗаписьОКВ Из КлассификаторТаблица Цикл
	
		СтатьяДДС       = ЗаписьОКВ.Наименование;
		НомерГруппы  	= ЗаписьОКВ.НомерГруппы; 
		Родитель		= ЗаписьОКВ.Родитель;
		
		ЗаписатьЭлементСправочника(СтатьяДДС, НомерГруппы, Родитель, ТЗ_ГруппыСправочника);     	
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Вопрос = "Справочник будет перезаполнен! Продолжить?";
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), Вопрос, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, );
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ  = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ЗаполнитьНаСервере();
	
	Элементы.Список.Обновить();

КонецПроцедуры

// Записывает текущиц элемент классификатор с учетом
// иерархии 
&НаСервере  
Функция ЗаписатьЭлементСправочника(СтатьяДДС, НомерГруппы, Родитель, ТЗ_ГруппыСправочника)	
	НайденнаяСсылка =  Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию(СтатьяДДС);
	Если НайденнаяСсылка.Пустая() Тогда
		Если ЗначениеЗаполнено(НомерГруппы) Тогда
			Объект = Справочники.СтатьиДвиженияДенежныхСредств.СоздатьГруппу();	
		Иначе 
			Объект = Справочники.СтатьиДвиженияДенежныхСредств.СоздатьЭлемент();	
		КонецЕсли;
		Объект.Наименование = СтатьяДДС;		
		Если ЗначениеЗаполнено(Родитель) Тогда
			НаименованиеГруппыРодителя = ТЗ_ГруппыСправочника.Найти(Родитель, "НомерГруппы").Наименование; 
			Объект.Родитель  =  Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию(НаименованиеГруппыРодителя);
		КонецЕсли;
		
	Иначе
		Объект = НайденнаяСсылка.ПолучитьОбъект();
	КонецЕсли;	
	
	Попытка
		Объект.Записать();
	Исключение
		Сообщить("Не удалось записать элемент: " + СтатьяДДС + " - " + ОписаниеОшибки(), СтатусСообщения.Важное);
	КонецПопытки;
		
	Возврат Объект.Ссылка;
КонецФункции // ЗаписатьЭлементСправочника  
