#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Необходимо заполнить основной договор
	Если НЕ ЭтоГруппа И НЕ ЗначениеЗаполнено(ОсновнойДоговорКонтрагента) Тогда
		
		НадоСоздатьДоговор = Истина;

		Если НЕ ЭтоНовый() Тогда
			// Пытаемся найти уже созданный договор
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ДоговорыКонтрагентов.Ссылка КАК Договор
				|ИЗ
				|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
				|ГДЕ
				|	ДоговорыКонтрагентов.Владелец = &Владелец");
			Запрос.УстановитьПараметр("Владелец", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если Не РезультатЗапроса.Пустой() Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				Выборка.Следующий();
				ОсновнойДоговорКонтрагента = Выборка.Договор;
				
				НадоСоздатьДоговор = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НадоСоздатьДоговор Тогда
			ОсновнойДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку();
			ДополнительныеСвойства.Вставить("НовыйОсновнойДоговор", ОсновнойДоговорКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()
	
// Процедура - обработчик события ПриЗаписи.
//
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Создадим договор контрагента по ссылке, созданной в событии ПередЗаписью()
	Если Не ЭтоГруппа И ДополнительныеСвойства.Свойство("НовыйОсновнойДоговор") Тогда
		
		ДоговорОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		ДоговорОбъект.Заполнить(Ссылка);
		ДоговорОбъект.УстановитьСсылкуНового(ДополнительныеСвойства.НовыйОсновнойДоговор);
		БухгалтерскийУчетКлиентСервер.ЗаписатьСправочникОбъект(ДоговорОбъект);

		ДополнительныеСвойства.Удалить("НовыйОсновнойДоговор");
		
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

 #КонецОбласти
	
#КонецЕсли