#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда
Перем СсылкаДоговора; // В переменной хранится ссылка нового для создания нового договора.
	
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция создает новый договор по умолчанию.
//
Функция СоздатьДоговорПоУмолчанию()
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
	
	Если СсылкаДоговора <> Неопределено Тогда
		НовыйДоговор.УстановитьСсылкуНового(СсылкаДоговора);
	КонецЕсли;
	
	НовыйДоговор.Наименование		= "Основной договор";
	НовыйДоговор.ВалютаРасчетов 	= ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	НовыйДоговор.Организация		= Справочники.Организации.ОсновнаяОрганизация;
	НовыйДоговор.Владелец			= Ссылка;		
	НовыйДоговор.ВидДоговора		= ВидОсновногоДоговора;
	Если  ВидОсновногоДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда  
		НовыйДоговор.СуммаВключаетНалоги 	= Истина;
	Иначе 
		НовыйДоговор.СтавкаНСП = Справочники.СтавкиНСП.Торговля; 		   
	КонецЕсли;
	НовыйДоговор.ВидПоставкиНДС = Справочники.ВидыПоставокНДС.ОблагаемыеПоСтавке12Процентов;
		
	БухгалтерскийУчетКлиентСервер.ЗаписатьСправочникОбъект(НовыйДоговор);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НовыйДоговор.Ссылка;
	
КонецФункции // СоздатьДоговорПоУмолчанию()
	
// Процедура - обработчик события ПередЗаписью.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	СсылкаДоговора = Неопределено;
	
	// Необходимо заполнить основной договор
	Если НЕ ЭтоГруппа И НЕ ЗначениеЗаполнено(ОсновнойДоговорКонтрагента) Тогда
		Если ЭтоНовый() Тогда
			// Создаем ссылку договора, который будет создан после записи контрагента
			// и записываем ее в реквизит ДоговорПоУмолчанию
			СсылкаДоговора = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку();
			ОсновнойДоговорКонтрагента = СсылкаДоговора;
		Иначе
			// Пытаемся найти уже созданный договор
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ДоговорыКонтрагентов.Ссылка КАК Договор
				|ИЗ
				|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
				|ГДЕ
				|	ДоговорыКонтрагентов.Владелец = &Владелец");
			Запрос.УстановитьПараметр("Владелец", Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				// Если нашли, ставим первый попавшийся
				ОсновнойДоговорКонтрагента = Выборка.Договор;
			Иначе
				// Если не нашли - создаем новый
				ОсновнойДоговорКонтрагента = СоздатьДоговорПоУмолчанию();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()
	
// Процедура - обработчик события ПриЗаписи.
//
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Создадим договор контрагента по ссылке, созданной в событии ПередЗаписью
	Если СсылкаДоговора <> Неопределено Тогда
		
		СоздатьДоговорПоУмолчанию();
		
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()
	
#КонецЕсли