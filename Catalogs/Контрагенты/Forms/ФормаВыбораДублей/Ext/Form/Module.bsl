////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокДублей.Параметры.УстановитьЗначениеПараметра("ИНН", СокрЛП(Параметры.ИНН));
	ЭтаФорма.Заголовок = НСтр("ru = 'Список дублей по ИНН'");
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СписокДублейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПередачи = Новый Структура("Ключ", Элемент.ТекущиеДанные.Ссылка);
	ПараметрыПередачи.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта",
				  ПараметрыПередачи, 
				  Элемент,
				  ,
				  ,
				  ,
				  Новый ОписаниеОповещения("ОбработатьРедактированиеЭлемента", ЭтаФорма));
	
КонецПроцедуры
			  
&НаКлиенте
Процедура СписокДублейПриАктивизацииСтроки(Элемент)
	
	ДанныеТекущейСтроки = Элементы.СписокДублей.ТекущиеДанные;
	
	Если НЕ ДанныеТекущейСтроки = Неопределено Тогда
		
		ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументыПоКонтрагенту(Команда)
	
	ТекущиеДанныеСписка = Элементы.СписокДублей.ТекущиеДанные;
	Если ТекущиеДанныеСписка = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Контрагент", ТекущиеДанныеСписка.Ссылка);
	ПараметрыФормы = Новый Структура("КлючНастроек, Отбор, СформироватьПриОткрытии", "Контрагент", СтруктураОтбора, Истина);
	
	//ОткрытьФорму("Обработка.ДокументыПоКонтрагенту.Форма.ДокументыПоКонтрагенту", ПараметрыФормы, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбработатьРедактированиеЭлемента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Элементы.СписокДублей.Обновить();
КонецПроцедуры

// Процедура обработки активизации строки списка.
//
&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиСписка()
	
	ТекущиеДанныеСписка = Элементы.СписокДублей.ТекущиеДанные;
	Если ТекущиеДанныеСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ОткрытьДокументыПоКонтрагенту.Заголовок = "Документы по контрагенту (" + ПолучитьКоличествоДокументовКонтрагента(ТекущиеДанныеСписка.Ссылка) + ")";
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

&НаСервереБезКонтекста
Функция ПолучитьКоличествоДокументовКонтрагента(Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументыПоКонтрагенту.Ссылка) КАК КоличествоДокументов
		|ИЗ
		|	КритерийОтбора.ДокументыПоКонтрагенту(&Контрагент) КАК ДокументыПоКонтрагенту";

	Запрос.УстановитьПараметр("Контрагент", Контрагент);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.КоличествоДокументов;
	КонецЕсли;

КонецФункции
