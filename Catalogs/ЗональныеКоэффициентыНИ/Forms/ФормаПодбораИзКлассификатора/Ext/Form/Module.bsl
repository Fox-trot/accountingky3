
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Заполнение списка валют из ОКВ
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьЗональныеКоэффициенты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗональныеКоэффициенты

&НаКлиенте
Процедура ЗональныеКоэффициентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборВСпискеЗональныхКоэффициентов(СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьЗональныеКоэффициенты()
	
	// Заполняет Зональные коэффицента из Макета
	
	КлассификаторXML = Справочники.ЗональныеКоэффициентыНИ.ПолучитьМакет("ЗональныеКоэффициенты").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого ЗаписьОКВ Из КлассификаторТаблица Цикл
		НоваяСтрока = ЗональныеКоэффициенты.Добавить();
		НоваяСтрока.Зона         = ЗаписьОКВ.Наименование;
		НоваяСтрока.Коэффициент  = ЗаписьОКВ.Коэффициент; 
		НоваяСтрока.Описание     = ЗаписьОКВ.Описание;
	КонецЦикла;
	
КонецПроцедуры       

&НаСервере
Функция СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки)
	
	ТекущаяСсылка = Неопределено;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ЗональныеКоэффициенты[НомерСтроки];
		
		СтрокаВБазе = Справочники.ЗональныеКоэффициентыНИ.НайтиПоНаименованию(ТекущиеДанные.Зона);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.ЗональныеКоэффициенты.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Справочники.ЗональныеКоэффициентыНИ.СоздатьЭлемент(); 		
		НоваяСтрока.Наименование              = ТекущиеДанные.Зона;
		НоваяСтрока.Коэффициент				  = ТекущиеДанные.Коэффициент;   			
		НоваяСтрока.Записать();
		
		Если НомерСтроки = Элементы.ЗональныеКоэффициенты.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;               		
				
	КонецЦикла;
	
	Возврат ТекущаяСсылка;

КонецФункции

&НаКлиенте
Процедура ОбработатьВыборВСпискеЗональныхКоэффициентов(СтандартнаяОбработка = Неопределено)
		
	// Добавление элемента справочника и вывод результата пользователю
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСсылка = СохранитьВыбранныеСтроки(Элементы.ЗональныеКоэффициенты.ВыделенныеСтроки);
	
	ОповеститьОВыборе(ТекущаяСсылка);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Зональные коэффициенты добавлены.'"), , "",БиблиотекаКартинок.Информация32);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти      
