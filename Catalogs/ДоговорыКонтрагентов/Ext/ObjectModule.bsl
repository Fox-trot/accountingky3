#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Организация = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяОрганизация");
	ВалютаРасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		// Программное заполнение по методу Заполнить()
		СтандартнаяОбработка = Ложь;
		
		Владелец = ДанныеЗаполнения;
		ВидДоговора = ?(ЗначениеЗаполнено(ДанныеЗаполнения.ВидОсновногоДоговора), ДанныеЗаполнения.ВидОсновногоДоговора, Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		
		Если ДанныеЗаполнения.ВидОсновногоДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда  
			СуммаВключаетНалоги = Истина;
		КонецЕсли;
		
		ВидПоставкиНДС = Справочники.ВидыПоставокНДС.ОблагаемыеПоСтавке12Процентов;		
		СтавкаНДС = Справочники.СтавкиНДС.Стандарт;
	КонецЕсли;
	
	Наименование = Справочники.ДоговорыКонтрагентов.НаименованиеДоговора(ВалютаРасчетов);
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком
		Или ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее Тогда	
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаНДС");		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Предварительная очистка.
	Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком
		Или ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее Тогда	
		СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
		ТипЦен = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка();
		ВидПоставкиНДС = Справочники.ВидыПоставокНДС.ПустаяСсылка();
		СуммаВключаетНалоги = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипЦен) Тогда 
		СуммаВключаетНалоги = ТипЦен.ЦенаВключаетНалоги;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ВалютаРасчетов) Тогда 
		ВалютаРасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ОсновныеДоговорыКонтрагента.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.ВидДоговора.Установить(ВидДоговора);
	НаборЗаписей.Отбор.Контрагент.Установить(Владелец);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запись = НаборЗаписей[0];
	Если Запись.Договор = Ссылка Тогда
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли