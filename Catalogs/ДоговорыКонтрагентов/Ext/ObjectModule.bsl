#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Организация = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяОрганизация");
	ВалютаРасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ДанныеУчетнойПолитики = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(ТекущаяДатаСеанса(), Организация);
	СтавкаНДС = ДанныеУчетнойПолитики.СтавкаНДСПоУмолчанию;
	СтавкаНСП = ДанныеУчетнойПолитики.СтавкаНСППоУмолчанию; 		   
	СтавкаНСПУслуги = ДанныеУчетнойПолитики.СтавкаНСППоУмолчанию; 		   
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		// Программное заполнение по методу Заполнить()
		СтандартнаяОбработка = Ложь;
		
		Владелец = ДанныеЗаполнения;
		ВидДоговора = ?(ЗначениеЗаполнено(ДанныеЗаполнения.ВидОсновногоДоговора), ДанныеЗаполнения.ВидОсновногоДоговора, Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		
		Если ДанныеЗаполнения.ВидОсновногоДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда  
			СуммаВключаетНалоги = Истина;
		КонецЕсли;
		
		ВидПоставкиНДС = Справочники.ВидыПоставокНДС.ОблагаемыеПоСтавке12Процентов;		
	КонецЕсли;
	
	Наименование = Справочники.ДоговорыКонтрагентов.НаименованиеДоговора(ВалютаРасчетов);
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ЗначениеЗаполнено(ТипЦен) Тогда 
		СуммаВключаетНалоги = ТипЦен.ЦенаВключаетНалоги;
	КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(ВалютаРасчетов) Тогда 
		ВалютаРасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#КонецЕсли